{"query": "#defaultView:ScatterChart\n# Statistics on number of recipients for award wrt. gender\nSELECT ?men ?women ?award ?awardLabel WHERE {\n{  SELECT (COUNT(?recipient_male) AS ?men) (COUNT(?recipient_female) AS ?women) ?award WHERE {\n    ?award wdt:P31 wd:Q1792571   # art prize\n    { \n      ?recipient_male wdt:P166 ?award .\n      ?recipient_male wdt:P21 wd:Q6581097 .\n    }\n    UNION \n    {\n      ?recipient_female wdt:P166 ?award .\n      ?recipient_female wdt:P21 wd:Q6581072 .\n    }  \n  }\n  GROUP BY ?award\n           }\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Gender balance of art prizes", "target": ""}
{"query": "select distinct ?image (count(distinct ?item) as ?count) where\n{ ?item wdt:P31 wd:Q5 . ?item2 wdt:P31 wd:Q5 . # both are people\n  ?item wdt:P18 ?image . ?item2 wdt:P18 ?image . # both have the same image\n  filter(?item != ?item2) . # ensure different item IDs (no duplicates)\n} group by ?image order by desc(?count)", "context": "Images used on more than one item about a person", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?placeLabel ?roadLabel ?coords WHERE {\n  ?item wdt:P170 wd:Q1671666 ;\n        wdt:P131 ?place ;\n        wdt:P669 ?road ;\n        wdt:P625 ?coords ;\n        rdfs:label ?itemL filter (lang(?itemL) = \"fr\") .\n  FILTER (CONTAINS(?itemL, \"VRS_\"))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?itemLabel ", "context": "Artworks by street artist Invader in Versailles", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?English_alphabet ?English_alphabetLabel ?media WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?English_alphabet wdt:P361 wd:Q754673.\n  OPTIONAL { ?English_alphabet p:P2919 ?label_in_sign_language.\n             ?label_in_sign_language ps:P2919 ?media.\n             ?label_in_sign_language pq:P407 wd:Q14759.\n             FILTER(regex(str(?media), \"\\\\.svg$\", \"i\"))\n           }\n}\nORDER BY ?English_alphabetLabel", "context": "American Sign Language signs for the letters of the English alphabet", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?French_alphabet ?French_alphabetLabel ?media WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?French_alphabet wdt:P361 wd:Q755124.\n  OPTIONAL { ?French_alphabet p:P2919 ?label_in_sign_language.\n             ?label_in_sign_language ps:P2919 ?media.\n             ?label_in_sign_language pq:P407 wd:Q33302\n           }\n}\nORDER BY ?French_alphabetLabel", "context": "French Sign Language signs for the letters of the French alphabet", "target": ""}
{"query": "SELECT ?character ?characterLabel ?imgself ?imgactor ?killerLabel ?methodLabel ?genusLabel ?placeLabel ?episodeLabel WHERE {\n  ?character wdt:P1080 wd:Q2461698.\n  OPTIONAL { ?character wdt:P509 ?method. }\n  OPTIONAL { ?character wdt:P157 ?killer. }\n  OPTIONAL { ?character wdt:P175 ?actor.\n            ?actor wdt:P18 ?imgactor.}\n  OPTIONAL {?character wdt:P18 ?imgself.}\n  ?character p:P1196 ?statement.\n  ?statement ps:P1196 ?genus.\n  OPTIONAL {?character wdt:P20 ?place}\n  OPTIONAL {?statement  prov:wasDerivedFrom [ pr:P248 ?episode ].}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?dod", "context": "All deaths in Game of Thrones", "target": ""}
{"query": "#defaultView:Timeline\nPREFIX dct: <http://purl.org/dc/terms/>\nPREFIX skos: <http://www.w3.org/2004/02/skos/core#>\nPREFIX dbc: <http://dbpedia.org/resource/Category:>\n\nSELECT DISTINCT  ?wditem ?wditemLabel ?date ?image ?work ?workLabel WHERE {  \n    SERVICE <http://dbpedia.org/sparql> {\n        ?item dct:subject|dct:subject/skos:broader dbc:History_of_telecommunications ;\n              owl:sameAs ?wditem FILTER regex (?wditem, \"wikidata.org\") .\n    }        \n    SERVICE <https://query.wikidata.org/sparql> {\n\t  {?wditem wdt:P569 ?date FILTER (YEAR(?date)>1700).}\n      UNION\n      {?wditem wdt:P571|wdt:P577|wdt:P580 ?date .}\n      OPTIONAL {?wditem wdt:P800 ?work. ?work rdfs:label ?workLabel FILTER (LANG(?workLabel)=\"en\") .}\n      OPTIONAL {?wditem wdt:P18 ?image } .  \n      ?wditem rdfs:label ?wditemLabel FILTER (LANG(?wditemLabel)=\"en\") .\n    }\n}", "context": "Timeline for the History of Telecommunications", "target": ""}
{"query": "#defaultView:Graph\n# by @piecesofuk 16th April 2019\nSELECT DISTINCT ?line ?lineLabel ?rgb ?p ?pLabel WHERE {\n  VALUES $IS_ON_LINE { wdt:P81 }\n  VALUES $IS_PART_OF { wdt:P361 }\n  VALUES $LONDON_UNDERGROUND { wd:Q20075 }\n  \n  ?p $IS_ON_LINE ?line .\n  ?line $IS_PART_OF $LONDON_UNDERGROUND .\n  ?line wdt:P465 ?rgb . # gets the tube line's colour\n  { # inner select returns all stations which are on 3 or more London Underground lines\n    SELECT ?p (COUNT(?line) AS ?count) WHERE {\n      VALUES $IS_A { wdt:P31 }\n      VALUES $TUBE_STATION { wd:Q14562709 }\n      VALUES $IS_ON_LINE { wdt:P81 }\n      VALUES $IS_PART_OF { wdt:P361 }\n      VALUES $LONDON_UNDERGROUND { wd:Q20075 }\n\n      ?p $IS_A $TUBE_STATION .\n      ?p $IS_ON_LINE ?line .\n      ?line $IS_PART_OF $LONDON_UNDERGROUND .\n    } GROUP BY ?p\n    HAVING (?count >= 3)\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of London Tube stations that are on at least three lines", "target": ""}
{"query": "#Esglèsies de Sri Lanka\n#List of churchs in Sri Lanka\n#defaultView:Map\nSELECT DISTINCT ?esglesia ?esglesialabel ?coor\nWHERE {\n  ?esglesia wdt:P31 wd:Q16970 ;\n          ?range wd:Q854;\n          wdt:P625 ?coor.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of churches in Sri Lanka", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?nigerianwomen ?nigerianwomenLabel ?placeofbirthLabel ?datebirth ?locationLabel?image\nWHERE {\n?nigerianwomen wdt:P31 wd:Q5.\n?nigerianwomen wdt:P21 wd:Q6581072.\n?nigerianwomen wdt:P27 wd:Q1033.\nOPTIONAL {?nigerianwomen wdt:P19 ?placeofbirth}.\nOPTIONAL {?nigerianwomen wdt:P569 ?datebirth}.\nOPTIONAL {?nigerianwomen wdt:P18 ?image}.\n\n \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }}\n", "context": "Nigerian women with Wikipedia article and pictures", "target": ""}
{"query": "#Map of places of residence of accused witches.\n#defaultView:Map\n\n#You need to use the name ?layer as the variable you are colour coding by \n#It's much easier to change later if you do this in the SELECT area, using e.g. (?Gender as ?layer)\n\nSELECT ?person ?personLabel ?accusedwitch ?precision ?coords ?Occupation ?Residence ?image (?Occupation as ?layer) \nWHERE {\n  ?person wdt:P4478 ?accusedwitch .\n  ?person wdt:P31 wd:Q5.\n  ?person wdt:P21 ?gender .\n  ?person wdt:P551 ?residence .\n  OPTIONAL { ?person wdt:P106 ?occupation} \n  OPTIONAL { ?person wdt:P3716 ?socialclassification }\n  ?residence wdt:P625 ?coords.\n  OPTIONAL { ?article schema:about ?person. }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n    ?person rdfs:label ?personLabel.\n    ?residence rdfs:label ?Residence .\n    ?occupation rdfs:label ?Occupation.\n    ?gender rdfs:label ?Gender.\n    ?socialclassification rdfs:label ?SocialClassification .\n  }\n}", "context": "Map of places of residence for accused witches with a layer for occupations", "target": ""}
{"query": "#defaultView:Map\n# map of items that have some relation to Saint George\nSELECT ?thing ?label ?img ?loc ?layer ?propertyLabel WHERE {\n  VALUES $ST_GEORGE { wd:Q48438 }\n  ?thing ?p $ST_GEORGE .\n  ?thing wdt:P18 ?img .\n  ?thing wdt:P625 ?loc .\n  ?property wikibase:directClaim ?p . # needed to get property labels\n  # does the item have English labels?\n  OPTIONAL { \n    ?thing rdfs:label ?label\n    FILTER (LANG(?label) = \"en\")\n  }\n  BIND(IF(BOUND(?label), \"Items with English Labels\", \"Items without English Labels\") AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} \n", "context": "Map of Wikidata items that are dedicated to, named after or depict etc, St George.", "target": ""}
{"query": "#Partigiani Italiani by death place\n#defaultView:Map{\"layer\":\"?year\"}\nSELECT ?item ?itemLabel ?_coordinates ?_image ?death_placeLabel (year(?death_date) as ?year) WHERE {\n  ?item wdt:P106 wd:Q212948;   \n        wdt:P20/wdt:P17 wd:Q38;\n        wdt:P20/wdt:P625 ?_coordinates;   # their deathplace, specifically the coordinates of their birthplace\n        wdt:P20 ?death_place;\n        p:P570/psv:P570 ?death_date_node .\n        ?death_date_node wikibase:timeValue ?death_date .\n        OPTIONAL { ?item wdt:P18 ?_image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }  # labels in English\n}", "context": "Partigiani color-coded by year of death", "target": ""}
{"query": "SELECT ?count ?typeLabel ?example ?exampleLabel ?exampleDescription ?afterLabel ?afterDescription WHERE {\n  {\n    select distinct ?type (count(distinct ?item) as ?count) (SAMPLE(?item) AS ?example) WHERE {\n        ?item wdt:P138 ?namesake . ?namesake wdt:P31 ?type . \n        ?item wdt:P31 wd:Q5 .\n    } group by ?type \n  }\n  ?example wdt:P138 ?after. ?after wdt:P31 ?type .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by desc(?count)\n", "context": "Some people are named after the strangest sorts of things", "target": ""}
{"query": "#defaultView:Timeline\nPREFIX dct: <http://purl.org/dc/terms/>\nPREFIX skos: <http://www.w3.org/2004/02/skos/core#>\nPREFIX dbc: <http://dbpedia.org/resource/Category:>\n\nSELECT DISTINCT  ?wditem ?wditemLabel ?date ?image ?work ?workLabel ?coord WHERE {  \n    SERVICE <http://dbpedia.org/sparql> {\n        ?item dct:subject|dct:subject/skos:broader dbc:Illuminati_members ;\n              owl:sameAs ?wditem FILTER regex (?wditem, \"wikidata.org\") .\n    }        \n    SERVICE <https://query.wikidata.org/sparql> {\n\t  {?wditem wdt:P569 ?date FILTER (YEAR(?date)<2050).}\n      #UNION\n      #{?wditem wdt:P571|wdt:P577|wdt:P580|wdt:P619 ?date .}\n      #{?wditem wdt:P625 ?coord .}\n     # OPTIONAL {?wditem wdt:P800 ?work. ?work rdfs:label ?workLabel FILTER (LANG(?workLabel)=\"en\") .}\n      OPTIONAL {?wditem wdt:P18 ?image } .  \n      ?wditem rdfs:label ?wditemLabel FILTER (LANG(?wditemLabel)=\"en\") .\n    }\n}", "context": "Timeline for members of the Illuminati Order", "target": ""}
{"query": "SELECT ?Periodikum ?PeriodikumLabel (YEAR(?Jahr) AS ?Inception) (IRI(CONCAT('https://twitter.com/hashtag/',?Twitter_Hashtag,'?src=hash')) AS ?TwitterURL) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Periodikum (wdt:P31/(wdt:P279*)) wd:Q1002697;\n    wdt:P571 ?Jahr.\n  FILTER(?Jahr < \"1900-01-01\"^^xsd:dateTime)\n   ?Periodikum wdt:P2572 ?Twitter_Hashtag. \n}\nLIMIT 100", "context": "Newspapers founded before 1900 with their Twitter hashtag", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?signImage ?namedAfter ?namedAfterLabel ?fantoir {\n  ?item wdt:P31/wdt:P279* wd:Q83620 ; wdt:P17 wd:Q142 ; wdt:P131 wd:Q647 ; wdt:P1766 ?signImage .\n  OPTIONAL { ?item wdt:P138 ?namedAfter }\n  OPTIONAL { ?item wdt:P3182 ?fantoir }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" . }\n}\nORDER BY ?itemLabel", "context": "Street signs in Rennes", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?pobCoords\", \"?conCoords\",  \"?line\", \"?rgb\"]}\nSELECT ?mp ?mpLabel ?image ?pob ?pobLabel ?pobCoords ?con ?conLabel ?conCoords ?line ?rgb ?layer WHERE {\n  $mp wdt:P19 ?pob .\n  OPTIONAL { ?mp wdt:P18 ?image .}\n  ?pob wdt:P625 ?pobCoords .\n  ?pob p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?poblon; wikibase:geoLatitude  ?poblat; ] ].\n  $mp p:P39 ?mpstmt .\n  ?mpstmt ps:P39 wd:Q30524710 .\n  ?mpstmt pq:P768 ?con .  \n  ?con wdt:P625 ?conCoords .\n  ?con p:P625 [\n            ps:P625 [];\n            psv:P625 [\n               wikibase:geoLongitude ?conlon;\n               wikibase:geoLatitude  ?conlat; \n               ]\n          ].\n  MINUS {?mpstmt pq:P582 ?e } \n  ?mpstmt pq:P4100 ?party .\n  ?party wdt:P465 ?rgb .\n  ?party rdfs:label ?layer .\n  FILTER (LANG(?layer) = \"en\")\n  BIND(CONCAT('LINESTRING (', STR(?poblon), ' ', STR(?poblat), ',', STR(?conlon), ' ', STR(?conlat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Lines linking UK MPs' place of birth and their constituency with colour-coded layers for each political party", "target": ""}
{"query": "SELECT DISTINCT ?person ?personLabel ?born ?bornLabel ?coord {\n  ?position wdt:P279 wd:Q1711695 .           # 1. find all the MSP positions\n  ?person wdt:P39 ?position .                # 2. did they hold any MSP position\n  ?person wdt:P31 wd:Q5 .                    # 2a. check they were actually people (in case we have any fictional MSPs)\n  ?person wdt:P19 ?born .                    # 3. where were they born\n  MINUS { ?born wdt:P131* wd:Q22 . }         # 4. this bit filters down to just \"not in Scotland\"\n  OPTIONAL { ?born wdt:P625 ?coord . }       # 5. coordinates if available\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n}\n#defaultView:Map", "context": "Map of birthplaces for Members of Scottisch Parliament born outside Scotland", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?geo ?type WHERE {\n  { ?item wdt:P31*/wdt:P279* wd:Q12323 . }\n  UNION\n  { ?item wdt:P31*/wdt:P279* wd:Q131681 . }\n  ?item wdt:P625 ?geo .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Global map of dams and reservoirs", "target": ""}
{"query": "#defaultView:TreeMap\nSELECT \n  # ?language1\n  ?language1Label\n  # ?language2\n  ?language2Label\n  (COUNT(?concept) AS ?count) \n{\n  ?concept ^wdt:P5137 / ^ontolex:sense ?lexeme1, ?lexeme2 .\n  ?lexeme1 dct:language ?language1 .\n  ?lexeme2 dct:language ?language2 .\n  ?language1 wdt:P218 ?language1Label .\n  ?language2 wdt:P218 ?language2Label .\n  # FILTER (?language1 != ?language2)\n}\nGROUP BY ?language1 ?language1Label ?language2 ?language2Label\nHAVING (COUNT(?concept) > 10)", "context": "Lexemes language overlap tree map", "target": ""}
{"query": "#defaultView:Timeline\nPREFIX dct: <http://purl.org/dc/terms/>\nPREFIX skos: <http://www.w3.org/2004/02/skos/core#>\nPREFIX dbc: <http://dbpedia.org/resource/Category:>\nPREFIX dbo: <http://dbpedia.org/ontology/>\n\nSELECT DISTINCT  ?wditem ?wditemLabel ?date ?image ?work ?workLabel ?coord WHERE {  \n    SERVICE <http://dbpedia.org/sparql> {\n        ?item dct:subject dbc:Pioneers_of_photography ;\n        #?item dct:subject|dct:subject/skos:broader|dct:subject/skos:broader/skos:broader dbc:Pioneers_of_photography ;\n              owl:sameAs ?wditem FILTER regex (?wditem, \"wikidata.org\") .\n        #?item dct:subject|dct:subject/skos:broader|dct:subject/skos:broader/skos:broader dbc:19th_century_in_art .\n    }        \n    SERVICE <https://query.wikidata.org/sparql> {\n\t  {?wditem wdt:P569 ?date FILTER (YEAR(?date)<1901).}\n     # UNION\n     # {?wditem wdt:P571|wdt:P577|wdt:P580|wdt:P619 ?date.}\n     # FILTER (YEAR(?date)<1914).\n      #{?wditem wdt:P625 ?coord .}\n     # OPTIONAL {?wditem wdt:P800 ?work. ?work rdfs:label ?workLabel FILTER (LANG(?workLabel)=\"en\") .}\n      OPTIONAL {?wditem wdt:P18 ?image } .  \n      ?wditem rdfs:label ?wditemLabel FILTER (LANG(?wditemLabel)=\"en\") .\n    }\n}", "context": "Timeline of pioneers of photography, via Wikidata and DBpedia", "target": ""}
{"query": "\n#defaultView:Map\nSELECT DISTINCT ?synagoga ?synagogaLabel ?coor ?kategorie_na_Commons ?obr WHERE {\n  ?synagoga wdt:P31 wd:Q34627;\n    ?range wd:Q213;\n    wdt:P625 ?coor.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs\". }\n  OPTIONAL { ?synagoga wdt:P373 ?kategorie_na_Commons. }\n  OPTIONAL { ?synagoga wdt:P18 ?obr. }\n}", "context": "Maps of synagogues in Czech Republic", "target": ""}
{"query": "# Lexemes with usage examples but without any senses, in certain language\nSELECT ?l ?lemma ?example  WHERE {\n  ?l a ontolex:LexicalEntry ; dct:language ?language ;\n       wikibase:lemma ?lemma .\n  ?language wdt:P218 'sv'.\n  ?l wdt:P5831 ?example.\n  FILTER NOT EXISTS {?l ontolex:sense ?sense }\n}\nORDER BY ?lemma", "context": "Lexemes that lack Senses but have at least one usage example", "target": ""}
{"query": "SELECT ?country ?countryLabel ?street ?streetLabel ?image WHERE {\n  ?country wdt:P31 wd:Q3624078 ; wdt:P463 wd:Q458 .\n  OPTIONAL {\n    ?street wdt:P31/wdt:P279* wd:Q83620 ; wdt:P131 wd:Q647 ; wdt:P138 ?country .\n    OPTIONAL { ?street wdt:P18 ?image } .\n  } .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\" . }\n}\nORDER BY ?countryLabel", "context": "European countries that have a street named after them in Rennes", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?author ?authorLabel ?coord ?image WHERE {\n  ?author wdt:P31 wd:Q55818 .\n  ?author wdt:P625 ?coord\n  OPTIONAL {?author wdt:P18 ?image .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, de, fr, es, it\" }\n} \n", "context": "Map of meteor impact craters", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?p ?pLabel ?size ?rgb WHERE {\n  ?p wdt:P31 wd:Q1075 .\n  ?p wdt:P465 ?rgb .   # sRGB color hex triplet\n  BIND (1 AS ?size)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ASC (LCASE(?pLabel)) \n", "context": "Bubble chart of all instances of color which have an sRGB color hex triplet", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (COUNT(?riot) AS ?numOfRiots) ?country ?countryLabel\nWHERE {\n  ?riot wdt:P31/wdt:P279* wd:Q124757 .\n  ?riot wdt:P17 ?country .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?country ?countryLabel ORDER BY DESC(?numOfRiots)\n", "context": "Number of riots per country", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?countryFemale ?countryFemaleLabel (?countFemales / (?countMales + ?countFemales) AS ?ratioFemales) WHERE {\n  {\n    SELECT ?countryMale (COUNT(?itemMale) AS ?countMales) WHERE {\n      ?itemMale wdt:P106 wd:Q82955;\n        wdt:P569 ?dateBirthMale;\n        wdt:P31 wd:Q5;\n        wdt:P21 wd:Q6581097;\n        wdt:P27 ?countryMale.\n      FILTER((YEAR(?dateBirthMale)) > 1960 )\n    }\n    GROUP BY ?countryMale\n  }\n  {\n    SELECT ?countryFemale (COUNT(?itemFemale) AS ?countFemales) WHERE {\n      ?itemFemale wdt:P106 wd:Q82955;\n        wdt:P569 ?dateBirthFemale;\n        wdt:P31 wd:Q5;\n        wdt:P21 wd:Q6581072;\n        wdt:P27 ?countryFemale.\n      FILTER((YEAR(?dateBirthFemale)) > 1960 )\n    }\n    GROUP BY ?countryFemale\n  }\n  FILTER(?countryMale = ?countryFemale)\n  FILTER((?countMales + ?countFemales) > 300 )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC (?ratioFemales)", "context": "European country having the most female politicians", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?data (URI(CONCAT(\"https://facebook.com/\",?facebook)) AS ?fb)  (SAMPLE(?coord) AS ?coord) WHERE {\n  ?item wdt:P31 wd:Q51404, wd:Q27968055;\n        wdt:P17 wd:Q38;\n        wdt:P131 ?paese;\n        wdt:P361 ?partof;\n        wdt:P585 ?data.\n  ?paese wdt:P625 ?coord.\n  ?partof wdt:P2013 ?facebook.\n\tFILTER((?data >= \"2019-01-01T00:00:00Z\"^^xsd:dateTime) && (?data <= \"2019-12-31T00:00:00Z\"^^xsd:dateTime))\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n} GROUP BY ?item ?itemLabel ?data ?facebook ?fb", "context": "Gay parades in Italy in 2019", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord1\", \"?coord2\", \"?line\"]}\nSELECT * WHERE {\n  ?comm1 wdt:P31 wd:Q484170 ; wdt:P131 wd:Q12549 ; wdt:P625 ?coord1 ; wdt:P47 ?comm2.\n  MINUS { ?comm1 p:P31 [ ps:P31 wd:Q484170 ; pq:P582 [] ] . }\n  MINUS { ?comm2 p:P31 [ ps:P31 wd:Q484170 ; pq:P582 [] ] . }\n  ?comm2 wdt:P625 ?coord2 .\n  ?comm1 p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].\n  ?comm2 p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n}", "context": "Map of communes of the French area \"Ille-et-Vilaine\" which share borders", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?popCoords\", \"?subjectCoords\",  \"?line\"]}\nSELECT ?print ?printLabel ?image ?pop ?popLabel ?popCoords ?subject ?subjectLabel ?subjectCoords ?line ?layer WHERE {\n  $print wdt:P361 wd:Q21542493 ;\n         wdt:P291 ?pop .\n  OPTIONAL { ?print wdt:P18 ?image .}\n  ?pop wdt:P625 ?popCoords .\n  ?pop p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?poplon; wikibase:geoLatitude  ?poplat; ] ].\n  $print p:P921 ?printstmt .\n  ?printstmt ps:P921 ?subject .\n  ?subject wdt:P625 ?subjectCoords .\n  ?subject p:P625 [\n            ps:P625 [];\n            psv:P625 [\n               wikibase:geoLongitude ?subjectlon;\n               wikibase:geoLatitude  ?subjectlat; \n               ]\n          ].\n  ?pop rdfs:label ?layer .\n  FILTER (LANG(?layer) = \"en\")\n  BIND(CONCAT('LINESTRING (', STR(?poplon), ' ', STR(?poplat), ',', STR(?subjectlon), ' ', STR(?subjectlat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Connections between the subject and printing location of Welsh landscape prints", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?countryLabel ?coords WHERE \n{\n  ?item wdt:P31 wd:Q25391.\n  OPTIONAL { ?item wdt:P17 ?country }\n  OPTIONAL { ?item wdt:P625 ?coords }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?countryLabel ?itemLabel ", "context": "Dunes in Wikidata", "target": ""}
{"query": "#defaultView:Graph\nSELECT\n  ?author1 ?author1Label ?image1 ?rgb\n  ?author2 ?author2Label ?image2 \nWITH {\n  SELECT\n    ?author1 (SAMPLE(?image1_) AS ?image1)\n    ?author2 (SAMPLE(?image2_) AS ?image2)\n    (SAMPLE(?rgb_) AS ?rgb)\n  WHERE {\n    ?work wdt:P50 ?author1 , ?author2 ;\n          wdt:P1433 wd:Q6294930 .\n\n    # Only display co-authorship for certain types of documents\n    # Journal and conference articles, books, not (yet?) software\n    VALUES ?publication_type { wd:Q13442814 wd:Q571 wd:Q26973022 wd:Q17928402 wd:Q947859 }\n    FILTER EXISTS { ?work wdt:P31 ?publication_type . }\n\n    # No self-links.\n    FILTER (?author1 != ?author2)\n\n    # Images\n    OPTIONAL { ?author1 wdt:P18 ?image1_ }\n    OPTIONAL { ?author2 wdt:P18 ?image2_ }\n\n    # Coloring of the nodes\n    BIND(\"FFFFFF\" AS ?rgb_)\n  }\n  GROUP BY ?author1 ?author2\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,sv,ru,zh\".\n  }\n}\n", "context": "Co-author network of Journal of Cheminformatics authors", "target": ""}
{"query": "#Carte des services d'archives\n#defaultView:Map\nSELECT * WHERE {\n  ?item wdt:P31*/wdt:P279* wd:Q166118;\n        wdt:P625 ?geo .\n}#opendata", "context": "Map of archives in the world", "target": ""}
{"query": "SELECT ?item ?taxonname  (group_concat(?letter; separator='') AS ?reversetaxonname ) with {\nSELECT ?item ?taxonname WHERE {\n  ?item wdt:P31 wd:Q16521 ;\n        wdt:P105 wd:Q7432 ;\n        wdt:P171* wd:Q7367 ;\n        wdt:P225 ?taxonname .\n} limit 100 } as %i\n\nwhere {\n  include %i\n  BIND (?taxonname AS ?s)\n  VALUES ?d1 { 0 1 2 3 4 5 6 7 8 9 }\n  VALUES ?d2 { 0 1 2 3 4 5 6 7 8 9 }\n  VALUES ?d3 { 0 1 2 3 4 5 6 7 8 9 }\n  BIND (100 * ?d3 + 10 * ?d2 + ?d1 + 1 AS ?i)\n  BIND (SUBSTR(?s, ?i, 1) AS ?letter)\n}\nGROUP BY ?item ?taxonname\n\n", "context": "Reverse taxon names of 100 mosquitoes", "target": ""}
{"query": "SELECT ?lexeme ?word WHERE {\n  ?lexeme a ontolex:LexicalEntry ; dct:language ?language ; ontolex:lexicalForm ?form .\n  ?form ontolex:representation ?word .\n  ?language wdt:P218 'en' .\n  filter(strlen(?word)=5)\n  filter(strstarts(ucase(?word),\"F\"))  \n} order by ?word", "context": "Five-letter words beginning with \"f\", from wikidata's lexeme collection", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?image WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q7578326 ; wdt:P131+ wd:Q456 .\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\" . }\n}", "context": "Péniches in Lyon", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?item ?itemLabel ?startdate (SAMPLE(?image) AS ?image)\nWHERE\n{\n    ?item wdt:P31 wd:Q146.      \n    ?item  p:P39 ?mouserOffice. \n    ?mouserOffice ps:P39 wd:Q198641.\n    ?mouserOffice pq:P580 ?startdate .\n\t#?item wdt:P569 ?startdate .\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n    OPTIONAL { ?item wdt:P18 ?image }\n}\nGROUP BY ?item ?itemLabel ?startdate", "context": "Timeline of the Chief Mousers of 10 Downing Street, with photographs when available", "target": ""}
{"query": "SELECT ?item ?itemLabel\nWHERE\n{\n\t?item wdt:P31 wd:Q12308941;\n    # ?item wdt:P31/wdt:P279* wd:Q12308941;\n    \t      rdfs:label ?itemLabel;\n         FILTER (lang(?itemLabel) = \"fr\") .\n    \t FILTER regex (?itemLabel, \"(ée)$\").\n}\nLIMIT 100\n", "context": "French male first names ending with \"ée\"", "target": ""}
{"query": "# Nouns without gender in a language\nSELECT ?l ?lemma WHERE {\n   ?l a ontolex:LexicalEntry ; dct:language ?language ;\n        wikibase:lexicalCategory wd:Q1084;\n        wikibase:lemma ?lemma .\n  ?language wdt:P218 'cs'\nMINUS { ?l wdt:P5185 []. }\n}\nORDER BY ?lemma", "context": "Nouns without grammatical gender in a chosen language", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?constituencyLabel ?partyLabel ?linkcount\n{\n ?item p:P39 ?ps .\n ?ps ps:P39 wd:Q30524710 . # current Parliament\n OPTIONAL { ?ps pq:P768 ?constituency . }\n OPTIONAL { ?ps pq:P4100 ?party . }\n filter not exists { ?ps pq:P582 ?end . } . ?item wikibase:sitelinks ?linkcount .\n SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n}ORDER BY desc(?linkcount)", "context": "Current UK Members of Parliament ranked by number of Wikimedia sitelinks", "target": ""}
{"query": "SELECT ?dessin_d_architecture ?image ?geoloc ?cr_ateurLabel ?d_peintLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?dessin_d_architecture wdt:P31 wd:Q184784.\n  OPTIONAL { ?dessin_d_architecture wdt:P18 ?image. }\n  OPTIONAL { ?dessin_d_architecture wdt:P625 ?geoloc. }\n  OPTIONAL {  }\n  OPTIONAL {  }\n  OPTIONAL { ?dessin_d_architecture wdt:P170 ?cr_ateur. }\n  OPTIONAL { ?dessin_d_architecture wdt:P180 ?d_peint. }\n}\nLIMIT 100", "context": "Architectural drawings depicting cathedrals in France", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?cid ?genre (COUNT(*) AS ?count) WHERE {\n  ?item wdt:P31 wd:Q215380;\n  wdt:P138 ?ref.\n  ?ref wdt:P1080 wd:Q81738.\n  ?item wdt:P136 ?cid.\n  OPTIONAL {\n    ?cid rdfs:label ?genre.\n    FILTER((LANG(?genre)) = \"en\")\n  }\n}\nGROUP BY ?cid ?genre\nORDER BY DESC (?count) (?genre)", "context": "Music bands named after Tolkien's universe, by genre", "target": ""}
{"query": "# When were the usage examples in a given languages published? \n#defaultView:BarChart\nSELECT DISTINCT ?year (COUNT(?stated_in) AS ?count ) WHERE {\n  ?l a ontolex:LexicalEntry;\n       dct:language ?language.\n  ?language wdt:P218 'sv'.\n  ?l wdt:P5831 ?example.\n    ?l p:P5831 ?statement.\n    ?statement prov:wasDerivedFrom ?refnode.\n    ?refnode pr:P248 ?stated_in.\n    ?stated_in wdt:P577 ?date.\n    BIND(str(YEAR(?date)) AS ?year)\n}\nGROUP BY ?year\nORDER BY ?year\n\n", "context": "Publication dates of works used as usage example for Swedish Lexemes", "target": ""}
{"query": "SELECT\n  (?female_organizers / (?male_organizers + ?female_organizers) AS ?organizer_ratio)\n  ?female_organizers ?male_organizers\n  (?female_authors / (?male_authors + ?female_authors) AS ?author_ratio)\n  ?female_authors ?male_authors\n  ?event ?eventLabel ?eventDescription\n{\n  {\nSELECT\n  ?event\n  (COUNT(DISTINCT ?male_organizer) AS ?male_organizers) \n  (COUNT(DISTINCT ?female_organizer) AS ?female_organizers) \n  (COUNT(DISTINCT ?male_author) AS ?male_authors) \n  (COUNT(DISTINCT ?female_author) AS ?female_authors) \n{\n  VALUES ?event_type { wd:Q2020153 wd:Q40444998 }\n  ?event wdt:P31 ?event_type .\n  ?proceedings wdt:P4745 ?event .\n  {\n    ?event wdt:P664 ?male_organizer . \n    ?male_organizer wdt:P21 wd:Q6581097 .\n  }\n  UNION\n  {\n    ?event wdt:P664 ?female_organizer . \n    ?female_organizer wdt:P21 wd:Q6581072 .\n  }\n  {\n    ?work wdt:P1433 ?proceedings ; wdt:P50 ?male_author .\n    ?male_author wdt:P21 wd:Q6581097 .\n  }\n  UNION\n  {\n    ?work wdt:P1433 ?proceedings ; wdt:P50 ?female_author .\n    ?female_author wdt:P21 wd:Q6581072 .\n  }\n}\nGROUP BY ?event \n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?male_organizers)    ", "context": "Ratios of male/female organizers and speakers at conferences", "target": ""}
{"query": "#defaultView:Map\nSELECT ?sl ?slLabel ?coord ?country ?countryLabel # ?indigenous ?indigenousLabel \nWHERE \n{\n  ?sl wdt:P31 wd:Q34228.\n#  ?sl wdt:P2341 ?indigenous.\n  ?sl wdt:P17 ?country.\n  ?country wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"ca,en\". }\n}", "context": "World map of sign languages by country", "target": ""}
{"query": "#Punk bands from Indonesia categorized by location formed\n#defaultView:BarChart\n\nSELECT ?locationLabel (COUNT(?band) AS ?count)\nWHERE {\n\t?band wdt:P31/wdt:P279* wd:Q25391823 ;\n\t\twdt:P495 wd:Q252;\n    OPTIONAL {\n\t\t?band wdt:P856 ?website\n\t}\n    OPTIONAL {\n\t\t?band wdt:P740 ?location\n\t}\n\tSERVICE wikibase:label {\n\t\tbd:serviceParam wikibase:language \"en,id\" .\n\t}\n}\n\nGROUP BY ?locationLabel", "context": "Number of punk bands from Indonesia by region", "target": ""}
{"query": "\n#defaultView:Map{\"layer\":\"?county\"}\n\nSELECT DISTINCT ?parish ?parishLabel ?coords ?county ?countyLabel WHERE {\n     \n     ?parish wdt:P31/wdt:P279* wd:Q28931907 .\n     ?county wdt:P31/wdt:P279* wd:Q20974973 .\n     \n     ?parish wdt:P131 ?county .\n     \n   ?parish wdt:P625 ?coords.\n  \n   SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n   }\n }\nLIMIT 15000\n ", "context": "Map of parishes of New South Wales, Australia, colour-coded by county", "target": ""}
{"query": "#defaultView:Map{\"layer\":\"?langs\"}\nSELECT ?item (SAMPLE(?label) AS ?label) (SAMPLE(?coords) AS ?coords) (GROUP_CONCAT(?lang) AS ?langs) (COUNT(DISTINCT ?label) as ?desccount)\nWHERE {\n  ?item wdt:P17  wd:Q16 .\n  ?item wdt:P625 ?coords.\n\n  FILTER NOT EXISTS {\n    ?item rdfs:label ?englishLabel .\n    FILTER(LANGMATCHES(LANG(?englishLabel),\"EN\"))\n  } .\n  ?item rdfs:label ?label\n  BIND (LANG(?label) AS ?lang)\n} GROUP BY ?item\nORDER BY DESC(?desccount)", "context": "places in Canada with no English label", "target": ""}
{"query": "# LexData languages by number of usage examples\nSELECT ?languageLabel (COUNT(?example) AS ?count ) WHERE {\n  ?l a ontolex:LexicalEntry; dct:language ?language.\n  ?l wdt:P5831 ?example.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?languageLabel\nORDER BY DESC(?count)", "context": "Languages having the most usage examples in Wikidata's lexicographical data", "target": ""}
{"query": "#Map of places of residence of accused witches.\n#defaultView:Map{\"hide\":[\"?coord\", \"?layer\"], \"markercluster\": {\"maxClusterRadius\": 50}}\n\n#You need to use the name ?layer as the variable you are colour coding by \n#It's much easier to change later if you do this in the SELECT area, using e.g. (?Gender as ?layer)\n\nSELECT DISTINCT ?person ?personLabel ?accusedwitch ?precision ?coords ?Occupation ?Residence ?image (?Gender as ?layer) \nWHERE {\n  ?person wdt:P4478 ?accusedwitch .\n  ?person wdt:P31 wd:Q5.\n  ?person wdt:P21 ?gender .\n  ?person wdt:P551 ?residence .\n  ?residence wdt:P625 ?coords.\n  OPTIONAL { ?article schema:about ?person. }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n    ?person rdfs:label ?personLabel.\n    ?residence rdfs:label ?Residence .\n    ?occupation rdfs:label ?Occupation.\n    ?gender rdfs:label ?Gender.\n  }\n}", "context": "Map of places of residence of accused witches in Scotland, colour-coded by gender", "target": ""}
{"query": "# Camille Saint-Saëns symphonies\nSELECT ?symphony ?symphonyLabel (YEAR(?inception) AS ?year)\nWHERE\n{\n  ?symphony wdt:P31 wd:Q9734;\n            wdt:P86 wd:Q150445;\n            wdt:P571 ?inception.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY ?inception", "context": "Symphonies written by Camille Saint-Saëns", "target": ""}
{"query": "# Artworks depicting the moon\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?itemDescription ?creatorLabel ?image WHERE {\n    ?item wdt:P31*/wdt:P279* wd:Q838948.\n    ?item wdt:P180 wd:Q405.\n    ?item wdt:P170 ?creator\n  OPTIONAL { ?item wdt:P18 ?image } # Optionally with an image\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,fr\" }\n}", "context": "Artworks depicting the Moon", "target": ""}
{"query": "# Temporal distribution of editions in a language\n#defaultView:BarChart\nSELECT DISTINCT ?year (COUNT(?published) AS ?count) WHERE {\n  ?item wdt:P31 wd:Q3331189;\n    wdt:P407 wd:Q9027;\n    wdt:P577 ?published.\n  FILTER(!(ISBLANK(?published)))\n  BIND(STR(YEAR(?published)) AS ?year)\n}\nGROUP BY ?year\nORDER BY (?year)", "context": "Temporal distribution of book editions in Swedish", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?Donejakue_bidea ?Donejakue_bideaLabel ?koordenatuak ?layerLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es,en\". }\n  ?Donejakue_bidea (wdt:P361*) wd:Q41150.\n  ?Donejakue_bidea wdt:P131*/^wdt:P527 wd:Q47588.\n  ?Donejakue_bidea wdt:P625 ?koordenatuak.\n  ?Donejakue_bidea wdt:P361 ?bidea.\n  ?bidea wdt:P31 wd:Q337912.\n  BIND (?bidea AS ?layer)\n}", "context": "Settlements along the Way of Saint James in Basque Country", "target": ""}
{"query": "#Map of places of residence of accused witches.\n#defaultView:Map{\"hide\":[\"?coord\", \"?layer\"], \"markercluster\": {\"maxClusterRadius\": 50}}\n\n#You need to use the name ?layer as the variable you are colour coding by \n#It's much easier to change later if you do this in the SELECT area, using e.g. (?Gender as ?layer)\n\nSELECT DISTINCT ?person ?personLabel ?associated ?precision ?coords ?Residence ?image (?Gender as ?layer) \nWHERE {\n  ?person wdt:P4524 ?associated.\n  ?person wdt:P31 wd:Q5.\n  OPTIONAL\n  {\n  ?person wdt:P21 ?gender .\n    }\n  ?person wdt:P551 ?residence .\n  ?residence wdt:P625 ?coords.\n  OPTIONAL { ?article schema:about ?person. }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n    ?person rdfs:label ?personLabel.\n    ?residence rdfs:label ?Residence .\n    ?gender rdfs:label ?Gender.\n  }\n}", "context": "Map of places of residence of accused witches in Scotland", "target": ""}
{"query": "# Articles \nSELECT DISTINCT ?language ?groupeLabel ?languageLabel ?sitelink\nWHERE {\n  {\n    VALUES (?language ?groupe) {\n      (wd:Q33302 wd:Q496250)\n      (wd:Q150 wd:Q23492)\n      }\n  } UNION {\n    ?language wdt:P361 ?groupe .\n    ?groupe wdt:P361 wd:Q496250 .\n  }\n  \n  OPTIONAL {\n    ?sitelink schema:about ?language .\n    ?sitelink schema:inLanguage \"fr\" .\n    ?sitelink schema:isPartOf <https://fr.wikipedia.org/>.\n  }\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n} ORDER BY ?languageLabel", "context": "List of regional languages of France", "target": ""}
{"query": "#Autrices de langue françaises mortes avant 1949\nSELECT ?autrice ?autriceLabel ?lieuNaissance ?lieuNaissanceLabel ?lieuMort ?lieuMortLabel (MIN(?img) AS ?image) ?prenomLabel (MIN(?idIsni) as ?isni) ?idBnf (MIN(?n) AS ?naissance) (MAX(?m) AS ?mort) WHERE {\n  ?autrice wdt:P106 wd:Q36180.\n  ?autrice wdt:P21 wd:Q6581072.\n  ?autrice wdt:P570 ?m.\n  { ?autrice wdt:P1412 wd:Q150. }\n  UNION\n  { ?autrice wdt:P1412 wd:Q1473289. }\n  OPTIONAL { ?autrice wdt:P569 ?n.}\n  OPTIONAL { ?autrice wdt:P19 ?lieuNaissance.}\n  OPTIONAL { ?autrice wdt:P20 ?lieuMort.}\n  OPTIONAL { ?autrice wdt:P18 ?img. }\n  OPTIONAL { ?autrice wdt:P735 ?prenom. }\n  OPTIONAL { ?autrice wdt:P213 ?idIsni. }\n  OPTIONAL { ?autrice wdt:P268 ?idBnf. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\". }\n  FILTER((YEAR(?m)) < 1949)\n}\nGROUP BY ?autrice ?autriceLabel ?lieuNaissance ?lieuNaissanceLabel ?lieuMort ?lieuMortLabel ?image ?prenomLabel ?isni ?idBnf ?mort ?naissance\nORDER BY DESC(?naissance)", "context": "French female authors whose work is in the public domain", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?item ?itemLabel ?start ?image (GROUP_CONCAT(DISTINCT ?creatorLabel;separator=\", \") AS ?creators) WHERE {\n  { \n   { ?item wdt:P31 wd:Q65966993 }  # direct instances of hypertext systems\n   UNION\n   { ?item wdt:P279 wd:Q65966993 } # specific types of hypertext systems\n  }\n  OPTIONAL { \n    { ?item wdt:P50 ?creator } UNION  # author\n    { ?item wdt:P170 ?creator } UNION # creator\n    { ?item wdt:P61 ?creator } UNION  # discoverer or inventor\n    { ?item wdt:P178 ?creator }       # developer\n  }\n  OPTIONAL {\n    { ?item wdt:P571 ?start } UNION\n    { ?item wdt:P580 ?start }\n  }\n  OPTIONAL { ?item wdt:P18 ?image } .\n  \n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n    ?item rdfs:label ?itemLabel .\n    ?creator rdfs:label ?creatorLabel .\n  }\n} GROUP BY ?item ?itemLabel ?start ?image", "context": "Timeline of hypertext systems", "target": ""}
{"query": "#defaultView:Map\nSELECT ?koord ?megalito ?megalitoLabel ?layerLabel  WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es,en\". }\n  ?megalito wdt:P1435 wd:Q66112157.\n  ?megalito wdt:P625 ?koord.\n  ?megalito wdt:P31 ?mota\n  BIND (?mota AS ?layer)\n}", "context": "Megaliths in the Basque Country, color-coded by type", "target": ""}
{"query": "SELECT DISTINCT ?band ?bandLabel ?image ?date_image WHERE {\n  ?band wdt:P31 wd:Q215380. #music band\n  ?band wdt:P136*/wdt:P279* wd:Q38848. #playing heavy metal or a sub-genre of it\n  ?band wdt:P495 wd:Q33. # coming from Finland \\../.\n  OPTIONAL {?band p:P18 ?istat. # with an image\n           ?istat ps:P18 ?image.}\n  OPTIONAL {?band p:P18 ?istat. # with a date for the image\n           ?istat pq:P585 ?date_image.}\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en, fi\". }\n}", "context": "Heavy metal bands from Finland with or without picture and date of the picture", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?typeLabel (COUNT(?item) AS ?count) WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q1030034;\n    wdt:P31 ?type.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?typeLabel\nLIMIT 50", "context": "Types of GLAMs in Wikidata", "target": ""}
{"query": "SELECT ?band ?bandLabel\nWHERE {\n  ?band wdt:P31/wdt:P279* wd:Q215380 ;\n        wdt:P138 ?name .\n  ?name wdt:P1080 wd:Q81738 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }", "context": "Bands whose name is part of Tolkien's legendarium", "target": ""}
{"query": "#defaultView:Map{\"layer\": \"?operatorLabel\"}\nSELECT \n  ?farm ?farmLabel\n  ?geo\n  ?operator ?operatorLabel\n{\n  ?farm wdt:P31 wd:Q1357601 .\n  ?farm wdt:P625 ?geo .\n  OPTIONAL { \n    ?farm wdt:P137 ?operator .\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,da\". }\n}", "context": "Map of offshore windfarms", "target": ""}
{"query": "SELECT ?country ?countryLabel ?countrySitelinks ?city ?cityLabel ?citySitelinks ?rate\nWHERE \n{\n  ?city wdt:P31/wdt:P279* wd:Q515 .\n  ?city wdt:P17 ?country .\n  ?city wikibase:sitelinks ?citySitelinks .\n\n  ?country wikibase:sitelinks ?countrySitelinks .\n  #MINUS { ?country wdt:P582 ?_ . }\n  #MINUS { ?country wdt:P576 ?__ . }\n  \n  \n  FILTER (?citySitelinks > ?countrySitelinks )\n  BIND ( ROUND((?citySitelinks / ?countrySitelinks)*100)/100 AS ?rate)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr, en\". }\n} ORDER BY DESC(?rate)", "context": "Cities having more Wikipedia sitelinks than their country", "target": ""}
{"query": "SELECT\n  ?item ?itemLabel ?siteLink\n       WHERE {\n         ?siteLink schema:about ?item;\n         schema:isPartOf <https://bn.wikisource.org/>;\n         wikibase:badge wd:Q20748093.\n         SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\".}\n         }", "context": "Books in Bengali Wikisource that are validated", "target": ""}
{"query": "#Flightless birds of New Zealand\n#defaultView:ImageGrid\nSELECT ?flightlessbird ?flightlessbirdLabel ?image WHERE {\n  ?bird wdt:P3512 wd:Q372949 ; \n        wdt:P18 ?image ;\n        wdt:P183 wd:Q664 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ", "context": "Flightless birds of New Zealand", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coordinates\nWHERE\n{\n#ist Klasse Insel\n  ?item wdt:P31 wd:Q23442 ;\n#in Verwaltungseinheit Deutschland\n          ?range wd:Q183;\n          wdt:P625 ?coordinates.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }", "context": "Islands in Germany", "target": ""}
{"query": "#defaultView:Map{\"layer\":\"?paisLabel\"}\nSELECT ?item ?itemLabel ?comienzo ?lugar ?imagen ?paisLabel {\n  ?item wdt:P31 wd:Q8343784.\n  {?item wdt:P17 wd:Q77} UNION {?item wdt:P17 wd:Q414} UNION {?item wdt:P17 wd:Q298}.\n  ?item wdt:P17 ?pais.\n  OPTIONAL {?item wdt:P18 ?imagen}.\n  OPTIONAL {?item wdt:P625 ?lugar}.\n          SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],es\". }\n}", "context": "Clandestine center of detention and torture in Chile, Uruguay, and Argentina", "target": ""}
{"query": "# same-sex married couples where neither partner has a sexual orientation\nSELECT DISTINCT ?person1 ?person2 ?person1Label ?person1Description ?person2Label ?person2Description WHERE {\n  ?person1 wdt:P31 wd:Q5;\n           wdt:P21 ?gender;\n           wdt:P569 ?dob1;\n           wdt:P26 ?person2.\n  ?person2 wdt:P31 wd:Q5;\n           wdt:P569 ?dob2;\n           wdt:P21 ?gender.\n  MINUS { ?person1 wdt:P91 ?orientation. }\n  MINUS { ?person2 wdt:P91 ?orientation. }\n  # arbitrarily filter out some early results, we’re more likely to find references for recent people\n  FILTER(?dob1 >= \"1950-01-01\"^^xsd:dateTime)\n  FILTER(?dob2 >= \"1950-01-01\"^^xsd:dateTime)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Same-sex married couples where neither partner has a sexual orientation", "target": ""}
{"query": "SELECT ?competition ?competitionLabel ?date ?end WHERE {\n  { ?competition wdt:P31 wd:Q2990963. }\n  UNION\n  {\n    ?competition wdt:P31 wd:Q16510064.\n    ?competition wdt:P361 ?comp.\n    ?comp wdt:P31 wd:Q2990963.\n  }\n  UNION\n  {\n    ?competition wdt:P361 ?segment.\n    ?segment wdt:P31 wd:Q16510064.\n  }\n  OPTIONAL { ?competition wdt:P580 ?start. }\n  OPTIONAL { ?competition wdt:P582 ?end. }\n  OPTIONAL { ?competition wdt:P585 ?pointInTime. }\n  BIND(COALESCE(?start, ?pointInTime) AS ?date)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  FILTER((BOUND(?start)) || (BOUND(?pointInTime)))\n  FILTER(?date >= (NOW() - \"P1D\"^^xsd:duration))\n}\nORDER BY ?date", "context": "Future figure skating competitions", "target": ""}
{"query": "SELECT ?competition ?competitionLabel ?date ?end WHERE {\n  { ?competition wdt:P31 wd:Q2990963. }\n  UNION\n  {\n    ?competition wdt:P31 wd:Q16510064.\n    ?competition wdt:P361 ?comp.\n    ?comp wdt:P31 wd:Q2990963.\n  }\n  UNION\n  {\n    ?competition wdt:P361 ?segment.\n    ?segment wdt:P31 wd:Q16510064.\n  }\n  OPTIONAL { ?competition wdt:P580 ?start. }\n  OPTIONAL { ?competition wdt:P582 ?end. }\n  OPTIONAL { ?competition wdt:P585 ?pointInTime. }\n  BIND(COALESCE(?start, ?pointInTime) AS ?date)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  FILTER((BOUND(?start)) || (BOUND(?pointInTime)))\n  FILTER(?end >= (NOW() - \"P1D\"^^xsd:duration))\n  FILTER((NOW()) >= ?date)\n}\nORDER BY ?date", "context": "Currently happening figure skating competitions", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?image {\n  ?item wdt:P31/wdt:P279* wd:Q1229765 ; wdt:P17 wd:Q142 ; rdfs:label ?itemLabel .\n  FILTER (LANG(?itemLabel) = \"fr\" && STRSTARTS(?itemLabel, \"A\")) .\n  OPTIONAL { ?item wdt:P18 ?image } .\n}\nORDER BY ?itemLabel\n", "context": "French ships whose name start with an A", "target": ""}
{"query": "#defaultView:Map\nselect ?item ?itemLabel ?place ?placeLabel ?coords ?born where\n{\n  ?item wdt:P39 wd:Q66688984 . optional { ?item wdt:P19 ?place . ?place wdt:P625 ?coords } . optional { ?item wdt:P569 ?born . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\n", "context": "Map of birth places of delegates to the Alaska Constitutional Convention", "target": ""}
{"query": "# people who are used as property examples, and the number of times they're used\nSELECT DISTINCT ?item ?itemLabel (count(?example) as ?example_count) (count(?property) as ?property_count) WHERE \n{\n   ?property p:P1855 ?example . ?example ps:P1855 ?item .\n   ?item wdt:P31 wd:Q5 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group  by ?item ?itemLabel order by desc(?property_count)", "context": "People used as examples in Wikidata properties", "target": ""}
{"query": "# All hillforts on Wikidata, coloured by their heritage status (mostly varies by country)\nselect ?site ?siteLabel ?siteDescription ?location ?photo ?placeNameLabel ?heritageLabel (?heritage as ?layer)\nWHERE {\n  ?site wdt:P31 ?place. ?place wdt:P279* wd:Q744099 .\n  ?site wdt:P625 ?location.\n  optional {    \n    ?site wdt:P31 ?placeName.     \n    ?site wdt:P18 ?photo. \n  }\n    ?site wdt:P1435 ?heritage.\n        SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,lt,es,[AUTO_LANGUAGE]\". }\n}\n#defaultView:Map", "context": "Hillforts mapped with their heritage designation/protection status", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?youtuber ?youtuberLabel ?image WHERE {\n  ?youtuber wdt:P106 wd:Q17125263.\n  MINUS { ?youtuber wdt:P2397 ?channel. }\n  OPTIONAL { ?youtuber wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "YouTubers without a YouTube channel", "target": ""}
{"query": "SELECT ?item ?death ?itemLabel {\n  ?item wdt:P31 wd:Q5 ; wdt:P106 wd:Q10349745 ; wdt:P509 wd:Q61037771 .\n  OPTIONAL { ?item wdt:P570 ?death }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?death", "context": "Racing automobile drivers who died in a car crash", "target": ""}
{"query": "SELECT ?person ?personLabel ?positionLabel ?date (URI(CONCAT(\"https://discovery.nationalarchives.gov.uk/details/c/\",?ukna)) AS ?url) WHERE {\n?person wdt:P3029 ?ukna; # person in UK National Archives\n   wdt:P39 ?position . ?position wdt:P361 wd:Q112014. # held position in Cabinet of the UK\n ?person wdt:P570 ?date # death date\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "UK Cabinet Ministers and their links in the UK National Archives", "target": ""}
{"query": "# most common properties on senses\nSELECT ?property ?propertyLabel ?count WITH {\n  SELECT ?property (COUNT(?statement) AS ?count) WHERE {\n    hint:SubQuery hint:optimizer \"None\".\n    ?lexeme ontolex:sense ?sense.\n    ?sense ?p ?statement.\n    ?property wikibase:claim ?p.\n  }\n  GROUP BY ?property\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?count)", "context": "Most common properties on Senses", "target": ""}
{"query": "#defaultView:Map\nselect ?actor ?actorLabel ?pobLabel ?geo ?image {\n  wd:Q83495 wdt:P161 ?actor .\n  # ?movie wdt:P179 wd:Q1210827 .\n  optional { ?actor wdt:P19 ?pob . }\n  optional { ?pob wdt:P625 ?geo . }\n  optional { ?actor wdt:P18 ?image . }\n  service wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Place of birth of actors in The Matrix", "target": ""}
{"query": "#defaultView:Graph\n#All species in the Heron (Ardeidae, Q18789) family with an IBP alpha code and an image (if available)\nSELECT ?item (?ibpCode AS ?itemLabel) ?image ?genus ?genusLabel WHERE {\n  ?item wdt:P31 wd:Q16521.\n  ?item wdt:P105 wd:Q7432.\n  ?item wdt:P171* wd:Q18789.\n  ?item wdt:P171 ?genus.\n  ?item wdt:P7182 ?ibpCode.\n  OPTIONAL {?item wdt:P18 ?image.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of all species in the Heron (Ardeidae) family with an IBP alpha code", "target": ""}
{"query": "SELECT \n  (REPLACE(STR(?item),\".*Q\",\"Q\") AS ?qid) \n  (REPLACE(STR(?tgt),\".*Q\",\"Q\") AS ?tgtQid) \n  ?tgtLabel \n  (URI(CONCAT(\"https://www.wikidata.org/w/index.php?title=\",?qid,\"&action=history\")) as ?history)\nWHERE \n{\n  ?tgt wdt:P31 wd:Q5 .\n  ?tgt wdt:P106 wd:Q2374149 .\n  ?item owl:sameAs ?tgt .\n  FILTER ( contains(STR(?tgt), \"Q1043\"))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,en\" }\n}\nORDER BY  ?qid", "context": "Number of times a Wikidata item for Carl von Linnaeus was created", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?personLabel ?item ?itemLabel ?img ?Alvin WHERE {\n  ?person   wdt:P6821 ?id.\n  BIND(URI(CONCAT(\"http://www.alvin-portal.org/alvin/view.jsf?pid=\", ?id)) AS ?Alvin)\n\n  ?item p:P225 ?ScienceNameStat.\n  ?ScienceNameStat pq:P405 ?person.\n  ?item wdt:P18 ?img.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Picture of species were the author(s) of the first scientific description is in WD and connected to Uppsala University Alvin database", "target": ""}
{"query": "\nSELECT ?personLabel ?item ?itemLabel ?img ?Alvin WHERE {\n  ?person   wdt:P6821 ?id.\n  BIND(URI(CONCAT(\"http://www.alvin-portal.org/alvin/view.jsf?pid=\", ?id)) AS ?Alvin)\n\n  ?item p:P225 ?ScienceNameStat.\n  ?ScienceNameStat pq:P405 ?person.\n  minus {?item wdt:P18 ?img}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "same query but where we miss a picture in Wikidata", "target": ""}
{"query": "# twins with different places of birth\n# two items are considered twins if they’re siblings,\n# and either the “sibling” statement carries a “type of kinship: twins” qualifier (optionally with a subclass of “twin”),\n# or both have the same date of birth (with “day” precision)\nSELECT ?twin1 ?birth1 ?twin2 ?birth2 ?twin1Label ?birth1Label ?twin2Label ?birth2Label WHERE {\n  ?twin1 wdt:P31 wd:Q5;\n         wdt:P19 ?birth1;\n         p:P3373 ?siblingStatement.\n  ?siblingStatement ps:P3373 ?twin2.\n  { ?siblingStatement pq:P1039/wdt:P279* wd:Q14756018. } UNION\n  { ?twin1 p:P569/psv:P569 ?dateOfBirth. ?twin2 p:P569/psv:P569 ?dateOfBirth. ?dateOfBirth wikibase:timePrecision \"11\"^^xsd:integer. }\n  ?twin2 wdt:P31 wd:Q5;\n         wdt:P19 ?birth2.\n  FILTER(?birth1 != ?birth2)\n  FILTER(STR(?twin1) < STR(?twin2))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Twins with different places of birth", "target": ""}
{"query": "# places in Scottish local government areas\nselect ?item ?itemLabel ?coords ?elseLabel (?else as ?layer)\nwhere { \n  ?item wdt:P131 ?else . ?else wdt:P131+ wd:Q22  .\n  ?item wdt:P625 ?coords .\n  filter not exists { ?item wdt:P131 ?else2 . filter(str(?else2) != str(?else)) } \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\n#defaultView:Map\n", "context": "Places in Scottish local government areas", "target": ""}
{"query": "#defaultView:Map\n#Create a Map with all Libraries in Austria\nSELECT ?lib ?libLabel ?coord ?kindOfLibLabel (?kindOfLibLabel AS ?layer) WHERE {\n  hint:Query hint:optimizer \"None\".\n  ?lib (wdt:P131+) wd:Q40;\n    (wdt:P31/(wdt:P279*)) wd:Q7075;\n    wdt:P31 ?kindOfLib;\n    wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE/wd],en\". }\n}\n", "context": "Libraries in Austria", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?clCoord\", \"?blCoord\", \"?line\"]}\n#linking central libraries in California to their branch library \nSELECT ?libSystem ?libSystemLabel ?centralLibrary ?centralLibraryLabel ?clCoord ?branchLibrary ?branchLibraryLabel ?blCoord ?line ?layer WHERE {\n  ?libSystem wdt:P31/wdt:P279* wd:Q28324850 ; wdt:P131 ?loc ;\n                 wdt:P527 ?centralLibrary ;\n                 wdt:P527 ?branchLibrary .\n  ?centralLibrary wdt:P31 wd:Q12317349 ;\n                  wdt:P625 ?clCoord .\n  ?branchLibrary wdt:P31 wd:Q11396180 ; \n                 wdt:P625 ?blCoord .\n  \n  ?centralLibrary p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?clLon; wikibase:geoLatitude ?clLat; ] ].\n  ?branchLibrary p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?blLon; wikibase:geoLatitude ?blLat; ] ].\n  \n  BIND(CONCAT('LINESTRING (', STR(?clLon), ' ', STR(?clLat), ',', STR(?blLon), ' ', STR(?blLat), ')') AS ?str1) .\n  BIND(STRDT(?str1, geo:wktLiteral) AS ?line) .\n  \n  BIND(?libSystem as ?layer)\n  \n  { SELECT * { ?loc wdt:P131* wd:Q99 . } }\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\n  \n  ", "context": "Library systems in California", "target": ""}
{"query": "#defaultView:Map\nSELECT ?waterfall ?waterfallLabel ?coord WHERE {\n  ?waterfall wdt:P31 wd:Q34038;\n    wdt:P625 ?coord.\n  MINUS { ?waterfall wdt:P4614 ?range. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Waterfalls without a drainage basin yet", "target": ""}
{"query": "#Cats, with pictures\n#added before 2016-10\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?pic WHERE {\n  ?item wdt:P180 wd:Q7307;\n    wdt:P18 ?pic.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Works of art depicting kisses", "target": ""}
{"query": "#TEMPLATE={ \"template\": { \"en\": \"Longest single-word lexemes in ?language without repeating letters.\" }, \"variables\": { \"?language\": { \"query\": \"SELECT ?id WHERE { ?lexeme dct:language ?id. } GROUP BY ?id ORDER BY DESC(COUNT(*)) LIMIT 50\" } } }\nPREFIX dct: <http://purl.org/dc/terms/> # workaround for T233148\nSELECT ?lexeme ?lemma (STRLEN(?lemma) AS ?length) WHERE {\n  BIND(wd:Q1860 AS ?language)\n  ?lexeme dct:language ?language;\n          wikibase:lemma ?lemma.\n  FILTER(STRLEN(?lemma) >= 10) # cheap filter to avoid running REGEX() on too many lemmata\n  FILTER(!REGEX(LCASE(?lemma), \"(.).*\\\\1\"))\n  FILTER(!CONTAINS(?lemma, \" \"))\n}\nORDER BY DESC(?length)\nLIMIT 10", "context": "Longest English word without a repeated letter", "target": ""}
{"query": "#TEMPLATE={ \"template\": { \"en\": \"Longest lexemes in ?language where all letters are in alphabetical order (A-Z only, no repeated letters).\" }, \"variables\": { \"?language\": { \"query\": \"SELECT ?id WHERE { ?lexeme dct:language ?id. } GROUP BY ?id ORDER BY DESC(COUNT(*)) LIMIT 50\" } } }\nPREFIX dct: <http://purl.org/dc/terms/> # workaround for T233148\nSELECT ?lexeme ?lemma (STRLEN(?lemma) AS ?length) WHERE {\n  BIND(wd:Q1860 AS ?language)\n  ?lexeme dct:language ?language;\n          wikibase:lemma ?lemma.\n  FILTER(STRLEN(?lemma) >= 5)\n  # first version allows repeated letters, second one doesn’t\n  # FILTER(REGEX(?lemma, \"^A*B*C*D*E*F*G*H*I*J*K*L*M*N*O*P*Q*R*S*T*U*V*Q*X*Y*Z*$\", \"i\"))\n  FILTER(REGEX(?lemma, \"^A?B?C?D?E?F?G?H?I?J?K?L?M?N?O?P?Q?R?S?T?U?V?Q?X?Y?Z?$\", \"i\"))\n}\nORDER BY DESC(?length)", "context": "Longest alphabetically sorted words in English", "target": ""}
{"query": "select distinct ?item ?itemLabel ?elseLabel ?coords (?elseLabel as ?layer)\nwhere\n{ ?item wdt:P131 ?else . \n  ?else wdt:P31 wd:Q5124673 . \n  ?item wdt:P625 ?coords .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}\n#defaultView:Map\n", "context": "Civil parishes of Scotland, revealed through their listed buildings", "target": ""}
{"query": "SELECT ?seq ?item ?itemLabel ?countyLabel ?councilLabel ?area ?pop ?vob_url WHERE {\n    ?item p:P528 ?seq_stmt .\n    ?seq_stmt ps:P528 ?seq .\n    ?seq_stmt pq:P972 wd:Q68810696 .\n  \n    ?item p:P131 ?adm_stmt .\n    ?adm_stmt pq:P580 ?d \n    FILTER(xsd:date(?d) = \"1996-04-01\"^^xsd:date) .\n    ?adm_stmt ps:P131 ?council .\n\n    ?item p:P131 ?adm_stmt2 .\n    ?adm_stmt2 pq:P582 ?d2 \n    FILTER(xsd:date(?d2) = \"1975-05-15\"^^xsd:date) .\n    ?adm_stmt2 ps:P131 ?county .\n  \n    OPTIONAL{?item wdt:P2046 ?area} .\n    OPTIONAL{?item wdt:P1082 ?pop} .\n    OPTIONAL{\n        ?item wdt:P3615 ?vob .\n        wd:P3615 wdt:P1630 ?fmt .\n        BIND(IRI(REPLACE(?fmt, '\\\\$1'  , ?vob)) AS ?vob_url) . \n    }\n  \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?seq", "context": "Scottish civil parishes by county and present-day council area", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?countryLabel ?coords WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q7075;\n    wdt:P17 ?country.\n  ?country wdt:P30 wd:Q15.\n  OPTIONAL{?item wdt:P625 ?coords.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Libraries in Africa", "target": ""}
{"query": "# most recently dissolved enterprises that were over 200 years old\nSELECT ?company ?companyLabel ?founded ?dissolved (xsd:integer((?dissolved - ?founded)/365.2425) AS ?years) WHERE {\n  ?company wdt:P1454/wdt:P279* wd:Q6881511; # the instance/subclass tree for companies seems to be a bit messy, so go via “legal form” instead\n           wdt:P571 ?founded;\n           wdt:P576 ?dissolved.\n  FILTER(?founded < (NOW() - \"P200Y\"^^xsd:duration))\n  FILTER(?dissolved > (?founded + \"P200Y\"^^xsd:duration))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de\". } # final fallback to German because as of 2019-09-26, many results were German(-language) companies\n}\nORDER BY DESC(?dissolved)\nLIMIT 25", "context": "Most recently dissolved enterprises that were over 200 years old", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?i ?iLabel ?layer ?c {\n  ?item wdt:P31/wdt:P279* wd:Q4260475 ; wdt:P31 ?thing ; wdt:P17 wd:Q668 ; wdt:P131/wdt:P131* wd:Q1546240 ; wdt:P625 ?c.\n  ?thing rdfs:label ?tl. FILTER(LANG(?tl)=\"en\")\n  BIND(?tl as ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Map of medical facilities in Murshidabad district, India, color-coded by type", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?cd\",\"?l,\"?rgb\"]}\nSELECT DISTINCT ?cd ?l ?i ?iLabel ?ct ?ctLabel ?rgb ?x WITH{SELECT ?i ?cd ?lat1 ?lon1 ?x(sample(?op)as?op)WHERE{{?i wdt:P31 wd:Q1248784}UNION{?i wdt:P31 wd:Q644371}?i wdt:P625 ?cd;wdt:P17 wd:Q668;p:P625/psv:P625 ?y.?y wikibase:geoLatitude ?lat1;wikibase:geoLongitude ?lon1.OPTIONAL{?i wdt:P137 ?op}}GROUP BY ?i ?cd ?lat1 ?lon1 ?x}AS%s\nWITH{SELECT ?l ?o ?t WHERE{INCLUDE %s.?i wdt:P17 wd:Q668;p:P521 ?nss.?nss ps:P521 ?ns.?ns wdt:P17 wd:Q668;p:P625/psv:P625 ?z.?z wikibase:geoLatitude ?lat2;wikibase:geoLongitude ?lon2.OPTIONAL{?nss pq:P137 ?o}FILTER(STR(?i)< STR(?ns)).BIND(CONCAT('LINESTRING(',STR(?lon1),' ',STR(?lat1),',',STR(?lon2),' ',STR(?lat2),')')AS?str).BIND(STRDT(?str,geo:wktLiteral)AS?l)}}AS%l\nWHERE{{INCLUDE%s}UNION{INCLUDE%l}.BIND(IF(BOUND(?l),\"f0f\",\"007f00\") as ?rgb)SERVICE wikibase:label{bd:serviceParam wikibase:language \"en\".}}# ", "context": "Map of flight connections between airports in India", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?layerLabel ?coords WHERE {\n  wd:Q25579 (p:P625/psv:P625/wikibase:geoLatitude) ?threshold.\n  ?item (wdt:P31/(wdt:P279*)) wd:Q1030034;\n    (p:P625/psv:P625) _:b12.\n  _:b12 wikibase:geoLatitude ?northernmostLat.\n  OPTIONAL { ?item wdt:P17 ?layer. }\n  ?item wdt:P625 ?coords.\n  FILTER(?northernmostLat > ?threshold)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],sv,ru,en\". }\n}", "context": "GLAMs institutions located north of Umeå", "target": ""}
{"query": "SELECT ?match ?seriesLabel ?time ?home_teamLabel ?home_goals ?away_teamLabel ?away_goals {\n# uncomment next line if you only want the match or matches specified within the { }  \n# VALUES ?match { wd:Q29877824 }\n  ?match wdt:P179 ?series . \n  ?series wdt:P3450 wd:Q82595 .\n  ?match wdt:P585 ?time .  \n  ?match p:P1923 ?p1923stm_home.\n  ?p1923stm_home ps:P1923 ?home_team .\n  ?p1923stm_home pq:P2868 wd:Q24633211 .\n  ?p1923stm_home pq:P1351 ?home_goals . \n  ?match p:P1923 ?p1923stm_away.\n  ?p1923stm_away ps:P1923 ?away_team .\n  ?p1923stm_away pq:P2868 wd:Q24633216 .\n  ?p1923stm_away pq:P1351 ?away_goals . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?time ?home_teamLabel", "context": "Bundesliga football results", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?shape\",  \"?rgb\"]}\nSELECT ?pLabel ?sideLabel ?shape ?rgb WHERE {\n  OPTIONAL { ?p wdt:P1622 ?side } # driving side\n  OPTIONAL { ?p wdt:P3896 ?shape } # geoshape\n  \n  BIND ( COALESCE(\n    IF(?side = wd:Q14565199, \"0066B4\", 1/0), # blue\n    IF(?side = wd:Q13196750, \"F5831A\", 1/0), # orange\n    \"000000\"\n  ) AS ?rgb )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "On which side of the road do cars drive?", "target": ""}
{"query": "#Ambassades Belgique\n#defaultView:Map\nSELECT ?item ?itemLabel ?opeLabel ?image ?villeLabel ?coord\nWHERE \n{\n  ?item wdt:P31 wd:Q3917681.\n  ?item wdt:P17 wd:Q31.\n  ?item wdt:P137 ?ope.\n  optional {?item wdt:P18 ?image }\n  optional {?item wdt:P131 ?ville}\n  optional {?item wdt:P625 ?coord}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".}\n}\nOrder by ?opeLabel\n", "context": "Map of the embassies to Belgium", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?level ?item ?relation ?linkTo ?itemLabel ?pic\n{\n  hint:Query hint:optimizer \"None\"\n  { BIND(0 AS ?level) BIND(wd:Q560197 AS ?item) } UNION\n  {\n    VALUES ?relation { wdt:P22 wdt:P25 } \n    { BIND(1 AS ?level) BIND(wd:Q560197 AS ?linkTo) }\n    UNION\n    { BIND(2 AS ?level) ?linkTo wdt:P22|wdt:P25 wd:Q560197 }\n    UNION\n    { BIND(3 AS ?level) ?linkTo (wdt:P22|wdt:P25)/(wdt:P22|wdt:P25) wd:Q560197 }\n    UNION\n    { BIND(4 AS ?level) ?linkTo (wdt:P22|wdt:P25)/(wdt:P22|wdt:P25)/(wdt:P22|wdt:P25) wd:Q560197 }\n    ?item ?relation ?linkTo\n  }\n  OPTIONAL { ?item wdt:P18 ?pic }\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\"}\n}\nORDER BY ?level", "context": "Graph showing direct descendants of Prithwiraj Kapoor", "target": ""}
{"query": "#defaultView:BarChart\nSELECT (REPLACE(REPLACE(?nobelPrizeLabel_, \"^Nobel Prize in \", \"\"), \"^Nobel (.*) Prize$\", \"$1\") AS ?nobelPrizeLabel) (COUNT(DISTINCT ?winner) AS ?winners) (COALESCE(?genderLabel_, \"N/A\"@en) AS ?genderLabel) WHERE {\n  ?nobelPrize wdt:P361 wd:Q7191.\n  ?winner p:P166 [\n            ps:P166 ?nobelPrize;\n            pq:P585 ?date\n          ].\n  OPTIONAL { ?winner wdt:P21 ?gender. } # optional because organizations can win the Peace Prize\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?nobelPrize rdfs:label ?nobelPrizeLabel_.\n    ?gender rdfs:label ?genderLabel_.\n  }\n}\nGROUP BY ?nobelPrizeLabel_ ?genderLabel_", "context": "Nobel laureates by award type and gender", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?communeLabel ?coords ?image WHERE {\n  wd:Q2789507 wdt:P527 ?commune .\n  ?item wdt:P31/wdt:P279* wd:Q751876 ;\n        wdt:P131 ?commune .\n  OPTIONAL { ?item wdt:P625 ?coords }\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?communeLabel ?itemLabel", "context": "Map of châteaux in the French world heritage site \"the Loire Valley between Sully-sur-Loire and Chalonnes\"", "target": ""}
{"query": "#defaultView:Timeline\nselect ?flight ?flightLabel ?image ?start ?end where {\n  ?flight p:P39 ?pstmt.\n  {?pstmt ps:P39 wd:Q55018634}\n  UNION\n  {?pstmt ps:P39 wd:Q221922}\n  ?pstmt pq:P580 ?start . OPTIONAL {?pstmt pq:P582 ?end_tmp}.\n  BIND(IF(BOUND(?end_tmp),?end_tmp,NOW()) as ?end)\n  OPTIONAL { ?flight wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Timeline of the chief ministers of Haryana and Maharashtra", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT  ?lang (count(?item) as ?nr)\nWHERE\n{\n\t?item wdt:P1260 ?KsamURI.\n\t?item schema:description ?itemDescription\n    BIND (lang(?itemDescription) AS ?lang)\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"sv\"}\n\n} group by ?lang\n", "context": "Language representation of descriptions in Wikidata for objects connected to Ksamsök", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?team ?teamLabel ?image WHERE {\n  ?item wdt:P54 ?team. ?team wdt:P118 wd:Q16056350.\n  OPTIONAL { ?item wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Cricketers who have ever played on an Indian Super League team", "target": ""}
{"query": "#defaultView:TreeMap\nSELECT ?state ?stateLabel ?item ?itemLabel{\n  ?item p:P39 ?pstmt.\n  ?pstmt ps:P39 wd:Q42509248 ; pq:P768 ?dst. ?dst wdt:P131+ ?state. ?state wdt:P31/wdt:P279 wd:Q131541.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Tree map of members of the 16th Lok Sabha, grouped by state represented", "target": ""}
{"query": "#defaultView:Map\nSELECT ?bus_geltokia ?bus_geltokiaLabel ?koordenatuak WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es,en\". }\n  ?bus_geltokia wdt:P31 wd:Q953806.\n  ?bus_geltokia wdt:P131 wd:Q93366.\n  ?bus_geltokia wdt:P625 ?koordenatuak.\n}", "context": "Map of bus stops in Biscay", "target": ""}
{"query": "select distinct ?property ?propertyLabel (count(distinct ?item) as ?count) \nwhere { ?item ?property wd:Q24238356 . \n        SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n} group by ?propertyLabel ?property order by desc(?count)", "context": "Maintenance query: items which have a value of \"unknown\" (Q24238356) rather than the \"unknown\" special value", "target": ""}
{"query": "#defaultView:Map\nSELECT ?city ?cityLabel ?pop ?coord ?dist ?img\nWHERE {\n  ?city wdt:P31 wd:Q515.\n  ?city wdt:P1082 ?pop. \n  ?city wdt:P625 ?coord.\n  optional {?city wdt:P18 ?img}\n  filter (?pop > 200000).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n  BIND(geof:distance(\"Point(0 90)\"^^geo:wktLiteral, ?coord) as ?dist) \n}\norder by ?dist\nLIMIT 100", "context": "100 cities closest to the North pole with more than 200k inhabitants", "target": ""}
{"query": "PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\n#defaultView:BubbleChart\nSELECT DISTINCT ?coactorLabel (COUNT(?film) as ?films) WHERE {\n  ?film wdt:P31 wd:Q11424 ; wdt:P161 wd:Q9535 ; wdt:P161 ?coactor.\n  FILTER(?coactor != wd:Q9535)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?coactorLabel", "context": "Actors with whom Shah Rukh Khan collaborated, in a bubble chart showing how many times it happened", "target": ""}
{"query": "select ?WikiTree ?person ?personLabel ?statedinLabel ?refURL ?named ?cite ?page \n{\n\n   ?person wdt:P2949 ?WikiTreeID. #personer kopplade till SKBL\n#   ?person wdt:P27 wd:Q34.\n \n  BIND (URI(CONCAT(\"https://www.wikitree.com/wiki/\",?WikiTreeID)) AS ?WikiTree)\n   ?person ?prop ?statement. #all props\n   {?statement prov:wasDerivedFrom ?ref}\n   OPTIONAL {?ref pr:P854 ?refURL} \n   {?ref pr:P248 ?statedin} \n   OPTIONAL {?ref pr:P1810 ?named} \n   OPTIONAL {?ref pr:P1476 ?named} \n   OPTIONAL {?ref pr:P1683 ?cite} \n   OPTIONAL {?ref pr:P304 ?page} \n   minus { ?ref pr:P248 wd:Q379406}\n   {\n      { ?ref pr:P248 wd:Q10478639} #Deathbook\n   UNION\n      { ?ref pr:P248 wd:Q10501038} #Birthbook\n   UNION\n     { ?ref pr:P248 wd:Q10527384} #Householdrecords\n   }\n   FILTER (CONTAINS(str(?refURL),'riksarkivet')) .  \n   ?property wikibase:claim ?prop .\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,sv\"}\n}\nGroup by ?person ?personLabel ?statedin ?statedinLabel ?refURL ?named ?SPA ?cite ?page ?WikiTree\nOrder by ?personLabel ?statedinLabel \n", "context": "Persons in the genealogy family tree WikiTree were WIkidata has a source from the Swedish National Archives Birth/Death records", "target": ""}
{"query": "#defaultView:Map{\"hide\": \"?rgb\"}\nSELECT ?d ?dLabel ?shape ?ratio ?rgb WHERE {\n  ?d wdt:P31 wd:Q1149652; p:P6499 ?litpopstmt ; p:P6498 ?illitpopstmt ; wdt:P3896 ?shape.\n  ?litpopstmt ps:P6499 ?litpop ; pq:P585 \"+2011-01-01T00:00:00Z\"^^xsd:dateTime.\n  ?illitpopstmt ps:P6498 ?illitpop ; pq:P585 \"+2011-01-01T00:00:00Z\"^^xsd:dateTime.\n  BIND(xsd:integer(?illitpop)/xsd:integer(?litpop) AS ?ratio)\n  BIND(IF(?ratio < 0.2, \"00008b\", IF(?ratio < 0.4, \"006400\", IF(?ratio < 0.6, \"008000\", IF(?ratio < 0.8, \"32cd32\", IF(?ratio < 1, \"7fff00\", IF(?ratio < 1.2, \"ffa500\", IF(?ratio < 1.4, \"ff6347\", \"ff0000\"))))))) AS ?rgb)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Map of districts of India by ratio of illiterate to literate population in 2011", "target": ""}
{"query": "SELECT ?tv_episode ?tv_episodeLabel ?ja_title ?romaji ?date ?placeLabel ?seasonLabel ?series WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?tv_episode wdt:P31 wd:Q21191270;\n    wdt:P179 wd:Q5363072.\n  OPTIONAL {\n    ?tv_episode p:P1476 ?title.\n    ?title ps:P1476 ?ja_title;\n      pq:P2125 ?romaji.\n  }\n  OPTIONAL {\n    ?tv_episode p:P577 ?publication_date_node.\n    ?publication_date_node ps:P577 ?date;\n      pq:P291 ?place.\n  }\n  OPTIONAL {\n    ?tv_episode p:P179 ?qual.\n    ?qual pq:P1545 ?series.\n  }\n  OPTIONAL { ?tv_episode wdt:P4908 ?season. }\n}\nORDER BY ?date", "context": "List of episodes of Detective Conan's Case Closed, with date and place of publication and series and season number, sorted by date of publication", "target": ""}
{"query": "SELECT ?parentLabel ?parentStateLabel ?childLabel ?childStateLabel ?childDOB WHERE {\n  ?parent wdt:P40 ?child.\n  ?child wdt:P569 ?childDOB.\n\n  ?parent wdt:P39 ?parentOffice.\n  ?parentOffice wdt:P279 wd:Q889821.\n  ?parentOffice wdt:P361 ?parentGovt.\n  ?parentGovt wdt:P1001 ?parentState.\n  ?parentState wdt:P31 wd:Q35657.\n\n  ?child wdt:P39 ?childOffice.\n  ?childOffice wdt:P279 wd:Q889821.\n  ?childOffice wdt:P361 ?childGovt.\n  ?childGovt wdt:P1001 ?childState.\n  ?childState wdt:P31 wd:Q35657.\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n} ORDER BY ?childDOB", "context": "Parent and child who both had a governor mandate in the USA", "target": ""}
{"query": "SELECT ?name ?nameLabel ?dod ?image WHERE {\n ?name wdt:P27 wd:Q31 .\n  ?name wdt:P570 ?dod .\n  OPTIONAL {?name wdt:P18 ?image} .\n  FILTER (YEAR(?dod) = 1949)  .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n                       }", "context": "Belgian artists whose work will be in public domain starting in 2020", "target": ""}
{"query": "select distinct ?item (year(?release) AS ?released) ?name ?itemDescription\n(GROUP_CONCAT(distinct ?areaLabel;separator=\"; \") as ?setting) ?period \n(GROUP_CONCAT(distinct ?awardLabel;separator=\"; \") as ?awards) \nwhere { \n  ?item wdt:P144* wd:Q213019 . \n  optional { ?item wdt:P840 ?area }\n  optional { ?item wdt:P2408 ?date }  \n  optional { ?item wdt:P166 ?award }  \n  optional { ?item wdt:P577|wdt:P1191|wdt:P580 ?release }  \n  SERVICE wikibase:label\n   { bd:serviceParam wikibase:language \"en\".\n     ?item rdfs:label ?name .\n     ?area rdfs:label ?areaLabel .\n     ?award rdfs:label ?awardLabel .\n     ?date rdfs:label ?period . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?item ?name ?period ?itemDescription ?release\norder by ?released", "context": "Adaptations of The War of the Worlds", "target": ""}
{"query": "SELECT ?book  ?bookLabel ?url ?person ?personLabel ?personDescription ?page ?quote WHERE {\n  {\n    BIND( wd:Q61765464 AS ?book)\n    ?person wdt:P119 wd:Q657118;\n      p:P119 ?statement.\n    { ?statement prov:wasDerivedFrom ?ref. }\n    { ?ref pr:P248 wd:Q61765464. }\n    OPTIONAL { ?book wdt:P953 ?url. }\n    OPTIONAL { ?ref pr:P304 ?page. }\n    OPTIONAL { ?ref pr:P1683 ?quote. }\n    FILTER(STRSTARTS(STR(?person), \"http://www.wikidata.org/entity/Q\"))\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,sv\". }\n}", "context": "Graves at the Stockholm church Riddarholmskyrkan who use book Riddarholmskyrkan - inventories and graves (Q61765464) as a source", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coordinates\", \"?line\", \"?rgb\"]}\nSELECT DISTINCT ?cds ?line ?item ?itemLabel ?layer\nWITH {\nSELECT ?item ?cds ?lat1 ?lon1 ?layer WHERE {\n?item wdt:P17 wd:Q668; wdt:P625 ?cds ; wdt:P190 [].\n?item p:P625/psv:P625/wikibase:geoLatitude ?lat1 .\n?item p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?item rdfs:label ?nl. FILTER(LANG(?nl)=\"en\")\n  BIND(?nl as ?layer)\n}\n} AS %stations\nWITH {\nSELECT ?line ?layer WHERE {\nINCLUDE %stations .\n  ?item wdt:P190 ?ns.\n  ?item rdfs:label ?nl. FILTER(LANG(?nl)=\"en\")\n?ns p:P625/psv:P625/wikibase:geoLatitude ?lat2 .\n?ns p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\nBIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) .\nBIND(STRDT(?str, geo:wktLiteral) AS ?line) \nBIND(?nl as ?layer)\n}\n} AS %lines\nWHERE {\n{ INCLUDE %stations } UNION { INCLUDE %lines  } .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} #", "context": "Map of sisterly relationships with cities in India", "target": ""}
{"query": "#defaultView:Map\nSELECT ?language ?languageLabel ?coord WHERE {\n  ?language wdt:P31 wd:Q34770 ; wdt:P625 ?coord .\n  MINUS { ?l a ontolex:LexicalEntry ; dct:language ?language }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of the languages without any Wikidata Lexemes yet", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT * WHERE {\n  ?item wdt:P31 wd:Q3305213 .\n  ?item wdt:P136 wd:Q134307 .\n  ?item wdt:P180 wd:Q146 .\n  ?item wdt:P18 ?img .\n}\nlimit 100", "context": "Paintings of people with cats", "target": ""}
{"query": "SELECT ?firstLetter (COUNT(?form) AS ?count ) WHERE {\n  ?lexeme a ontolex:LexicalEntry ; dct:language ?language ; ontolex:lexicalForm ?form .\n  ?form ontolex:representation ?word .\n  ?language wdt:P218 'en' .\n  BIND ( SUBSTR(UCASE(STR(?word)), 1, 1) AS ?firstLetter )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?firstLetter\nORDER BY DESC(?count)", "context": "Number of English Lexemes in Wikidata by first letter", "target": ""}
{"query": "#defaultView:TreeMap\nSELECT ?prize ?prizeLabel ?winner ?winnerLabel {\n  ?winner p:P166 ?pstmt. ?pstmt ps:P166 ?prize. ?prize wdt:P31 wd:Q1350697.\n  ?pstmt pq:P585 ?time. BIND(YEAR(?time) as ?year)\n  FILTER(?year >= 1990)\n  FILTER(?year < 2000)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Tree map of 1990s National Film Award winners, sorted by specific award", "target": ""}
{"query": "SELECT ?disputed_area ?disputed_areaLabel ?article ?alternate_names ?part_ofLabel ?countryLabel  ?located_in_the_administrative_territorial_entityLabel  ?territory_claimed_byLabel  ?image ?locator_map_image ?website ?coordinate_location ?geoshape WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?disputed_area wdt:P31 wd:Q15239622.\n  \n  OPTIONAL { \n    ?article schema:about ?disputed_area .\n    ?article schema:isPartOf <https://en.wikipedia.org/>.\n           }\n  OPTIONAL { ?disputed_area wdt:P361 ?part_of. }\n  OPTIONAL { ?disputed_area wdt:P4970 ?alternate_names. }\n  OPTIONAL { ?disputed_area wdt:P361 ?part_of. }\n  OPTIONAL { ?disputed_area wdt:P17 ?country. }\n  OPTIONAL { ?disputed_area wdt:P131 ?located_in_the_administrative_territorial_entity. }\n  OPTIONAL { ?disputed_area wdt:P1336 ?territory_claimed_by. }\n  OPTIONAL { ?disputed_area wdt:P18 ?image. }\n  OPTIONAL { ?disputed_area wdt:P242 ?locator_map_image. }\n  OPTIONAL { ?disputed_area wdt:P856 ?website. }\n  OPTIONAL { ?disputed_area wdt:P625 ?coordinate_location. }\n  OPTIONAL { ?disputed_area wdt:P3896 ?geoshape. }\n}", "context": "Map of world's disputed areas", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?i ?iLabel ?j ?jLabel {\n  ?i wdt:P31 ?level. ?level wdt:P279 wd:Q877127; wdt:P279 wd:Q10864048.\n  ?j wdt:P31 ?level. ?level wdt:P279 wd:Q877127; wdt:P279 wd:Q10864048.\n  ?i wdt:P47 ?j.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"my,en\". }\n}", "context": "Graph of shared borders between regions of Myanmar", "target": ""}
{"query": "SELECT DISTINCT ?l ?lemma ?word WHERE {\n  ?l a ontolex:LexicalEntry ; dct:language wd:Q188 ; wikibase:lemma ?lemma ; ontolex:lexicalForm ?form .\n  ?form ontolex:representation ?word ; wikibase:grammaticalFeature wd:Q146786 .\n  FILTER regex (?word, \"[^e]n$\").\n}", "context": "Plurals in German ending with only \"n\" (not \"en\")", "target": ""}
{"query": "# works in the Women's + Gender History Digital Humanities list \n# (bit.ly/2RVz7ks)\n\nSELECT ?item ?itemLabel ?official_website WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P1343 wd:Q76510930.\n  OPTIONAL { ?item wdt:P856 ?official_website. }\n}\nORDER BY ASC(?itemLabel)", "context": "Women's + Gender History Digital Humanities list", "target": ""}
{"query": "prefix dct: <http://purl.org/dc/terms/>\nprefix ontolex: <http://www.w3.org/ns/lemon/ontolex#>\n#defaultView:BubbleChart\nSELECT ?lang ?langLabel (COUNT(?lexeme) as ?lexemes) {\n  ?lexeme dct:language ?lang.\n  wd:Q668 p:P2936/ps:P2936 ?lang.\n  FILTER(?lang != wd:Q1860)\n  FILTER(?lang != wd:Q150)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?lang ?langLabel\n\n", "context": "Number of lexemes on Wikidata in Indian languages", "target": ""}
{"query": "SELECT ?collecting ?collectingLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?collecting wdt:P195 wd:Q2087788.\n}\n", "context": "Collections of the National Gallery of Armenia imported into Wikidata", "target": ""}
{"query": "SELECT ?director (MIN(?date) AS ?first) (MAX(?date) AS ?last) (MAX(?date)-MIN(?date) AS ?span) WHERE {\n  ?movie wdt:P57 ?director ; wdt:P577 ?date .\n}\nGROUP BY ?director\nHAVING (?span > 20000 )\nORDER BY DESC(?span)", "context": "Movie directors having the longest carrier", "target": ""}
{"query": "SELECT ?id ?idLabel ?idDescription ?new {\n  ?id wikibase:directClaim ?pid .\n# ?id wikibase:propertyType wikibase:ExternalId .\n  BIND(Replace(STR(?id),\"http://www.wikidata.org/entity/P\",\" \") as ?new)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,sv\". }\n}\nORDER BY DESC(xsd:integer(?new))", "context": "Wikidata properties", "target": ""}
{"query": "SELECT ?id ?idLabel ?idDescription ?new{\n  ?id wikibase:directClaim ?pid .\n ?id wikibase:propertyType wikibase:ExternalId .\n  BIND(Replace(STR(?id),\"http://www.wikidata.org/entity/P\",\" \") as ?new)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,sv\". }\n}\nORDER BY DESC(xsd:integer(?new))", "context": "Wikidata External identifiers", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?loc ?c ?cLabel ?pop WHERE {\n\n  # comment out the next line to hide WD's idea of \"big\"\n  ?item wdt:P31 wd:Q1549591 . # big city 'a population of more than 100,000 inhabitants'\n  ?item wdt:P625 ?loc .\n  ?item wdt:P17 ?c . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n  ?item wdt:P1082 ?pop . \n  # FILTER( ?pop > 100000) # actually biggish cities\n}\nORDER BY DESC (?pop)\n", "context": "Map of locations of \"big cities\"", "target": ""}
{"query": "SELECT ?seat ?seatLabel ?party2017Label ?party2019Label ?mp2017 ?mp2017Label ?mp2019 ?mp2019Label ?article WHERE {\n  ?mp2017 p:P39 ?mp2017stmt.\n  ?mp2017stmt pq:P4100 ?party2017;\n    pq:P2715 wd:Q25052149;\n    pq:P768 ?seat.\n  ?mp2019 p:P39 ?mp2019stmt.\n  ?mp2019stmt pq:P4100 ?party2019;\n    pq:P2715 wd:Q30173038;\n    pq:P768 ?seat.\n  FILTER(?party2019 != ?party2017)\n  MINUS {\n    VALUES ?LabourVariant {wd:Q9630 wd:Q6467393}\n    VALUES ?LabourVariant1 {wd:Q9630 wd:Q6467393}\n    ?mp2017stmt pq:P4100 ?LabourVariant . ?mp2019stmt pq:P4100 ?LabourVariant1\n  }.\n\n  OPTIONAL {\n    ?article schema:about ?mp2019 .\n    ?article schema:isPartOf <https://en.wikipedia.org/>.\n    BIND('Y' AS ?art) \n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY (?seatLabel)", "context": "Seats which changed party in the 2019 UK General Election", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription ?SwedishNationalmuseum  WHERE {\n?item wdt:P2538 ?NatMusID.\nminus {?item wdt:P7704 ?EropeanaentityID}\nBIND(URI(CONCAT(\"http://data.europeana.eu/\",?EropeanaentityID)) AS ?Eropeanaentity)\nBIND(URI(CONCAT(\"http://collection.nationalmuseum.se/eMuseumPlus?service=ExternalInterface&module=artist&objectId=\",?NatMusID)) AS ?SwedishNationalmuseum)\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,nl,sv\". }\n} order by ?itemLabel", "context": "artists in Wikidata mapped to the Swedish National museums but are not an entity in Europeana", "target": ""}
{"query": "#defaultView:Map\nselect ?subj ?coord  ?img  ?pic ?article ?subjLabel ?subjDescription ?personLabel  ?Europeana\n{\n   ?person wdt:P7704 ?EE.\n   ?person wdt:P31 wd:Q5.  # personer\n   ?subj wdt:P138 ?person  # P138 uppkallad efter\n   OPTIONAL {?subj wdt:P625 ?coord}\n   OPTIONAL {?subj wdt:P18 ?img}\n   BIND (URI(CONCAT(\"http://data.europeana.eu/\",?EE)) AS ?Europeana)\n   OPTIONAL{ ?person wdt:P18 ?pic .} \t\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\"}\n }", "context": "Map", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?location ?locationLabel (COUNT(?item) AS ?count) WHERE {\n  ?item wdt:P1071 ?location .\n  ?item wdt:P31 wd:Q3305213 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  } GROUP BY ?location ?locationLabel \nORDER BY DESC (?count)\nLIMIT 250", "context": "Locations where paintings were made", "target": ""}
{"query": "# Swedish citizens who died in 1949\nSELECT ?name ?nameLabel (GROUP_CONCAT(DISTINCT ?profLabel; SEPARATOR = \", \") AS ?professions) ?image ?linkcount WHERE {\n  ?name wdt:P27 wd:Q34;\n    wdt:P570 ?dod;\n    wikibase:sitelinks ?linkcount .\n  OPTIONAL { ?name wdt:P18 ?image. }\n  OPTIONAL { ?name wdt:P106 ?prof.}\n  FILTER((YEAR(?dod)) = 1949 )\n    SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"sv,en\".\n    ?name rdfs:label ?nameLabel.\n    ?prof rdfs:label ?profLabel.\n  }\n}\nGROUP BY ?name ?nameLabel ?linkcount ?image\nORDER BY DESC (?linkcount)", "context": "Swedish citizens who died in 1949 and whose works will enter the public domain on 1 January 2020 with profession and image, ordered by number of Wikimedia articles", "target": ""}
{"query": "SELECT ?metro ?metroLabel ?locationLabel ?openingDate\nWHERE {\n  ?metro wdt:P31 wd:Q5503.\n  ?metro wdt:P131 ?location.\n  ?metro wdt:P1619 ?openingDate.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\norder by ?openingDate\nlimit 20", "context": "Oldest subways", "target": ""}
{"query": "# popes who were children of other popes\nSELECT ?parent ?parentLabel ?child ?childLabel WHERE {\n  ?parent wdt:P40 ?child;\n          wdt:P39 wd:Q19546.\n  ?child wdt:P39 wd:Q19546.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de\". }\n}", "context": "Popes who were children of other popes", "target": ""}
{"query": "# colorful state flags\n#defaultView:ImageGrid\nSELECT ?state ?stateLabel (COUNT(?color) AS ?count) ?image WITH {\n  SELECT DISTINCT ?state WHERE {\n    ?state wdt:P31/wdt:P279* wd:Q3624078;\n           p:P463 ?memberOfStatement.\n    ?memberOfStatement a wikibase:BestRank;\n                         ps:P463 wd:Q1065.\n    MINUS { ?memberOfStatement pq:P582 ?endTime. }\n    MINUS { ?state wdt:P576|wdt:P582 ?end. }\n  }\n} AS %states WHERE {\n  INCLUDE %states.\n  ?state wdt:P163 ?flag.\n  ?flag wdt:P462 ?color.\n  OPTIONAL { ?flag wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?state ?stateLabel ?image\nORDER BY DESC(?count)\nLIMIT 25", "context": "Colorful flags", "target": ""}
{"query": "SELECT ?horse ?birthdate ?deathdate ?age ?horseLabel ?horseDescription\n        \n        WHERE {\n  ?horse wdt:P31/wdt:P279* wd:Q726 .\n  ?horse wdt:P569 ?birthdate .\n?horse wdt:P570 ?deathdate .\nBIND(?deathdate - ?birthdate AS ?age)\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\" . }\n}\n\nORDER BY DESC (?age)", "context": "Horses sorted by age of death", "target": ""}
{"query": "#defaultView:Map\nselect ?album ?performer ?performerLabel ?albumLabel ?location where {\n  ?album wdt:P31/wdt:P279* wd:Q482994;\n         wdt:P175 ?performer;\n         wdt:P1259 ?location.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Locations depicted on album covers", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?line ?coords ?length WHERE {\n  {\n    ?item wdt:P31 wd:Q80830021.\n    OPTIONAL {\n      ?item p:P625 ?coords_stmt_west.\n      ?coords_stmt_west pq:P518 wd:Q80981719;\n        ps:P625 ?coords_west;\n        (psv:P625/wikibase:geoLatitude) ?lat1;\n        (psv:P625/wikibase:geoLongitude) ?lon1.\n    }\n    OPTIONAL {\n      ?item p:P625 ?coords_stmt_east.\n      ?coords_stmt_east pq:P518 wd:Q80982256;\n        ps:P625 ?coords_east; \n        (psv:P625/wikibase:geoLatitude) ?lat2;\n        (psv:P625/wikibase:geoLongitude) ?lon2.\n    }\n    OPTIONAL { ?item wdt:P2043 ?length. }\n    BIND(CONCAT(\"LINESTRING (\", STR(?lon1), \" \", STR(?lat1), \",\", STR(?lon2), \" \", STR(?lat2), \")\") AS ?str)\n    BIND(STRDT(?str, geo:wktLiteral) AS ?line)\n  }\n  UNION\n  {\n    ?item wdt:P31 wd:Q80962689; \n      wdt:P625 ?coords.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} \nORDER BY (?coords_west)", "context": "Forts and heritage-scheduled sections of the Antonine Wall in Scotland", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?languageLabel (COUNT(DISTINCT ?l) AS ?count) WHERE {\n  ?l a ontolex:LexicalEntry ; ontolex:lexicalForm ?form ; dct:language ?language .\n  ?form wdt:P443 ?file .\n  FILTER regex (str(?file), \"^http://commons.wikimedia.org/wiki/Special:FilePath/LL-\").\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?languageLabel", "context": "Number of Lexemes including recordings from Lingua Libre by language", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?occ ?occLabel (COUNT(?occLabel) AS ?occ_count) WHERE { \n  BIND(wd:Q72752496 as ?album)\n  \n  #VALUES ?school {}\n  #VALUES ?university {}\n  #VALUES ?religion {}\n  #VALUES ?health {}\n  #VALUES ?art {}\n  #VALUES ?science {}\n  \n  ?album wdt:P767 ?contributor.\n  ?contributor wdt:P106 ?occ.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n} \nGROUP BY ?occ ?occLabel\nORDER BY DESC(?occ_count)", "context": "Contributors to Jacob Heyblocqs vriendenboek and their occupations", "target": ""}
{"query": "#defaultView:BarChart\nSELECT  ?coauthor_countryLabel (COUNT(?author1) AS ?numberOfCoAuthors)\nWITH {\n  SELECT (COUNT(?work) AS ?count) ?author1 ?author2 WHERE {\n    # Find co-authors\n    ?searched_author wdt:P496  \"0000-0002-6778-0887\".\n    ?work wdt:P50 ?searched_author, ?author1, ?author2 .\n\n  }\n  GROUP BY ?author1 ?author2\n  ORDER BY DESC(?count)\n                    \n  # Limit the size of the graph, to avoid overburdening the browser\n  LIMIT 1000\n} AS %authors\nWITH {\n  SELECT ?author1 ?author2 ?coauthor_country WHERE {\n    INCLUDE %authors\n    \n    # Exclude self-links\n    FILTER (?author1 != ?author2)\n    \n   ?author1 wdt:P27 ?coauthor_country.\n  }\n} AS %result\nWHERE {\n  INCLUDE %result\n  # Label the results \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,fr,de,ru,es,zh,jp\". }\n}GROUP BY ?coauthor_countryLabel\nORDER BY DESC(?numberOfCoAuthors)", "context": "Number of Co-Authors by Country using ORCID to identify the \"searched author\"", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?cd\",\"?l\",\"?rgb\"]}\nSELECT DISTINCT ?cd ?l ?i ?iLabel ?ct ?ctLabel ?rgb ?o ?oLabel WITH{SELECT ?i ?cd ?lat1 ?lon1 (sample(?op)as?op)WHERE{{VALUES ?i {wd:Q601091}}UNION{wd:Q601091 wdt:P521 ?i} ?i wdt:P625 ?cd;p:P625/psv:P625 ?y.?y wikibase:geoLatitude ?lat1;wikibase:geoLongitude ?lon1.}GROUP BY ?i ?cd ?lat1 ?lon1}AS%s\nWITH{SELECT ?l ?o ?t WHERE{INCLUDE %s.wd:Q601091 p:P521 ?nss; p:P625/psv:P625 ?y2.?y2 wikibase:geoLatitude ?lati1;wikibase:geoLongitude ?long1 .?nss ps:P521 ?ns.?ns p:P625/psv:P625 ?z.?z wikibase:geoLatitude ?lat2;wikibase:geoLongitude ?lon2.OPTIONAL{?nss pq:P137 ?o}FILTER(STR(?i)< STR(?ns)).BIND(CONCAT('LINESTRING(',STR(?long1),' ',STR(?lati1),',',STR(?lon2),' ',STR(?lat2),')')AS?str).BIND(STRDT(?str,geo:wktLiteral)AS?l)}}AS%l\nWHERE{{INCLUDE%s}UNION{INCLUDE%l}.BIND(IF(BOUND(?l),IF(BOUND(?o),\"00f\",\"f00\"),\"007f00\") as ?rgb)SERVICE wikibase:label{bd:serviceParam wikibase:language \"zh,zh-cn,en\".}}# \n", "context": "Map of destinations from Wuhan Tianhe International Airport", "target": ""}
{"query": "# Find 50 philosophers for which DBpedia and Wikidata state different date of birth. Change DBpedia type to get for other occupations. \n\nPREFIX owl: <http://www.w3.org/2002/07/owl#>\nPREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\nPREFIX dbo: <http://dbpedia.org/ontology/>\nPREFIX dbp: <http://dbpedia.org/property/>\n\nSELECT DISTINCT ?personDBP ?personWD ?personWDLabel ?BirthDateDBP ?BirthDateWD\nWHERE {\n\nSERVICE <http://dbpedia.org/sparql> {\n?personDBP a dbo:Philosopher ;\ndbo:birthDate ?BirthDate_DBP;\nowl:sameAs ?personWD .\nFILTER regex (?personWD, \"wikidata.org\") \nBIND (xsd:date(?BirthDate_DBP) AS ?BirthDateDBP)\n\n}\n \nSERVICE <https://query.wikidata.org/sparql>  {\n?personWD wdt:P569 ?BirthDate_WD .\nBIND (xsd:date(?BirthDate_WD) AS ?BirthDateWD)  \n }\nFILTER (?BirthDateDBP != ?BirthDateWD)\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?BirthDateDBP)\nLIMIT 50\n", "context": "50 philosophers for which DBpedia and Wikidata state different date of birth", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?geo\",\"?rgb\"]}\nSELECT ?geo ?causes ?countryLabel ?rgb ?population (CONCAT(str(year(?datum)),\"-\",CONCAT(IF(month(?datum)<10,\"0\",\"\"),str(month(?datum))),\"-\",CONCAT(IF(day(?datum)<10,\"0\",\"\"),str(day(?datum)))) AS ?when) {\n  ?item wdt:P1269 wd:Q81068910 .\n  ?item wdt:P17 ?country .\n  ?country wdt:P1082 ?pop.\n  ?country wdt:P3896 ?geo .\n  MINUS { ?item wdt:P31 wd:Q186117 . }\n  OPTIONAL { ?item p:P1603 ?p1603stm .\n             ?p1603stm ps:P1603 ?nr_causes .\n             ?p1603stm wikibase:rank ?rank . FILTER(?rank=wikibase:PreferredRank)\n             OPTIONAL{?p1603stm pq:P585 ?datum }\n           }\n  BIND(IF(?nr_causes>=100,'FF0000',IF(?nr_causes>=10,'FF3333',IF(?nr_causes>=5,'FF6666',IF(?nr_causes>=2,'FF9999',IF(?nr_causes>0,'FFCCCC','FFFFFF'))))) AS ?rgb)\n  BIND(CONCAT(\"Number of cases coronavirus reported: \", str(?nr_causes)) AS ?causes)\n  BIND((round(?pop/100000)/10) AS ?popM )\n  BIND(CONCAT(\"Population (million): \", str(round(?popM))) AS ?population)\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n\n", "context": "Number of cases coronavirus reported per country", "target": ""}
{"query": "SELECT ?artistName ?albumTitle ?releaseDate\n\nWHERE {\n  ?artist wdt:P136 wd:Q851213 ;   # artist has genre of EDM\n  rdfs:label ?artistName . \n  FILTER ( lang(?artistName) = \"en\" )\n  \n  ?album wdt:P31 wd:Q482994 .\n  \n  ?album wdt:P175 ?artist ;    # album has performer\n  rdfs:label ?albumTitle ;\n         p:P577 ?releaseDateThing .# release date as (claim about the date?)\n  \n  ?releaseDateThing ps:P577 ?releaseDate .  # release date as ISO 8601\n  FILTER (?releaseDate < \"1980-01-01T00:00:00Z\"^^xsd:dateTime)  \n  FILTER ( lang(?albumTitle) = \"en\" )\n}\n", "context": "Release date of albums from before 1980 by artists of the EDM genre", "target": ""}
{"query": "SELECT DISTINCT ?book ?bookLabel\n{\n  VALUES ?booktype {wd:Q3331189 wd:Q571}\n  VALUES ?topic {wd:Q17884 wd:Q165371 wd:Q6517455 wd:Q33487 wd:Q51415\n                           wd:Q718858 wd:Q6636 wd:Q17625913 wd:Q189125\n                           wd:Q7458838 wd:Q43200 wd:Q486680 wd:Q206621\n                           wd:Q337084 wd:Q4162339 wd:Q1097630\n                }\n  ?book wdt:P31/wdt:P279* ?booktype;\n          wdt:P921 ?topic.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Books related to LGBTI+ topics", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?i ?iLabel ?layer ?c {\n  ?item wdt:P31/wdt:P279* wd:Q4260475 ; wdt:P31 ?thing ; wdt:P17 wd:Q668 ; wdt:P131/wdt:P131* wd:Q28769140 ; wdt:P625 ?c.\n  ?thing rdfs:label ?tl. FILTER(LANG(?tl)=\"en\")\n  BIND(?tl as ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Map of medical facilities in Kalimpong district, India, color-coded by type", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?marriage ?time ?countryLabel ?image\n{\n   wd:Q17422 wdt:P527 ?marriage.\n   ?marriage wdt:P580 ?time.\n   OPTIONAL{ ?marriage  wdt:P17 ?country.\n             ?country  wdt:P18 ?image.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Timeline of same-sex marriage legalization in various countries", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?date ?count ?measureLabel\nWHERE {\n  BIND(wd:Q81068910 AS ?outbreak).\n  {\n    BIND(wd:Q166231 AS ?measure).\n    ?outbreak p:P1603 ?cases.\n    ?cases prov:wasDerivedFrom/pr:P248/wdt:P50 wd:Q7817.\n    ?cases pqv:P585/wikibase:timeValue ?date.\n    ?cases psv:P1603/wikibase:quantityAmount ?count.\n    ?cases psv:P1603/wikibase:quantityAmount ?infection_count.\n  } UNION {\n    BIND(wd:Q4 AS ?measure).\n    ?outbreak p:P1120 ?deaths.\n    ?deaths prov:wasDerivedFrom/pr:P248/wdt:P50 wd:Q7817.\n    ?deaths pqv:P585/wikibase:timeValue ?date.\n    ?deaths psv:P1120/wikibase:quantityAmount ?count.\n    ?deaths psv:P1120/wikibase:quantityAmount ?death_count.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ASC(?date) DESC(?measure)", "context": "Chart of the number of infections and deaths casued since the outbreak of novel coronavirus, as reported by the World Health Organisation", "target": ""}
{"query": "PREFIX ag: <http://data.ordnancesurvey.co.uk/ontology/admingeo/>\nPREFIX sr: <http://data.ordnancesurvey.co.uk/ontology/spatialrelations/>\nSELECT ?item ?itemLabel ?adjacentItem ?adjacentItemLabel\nWITH\n{\n  SELECT ?item ?adjacent\n  WHERE\n  {\n    ?item wdt:P31 wd:Q27971968.\n    ?item wdt:P3120 ?TOID.\n    BIND(SUBSTR(?TOID,12,5) AS ?unitID)\n    SERVICE <http://data.ordnancesurvey.co.uk/datasets/os-linked-data/apis/sparql>\n    {\n      [] ag:hasUnitID ?unitID; sr:touches ?adjacent.\n    }\n  }\n} AS %federated_subquery\nWHERE\n{\n  INCLUDE %federated_subquery\n  BIND(SUBSTR(STR(?adjacent),37,16) AS ?adjacentTOID)\n  ?adjacentItem wdt:P3120 ?adjacentTOID.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}", "context": "Adjacent constituencies of the UK Parliament", "target": ""}
{"query": "SELECT ?awardLabel ?item ?itemLabel (GROUP_CONCAT(?winnerLabel; SEPARATOR = \", \") AS ?winners) ?work ?workLabel ?role ?roleLabel WHERE {\n  ?item p:P166 ?awardStatement.\n  ?awardStatement ps:P166 ?award;\n    pq:P805 wd:Q47496499.\n  OPTIONAL { ?awardStatement pq:P1346 ?winner. }\n  OPTIONAL { ?awardStatement pq:P1686 ?work. }\n  OPTIONAL { ?awardStatement pq:P453 ?role. }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?award rdfs:label ?awardLabel.\n    ?item rdfs:label ?itemLabel.\n    ?winner rdfs:label ?winnerLabel.\n    ?work  rdfs:label ?workLabel.\n    ?role  rdfs:label ?roleLabel.\n  }\n}\nGROUP BY ?awardLabel ?item ?itemLabel ?work ?workLabel ?role ?roleLabel", "context": "List of this year’s Academy Awards winners", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?item1 ?image1 ?item1Label ?item2 ?image2 ?item2Label ?edgeLabel \nWITH {\n  SELECT ?item1 WHERE {\n    VALUES ?item1 { wd:Q575689 wd:Q1816737 wd:Q1138235 wd:Q26318 wd:Q231270 wd:Q186304 wd:Q1375057 wd:Q1770624 wd:Q7883337 wd:Q59532027 wd:Q59532183 wd:Q59532283 wd:Q16113908 wd:Q48016876 wd:Q296403 wd:Q973212 wd:Q59532454 wd:Q21013519 wd:Q15935 wd:Q363421 wd:Q55428718 wd:Q55428786 wd:Q56034652 wd:Q63927876 wd:Q358087 wd:Q13605596 wd:Q59534229 wd:Q54997217 wd:Q365144 wd:Q780846 wd:Q20026735 wd:Q554091 wd:Q16209581}\n  }\n} AS %item1\n\nWITH {\n  SELECT (?item1 AS ?item2) WHERE {\n    INCLUDE %item1.\n  }\n} AS %item2\n\nWHERE {\n  INCLUDE %item1.\n  INCLUDE %item2.\n  ?item1 ?wdt ?item2.\n  ?edge wikibase:directClaim ?wdt;\n        a wikibase:Property.\n  OPTIONAL { ?item1 wdt:P18 ?image1. }\n  OPTIONAL { ?item2 wdt:P18 ?image2. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of the extended Kardashian clan", "target": ""}
{"query": "SELECT ?item ?itemLabel\nWHERE \n{\n  ?item wdt:P709 ?stat . \n  ?article schema:about ?item ;\n          schema:isPartOf <https://commons.wikimedia.org/> .\n  filter not exists {?item wdt:P18 [] .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}  \n", "context": "Scottish monuments with a commons sitelink, but no image", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT DISTINCT ?country ?countryLabel (count(?film) as ?count){\n  VALUES ?countryproperty {wdt:P17 wdt:P495}\n  ?film wdt:P136 wd:Q20442589;\n        ?countryproperty ?country.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP by ?country ?countryLabel\nORDER by ?count\n\n", "context": "Number of films with LGBT representation per country", "target": ""}
{"query": "# Cumulative count of items with the given name at a milestone\n# adapted by Jura1 from a query by 99of9, February 2020, [[Wikidata:Request_a_query/Archive/2019/06#dateCreated_instead_of_dateModified?]]\n#\n#defaultView:LineChart\nSELECT (SAMPLE(?date) AS ?date) (SUM(?count) AS ?cumulative_count) ?milestone\n{\n\n  { SELECT ?qid_rounded (count(?item) AS ?count) WHERE {\n      ?item wdt:P735 wd:Q4925477 .\n      BIND( xsd:integer( substr(str(?item), 33)) as ?qid)\n      BIND(xsd:integer(?qid/50000) as ?qid_cut)\n      BIND(?qid_cut*50000 as ?qid_rounded)\n    }\n    GROUP BY ?qid_rounded\n  }\n  wd:Q38074555 p:P1114 ?milestonep .\n  ?milestonep ps:P1114 ?milestone .\n  FILTER(?milestone > ?qid_rounded) .\n  ?milestonep pq:P585 ?date .\n}\nGROUP BY ?milestone", "context": "Importing from ThePeerage (Nov. 2019) added 50% to the number of Johns on Wikidata", "target": ""}
{"query": "select distinct ?person ?personLabel ?placeLabel ?workLabel ?roleLabel where\n{ ?person wdt:P19 ?place . ?place wdt:P131* wd:Q23100 .\n  ?work p:P161 ?cs . ?cs ps:P161 ?person . ?cs pq:P453 ?role .\n  ?work wdt:P136 ?sf . ?sf wdt:P279* wd:Q5967378 . \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by ?workLabel", "context": "SF movies and series with a significant character known to have been portrayed by an actor who was born in Liverpool", "target": ""}
{"query": "SELECT ?item ?itemLabel ?code ?native_label (lang(?native_label) AS ?lang) ?missing1 ?missing2 \n\nWITH {\n  SELECT ?item ?native_label ?code WHERE {\n    ?item wdt:P463 wd:Q8908 .\n    ?item wdt:P1705 ?native_label .\n    ?item wdt:P297 ?code .\n  }\n} AS %countries \n\nWHERE {\n    INCLUDE %countries .\n    OPTIONAL {\n        INCLUDE %countries .\n        BIND(SUBSTR(?code,1,1) AS ?first) .\n        BIND(?first AS ?missing1) .\n        FILTER(! CONTAINS(UCASE(?native_label), ?first) )\n    }\n    OPTIONAL {\n        INCLUDE %countries .\n        BIND(SUBSTR(?code,2,1) AS ?second) .\n        BIND(?second AS ?missing2) .\n        FILTER(! CONTAINS(UCASE(?native_label), ?second) ) .\n    }\n    FILTER(bound(?missing1) || bound(?missing2)) .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }             \n} ORDER BY ?itemLabel", "context": "countries in Europe whose ISO 2-letter abbreviation contains letters not in the native language name of their country", "target": ""}
{"query": "#defaultView:Map\nSELECT \n  ?item  ?label_and_description\n  ?audio ?coor ?img ?filename\nWHERE\n{\n    ?item p:P443 [ ps:P443 ?audio ; pq:P407 wd:Q9309 ] .\n    ?item wdt:P131+ wd:Q25 .  \n    ?item wdt:P625 ?coor .\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"cy\" . ?item  rdfs:label ?cy . \n                            ?item schema:description ?cyDescription . }\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . ?item  rdfs:label ?en . \n                            ?item schema:description ?enDescription . }\n    OPTIONAL { ?item wdt:P18 ?img }\n    BIND(uri(concat(\"https://commons.wikimedia.org/wiki/File:\",strafter(wikibase:decodeUri(str(?audio)),\"FilePath/\"))) as ?filename) \n    BIND( CONCAT ( COALESCE(?cy,\"\"), \"--\", COALESCE(?cyDescription,\"\"), \" [cy]; \", COALESCE(?en,\"\"), \"--\", COALESCE(?enDescription,\"\"), \" [en]\") as ?label_and_description) \n}", "context": "places names in Wales with Welsh pronunciation audio", "target": ""}
{"query": "#defaultView:Map{\"layer\": \"?typeLabel\"}\nSELECT DISTINCT ?s ?sLabel ?coor ?layer ?typeLabel ?website ?end ?end1\nWHERE {\nhint:Query hint:optimizer \"None\" .\n  ?s wdt:P131* wd:Q25 .  \n    { ?s wdt:P31/wdt:P279* wd:Q33506 }\n  UNION { ?s wdt:P31/wdt:P279* wd:Q7075 } \n  UNION { ?s wdt:P31/wdt:P279* wd:Q166118 } .\n       ?s   wdt:P625 ?coor .\n  ?s wdt:P31 ?type .\n  ?type wdt:P279* wd:Q1030034 .\nOPTIONAL { ?s wdt:P856 ?website . }\nOPTIONAL { ?s wdt:P576 ?end . }  \nOPTIONAL { ?s wdt:P582 ?end1 . }\n \nFILTER NOT EXISTS { ?s wdt:P576 ?end }. \n\n FILTER NOT EXISTS { ?s wdt:P582 ?end1}.           \n \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\n\n\n\n\n", "context": "Map of types of GLAMs in Wales", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?date (MAX(?count) AS ?max_count) ?measureLabel\nWHERE {\n  BIND(wd:Q81068910 AS ?outbreak).\n  {\n    BIND(wd:Q166231 AS ?measure).\n    ?outbreak p:P1603 ?cases.\n    ?cases prov:wasDerivedFrom/pr:P248/wdt:P50 wd:Q7817.\n    ?cases pqv:P585/wikibase:timeValue ?date.\n    ?cases psv:P1603/wikibase:quantityAmount ?count.\n    ?cases psv:P1603/wikibase:quantityAmount ?infection_count.\n  } UNION {\n    BIND(wd:Q4 AS ?measure).\n    ?outbreak p:P1120 ?deaths.\n    ?deaths prov:wasDerivedFrom/pr:P248/wdt:P50 wd:Q7817.\n    ?deaths pqv:P585/wikibase:timeValue ?date.\n    ?deaths psv:P1120/wikibase:quantityAmount ?count.\n    ?deaths psv:P1120/wikibase:quantityAmount ?death_count.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?date ?measureLabel\nORDER BY ASC(?date) DESC(?measure)", "context": "Updated chart of the number of infections and deaths caused since the outbreak of SARS-CoV-2, as reported by the World Health Organisation", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?start ?end ?child ?childLabel (MAX(?child_dob) AS ?child_dob1) (MIN(?child_age) AS ?child_age1) WHERE {\n  ?item p:P39 ?pm_stmt .\n  ?pm_stmt ps:P39 wd:Q14211 .\n  ?pm_stmt pq:P580 ?start .\n  OPTIONAL {?pm_stmt pq:P582 ?end} .\n  \n  ?item wdt:P40 ?child .\n  ?child wdt:P569 ?child_dob .\n  FILTER(! (?child_dob > ?end)) .\n  BIND (((?start - ?child_dob) / 365.24) AS ?child_age) .\n  FILTER (?child_age < 5) \n  FILTER NOT EXISTS {\n      ?child wdt:P570 ?child_dod . \n      FILTER (?child_dod < ?start) .\n  }\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?item ?itemLabel ?start ?end ?child ?childLabel\nORDER BY DESC(?start) ?child_dob1", "context": "British Prime Ministers with children under the age of 5 when elected, or born during their time in office", "target": ""}
{"query": "#Timeline of countries of origin of the winner of the European Film Award for Best European Film\n#defaultView:BarChart\nSELECT ?countryLabel (STR(YEAR(?date)) AS ?year) ?title WHERE {\n    #getting award winners for each year\n    ?item p:P166 ?st1; wdt:P31 wd:Q11424. ?st1 ps:P166 wd:Q777921; pq:P585 ?date.  \n    #making sure the statement is sourced with a page from the domain \"www.europeanfilmacademy.org\"\n    ?st1 prov:wasDerivedFrom ?ref1. \n    ?ref1 pr:P854 ?url1. FILTER(CONTAINS(STR(?url1), \"www.europeanfilmacademy.org\"))\n    #getting country of origin                                    \n    ?item p:P495 ?st2.\n    #making sure the country of origin is sourced with a page from the domain \"www.europeanfilmawards.eu\"\n    ?st2 ps:P495 ?country; prov:wasDerivedFrom ?ref2. \n    ?ref2 pr:P854 ?url2. FILTER(CONTAINS(STR(?url2), \"www.europeanfilmawards.eu\"))\n    #getting the titles\n    ?item wdt:P1476 ?orig_title; rdfs:label ?label. FILTER(LANG(?label)=\"en\")\n    BIND(IF(LANG(?orig_title) IN (\"en\", \"en-gb\", \"en-us\") || STR(?orig_title)=STR(?label), ?orig_title, CONCAT(?orig_title, \" (\", ?label, \")\")) AS ?title)\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY ?year ?countryLabel", "context": "Timeline of countries of origin of the winner of the European Film Award for Best European Film", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?image ?webpage ?person ?personLabel WHERE {\n  ?person wdt:P31 wd:Q5.\n  ?person wdt:P7943 ?id.\n  wd:P7943 wdt:P1630 ?url .\n  BIND(IRI(REPLACE(?id, '^(.+)$', ?url)) AS ?webpage).\n  OPTIONAL { ?person wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, fr\". }\n}", "context": "People with profiles on the Washington Post website", "target": ""}
{"query": "#In Our Time episodes and their topics\nselect distinct ?episode ?episodeLabel ?date ?bbc_link\n(GROUP_CONCAT(DISTINCT ?panelistLabel;separator=\"; \") as ?panelists) \n(count(DISTINCT ?panelist) as ?panelists_on_wd) \n(count(DISTINCT ?wp) as ?panelists_on_wp) \n?topic # (GROUP_CONCAT(DISTINCT ?topic;separator=\" ; \") as ?wikipedia_topics)\nwhere\n{ ?episode wdt:P31 wd:Q57608327 . ?episode wdt:P179 wd:Q3149655 . optional { ?episode wdt:P577 ?date }\n  optional { ?episode wdt:P710 ?panelist . optional { ?wp schema:about ?panelist . ?wp schema:isPartOf <https://en.wikipedia.org/>. } }\n  optional { ?episode wdt:P953 ?bbc_link }\n  optional { ?episode wdt:P921 ?subj . ?topic schema:about ?subj . ?topic schema:isPartOf <https://en.wikipedia.org/>. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n                         ?panelist rdfs:label ?panelistLabel .\n                         ?episode rdfs:label ?episodeLabel . }\n} group by ?episode ?episodeLabel ?date ?bbc_link ?topic order by desc(?date)", "context": "Subjects and panelists of the BBC radio programme In Our Time", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?image ?person ?personLabel WHERE {\n  ?person wdt:P31 wd:Q5.\n  ?person wdt:P1050 wd:Q84263196.\n  OPTIONAL { ?person wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, fr\". }\n}", "context": "People affected by COVID-19", "target": ""}
{"query": "#Map of hillforts in the UK\nSELECT DISTINCT ?item ?itemLabel ?coor ?image\nWHERE\n{\n  ?item wdt:P31/wdt:P279* wd:Q744099 .\n  ?item wdt:P17 wd:Q145 .\n  OPTIONAL { ?item wdt:P625 ?coor }\n  OPTIONAL { ?item wdt:P18 ?image }  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?coor ?image", "context": "Map of hillforts in the UK", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?image\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q34770 .\n  ?item wdt:P18 ?image .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?itemLabel", "context": "Images for language items", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?film ?filmLabel ?argitaratze_data WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?film wdt:P31 wd:Q11424;\n    wdt:P921 wd:Q44512.\n  ?film wdt:P577 ?argitaratze_data.\n}", "context": "Timeline of films about pandemics", "target": ""}
{"query": "#Map of hospitals\n#added 2017-08\n#defaultView:Map\nSELECT * WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q16917;\n        wdt:P625 ?geo .\n}", "context": "World map of hospitals", "target": ""}
{"query": "SELECT distinct ?item ?itemLabel ?twitter ?facebook ?linkedin ?instagram ?website WHERE \n{\n  VALUES ?thing {\n    wd:Q87764400  # NHS group \n    wd:Q6954197   # NHS trust\n    wd:Q6954187   # NHS foundation trust\n    wd:Q6954188   # NHS hospital trust\n    wd:Q6954189   # NHS mental health trust\n    wd:Q16986400  # NHS ambulance services trust\n    wd:Q16988141  # Community health NHS trust\n    wd:Q6664153   # Local health board (Wales) \n    wd:Q83425186  # NHS board (Scotland)   \n  }\n  ?item wdt:P31 ?thing.\n  minus { ?item wdt:P576 ?dissolved . }\n  optional { ?item wdt:P2002 ?twitter . }\n  optional { ?item wdt:P2013 ?facebook . }\n  optional { ?item wdt:P4264 ?linkedin . }\n  optional { ?item wdt:P2003 ?instagram . }\n  optional { ?item wdt:P856 ?website . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by ?itemLabel", "context": "English and Scottish health trusts & boards", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?developerLabel ?publisherLabel (COUNT(?award) AS ?nominations) WHERE {\n  ?item p:P1411 ?awardStat.\n  ?awardStat ps:P1411 ?award;\n    pq:P805 wd:Q84950595.\n  ?item wdt:P178 ?developer;\n    wdt:P123 ?publisher.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?developerLabel ?publisherLabel\nORDER BY DESC (?nominations)", "context": "Videogames nominated at the Pégases award", "target": ""}
{"query": "SELECT ?anc ?dob ?dod ?ancLabel WITH {SELECT ?ancestor {\n  {SELECT ?ancestor {wd:Q207 (wdt:P22|wdt:P25)* ?ancestor.}}\n  {SELECT ?ancestor {wd:Q9682 (wdt:P22|wdt:P25)* ?ancestor.}}\n}} AS %an WITH {SELECT ?ap {\n  INCLUDE %an\n  ?ancestor wdt:P22|wdt:P25 ?ap\n}} AS %an2 WHERE {\n  {SELECT DISTINCT (?ancestor AS ?anc) {INCLUDE %an}} MINUS {SELECT DISTINCT (?ap AS ?anc) {INCLUDE %an2}}\n  OPTIONAL {?anc wdt:P569 ?dob.}\n  OPTIONAL {?anc wdt:P570 ?dod.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?dob)", "context": "Most recent common ancestors of George W. Bush and Queen Elizabeth II", "target": ""}
{"query": "SELECT ?person ?personLabel ?portrait ?article ?sitelinks\nWHERE \n{\n  ?person wdt:P31 wd:Q5 .\n  ?person wdt:P1050 wd:Q84263196 .\n  ?person wikibase:sitelinks ?sitelinks\n  \n  OPTIONAL {\n    ?person wdt:P18 ?portrait\n  }\n \n  OPTIONAL {\n    ?article schema:about ?person .\n    ?article schema:isPartOf <https://en.wikipedia.org/> .\n  }\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?sitelinks)", "context": "Notable people with COVID-19 by number of sitelinks", "target": ""}
{"query": "#created by Mahir256\n#defaultView:Map{\"hide\":[\"?shape\", \"?rgb\"]}\nSELECT ?item ?itemLabel ?state ?stateLabel ?cases ?shape ?rgb ?time WHERE {\n  wd:Q84055514 wdt:P527 ?item. \n  ?item wdt:P276 ?state ; p:P1603 ?casestmt. \n  ?state wdt:P3896 ?shape.\n  ?casestmt ps:P1603 ?cases ; pq:P585 ?time .\n  { SELECT (MAX(?cases_) as ?mc) {\n      wd:Q84055514 wdt:P527/wdt:P1603 ?cases_\n  } }\n  FILTER NOT EXISTS { ?item p:P1603/pq:P585 ?time_. FILTER(?time < ?time_) }\n  BIND(IF(?cases > (5 * ?mc)/6,\"a50f15\", IF(?cases > (2 * ?mc)/3,\"de2d26\",\n       IF(?cases > ?mc/2,\"fb6a4a\", IF(?cases > ?mc/3,\"fc9272\",\n       IF(?cases > ?mc/6,\"fcbba1\", \"fee5d9\"))))) as ?rgb)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "State-level map", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?state ?time ?cases ?stateLabel {\n  wd:Q84055514 wdt:P527 ?statecases.\n  ?statecases wdt:P276 ?state ; p:P1603 ?casestmt.\n  ?casestmt ps:P1603 ?cases ; pq:P585 ?time.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "State-level line graph", "target": ""}
{"query": "SELECT ?item ?itemLabel ?count\nWHERE\n{\n  {\n    SELECT ?item (COUNT(DISTINCT ?country) AS ?count) WHERE {\n      ?item wdt:P31/wdt:P279* wd:Q23397 ;\n            wdt:P17 ?country .\n      FILTER NOT EXISTS { ?item wdt:P31 wd:Q47486890 }\n    }\n    GROUP BY ?item ?itemLabel\n    HAVING (?count >= 3)\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en\". }\n}\nORDER BY DESC(?count) ?itemLabel   ", "context": "Lakes spanning at least 3 countries", "target": ""}
{"query": "#Carte des restaurants Michelin au Luxembourg\n#defaultView:Map\nSELECT ?item ?itemLabel ?geo ?layer WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q11707;\n    wdt:P166 wd:Q20824563;\n    wdt:P17 wd:Q32;                          \n    wdt:P625 ?geo.\n  ?item p:P166 [ps:P166 wd:Q20824563; pq:P1114 ?layer]\n  MINUS {?item wdt:P576 []}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\n", "context": "Michelin starred restaurants in Luxembourg", "target": ""}
{"query": "# People who were born over a year after one of their parents died\n# Many of these indicate erroneous data-matching and may need to be fixed by hand\n\nSELECT ?item ?itemLabel ?born ?parent ?parentLabel ?parentdied ?days_posthumous WITH {\n  SELECT DISTINCT ?item ?born ?parent ?parentdied ?days_posthumous WHERE {\n    ?item wdt:P31 wd:Q5 . # people only, avoid racehorses!\n    # add an additional generator here if desired, ie \"Swedish people\" # ?item wdt:P27 wd:Q34\n    ?item wdt:P25|wdt:P22 ?parent . ?item wdt:P569 ?born .\n    ?parent wdt:P570 ?parentdied . filter(?parentdied < ?born) .\n    bind(round((?born-?parentdied)) as ?days_posthumous) . filter(?days_posthumous > 365) .\n    # filter to just those that are over a year different\n    # remember that some people can be born the year after their father died!\n  }\n} AS %results WHERE {\n  INCLUDE %results.\n  ?item p:P569 [ a wikibase:BestRank; psv:P569 [ wikibase:timeValue ?born; wikibase:timePrecision ?prec1 ] ] .\n  ?parent p:P570 [ a wikibase:BestRank; psv:P570 [ wikibase:timeValue ?parentdied; wikibase:timePrecision ?prec2 ] ] .\n  filter (?prec1 >= 9) . filter (?prec2 >= 9)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by desc(?born)\n", "context": "People born more than a year after one of their parents", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?born ?father ?fatherLabel ?fatherdied ?days_posthumous WHERE {\n ?item p:P39 ?statement. ?statement ps:P39 ?position . ?position wdt:P279* wd:Q16707842 .\n ?item wdt:P569 ?born . \n ?item wdt:P22 ?father . ?father wdt:P570 ?fatherdied . filter(?fatherdied < ?born) .\n bind(round((?born-?fatherdied)) as ?days_posthumous) .  \n filter(?days_posthumous < 1000) # omit one massively wrong \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by ?born\n", "context": "UK MPs who were born after their father's death", "target": ""}
{"query": "SELECT DISTINCT ?flag ?flagLabel ?countryLabel ?mountainLabel ?imageFlag\nWHERE \n{\n  ?flag wdt:P31 wd:Q186516;\n        wdt:P1001 ?country ;\n        wdt:P180* ?mountain ;\n        wdt:P18 ?imageFlag .\n  { ?mountain wdt:P279* wd:Q8502 . } UNION\n  { ?mountain wdt:P31/wdt:P279* wd:Q8502 . } UNION\n  { ?mountain wdt:P279* wd:Q46831 . } UNION\n  { ?mountain wdt:P31/wdt:P279* wd:Q46831 . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?countryLabel ?mountainLabel", "context": "National flags representing mountains", "target": ""}
{"query": "#Karte der Umgebindehäuser\n#added 2019-02\n#defaultView:Map{\"markercluster\": {}}\nSELECT * WHERE {\n  ?item (wdt:P31*/wdt:P279*) wd:Q1362233.\n  ?item wdt:P625 ?geo.\n  OPTIONAL { ?item wdt:P6375 ?Adresse. filter(lang(?Adresse)=\"de\")}\n  OPTIONAL { ?item wdt:P18 ?Bild. }\n}", "context": "Map of Umgebindehäuser (Upper Lusatian houses)", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?commune ?communeLabel ?coords ?image\nWHERE {\n  { ?item wdt:P31/wdt:P279* wd:Q164240 } UNION\n  { ?item wdt:P31/wdt:P279* wd:Q34023 }\n  ?item wdt:P131 ?commune .\n  ?commune p:P31 ?communeStatement ;\n           wdt:P131 wd:Q12642 .\n  ?communeStatement ps:P31/wdt:P279* wd:Q484170 .\n  FILTER NOT EXISTS { ?communeStatement pq:P582 ?x }\n  OPTIONAL { ?item wdt:P625 ?coords }\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?communeLabel ?itemLabel ", "context": "Map of megaliths in Morbihan (Bretagne)", "target": ""}
{"query": "#films mis à disposition en libre accès en VOD par artfilm.ch durant la crise du Covid-19\nSELECT DISTINCT ?film ?filmLabel (GROUP_CONCAT(DISTINCT(?réalLabel); separator=\", \") as ?réals) ?duree (MIN(?date) as ?oldest_date) ?accès\nWHERE \n{\n  ?film wdt:P1344 wd:Q89370484;\n        wdt:P2047 ?duree;\n        wdt:P577 ?date;\n        wdt:P57 ?réal.\n  ?film p:P973 ?describestmt.\n  ?describestmt ps:P973 ?accès.\n  ?describestmt pq:P407 wd:Q150.\n  FILTER STRSTARTS(STR(?accès), \"https://www.artfilm.ch\").\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de,it,es\".\n                         ?réal rdfs:label ?réalLabel.\n                         ?film rdfs:label ?filmLabel.\n                         }\n}\nGROUP BY ?film ?filmLabel ?duree ?accès\nORDER BY ?oldest_date", "context": "Movies from artfilm.ch in free access", "target": ""}
{"query": "#defaultView:Graph\nSELECT\n  ?topic1 ?topic1Label ?topic2 ?topic2Label\nWITH {\n  SELECT\n    (COUNT(?work) AS ?count) ?topic1 ?topic2\n  WHERE {\n    # Find works that are marked with main subject of the topic.\n    ?work wdt:P921 / ( wdt:P31*/wdt:P279* | wdt:P361+ | wdt:P1269+ ) wd:Q82069695 .\n    \n    # Identify co-occuring topics. \n    ?work wdt:P921 ?topic1, ?topic2 . \n\n    # Exclude the topic it self\n    FILTER (wd:Q82069695 != ?topic1 && wd:Q82069695 != ?topic2 && ?topic1 != ?topic2)\n  }\n  GROUP BY ?topic1 ?topic2\n  ORDER BY DESC(?count)\n\n  # There a performance problems in the browser: We cannot show large graphs,\n  # so we put a limit on the number of links displayed.\n  LIMIT 400\n\n} AS %results\nWHERE {\n  INCLUDE %results\n  \n  # Label the results\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,nl,no,ru,sv,zh\".\n  }\n}\n\n", "context": "Topic co-occurrence graph in the SARS-CoV-2 & #COVID19 literature", "target": ""}
{"query": "SELECT ?fibule ?fibuleLabel (SAMPLE(?date) AS ?dates) (GROUP_CONCAT(DISTINCT ?materiauLabel;separator=\", \") AS ?materiaux) ?lieuLabel ?image ?identifiant_Joconde WHERE {\n  ?fibule wdt:P31 wd:Q324926.\n  ?fibule wdt:P195 wd:Q1376.\n  OPTIONAL { ?fibule wdt:P571 ?date }\n  OPTIONAL { ?fibule wdt:P186 ?materiau }\n  OPTIONAL { ?fibule wdt:P189 ?lieu }\n  OPTIONAL { ?fibule wdt:P18 ?image }\n  OPTIONAL { ?fibule wdt:P347 ?identifiant_Joconde }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". ?fibule rdfs:label ?fibuleLabel . ?materiau rdfs:label ?materiauLabel . ?lieu rdfs:label ?lieuLabel }\n}\nGROUP BY ?fibule ?fibuleLabel ?lieuLabel ?image ?identifiant_Joconde", "context": "Fibulas in Musée Saint-Raymond with date, material, location of discovery", "target": ""}
{"query": "SELECT ?country ?countryLabel WHERE {\n    ?country wdt:P31 wd:Q3624078 . # sovereign state\n    ?country rdfs:label ?countryLabel .\n    filter(!regex(?countryLabel, 'a')) .\n}", "context": "Names of countries without letter \"a\"", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?diseaseLabel (MAX(?cases) AS ?numOfCases)\nWHERE {\n  ?disease wdt:P31/wdt:P279* wd:Q18123741 . # infectious disease\n  ?disease wdt:P1603 ?cases . # number of cases\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?diseaseLabel ORDER BY DESC(?numOfCases)", "context": "Infectious diseases and their number of cases", "target": ""}
{"query": "#defaultView:Map\nselect ?x ?xLabel ?image ?loc {\n  ?x wdt:P31 wd:Q182676 ; wdt:P17 wd:Q219 ; wdt:P625 ?loc .\n  \n  optional{?x wdt:P18 ?image} \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bg,en,pl\". }\n}", "context": "Map of mountain huts in Bulgaria", "target": ""}
{"query": "#Women and Cats in paintings\nSELECT ?item ?itemLabel ?img \nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q3305213 .\n  ?item wdt:P180 wd:Q146 .\n  ?item wdt:P180 wd:Q467 .\n  OPTIONAL { ?item wdt:P18 ?img }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Women with cats in paintings", "target": ""}
{"query": "#films de Peter Mettler connus de Wikidata et diffusés en VOD par artfilm.ch\nSELECT DISTINCT ?film ?filmLabel (GROUP_CONCAT(DISTINCT(?réalLabel); separator=\", \") as ?réals) ?duree (MIN(?date) as ?oldest_date) ?URL\nWHERE \n{\n  ?film wdt:P57 wd:Q7175862;\n        p:P3301 ?artfilm.\n  ?artfilm ps:P3301 ?artfilmCH;\n           pq:P2699 ?URL.\n  FILTER STRSTARTS(STR(?URL), \"https://www.artfilm.ch\").\n  OPTIONAL {?film wdt:P2047 ?duree.}\n  OPTIONAL {?film wdt:P577 ?date.}\n  OPTIONAL {?film wdt:P57 ?réal.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de,it,es\".\n                         ?réal rdfs:label ?réalLabel.\n                         ?film rdfs:label ?filmLabel.\n                         }\n}\nGROUP BY ?film ?filmLabel ?duree ?URL\nORDER BY ?oldest_date", "context": "Films by Peter Mettler available free on VOD", "target": ""}
{"query": "#films diffusés en VOD par artfilm.ch triés par réalisatrice et date\nSELECT DISTINCT ?film ?filmLabel ?réalLabel ?familleLabel ?duree (MIN(?date) as ?oldest_date) ?accès\nWHERE \n{\n?film p:P3301 ?artfilm.\n  ?artfilm ps:P3301 ?artfilmCH;\n           pq:P2699 ?URL.\n  FILTER STRSTARTS(STR(?URL), \"https://www.artfilm.ch\").\n  OPTIONAL {?film wdt:P2047 ?duree.}\n  OPTIONAL {?film wdt:P57 ?réal.}\n  OPTIONAL {?film wdt:P577 ?date.}\n  OPTIONAL {?réal wdt:P734 ?famille.}\n   ?réal wdt:P21 wd:Q6581072.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de,it,es\".\n                         ?réal rdfs:label ?réalLabel.\n                         ?film rdfs:label ?filmLabel.\n                         ?famille rdfs:label ?familleLabel.\n                         }\n}\nGROUP BY ?film ?filmLabel ?réalLabel ?familleLabel ?duree ?accès\nORDER BY STR(?familleLabel) ?réalLabel ?oldest_date", "context": "Films broadcast on VOD by artfilm.ch sorted by Director and Date", "target": ""}
{"query": "#films diffusés en VOD par artfilm.ch triés par réalisatrice et date\nSELECT DISTINCT ?film ?filmLabel ?réalLabel ?familleLabel ?duree (MIN(?date) as ?oldest_date) ?accès\nWHERE \n{\n?film p:P3301 ?artfilm.\n  ?artfilm ps:P3301 ?artfilmCH;\n           pq:P2699 ?URL.\n  FILTER STRSTARTS(STR(?URL), \"https://www.artfilm.ch\").\n  OPTIONAL {?film wdt:P2047 ?duree.}\n  OPTIONAL {?film wdt:P57 ?réal.}\n  OPTIONAL {?film wdt:P577 ?date.}\n  OPTIONAL {?réal wdt:P734 ?famille.}\n   ?réal wdt:P21 wd:Q6581072.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de,it,es\".\n                         ?réal rdfs:label ?réalLabel.\n                         ?film rdfs:label ?filmLabel.\n                         ?famille rdfs:label ?familleLabel.\n                         }\n}\nGROUP BY ?film ?filmLabel ?réalLabel ?familleLabel ?duree ?accès\nORDER BY STR(?familleLabel) ?réalLabel ?oldest_date", "context": "Swiss films listed by date and duration per minutes", "target": ""}
{"query": "SELECT DISTINCT ?langLabel ?langDate ?humanLabel ?picture WHERE {\n            ?lang (wdt:P31/(wdt:P279*)) wd:Q9143.\n            ?human wdt:P31 wd:Q5;\n            wdt:P18 ?picture.\n            { ?lang wdt:P287 ?human. }\n            UNION\n            { ?lang wdt:P170 ?human. }\n            UNION\n            { ?lang wdt:P943 ?human. }\n            UNION\n            { ?lang wdt:P178 ?human. }\n            SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n            OPTIONAL { ?lang wdt:P571 ?langDate. }\n        }\n        LIMIT 25\n", "context": "Programming Languages, Year of creation and Image of inventor", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?line ?coords ?length WHERE {\n  {\n    ?item wdt:P31 wd:Q80830021.\n    OPTIONAL {\n      ?item p:P625 ?coords_stmt_west.\n      ?coords_stmt_west pq:P518 wd:Q80981719;\n        ps:P625 ?coords_west;\n        (psv:P625/wikibase:geoLatitude) ?lat1;\n        (psv:P625/wikibase:geoLongitude) ?lon1.\n    }\n    OPTIONAL {\n      ?item p:P625 ?coords_stmt_east.\n      ?coords_stmt_east pq:P518 wd:Q80982256;\n        ps:P625 ?coords_east; \n        (psv:P625/wikibase:geoLatitude) ?lat2;\n        (psv:P625/wikibase:geoLongitude) ?lon2.\n    }\n    OPTIONAL { ?item wdt:P2043 ?length. }\n    BIND(CONCAT(\"LINESTRING (\", STR(?lon1), \" \", STR(?lat1), \",\", STR(?lon2), \" \", STR(?lat2), \")\") AS ?str)\n    BIND(STRDT(?str, geo:wktLiteral) AS ?line)\n  }\n  UNION\n  {\n    ?item wdt:P31 wd:Q80962689; \n      wdt:P625 ?coords.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} \nORDER BY (?coords_west)", "context": "Map of Antonine Wall items on Wikidata", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?TypeLabel ?CréateurLabel ?Date ?DépeintLabel ?DépartementLabel ?Atlas  (group_concat(?INV;separator=\"; \") as ?INVs) ?image WHERE{\n?item wdt:P195/wdt:P31* wd:Q11681271.\n#OPTIONAL{?item wdt:P170 ?Créateur.}\n#OPTIONAL{?item wdt:P571 ?Date.}\n#OPTIONAL{?item wdt:P180 ?Dépeint.}\n#OPTIONAL{?item wdt:P195 ?Département.}\n#OPTIONAL{?item wdt:P31 ?Type.}\nOPTIONAL{?item wdt:P18 ?image.}\nOPTIONAL{?item wdt:P217 ?INV.}\nOPTIONAL{?item wdt:P1212 ?Atlas.}\nFILTER(STRSTARTS(?INV, 'S')) .\nSERVICE wikibase:label {bd:serviceParam wikibase:language \"fr, en\".}\n}\nGROUP BY ?item ?itemLabel ?TypeLabel ?CréateurLabel ?Date ?DépeintLabel ?DépartementLabel ?Atlas ?image\nORDER BY ?INVs", "context": "Map of Hospitals in Buenos Aires on Spanish Wikipedia", "target": ""}
{"query": "#Karte der Restaurants mit Michelin-Stern in der Schweiz\n#defaultView:Map\nSELECT ?item ?itemLabel ?geo ?layer ?image WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q11707;\n    wdt:P17 wd:Q39;\n    wdt:P166 wd:Q20824563;\n    wdt:P625 ?geo.\n  OPTIONAL {?item wdt:P18 ?image}\n  ?item p:P166 [ps:P166 wd:Q20824563; pq:P1114 ?layer]\n  MINUS {?item wdt:P576 []}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de\". }\n}\n", "context": "Map of Michelin starred restaurants in Switzerland", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coordinates\",\"?rgb\"]}\nSELECT ?item ?itemLabel ?coordinates ?rgb WHERE {\n  ?instance ps:P31/wdt:P279* wd:Q1115575 .\n  ?item p:P31 ?instance .\n  #?item wdt:P131/wdt:P131 wd:Q23157 . \n  MINUS { ?instance pq:P582 [] } . \n  OPTIONAL { ?item wdt:P402 ?osm . BIND(\"008000\" AS ?rgb) } .\n  ?item wdt:P625 ?coordinates .\n  ?item wdt:P131 ?adm3 . ?adm3 wdt:P131 ?adm2 \n} ", "context": "English civil parishes on Wikidata with OSM relation ID", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?TypeLabel ?CréateurLabel ?Date ?DépeintLabel ?DépartementLabel ?Atlas  (group_concat(?INV;separator=\"; \") as ?INVs) ?image WHERE{\n?item wdt:P195/wdt:P31* wd:Q11681271.\nOPTIONAL{?item wdt:P170 ?Créateur.}\nOPTIONAL{?item wdt:P571 ?Date.}\nOPTIONAL{?item wdt:P180 ?Dépeint.}\nOPTIONAL{?item wdt:P195 ?Département.}\n#OPTIONAL{?item wdt:P31 ?Type.}\nOPTIONAL{?item wdt:P18 ?image.}\nOPTIONAL{?item wdt:P217 ?INV.}\nOPTIONAL{?item wdt:P1212 ?Atlas.}\nFILTER(STRSTARTS(?INV, 'MAO')) .\nSERVICE wikibase:label {bd:serviceParam wikibase:language \"fr, en\".}\n}\nGROUP BY ?item ?itemLabel ?TypeLabel ?CréateurLabel ?Date ?DépeintLabel ?DépartementLabel ?Atlas ?image\nORDER BY ?INVs", "context": "Collections in Louvre with an image on Wikimedia Commons", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?item ?itemLabel ?startdate (SAMPLE(?image) AS ?image)\nWHERE\n{\n    ?item wdt:P31 wd:Q146.      \n    ?item  p:P39 ?mouserOffice. \n    ?mouserOffice ps:P39 wd:Q198641.\n    ?mouserOffice pq:P580 ?startdate .\n\t#?item wdt:P569 ?startdate .\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n    OPTIONAL { ?item wdt:P18 ?image }\n}\nGROUP BY ?item ?itemLabel ?startdate", "context": "Chief Mousers", "target": ""}
{"query": "#get the list of semantic books - problem with langauge of wdt:P2093\n \nSELECT DISTINCT ?book ?title (GROUP_CONCAT (DISTINCT ?author; SEPARATOR = \", \") AS ?authors) ?pubDate\n{\n  VALUES ?book {wd:Q17744504 wd:Q17744476 wd:Q61718117 wd:Q59784970 wd:Q17744339 wd:Q51601248 wd:Q17744488 wd:Q56740176 wd:Q27942623 wd:Q27942623}\n         \n         ?book wdt:P1476 ?title.\n         OPTIONAL {?book (wdt:P50/rdfs:label)|(wdt:P98/rdfs:label)|wdt:P2093 ?author}\n         OPTIONAL {?book wdt:P577 ?pubDate}\n \nFILTER (LANG(?author)=\"\"||LANG(?author)=\"en\")\n}\nGROUP BY ?book ?title ?pubDate", "context": "Books about Semantics", "target": ""}
{"query": "# longest rivers that feed into another river\nSELECT ?river ?riverLabel (?lengthInM/1000 AS ?lengthInKm) ?mouthLabel WITH {\n  SELECT DISTINCT ?river ?lengthInM ?mouth WHERE {\n    ?river wdt:P31/wdt:P279* wd:Q4022;\n           wdt:P403 ?mouth;\n           p:P2043 ?lengthStatement. ?lengthStatement a wikibase:BestRank; psn:P2043/wikibase:quantityAmount ?lengthInM. hint:Prior hint:rangeSafe true.\n    ?mouth wdt:P31/wdt:P279* wd:Q4022.\n    FILTER(?lengthInM > 1000000)\n  }\n  ORDER BY DESC(?lengthInM)\n  LIMIT 10\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?lengthInM)", "context": "The longest river that feeds into another river", "target": ""}
{"query": "# longest rivers that don’t feed into a sea or ocean\nSELECT ?river ?riverLabel (?lengthInM/1000 AS ?lengthInKm) ?mouthLabel WITH {\n  SELECT DISTINCT ?river ?lengthInM ?mouth WHERE {\n    ?river wdt:P31/wdt:P279* wd:Q4022;\n           wdt:P403 ?mouth;\n           p:P2043 ?lengthStatement. ?lengthStatement a wikibase:BestRank; psn:P2043/wikibase:quantityAmount ?lengthInM. hint:Prior hint:rangeSafe true.\n    MINUS { ?mouth wdt:P31/wdt:P279* wd:Q165. }\n    MINUS { ?mouth wdt:P31/wdt:P279* wd:Q9430. }\n    FILTER(?lengthInM > 1000000)\n  }\n  ORDER BY DESC(?lengthInM)\n  LIMIT 10\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?lengthInM)", "context": "Longest rivers that do not feed into a sea or ocean", "target": ""}
{"query": "select\n  ?quantity\n  (sample(?label) as ?label)\n  (sample(?desc) as ?desc)\n  (sample(?symbol) as ?symbol)\n  ?formula\n  (\n    strdt(\n      concat(\n        \"<math><mtable columnalign=left>\",\n        group_concat(?symbolAndLabel),\n        \"</mtable></math>\"\n      ),\n      <http://www.w3.org/1998/Math/MathML>\n    ) as ?otherSymbols\n  )\nwhere {\n  ?quantity wdt:P279+ wd:Q71550118 .\n  optional { ?quantity rdfs:label ?label . filter (lang(?label) = \"en\") }\n  optional { ?quantity schema:description ?desc . filter (lang(?desc) = \"en\") }\n  ?quantity wdt:P2534 ?formula .\n  ?quantity wdt:P7235 ?symbol .\n  {\n    select distinct * where {\n      ?quantity p:P4934 [ pq:P7235 ?otherSymbol ; ps:P4934 / rdfs:label ?otherLabel ] .\n      filter (lang(?otherLabel) = \"en\")\n      bind (\n        concat(\n          \"<mrow>\",\n          str(?otherSymbol), \"<mtext>\", \": \", ?otherLabel, \"</mtext>\",\n          \"</mrow>\"\n        ) as ?symbolAndLabel\n      )\n    }\n  }\n}\ngroup by ?quantity ?formula\nlimit 500", "context": "Defining formulas of physical and other quantities", "target": ""}
{"query": "#defaultView:TreeMap\nSELECT ?iaLabel ?a ?ab ?abLabel WITH {\n\nSELECT ?a ?ia WHERE {\n  ?ia wdt:P31 wd:Q21199 . # natural number\n  ?ia wdt:P1181 ?a .      # its numeric value\n  FILTER (?ia != wd:Q713113 ) # we don't want 0.9999\n  FILTER (?a >= 1 && ?a <= 12 ) # between 1 and 12\n} } AS %A\n                     \nWITH { SELECT ?b ?ib WHERE {\n  INCLUDE %A\n  BIND (?a AS ?b)\n  BIND (?ia AS ?ib)\n}} AS %B\n\nWHERE {\n  INCLUDE %A\n  INCLUDE %B\n  BIND (?a * ?b AS ?ab )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "TreeMap visualising the 12 times table", "target": ""}
{"query": "SELECT (COUNT(?human) AS ?count) { ?human wdt:P31 wd:Q5 }", "context": "There are now more than 7 million people on Wikidata", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?node ?nodeLabel ?nodeImage ?childNode ?childNodeLabel ?childNodeImage ?rgb WHERE {\n  {\n    BIND(wd:Q84263196 AS ?node)\n    ?node ?p ?i.\n    OPTIONAL { ?node wdt:P18 ?nodeImage. }\n    ?childNode ?x ?p.\n    ?childNode rdf:type wikibase:Property.\n    FILTER(STRSTARTS(STR(?i), \"http://www.wikidata.org/entity/Q\"))\n    FILTER(STRSTARTS(STR(?childNode), \"http://www.wikidata.org/entity/P\"))\n  }\n  UNION\n  {\n    BIND(\"EFFBD8\" AS ?rgb)\n    wd:Q84263196 ?p ?childNode.\n    OPTIONAL { ?childNode wdt:P18 ?childNodeImage. }\n    ?node ?x ?p.\n    ?node rdf:type wikibase:Property.\n    FILTER(STRSTARTS(STR(?childNode), \"http://www.wikidata.org/entity/Q\"))\n  }\n  OPTIONAL {\n    ?node wdt:P18 ?nodeImage.\n    ?childNode wdt:P18 ?childNodeImage.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv,en\". }\n}", "context": "Recently published works on COVID-19", "target": ""}
{"query": "#defaultView:Map\nSELECT ?s ?sLabel ?coor ?operator ?operatorLabel ?layer ?capacity ?locationLabel \n\nWHERE {  \n  \n       ?s wdt:P31/wdt:P279* wd:Q4260475 ;\n          wdt:P625 ?coor ;\n          wdt:P137 ?operator ;\n          OPTIONAL {?s wdt:P18 ?image .}\n   ?s wdt:P31 ?type .\n\n  VALUES ?operator { wd:Q4898241 wd:Q2756337 wd:Q18343995 wd:Q5038490 wd:Q5197211 wd:Q4667178 wd:Q4761763 }\nBIND( \n       IF(?type = wd:Q1402739, \"Field Hospital\", \n       IF(?type = wd:Q569500, \"Community Health Centre\", \n       IF(?type = wd:Q16917, \"Hospital\",  \n       IF(?type = wd:Q718966, \"Doctor's Surgery\",   \n                   \"\")))) AS ?layer).  \n  minus {?s  wdt:P31 wd:Q64578911 .}\n  minus {?s  wdt:P31 wd:Q4287745 .}\n  \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\n", "context": "Welsh hospitals, health centres, doctors surgeries and temporary Covid19 hospitals", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coord ?img  ?layer WHERE {\n  ?item wdt:P8024 ?Napi;\n    (wdt:P19/wdt:P625) ?coord.\n  ?Nobel wdt:P279 wd:Q7191.\n  ?item wdt:P166 ?Nobel.\n  ?Nobel rdfs:label ?layer. FILTER(LANG(?layer) = \"en\")\n\n  OPTIONAL { ?item wdt:P18 ?img. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Map of countries receiving the Nobel peace prize", "target": ""}
{"query": "#added before 2016-10\n\n#All bibliotecas in Argentina with coordinates\nSELECT DISTINCT ?item ?name ?coord ?lat ?lon\nWHERE\n{\n hint:Query hint:optimizer \"None\" .\n ?item wdt:P131* wd:Q414 .\n ?item wdt:P31/wdt:P279* wd:Q7075 .\n ?item wdt:P625 ?coord .\n ?item p:P625 ?coordinate .\n ?coordinate psv:P625 ?coordinate_node .\n ?coordinate_node wikibase:geoLatitude ?lat .\n ?coordinate_node wikibase:geoLongitude ?lon .\n SERVICE wikibase:label {\n bd:serviceParam wikibase:language \"es\" .\n ?item rdfs:label ?name\n }\n}\nORDER BY ASC (?name)", "context": "Map of geolocated Argentine libraries", "target": ""}
{"query": "SELECT ?recipe ?recipeLabel\nWHERE\n{\n  ?recipe wdt:P31?/wdt:P279* wd:Q219239;\n            wdt:P527 wd:Q21546392.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\", \"fr\". }\n}\nORDER BY UCASE(STR(?recipeLabel))", "context": "Recipes that include garlic", "target": ""}
{"query": "#postcards A-trip-to\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?image\nWHERE \n{\n  ?item wdt:P31 wd:Q192425.\n  ?item wdt:P972 wd:Q61729277. \n  optional {\n    ?item wdt:P18 ?image\n   }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de\". }\n}", "context": "Images of postcards from the catalog of the Regional bibliography of Saxony", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?rgb\"]}\nSELECT DISTINCT ?itemLabel ?NumeroInv ?item ?coord ?blason ?archives ?rgb WHERE {\n  ?item wdt:P485 ?archives.\n  ?archives wdt:P31 wd:Q2860456.\n  ?item p:P485 ?instat.\n  ?instat ps:P485 ?AD83;\n    pq:P217 ?NumeroInv.\n  ?item wdt:P31 wd:Q484170;\n    wdt:P625 ?coord;\n    wdt:P94 ?blason.\n  BIND ( substr(str(?archives),34,6) AS ?rgb )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\". }\n}\nORDER BY (?itemLabel)", "context": "French municipalities which have the property \"archived by\" (P485) for the departmental archives of Bouches-du-Rhône", "target": ""}
{"query": "SELECT ?item ?itemLabel ?biztanleria ?koordenatuak WHERE {\n  { ?item wdt:P31 wd:Q2074737. }\n  UNION\n  { ?item wdt:P31 wd:Q484170. }\n  ?item ((wdt:P131*)/^wdt:P527) wd:Q47588;\n    wdt:P1082 ?biztanleria.\n  FILTER(?biztanleria < 5000 )\n  ?item wdt:P625 ?koordenatuak.\n    SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"eu\" .\n  }\n}", "context": "Municipalities with less than 5,000 inhabitants in the Basque Country", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?dob ?post ?postLabel ?birthplace ?birthplaceLabel\n\nWHERE {\n    hint:Query hint:optimizer \"None\".\n    ?post wdt:P279* wd:Q2285706 .\n    ?item wdt:P39 ?post .\n    ?item wdt:P19 ?birthplace .\n    ?birthplace wdt:P131/(wdt:P131?)/(wdt:P131?)/(wdt:P131?) wd:Q25 .\n    OPTIONAL {?item wdt:P569 ?dob}\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?postLabel ?dob ?itemLabel", "context": "People born in Wales who became head of government in Wales or overseas", "target": ""}
{"query": "SELECT (AVG($stardate) as $average_stardate)\nWHERE\n{\n #star trek films\n {?item wdt:P31 wd:Q61283808.}\n UNION\n {?startrek wdt:P31 wd:Q62573441.  #all star trek series\n ?item wdt:P179 ?startrek.} #members of star trek series\n ?item wdt:P1191 ?date. #get broadcast date (will create dupes if displayed)\n ?item p:P840 [ pq:P4895 ?stardate ]. \n MINUS { ?item wdt:P31 wd:Q21664088. } #exclude two parter entries\n OPTIONAL {?item wdt:P2047 ?duration.} #allow no durations to examine furtherdown\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?date)", "context": "Average stardate of Star Trek episodes", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT ?person ?personLabel ?personImage ?parent ?parentLabel ?parentImage WITH {\n\nSELECT ?person WHERE {\n  \n{SELECT DISTINCT ?person WHERE {\n  VALUES $ANCESTOR { wd:Q130805 wd:Q9181 } # George I & Abraham\n  $ANCESTOR wdt:P40* ?person . \n}}\n  VALUES $DESCENDANT { wd:Q180589 wd:Q302 } # Boris Johnson & Jesus\n  ?person wdt:P40* $DESCENDANT .\n} } AS %person\n\n  WITH {SELECT ?parent WHERE {\n  INCLUDE %person \n  BIND(?person AS ?parent)\n} } AS %parent\n\nWHERE {\n  INCLUDE %person\n  INCLUDE %parent\n  ?parent wdt:P40 ?person .\n  OPTIONAL {?person wdt:P18 ?personImage}\n  OPTIONAL {?parent wdt:P18 ?parentImage}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph showing direct ancestral lines linking Boris Johnson and George I, and Jesus and Abraham", "target": ""}
{"query": "\nSELECT ?item ?itemLabel ?collection ?collectionLabel ?pic\nWHERE \n{\n \n  ?item wdt:P170 wd:Q2939083 .\n  ?item wdt:P276 ?collection.\n\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?collectionLabel", "context": "Photos by Carlton Watkins with their collection", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?TypeLabel ?CréateurLabel ?Date ?DépeintLabel ?DépartementLabel ?Atlas  (group_concat(?INV;separator=\"; \") as ?INVs) ?image WHERE{\n?item wdt:P195/wdt:P31* wd:Q11681271.\n#OPTIONAL{?item wdt:P170 ?Créateur.}\n#OPTIONAL{?item wdt:P571 ?Date.}\n#OPTIONAL{?item wdt:P180 ?Dépeint.}\n#OPTIONAL{?item wdt:P195 ?Département.}\n#OPTIONAL{?item wdt:P31 ?Type.}\nOPTIONAL{?item wdt:P18 ?image.}\nOPTIONAL{?item wdt:P217 ?INV.}\nOPTIONAL{?item wdt:P1212 ?Atlas.}\nFILTER(STRSTARTS(?INV, 'S')) .\nSERVICE wikibase:label {bd:serviceParam wikibase:language \"fr, en\".}\n}\nGROUP BY ?item ?itemLabel ?TypeLabel ?CréateurLabel ?Date ?DépeintLabel ?DépartementLabel ?Atlas ?image\nORDER BY ?INVs", "context": "Works from Louvre Museum whose inventory number begins with an S", "target": ""}
{"query": "#Lieu de naissance des artistes dont une oeuvre est présente dans la collection du Musée des Beaux-Arts de Québec.\n#defaultView:Map\nSELECT ?artiste ?artisteLabel ?lieuN ?lieuNLabel ?coord\nWHERE {\n  ?artiste wdt:P6379 wd:Q2338135;\n           wdt:P19 ?lieuN.\n  ?lieuN wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\n", "context": "Map of artists with place of their birth on Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?image ?coords\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q2977 ;\n        wdt:P17 wd:Q142 ;\n        wdt:P625 ?coords ;\n        wdt:P18 ?image .\n  FILTER NOT EXISTS { ?item wdt:P5816 wd:Q56556915 }\n  { ?item wdt:P131 wd:Q90 } UNION\n  { ?item wdt:P131/wdt:P131 wd:Q90 } UNION\n  { ?item wdt:P131/wdt:P131/wdt:P131 wd:Q90 }\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?itemLabel", "context": "Location and image of the cathedrals of Paris", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel (MIN(YEAR(?date)) AS ?year) (COUNT(DISTINCT ?actor) AS ?number_of_cast_members)  (COUNT(DISTINCT ?actress) AS ?number_of_female_cast_members) WHERE {\n    ?item wdt:P31/wdt:P279* wd:Q11424; wdt:P1552 wd:Q45171911. \n    OPTIONAL{?item (p:P161/ps:P161|p:P725/ps:P725) ?actor\n            OPTIONAL{?actor wdt:P21 wd:Q6581072. BIND(?actor AS ?actress)}\n            }\n    OPTIONAL{?item wdt:P577 ?date}\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel\nHAVING (COUNT(DISTINCT ?actress) <2)\nORDER BY ?year", "context": "Films claimed to pass the Bechdel test with less than 2 female cast members in Wikidata", "target": ""}
{"query": "SELECT ?PersonLabel ?birthdate\nWHERE{\n  ?Person wdt:P106 wd:Q15855449.\n  ?Person p:P569/psv:P569 [ wikibase:timeValue ?birthdate; wikibase:timePrecision 11 ].\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Magicians with known precise birth date", "target": ""}
{"query": "SELECT ?PID ?PIDLabel (COUNT(?BBLT) AS ?Count) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?PID wdt:P106 wd:Q12299841;\n    wdt:P54 ?BBLT.\n  ?BBLT wdt:P118 wd:Q2470981.\n}\nGROUP BY ?PID ?PIDLabel\nORDER BY DESC (?Count)", "context": "Sheffield Shield cricket players and the number of states they have played in", "target": ""}
{"query": "SELECT ?wetenschapper ?wetenschapperLabel ?university ?universityLabel ?twitter ?followers (CONCAT(\"https://twitter.com/\",?twitter) AS ?twitterlink) ?pic WHERE {\n  ?wetenschapper wdt:P108|wdt:P1416 ?university ;\n                 p:P2002 ?twitterSt .\n  OPTIONAL { ?wetenschapper wdt:P18 ?pic . }\n  ?twitterSt ps:P2002 ?twitter .\n  OPTIONAL { ?twitterSt pqv:P3744/wikibase:quantityAmount ?followers . }\n  ?university wdt:P31 wd:Q3918 ;\n              wdt:P17 wd:Q55 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?followers)", "context": "Number of Twitter followers of scientists at Dutch universities", "target": ""}
{"query": "# All former MPs still living (by first election)\nSELECT DISTINCT ?article ?itemLabel ?constituencyLabel ?firstpartyLabel ?electionLabel (?start as ?firstdate) ?firsttermLabel\n{\n ?membership wdt:P279 wd:Q16707842 .  # find all MP positions\n ?item p:P39 ?positionStatement . ?positionStatement ps:P39 ?membership .  # and people who held such a position\n OPTIONAL { ?positionStatement pq:P768 ?constituency . } # constituency if known\n OPTIONAL { ?positionStatement pq:P4100 ?firstparty . } # party if known\n ?positionStatement pq:P580 ?start . ?positionStatement pq:P582 ?end .\n ?positionStatement pq:P2715 ?election . ?membership wdt:P2937 ?firstterm\n FILTER NOT EXISTS {\n ?membership2 wdt:P279 wd:Q16707842 .\n ?item p:P39 ?positionStatement2 . \n ?positionStatement2 ps:P39 ?membership2 . \n ?positionStatement2 pq:P582 ?end2 .\n FILTER (?end2 <= ?start) .  } # filter out any where they served earlier \n FILTER NOT EXISTS {\n ?membership3 wdt:P279 wd:Q16707842 . ?item p:P39 ?positionStatement3 . \n ?positionStatement3 ps:P39 ?membership3 . FILTER NOT EXISTS { ?positionStatement3 pq:P582 ?end3 . } }\n #not current\n filter not exists { ?item wdt:P570 ?died } . \n optional { ?article schema:about ?item . ?article schema:isPartOf <https://en.wikipedia.org/>. }\n SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n} order by ?firstdate", "context": "Former United Kingdom Parliament MP's still living (by first election)", "target": ""}
{"query": "#Number of paintings in National Gallery on Wikidata with artist&gender\nSELECT (COUNT(?item) AS ?count)\nWHERE {\n   ?item wdt:P195 wd:Q180788 . #National Gallery\n   ?item wdt:P170 ?artist.\n   ?artist wdt:P21 wd:Q6581072. #male = Q6581097\n}", "context": "Number of paintings in National Gallery with artist and gender", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?image ?location ?distance ?sitelink WHERE {\n  SERVICE wikibase:around {\n    ?item wdt:P625 ?location.\n    bd:serviceParam wikibase:center \"[AUTO_COORDINATES]\".\n    bd:serviceParam wikibase:radius \"1\".\n    bd:serviceParam wikibase:distance ?distance.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,es,de\". }\n OPTIONAL { ?item wdt:P18 ?image. } \n OPTIONAL {\n    ?sitelink schema:about ?item.\n    FILTER(REGEX(STR(?sitelink), \"[AUTO_LANGUAGE].wikipedia.org/wiki/\"))\n  }\n  ?item wdt:P31/wdt:P279* wd:Q294440.\n}\nORDER BY asc(?distance)", "context": "Nearby table tennis tables to public spaces for the Wiki Explorer Android application", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?site ?siteLabel (YEAR(?date) AS ?year) ?area ?coords ?image\nWHERE \n{\n  ?site wdt:P31/wdt:P279* wd:Q473972 ;\n        wdt:P17 wd:Q142 ;\n        wdt:P2046 ?area ;\n        p:P1435 ?protectionStatement ;\n        wdt:P625 ?coords .\n  ?protectionStatement ps:P1435 wd:Q19683138 ;\n                       pq:P580 ?date .\n  OPTIONAL { ?site wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?year ?siteLabel", "context": "Map of Ramsar sites in France", "target": ""}
{"query": "# highest software version\nSELECT ?item ?itemLabel ?version WITH {\n  SELECT ?item ?version ?versionNumber WHERE {\n    ?item wdt:P348 ?version.\n    BIND(xsd:integer(REPLACE(STRBEFORE(?version, \".\"), \"^v\", \"\")) AS ?versionNumber)\n  }\n  ORDER BY DESC(?versionNumber)\n  LIMIT 25\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?versionNumber)", "context": "Highest software version numbers recorded on Wikidata", "target": ""}
{"query": "select\n  ?quantity (sample(?quantityLabel) as ?quantityLabel) (sample(?quantityDesc) as ?quantityDesc)\n  ?unit (sample(?unitLabel) as ?unitLabel) (sample(?unitDesc) as ?unitDesc)\n  (group_concat(?unitSymbol; separator = \"; \") as ?unitSymbols)\n  (sample(?unitStatus) as ?unitStatus)\nwhere {\n  values ?source { wd:Q68977959 }\n\n  ?quantity wdt:P279+ wd:Q71550118 .\n  optional { ?quantity rdfs:label ?quantityLabel . filter (lang(?quantityLabel) = \"en\") }\n  optional { ?quantity schema:description ?quantityDesc . filter (lang(?quantityDesc) = \"en\") }\n\n  ?quantity p:P8111 ?quantityStatement .\n  ?quantityStatement prov:wasDerivedFrom [ pr:P248 ?source ] ; ps:P8111 ?unit .\n  optional { ?unit rdfs:label ?unitLabel . filter (lang(?unitLabel) = \"en\") }\n  optional { ?unit schema:description ?unitDesc . filter (lang(?unitDesc) = \"en\") }\n  optional {\n    ?unit p:P5061 [ prov:wasDerivedFrom [ pr:P248 ?source ] ; ps:P5061 ?unitSymbol ] .\n    filter (lang(?unitSymbol) = \"en\")\n  }\n\n  optional { ?quantityStatement a ?bestRank . }\n  bind (if(bound(?bestRank) && ?bestRank = wikibase:BestRank, \"<recommended>\", \"<accepted>\") as ?unitStatus)\n} group by ?quantity ?unit", "context": "Recommended units of measurement listed in the SI Brochure (9th, concise edition)", "target": ""}
{"query": "SELECT ?udalerria ?udalerriaLabel ?mugakideak ?mugakideakLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  { ?udalerria wdt:P31 wd:Q2074737. }\n  UNION\n  { ?udalerria wdt:P31 wd:Q484170. }\n  ?udalerria ((wdt:P131*)/^wdt:P527) wd:Q47588.\n  ?udalerria wdt:P47 ?mugakideak.\n}", "context": "Border with which all the municipalities of the Basque Country share", "target": ""}
{"query": "#defaultView:Map{\"layer\":\"?typeOfLabel\"}\nSELECT ?CentrosSalud  ?CentrosSaludLabel ?typeOf ?typeOfLabel ?coordenadas ?ubicaci_n ?ubicaci_nLabel ?imagen ?sitelinkES\nWHERE {\n  {?CentrosSalud wdt:P31  wd:Q16917.}\n UNION {?CentrosSalud wdt:P31  wd:Q569500.}\n  ?CentrosSalud wdt:P17 wd:Q414.\n   ?CentrosSalud wdt:P31 ?typeOf\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],es\". }\n  OPTIONAL {?CentrosSalud wdt:P625  ?coordenadas.}\n  OPTIONAL {?CentrosSalud wdt:P18  ?imagen.}\n  OPTIONAL {  ?sitelinkES schema:isPartOf <https://es.wikipedia.org/>;\n  schema:about ?CentrosSalud.\n\n}\n\n}\n", "context": "Map of hospitals (blue) and health centers (green) of Argentina", "target": ""}
{"query": "SELECT ?person ?personLabel ?number ?loyalLabel WHERE {\n  ?person wdt:P31 wd:Q5.\n  ?person wdt:P1120 ?number.\n#  ?person wdt:P1345 ?number.\n  OPTIONAL {?person wdt:P945 ?loyal.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\nORDER BY DESC(?number)", "context": "Best snipers of the world by number of victims of killer", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?sous_s_rie_F_19_des_Archives_nationales ?sous_s_rie_F_19_des_Archives_nationalesLabel ?image ?coordonn_es_g_ographiques ?date_de_fondation_ou_de_cr_ation ?cr_ateur ?cr_ateurLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?sous_s_rie_F_19_des_Archives_nationales wdt:P361 wd:Q64556342.\n  OPTIONAL { ?sous_s_rie_F_19_des_Archives_nationales wdt:P18 ?image. }\n  OPTIONAL { ?sous_s_rie_F_19_des_Archives_nationales wdt:P625 ?coordonn_es_g_ographiques. }\n  OPTIONAL { ?sous_s_rie_F_19_des_Archives_nationales wdt:P571 ?date_de_fondation_ou_de_cr_ation. }\n  OPTIONAL { ?sous_s_rie_F_19_des_Archives_nationales wdt:P170 ?cr_ateur. }\n}\nLIMIT 1000", "context": "Plans and photographs of the restoration works of cathedrals in the 19th century contained in the French National Archives)", "target": ""}
{"query": "#defaultView:Map\nSELECT * WHERE {\n  ?q wdt:P17 wd:Q142 ; wdt:P31/wdt:P279* wd:Q55488 ; wdt:P625 ?coord .\n}", "context": "Map of railways stations in France", "target": ""}
{"query": "SELECT ?country ?countryLabel ?leader ?leaderLabel ?birthdate ?age\nWHERE {\n  ?country wdt:P31 wd:Q6256 ; wdt:P30 wd:Q15 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL {\n    ?country wdt:P6 ?leader .\n    OPTIONAL {\n      ?leader wdt:P569 ?birthdate .\n      BIND(YEAR(NOW()) - YEAR(?birthdate) - IF(MONTH(NOW()) < MONTH(?birthdate) || (MONTH(NOW()) = MONTH(?birthdate) && DAY(NOW()) < DAY(?birthdate)), 1, 0) AS ?age) .\n    }\n  }\n}\nORDER BY ?countryLabel ?leaderLabel", "context": "List of African countries with their current leaders and their ages", "target": ""}
{"query": "SELECT DISTINCT ?student ?studentLabel ?advisor ?advisorLabel ?thesisLabel ?year WHERE {\n  VALUES ?professions {\n    wd:Q170790\n    wd:Q82594\n  }\n  ?student wdt:P184 ?advisor;\n    wdt:P106 ?professions.\n  OPTIONAL {\n    ?student wdt:P1026 ?thesis;\n      p:P1026 _:b1.\n    _:b1 pq:P585 ?thesisDate.\n    BIND(YEAR(?thesisDate) AS ?year)\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}\nLIMIT 10", "context": "Point in time of Advisor and Student relationships for professions mathematician and computer scientist", "target": ""}
{"query": "SELECT ?painting ?image ?paintingLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?painting wdt:P31 wd:Q93184;\n    wdt:P170 wd:Q1064767.\n  OPTIONAL { ?painting wdt:P18 ?image. }\n  ?painting wdt:P180 wd:Q11446.\n}\nLIMIT 1000", "context": "Drawings with an image that depicts a ship", "target": ""}
{"query": "#defaultView:Map\nselect ?shape ?parkLabel ?image {\n  ?park wdt:P31/wdt:P279* wd:Q46169 .\n  ?park wdt:P17 wd:Q34 .\n  ?park wdt:P3896 ?shape .\n  OPTIONAL { ?park wdt:P18 ?image . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} limit 100", "context": "Map of National parks in Sweden", "target": ""}
{"query": "#defaultView:Map\nselect ?thing ?label ?shape {\n  ?thing wdt:P3896 ?shape .\n  ?thing wdt:P17 wd:Q30 .\n  ?thing rdfs:label ?label .\n  filter(lang(?label) = 'en')\n  filter(contains(ucase(?label), \"A\"))\n  ?thing wdt:P31 wd:Q35657 .\n}", "context": "US States with an A", "target": ""}
{"query": "SELECT ?mathematician ?mathematicianLabel ?dob ?sibling ?siblingLabel ?siblingProfessionsLabel\nWHERE\n{\n  VALUES ?professions {wd:Q170790 wd:Q82594} .\n  VALUES ?siblingProfessions {wd:Q639669 wd:Q33999}.\n  \n  ?mathematician wdt:P106 ?professions ;\n                 wdt:P3373 ?sibling ;\n                 wdt:P569 ?dob .\n\n  ?sibling wdt:P106 ?siblingProfessions .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}\nORDER BY ASC(?dob)", "context": "Famous mathematicians related to famous actors", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord1\", \"?coord2\", \"?str\", \"?line\"]}\nSELECT * WHERE {\n  ?comm1 p:P31/ps:P31/wdt:P279* wd:Q55488 ; wdt:P17 wd:Q145 ; wdt:P625 ?coord1 ; wdt:P197 ?comm2 .\n  MINUS { ?comm1 p:P31 [ ps:P31 wd:Q55488 ; pq:P582 [] ] } .\n  MINUS { ?comm2 p:P31 [ ps:P31 wd:Q55488 ; pq:P582 [] ] } .\n  MINUS { ?comm1 p:P31 [ ps:P31 wd:Q928830 ] } .\n  MINUS { ?comm2 p:P31 [ ps:P31 wd:Q928830 ] } .\n  ?comm2 wdt:P625 ?coord2 .\n  ?comm1 p:P625 [ ps:P625 []; psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ] .\n  ?comm2 p:P625 [ ps:P625 []; psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ] .\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) .\n}", "context": "West Highlands of Scotland rail line connections", "target": ""}
{"query": "PREFIX vrank:<http://purl.org/voc/vrank#>\n\nSELECT DISTINCT ?uni ?uniLabel ?pr WHERE {\n  ?uni wdt:P31/wdt:P279* wd:Q3918.\n  SERVICE <http://dbpedia.org/sparql> {\n    ?uni vrank:hasRank/vrank:rankValue ?pr\n  }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n  }\n} ORDER BY DESC(?pr) LIMIT 50", "context": "Universities ranked by PageRank on English Wikipedia", "target": ""}
{"query": "#defaultView:TreeMap\nselect ?country ?countryLabel ?state ?stateLabel ?county ?countyLabel {\n  ?state wdt:P31/wdt:P279 wd:Q10864048 .\n  ?county wdt:P31/wdt:P279 wd:Q13220204 .\n  ?state wdt:P17 ?country . \n  ?county wdt:P131 ?state .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Interactive, zoomable tree map of second level administrative regions of the world", "target": ""}
{"query": "SELECT ?itemLabel (COUNT(distinct ?alfabet) AS ?cnt) WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q9788.\n  FILTER NOT EXISTS {?alfabet wdt:P31/wdt:P279* wd:Q3241972.}\n  FILTER NOT EXISTS {?alfabet wdt:P279+ wd:Q9788.}\n  FILTER NOT EXISTS {?alfabet wdt:P31/wdt:P279* wd:Q15139721.}\n  FILTER NOT EXISTS {?alfabet wdt:P31/wdt:P279* wd:Q10856962.}\n  FILTER NOT EXISTS {?alfabet wdt:P31/wdt:P279* wd:Q188725.}\n  ?alfabet wdt:P527 ?item.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"ru, en\" . \n  }\n}\nGROUP BY ?item ?itemLabel\nORDER BY DESC(?cnt)", "context": "Number of alphabets which use this letter", "target": ""}
{"query": "#Metabolites and the species where they are found in\nPREFIX pr: <http://www.wikidata.org/prop/reference/>\nPREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\nPREFIX prov: <http://www.w3.org/ns/prov#>\n\nSELECT ?compound ?compoundLabel ?speciesLabel ?source ?sourceLabel ?doi ?wpid WHERE {\n  ?compound wdt:P31 wd:Q11173.\n  MINUS { ?compound wdt:P31 wd:Q8054. }\n  ?compound p:P703 ?statement.\n  ?statement rdf:type wikibase:BestRank.\n  ?statement ps:P703 ?species.\n  FILTER ( ?species != wd:Q15978631 )\n  OPTIONAL {\n    ?statement (prov:wasDerivedFrom/pr:P248) ?source.\n    OPTIONAL { ?source wdt:P2410 ?wpid. }\n    OPTIONAL { ?source wdt:P356 ?doi. }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY ASC(?compound)", "context": "Metabolites and the species where they are found in", "target": ""}
{"query": "#defaultView:Map\nSELECT ?p ?pLabel ?loc WITH {\n  \nSELECT ?p ?longitude ?test WHERE {\n  ?p wdt:P17 wd:Q145 .\n  ?p p:P625 [ \n    psv:P625 [  \n      wikibase:geoLongitude ?longitude ] \n    ]\n  FILTER (ABS(?longitude + 1.83333) < 0.01)\n  } } AS %matches\nWHERE {\n  INCLUDE %matches\n  ?p wdt:P625 ?loc .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Items in the United Kingdom with coordinate location (P625) on or near 1 degree 50 west", "target": ""}
{"query": "#defaultView:Map\nSELECT ?museum ?museumLabel ?coord ?image WHERE {\n  ?museum wdt:P31/wdt:P279* wd:Q33506 ; wdt:P17 wd:Q948 .\n  OPTIONAL { ?museum wdt:P625 ?coord }\n  OPTIONAL { ?museum wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of Tunisian museums", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?line\"]}\nSELECT ?q1 ?q2 ?label ?coord1 ?coord2 ?dist ?layer ?line WHERE {\n  ?q1 wdt:P31 wd:Q484170 ; rdfs:label ?label ; wdt:P625 ?coord1 . FILTER (lang(?label) = \"fr\").\n  ?q1 p:P625/psv:P625 ?valuenode1. ?valuenode1 wikibase:geoLatitude ?lat1 . ?valuenode1 wikibase:geoLongitude ?lon1.\n  ?q2 wdt:P31 wd:Q484170 ; rdfs:label ?label ; wdt:P625 ?coord2 . FILTER (lang(?label) = \"fr\").\n  ?q2 p:P625/psv:P625 ?valuenode2. ?valuenode2 wikibase:geoLatitude ?lat2 . ?valuenode2 wikibase:geoLongitude ?lon2.\n  FILTER ( str(?q1) > str(?q2) )\n  BIND(geof:distance(?coord1, ?coord2) AS ?dist).\n  BIND(floor(?dist/100) AS ?layer).\n  BIND(CONCAT('LINESTRING (', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line)\n  FILTER ( ?dist > 100 )\n}\nLIMIT 100", "context": "Map of pairs of homonymous French municipalities linked by a colored line according to the distance", "target": ""}
{"query": "#female botanists and botanical collectors born<1900, sorted by number of sitelinks, with some related info\n#\nSELECT ?item ?itemLabel (SAMPLE(?occname) AS ?occname) ?sitelinks ?WDstatements ?article (SAMPLE(?birth) AS ?birth) (SAMPLE(?death) AS ?death) (SAMPLE(?bloodhound) AS ?bloodhound) (SAMPLE(?harvard) AS ?harvard) WHERE {\n  #?occ wdt:P279|wdt:P279/wdt:P279* wd:Q2374149.\n  VALUES ?occ { wd:Q2374149 wd:Q2083925 }\n  ?item wdt:P106 ?occ .\n  OPTIONAL { ?item wdt:P108 ?emp . }\n  ?item wdt:P21 wd:Q6581072;\n    wikibase:statements ?WDstatements;\n    wikibase:sitelinks ?sitelinks.\n  OPTIONAL {\n   ?article schema:about ?item;\n      schema:inLanguage \"en\";\n      schema:isPartOf <https://en.wikipedia.org/>.\n  }\n  ?occ rdfs:label ?occname .\n  OPTIONAL {?item wdt:P569 ?birth .}\n  OPTIONAL {?item wdt:P570 ?death .}\n  OPTIONAL {?item wdt:P6944 ?bloodhound .}\n  OPTIONAL {?item wdt:P6264 ?harvard .}\n  FILTER(LANG(?occname) = \"en\").\n  FILTER((?birth<\"1900-01-01T00:00:00Z\"^^xsd:dateTime)||(?death<\"1920-01-01T00:00:00Z\"^^xsd:dateTime)).\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n  }\n}\nGROUP BY ?item ?itemLabel ?sitelinks ?WDstatements ?article\nORDER BY DESC (?sitelinks)", "context": "Women botanists or botanical collectors with a birth date prior to 1900", "target": ""}
{"query": "SELECT ?_vBIB20 ?_vBIB20Label ?datum ?tibURL WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?_vBIB20 wdt:P361 wd:Q94495218;\n           wdt:P577 ?datum;\n           wdt:P973 ?tibURL.\n}\nLIMIT 100", "context": "vBIB20 lectures in Wikidata", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?layer\",\"?label\",\"?geo\"]}\nSELECT DISTINCT ?item ?itemLabel ?pic ?geo ?layer ?iNatlinkI ?iNatlinkII WHERE {\n  ?item wdt:P31 wd:Q1221156.\n  ?item wdt:P7471 ?iNat.\n  ?item wdt:P3896 ?geo.\n  ?item rdfs:label ?label FILTER (lang(?label) = \"de\") .\n  BIND(?label AS ?layer).\n  BIND (?itemLabel AS ?rgb)\n  OPTIONAL { ?item wdt:P18 ?pic. }\n  BIND(URI(CONCAT(\"https://www.inaturalist.org/places/\", STR(?iNat))) AS ?iNatlinkI)\n  BIND(URI(CONCAT(\"https://www.inaturalist.org/observations?subview=grid&view=species&place_id=\", STR(?iNat))) AS ?iNatlinkII)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en\". }\n}", "context": "Map of German states with links to iNaturalist observations", "target": ""}
{"query": "#defaultView:Graph\nSELECT \n  ?author1 ?author1Label (SAMPLE(?image1_) AS ?image1) ?rgb\n  ?author2 ?author2Label (SAMPLE(?image2_) AS ?image2)\n\nWITH {\n  SELECT\n    ?vortrag1 ?author1 \n    (SAMPLE(?dark_rgb_) AS ?dark_rgb)\n    (SAMPLE(?light_rgb_) AS ?light_rgb)\n  WHERE {\n    # Find people at the event\n    { \n      ?vortrag1 wdt:P361 wd:Q94495218; wdt:P50 ?author1 . \n      BIND (\"EEEEEE\" AS ?dark_rgb_)\n    }\n\n  }\n  GROUP BY ?author1 ?vortrag1\n} AS %authors\nWITH {\n  SELECT DISTINCT ?author1 ?author2 ?rgb  WHERE {\n    INCLUDE %authors\n    #wd:Q94495218 (wdt:P823 | wdt:P664 | ^wdt:P1344 | wdt:P710 | ^wdt:P4745 / ^wdt:P1433 / wdt:P50)  ?author2 . \n            \n    # Find co-authors on #vBIB\n    ?vortrag2 wdt:P361 wd:Q94495218; wdt:P50 ?author2 . \n    FILTER (?author1 != ?author2)\n    FILTER (?vortrag1 = ?vortrag2)\n    BIND(COALESCE(?dark_rgb, ?light_rgb) AS ?rgb)\n  }\n} AS %result\nWHERE {\n  INCLUDE %result\n    \n  OPTIONAL { ?author1 wdt:P18 ?image1_ . }\n  OPTIONAL { ?author2 wdt:P18 ?image2_ . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,nl,no,sv,ru,zh\".\n  }\n}\nGROUP BY ?author1 ?author1Label ?rgb ?author2 ?author2Label  ?vortrag1 ?vortrag2\n ", "context": "Network of researchers who are vBIB20 co-presenters and are linked with a Wikidata item", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?marriage ?marriageLabel ?time ?image\n{\n  {\n    SELECT ?marriage ?time {\n      ?marriage wdt:P31|wdt:P279 wd:Q17422;\n                wdt:P580 ?time.\n    }\n  }\n  UNION {\n    SELECT ?marriage ?time {\n      ?marriage wdt:P31|wdt:P279 wd:Q17422;\n            wdt:P585 ?time.\n    }\n  }\n  OPTIONAL{\n    ?marriage wdt:P18 ?image.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Timeline of legalized same-sex marriages by country", "target": ""}
{"query": "SELECT ?item ?itemLabel ?countryLabel ?weight ?unitLabel ?img WHERE {\n    ?item wdt:P31 wd:Q170984 .\n    OPTIONAL {\n      ?item p:P2067 ?weight_stmt .\n      ?weight_stmt ps:P2067 ?weight .\n      ?weight_stmt psv:P2067/wikibase:quantityUnit ?unit .\n      ?weight_stmt psv:P2067/wikibase:quantityNormalized/wikibase:quantityAmount ?normalised .\n    }\n    OPTIONAL {?item wdt:P17 ?country} .\n    OPTIONAL {?item wdt:P18 ?img} .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,fr,de,es,it,ru\". }\n} ORDER BY DESC(?normalised) ?itemLabel", "context": "Weight of royal crowns", "target": ""}
{"query": "#created by Mahir256\n#defaultView:Map{\"hide\":[\"?shape\", \"?rgb\"]}\nSELECT ?item ?itemLabel ?state ?stateLabel ?cases ?shape ?rgb ?time WHERE {\n  wd:Q84055514 wdt:P527 ?item. \n  ?item wdt:P276 ?state ; p:P1603 ?casestmt. \n  ?state wdt:P3896 ?shape.\n  ?casestmt ps:P1603 ?cases ; pq:P585 ?time .\n  { SELECT (MAX(?cases_) as ?mc) {\n      wd:Q84055514 wdt:P527/wdt:P1603 ?cases_\n  } }\n  FILTER NOT EXISTS { ?item p:P1603/pq:P585 ?time_. FILTER(?time < ?time_) }\n  BIND(IF(?cases > (5 * ?mc)/6,\"a50f15\", IF(?cases > (2 * ?mc)/3,\"de2d26\",\n       IF(?cases > ?mc/2,\"fb6a4a\", IF(?cases > ?mc/3,\"fc9272\",\n       IF(?cases > ?mc/6,\"fcbba1\", \"fee5d9\"))))) as ?rgb)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Recorded COVID-19 cases in difference Indian states", "target": ""}
{"query": "SELECT ?p ?pLabel ?groupLabel (URI(CONCAT(\"https://twitter.com/\", ?tw)) AS ?twitterlink) WHERE {\n?p p:P39 ?s. ?s ps:P39 wd:Q77685926; pq:P4100 ?group\nOPTIONAL {?p wdt:P2002 ?tw}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?groupLabel ?pLabel", "context": "Twitter accounts of current UK MP's", "target": ""}
{"query": "SELECT ?thing ?thingLabel ?count with {\n  SELECT ?thing (count(?thing) as ?count) WHERE { \n    ?item wdt:P108 ?thing .\n    ?item wdt:P21 wd:Q6581072 .\n    ?item wdt:P31 wd:Q5 . \n    } group by ?thing } as %i\nwhere\n  {\n  include %i\n  minus {?thing wdt:P31/wdt:P279* wd:Q3918 . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }  order by desc(?count)\n      \n\n\n\n\n\n", "context": "Number of female employees at different organizations that Wikidata knows about", "target": ""}
{"query": "#defaultView:Map\nSELECT * WHERE {\n  ?bib wdt:P31/wdt:P279* wd:Q7075 ;\n       wdt:P2037 ?github ;\n       wdt:P625 ?coordinate .\n}", "context": "Location of libraries that have a GitHub repository", "target": ""}
{"query": "#defaultView:Map\nSELECT ?Die_Gartenlaube ?Die_GartenlaubeLabel ?schlagwort1Label ?schlagwort2Label ?coords WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  {\n    SELECT * WHERE {\n      ?Die_Gartenlaube wdt:P1433 wd:Q655617;\n        wdt:P921 ?schlagwort1;\n                       wdt:P921 ?schlagwort2.\n    }\n  }\n  { SELECT * WHERE { ?schlagwort1 wdt:P31 wd:Q12136. } }\n  { SELECT * WHERE { ?schlagwort2 wdt:P625 ?coords. }}\n}\n", "context": "Location of 19th century infectious diseases published in Die Gartenlaube", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT ?blkoe ?blkoeLabel ?image ?artikel ?artikelLabel ?edgeLabel WHERE {\n  ?artikel wdt:P1433 wd:Q655617;\n    wdt:P921 ?schlagwort.\n  ?schlagwort wdt:P31 wd:Q5; wdt:P18 ?image.\n  ?blkoe wdt:P1433 wd:Q665807;\n         wdt:P31 wd:Q19389637;\n    wdt:P921 ?schlagwort.\n  BIND(\"Thema in\" AS ?edgeLabel)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?schlagwort", "context": "People with an entry in the Biographical Lexicon of the Austrian Empire and biographical articles in Die Datenlaube", "target": ""}
{"query": "#################################################################\n# Fictional \"spaceship captain\"s from the \"Star Trek multiverse\" \n#################################################################\nSELECT \n  ?char \n  ?charName    \n  (GROUP_CONCAT(DISTINCT ?occupationLabel; SEPARATOR = \",\") AS ?arrOccupations) \n\nWHERE {\n    ?char wdt:P1080 wd:Q18043309; # \"from narrative universe\" \"Start Trek multiverse\"\n          wdt:P1080 ?universe; #\"from narrative universe\"\n          wdt:P31 wd:Q15632617; # \"instance of\" \"fictional human\"          \n          wdt:P106 wd:Q87265645; # occupation \"spaceship captain\"          \n          wdt:P106 ?occupation.\n\n    SERVICE wikibase:label {\n      bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n      ?char rdfs:label ?charName.\n      ?universe rdfs:label ?universeLabel.\n      ?type rdfs:label ?typeLabel.\n      ?country_of_citizenship  rdfs:label ?country_of_citizenshipLabel.      \n      ?place_of_birth rdfs:label ?place_of_birthLabel.\n      ?occupation rdfs:label ?occupationLabel.\n    }  \n  }\nGROUP BY ?char ?charName ?arrOccupations\n\n", "context": "Fictional spaceship captains from the Star Trek multiverse", "target": ""}
{"query": "#defaultView:Timeline\nPREFIX dct: <http://purl.org/dc/terms/>\nPREFIX skos: <http://www.w3.org/2004/02/skos/core#>\nPREFIX dbc: <http://dbpedia.org/resource/Category:>\nPREFIX dbo: <http://dbpedia.org/ontology/>\n\nSELECT DISTINCT  ?wditem ?wditemLabel ?date ?image ?work ?workLabel ?coord ?description WHERE {  \n    SERVICE <http://dbpedia.org/sparql> {\n        ?item dct:subject dbc:Selenographers ;\n              owl:sameAs ?wditem FILTER regex (?wditem, \"wikidata.org\") .\n    }        \n    SERVICE <https://query.wikidata.org/sparql> {\n\t  {?wditem wdt:P569 ?date .}\n      OPTIONAL {?wditem wdt:P18 ?image } .  \n      ?wditem rdfs:label ?wditemLabel FILTER (LANG(?wditemLabel)=\"en\") .\n    }\n}", "context": "Timeline of Selenographers, for the birthday of German astronomer Johann Heinrich von Mädler and his famous map of the Moon", "target": ""}
{"query": "#defaultView:Map\nSELECT ?place ?placeLabel ?location ?image ?layer WHERE {\n  {\n    wd:Q15296697 wdt:P625 ?arcLoc.\n    SERVICE wikibase:around {\n      ?place wdt:P625 ?location.\n      bd:serviceParam wikibase:center ?arcLoc;\n        wikibase:radius \"2\".\n    }\n    BIND(\"pov\" AS ?layer)\n    OPTIONAL { ?place wdt:P18 ?image. }\n  }\n  UNION\n  {\n    BIND(wd:Q15296697 AS ?place)\n    ?place wdt:P625 ?location; wdt:P18 ?image\n    BIND(\"Zentrum\" AS ?layer)\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Points of interest 2 km within St. Jakobus church, Pesterwitz", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?image WHERE {\n  ?item wdt:P921 wd:Q65560542 .  \n  ?item wdt:P31 wd:Q3305213 . \n  ?item wdt:P18 ?image . \n  } \n LIMIT 4000", "context": "Images of religious art whose main theme is Pentecost", "target": ""}
{"query": "#defaultView:Map\nSELECT ?author ?authorLabel ?orcid ?twitter ?location\nWHERE\n{\n  ?author wdt:P31 wd:Q5;\n          wdt:P496 ?orcid;\n          wdt:P2002 ?twitter.\n  OPTIONAL { ?author wdt:P551/wdt:P625 ?location. }\n  OPTIONAL { ?author wdt:P19/wdt:P625 ?location. }\n  FILTER(BOUND(?location)).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "World map of authors with both an ORCID and Twitter account ID's", "target": ""}
{"query": "#defaultView:Map\n#Map of Art+Feminism 2020 editathons showing DATE, NUMBER OF PARTICIPANTS, DASHBOARD LINK\nSELECT ?location ?locationLabel ?coord ?page ?point_in_time ?number_of_participants WHERE {\n  wd:Q95116664 p:P276 ?statement.\n  ?statement ps:P276 ?location.\n  ?location wdt:P625 ?coord.\n  OPTIONAL { ?statement pq:P856 ?page. }\n  OPTIONAL { ?statement pq:P585 ?point_in_time. }\n  OPTIONAL { ?statement pq:P1132 ?number_of_participants. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of 2020 Art+Feminism editathons", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?itemDescription (URI(REPLACE(?pattern, '\\\\$1', ?carthalia)) AS ?carthalia_URL) (SAMPLE(?coord) AS ?coord) \nWHERE {\n        wd:P5652 wdt:P1630 ?pattern .\n        ?item wdt:P5652 ?carthalia .\n        ?item wdt:P625 ?coord .\n        SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,fr,it,ru\". }\n    }\nGROUP BY ?item ?itemLabel ?itemDescription ?carthalia ?carthalia_URL ?pattern ?coord\nLIMIT 6000", "context": "Postcards of exterior and interior views of theatres on CARTHALIA", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coor ?popul\n{\n    ?item wdt:P31/wdt:P279* wd:Q7930989;\n          wdt:P17 wd:Q159.\n    MINUS {[] (wdt:P19|wdt:P20)/wdt:P131* ?item .}\n    ?item wdt:P625 ?coor.\n  ?item wdt:P1082 ?popul.\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?popul)", "context": "List of cities (or map) of Russia where no one famous was born and died", "target": ""}
{"query": "\nSELECT\n  ?number_of_citing_works\n  ?author ?authorLabel\n  ?organization ?organizationLabel\n  ?example_work ?example_workLabel\nWITH {\n  SELECT DISTINCT ?author WHERE {\n    ?author wdt:P27 | wdt:P1416/wdt:P17 | wdt:P108/wdt:P17 wd:Q43 .\n  }\n} AS %authors\nWITH {\n  SELECT\n    ?author \n    (COUNT(DISTINCT ?citing_work) AS ?number_of_citing_works)\n    (SAMPLE(?organization_) AS ?organization)\n    (SAMPLE(?work) AS ?example_work)\n  WHERE {\n    INCLUDE %authors\n    ?work wdt:P50 ?author .\n    OPTIONAL { ?citing_work wdt:P2860 ?work . }\n    OPTIONAL {\n      ?author wdt:P1416 | wdt:P108 ?organization_ .\n      ?organization_ wdt:P17 wd:Q43\n    }\n  }\n  GROUP BY ?author \n} AS %results\nWHERE {\n  INCLUDE %results\n  service wikibase:label { bd:serviceParam wikibase:language \"en\" . } \n}\nORDER BY DESC(?number_of_citing_works) \n", "context": "Turkish scientists by citation count", "target": ""}
{"query": "PREFIX wd: <http://www.wikidata.org/entity/>\nPREFIX wdt: <http://www.wikidata.org/prop/direct/>\nSELECT ?paper ?paperLabel ?video WHERE {\n  ?paper wdt:P1433 wd:Q95688968 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n  OPTIONAL {?paper wdt:P973 ?video .}\n} ORDER BY ?paperLabel", "context": "ESWC2020 papers with links to video lectures", "target": ""}
{"query": "SELECT ?item ?itemLabel\n   WHERE \n     {\n       ?item wdt:P31 wd:Q16521 .\n       ?item wdt:P171* wd:Q5113 .  \n       FILTER EXISTS { ?wen schema:about ?item . ?wen schema:inLanguage \"en\" }\n       FILTER NOT EXISTS {?item wdt:P18 ?pic}\n       SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" .}\n       }", "context": "Species of birds", "target": ""}
{"query": "SELECT\n  ?year ?fellow ?fellowLabel ?fellowDescription ?image ?orcid ?twitter ?github ?homepage ?wikimedia\nWHERE {\n  ?fellow p:P166 ?award_statement .  \n  ?award_statement ps:P166 wd:Q42092479 .\n  OPTIONAL {\n    ?award_statement pq:P580 ?time \n    BIND(YEAR(?time) AS ?year)\n  }\n  OPTIONAL { ?fellow wdt:P18 ?image }\n  OPTIONAL {\n    ?fellow wdt:P496 ?orcid_id\n    BIND(URI(CONCAT(\"https://orcid.org/\",?orcid_id)) AS ?orcid)\n  } \n  OPTIONAL {\n    ?fellow wdt:P2037 ?github_name\n    BIND(URI(CONCAT(\"https://github.com/\",?github_name)) AS ?github)\n  } \n  OPTIONAL {\n    ?fellow wdt:P2002 ?twitter_name\n    BIND(URI(CONCAT(\"https://twitter.com/\",?twitter_name)) AS ?twitter)            \n  }\n  OPTIONAL {\n    ?fellow wdt:P856 ?homepage\n  }\n  OPTIONAL {    \n    ?fellow wdt:P4174 ?wikimedia_name\n    BIND(URI(CONCAT(\"https://www.wikidata.org/w/index.php?title=Special%3ACentralAuth&target=\",?wikimedia_name)) AS ?wikimedia)            \n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,no,ru,sv,zh\" . }\n}\nORDER BY DESC(?year)", "context": "List of open science fellows", "target": ""}
{"query": "SELECT distinct ?p1 ?p1Label ?p2 ?p2Label WHERE {\n  ?p1 wdt:P39 wd:Q1939555 .\n  ?p2 wdt:P39 wd:Q1939555 .\n  ?p1 wdt:P26 ?p2 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Spouses of the German Bundestag", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?citizen_ofLabel\nWHERE {\n  hint:Query hint:optimizer \"None\".\n  SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:api \"Search\";\n                    wikibase:endpoint \"www.wikidata.org\";\n                    mwapi:srsearch \"slave haswbstatement:P31=Q5\".\n    ?title wikibase:apiOutput mwapi:title.\n  }\n  BIND(IRI(CONCAT(STR(wd:), ?title)) AS ?item)\n  FILTER NOT EXISTS { ?item wdt:P106 wd:Q17769800. }\n  optional {?item wdt:P27 ?citizen_of . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n# LIMIT 10000", "context": "MWAPI searches in wikidata about people descibed as slave traders by citizenship", "target": ""}
{"query": "#defaultView:Graph\n#Rose cutivars, query by Jura1, June 14, 2020:  https://www.wikidata.org/wiki/Wikidata:WikiProject_Roses/lists/cultivars/Wikipedia_articles\nSELECT ?item ?itemLabel ?year1 ?pic1 ?hasdescendant ?hasdescendantLabel ?year2 ?pic2 # ?edgeLabel\n{\n\t?item wdt:P31 wd:Q26817508 . \n    ?item ^ wdt:P1531 ?hasdescendant . \n    OPTIONAL { ?item wdt:P18 ?pic1 }.  \n    OPTIONAL { ?hasdescendant wdt:P18 ?pic2 } .  \n    OPTIONAL { ?hasdescendant wdt:P170 ?edge } \n    OPTIONAL { ?item wdt:P571 ?inc1 .          BIND(YEAR(?inc1) as ?year1)  } .  \n    OPTIONAL { ?hasdescendant wdt:P571 ?inc2 . BIND(YEAR(?inc2) as ?year2)  } .  \n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Rose cultivars", "target": ""}
{"query": "#List of present-day countries and capital(s)\n#added before 2016-10\nSELECT DISTINCT ?country ?countryLabel ?capital ?capitalLabel\nWHERE\n{\n  ?country wdt:P31 wd:Q3624078 .\n  #not a former country\n  FILTER NOT EXISTS {?country wdt:P31 wd:Q3024240}\n  #and no an ancient civilisation (needed to exclude ancient Egypt)\n  FILTER NOT EXISTS {?country wdt:P31 wd:Q28171280}\n  OPTIONAL { ?country wdt:P36 ?capital } .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY ?countryLabel", "context": "List of present-day countries and capital(s)", "target": ""}
{"query": "SELECT distinct ?country ?countryLabel ?aa WHERE {\n\n    ?country p:P31 [ps:P31 wd:Q3624078 ] . # sovereign state\n    ?country skos:altLabel ?alias .\n  bind(str(?alias) as ?aa)\n#    filter(lang(?alias)=\"en\")\n    SERVICE wikibase:label {\n       bd:serviceParam wikibase:language \"en\"\n    }\n}", "context": "Aliases of all sovereign states in Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?slave ?slaveLabel (SAMPLE(?image) AS ?image) ?coords WHERE {\n  ?slave wdt:P3716|wdt:P106 wd:Q12773225.\n  ?item wdt:P138|wdt:P180|wdt:P547 ?slave .\n  OPTIONAL { ?item wdt:P18 ?image. }\n  OPTIONAL { ?item wdt:P625 ?coords. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" } .\n} \nGROUP BY ?item ?itemLabel ?slave ?slaveLabel ?coords\nLIMIT 10000", "context": "Monuments named after/commemorating/depicting slave traders", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?statueLocation\"]}\nSELECT DISTINCT ?statue ?statueLabel ?statueLocation ?statueImage ?person ?personLabel ?personDescription WHERE {\n  ?statue wdt:P31/wdt:P279* wd:Q860861;\n          wdt:P180|wdt:P547 ?person.\n  OPTIONAL { ?statue wdt:P625 ?statueLocation. }\n  OPTIONAL { ?statue wdt:P18 ?statueImage. }\n  ?person wdt:P31 wd:Q5.\n  {\n    VALUES ?occupation { wd:Q10076267 wd:Q17769800 }\n    ?person wdt:P106 ?occupation.\n  } UNION {\n    ?person wdt:P1830|^wdt:P127 ?slave.\n    ?slave wdt:P31 wd:Q5;\n           p:P3716/ps:P3716 wd:Q12773225.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Statues and monuments of slaveholders or traders", "target": ""}
{"query": "# members of the Ku Klux Klan whose description doesn’t mention that\nSELECT ?item ?itemLabel ?itemDescription WHERE {\n  ?item wdt:P31 wd:Q5;\n        wdt:P463 wd:Q47131;\n        rdfs:label ?itemLabel;\n        schema:description ?itemDescription.\n  FILTER(LANG(?itemLabel) = \"en\")\n  FILTER(LANG(?itemDescription) = \"en\")\n  FILTER(!CONTAINS(?itemDescription, \"Klux\"))\n  FILTER(!CONTAINS(?itemDescription, \"KKK\"))\n}", "context": "Members of the Ku Klux Klan whose English description doesn’t mention that", "target": ""}
{"query": "#People that received both Academy Award and Nobel Prize\nSELECT DISTINCT ?base ?baseLabel ?namesake ?namesakeLabel WHERE {\n  ?base wdt:P31 wd:Q245016.\n  ?base wdt:P17 wd:Q30.\n  ?base wdt:P138 ?namesake.\n  ?namesake wdt:P241 wd:Q1752901.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n  }\n}", "context": "Bases named after Union soldiers", "target": ""}
{"query": "SELECT DISTINCT\n  ?conf ?confLabel\n  ?startDate ?endDate\n  ?url \n  ?hashtag\n  (GROUP_CONCAT(DISTINCT ?envLabel;separator=\", \") AS ?environments)\n  (GROUP_CONCAT(DISTINCT ?orgLabel;separator=\", \") AS ?organizers)\nWHERE {\n\n  # web conferences with homepage\n  ?conf wdt:P31/wdt:P279* wd:Q1543677 ; \n        wdt:P856 ?url .\n  \n  OPTIONAL {   # which software is used?\n    ?conf wdt:P2283 ?env .\n    ?env wdt:P31/wdt:P279* wd:Q7397\n  }\n\n  OPTIONAL { ?conf wdt:P580 ?startDate }\n  OPTIONAL { ?conf wdt:P582 ?endDate }\n  OPTIONAL { ?conf wdt:P2572 ?hashtag }\n  OPTIONAL { ?conf wdt:P664 ?org }\n\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n    ?conf rdfs:label ?confLabel .\n    ?env rdfs:label ?envLabel .\n    ?org rdfs:label ?orgLabel .\n  }\n} GROUP BY ?conf ?confLabel ?url ?startDate ?endDate ?hashtag ?organizers", "context": "List of virtual conferences and their environment", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT ?Juror ?JurorLabel ?imageJur ?Preistraeger ?PreistraegerLabel (YEAR(?Zeitpunkt) AS ?Jahr) ?image ?edgeLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Preistraeger p:P166 ?Preis.\n  OPTIONAL { ?Preistraeger wdt:P18 ?image. }\n  ?Preis ps:P166 wd:Q339982;\n    pq:P585 ?Zeitpunkt.\n  OPTIONAL {\n    ?Preis pq:P4353 ?Juror.\n    OPTIONAL { ?Juror wdt:P18 ?imageJur. }\n  }\n  BIND(\"nominiert \" AS ?edgeLabel)\n}\nORDER BY DESC (?Zeitpunkt)", "context": "Network of the Erich Fried Prize winners", "target": ""}
{"query": "# If wife of Narimantas is daughter of Toqta, khan of the Golden Horde, then Genghis Khan had a number of Russian descents. This is currently not proven.\n# See https://en.wikipedia.org/wiki/Descent_from_Genghis_Khan#Russia_and_East_Europe\nSELECT ?item ?itemLabel (COUNT(DISTINCT ?sitelink) as ?wp) {\n  wd:Q720 wdt:P40+ ?item.\n  ?item wdt:P27 ?nat.\n  VALUES ?nat {wd:Q186096 wd:Q34266 wd:Q139319 wd:Q2305208 wd:Q2184 wd:Q15180 wd:Q159}\n  ?sitelink schema:about ?item; schema:isPartOf ?site.\n  ?site wikibase:wikiGroup \"wikipedia\" .\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n  }\n} GROUP BY ?item ?itemLabel ORDER BY DESC(?wp)", "context": "Notable Russian people with possible (but unproved) descent from Genghis Khan", "target": ""}
{"query": "SELECT ?item ?itemLabel (COUNT(DISTINCT ?sitelink) as ?wp) {\n  ?ancestor wdt:P40* ?item.\n  ?ancestor wdt:P172 ?ethnics.\n  ?ethnics wdt:P279* wd:Q36747.\n  ?item wdt:P106 ?occupation.\n  {?occupation wdt:P279* wd:Q1650915.} UNION {?occupation wdt:P279* wd:Q3400985.}\n  ?sitelink schema:about ?item; schema:isPartOf ?site.\n  ?site wikibase:wikiGroup \"wikipedia\" .\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n  }\n} GROUP BY ?item ?itemLabel ORDER BY DESC(?wp)", "context": "Researchers or academics with Native American ancestry", "target": ""}
{"query": "\nSELECT ?value ?valueLabel ?cnt\n{\n  {\nSELECT ?value  (COUNT(?value) AS ?cnt)\n       {\n          ?item wdt:P172 ?value \n       }\n       GROUP BY ?value\n\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?cnt)\n", "context": "Count of items by ethnic group", "target": ""}
{"query": "#defaultView:Map\nSELECT ?station ?stationLabel ?coords ?line ?layer ?rgb WHERE {\n?station wdt:P31 wd:Q928830; \n         wdt:P131* wd:Q104231;\n         wdt:P625 ?coords;\n         p:P197 [ps:P197 ?pred;\n                 pq:P81 ?subwayLine].\n?pred wdt:P625 ?cds;\n      wdt:P31 wd:Q928830.\nBIND(IF(xsd:double(?depth/10) = xsd:integer(?depth/10),?cds,\"\") as ?coords)\n?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\nBIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n?subwayLine wdt:P465 ?rgb .\nVALUES (?subwayLine ?layer) {\n  (wd:Q1185773 \"Gröna linjen\")  #Green line\n  (wd:Q766945 \"Röda linjen\")    #Red line\n  (wd:Q887043 \"Blå linjen\")     #Blue line\n}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"sv\". }         \n}", "context": "Metro map of Stockholm Country showing line colors and layers", "target": ""}
{"query": "SELECT DISTINCT ?country ?countryLabel\nWITH {\n  SELECT DISTINCT ?country { ?country wdt:P31 wd:Q6256 }\n} AS %allcountries\nWITH {\n  SELECT DISTINCT ?country { [] wdt:P1435 wd:Q9259 ; wdt:P17 ?country }\n} AS %heritagecountries\nWHERE {\n  INCLUDE %allcountries\n  MINUS { INCLUDE %heritagecountries }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?countryLabel", "context": "Countries that have no world heritage sites", "target": ""}
{"query": "# Statues of Women, per country\nSELECT ?country ?countryLabel (COUNT(DISTINCT ?item) AS ?count) WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q179700 ; wdt:P17 ?country ; wdt:P180 ?depicts .\n  ?depicts wdt:P31 wd:Q5 ; wdt:P21 wd:Q6581072 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?country ?countryLabel\nORDER BY DESC(?count)", "context": "Number of statues of women, per country", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?region ?regionLabel ?country ?countryLabel ?area ?population ?density ?coords\nWHERE \n{\n  ?region wdt:P31/wdt:P279* wd:Q10864048 ;\n          wdt:P17 ?country ;\n          wdt:P1082 ?population ;\n          p:P2046/psv:P2046 [\n            wikibase:quantityAmount ?baseArea ;\n            wikibase:quantityUnit [\n              p:P2370/psv:P2370 [\n                wikibase:quantityAmount ?unitArea;\n                wikibase:quantityUnit wd:Q25343\n              ]\n            ]\n          ] ;\n          wdt:P625 ?coords .\n  FILTER NOT EXISTS { ?region wdt:P576 ?x }\n  BIND(?baseArea * ?unitArea / 1000000 AS ?area) .\n  FILTER ( ?area > 0 )\n  FILTER ( ?population > 0 )\n  BIND ((?population / ?area) AS ?density)\n  FILTER ( ?density < 30 )\n  ?country wdt:P30 wd:Q46 .\n  FILTER NOT EXISTS { ?country wdt:P17 wd:Q3024240 }\n  FILTER NOT EXISTS { ?country wdt:P576 ?x }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?density ?countryLabel ?regionLabel", "context": "1st level subdivisions of Europe (and colonies) with less than 30 inhabitants per km²", "target": ""}
{"query": "#Former US presidents/vice-presidents, current age or age at death; years alive after last time an office held\n#includes incumbents with current date\n#by Jura1, 2020-06-20\n\nSELECT ?person ?personLabel (MAX(?end_or_now) as ?last_date)  (MAX(?age_end_or_now) as ?age_at_date) (?when - YEAR(?last_date)  as ?years_alive_since) ?dob ?dod ?when ?age  \n{\n  VALUES ?office {  wd:Q11699 wd:Q11696 }\n  ?st ps:P39 ?office .\n  ?person wdt:P31 wd:Q5; p:P39 ?st . \n  OPTIONAL { ?st pq:P582 ?e } \n  BIND( COALESCE(?e, NOW() ) as ?end_or_now) \n  ?person wdt:P569 ?dob .\n  OPTIONAL { ?person wdt:P570 ?dod } .\n  BIND( COALESCE( YEAR( ?dod) , YEAR(NOW()) ) as ?when )  \n  BIND( ?when - YEAR(?dob) as ?age) \n  BIND(YEAR(?end_or_now)-YEAR(?dob) as ?age_end_or_now)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nGROUP BY ?person ?personLabel ?dob ?dod ?age ?when\nORDER BY DESC(?age)", "context": "Ages of US presidents/vice-presidents: today, when leaving office, at death", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coords\", \"?line\",\"?rgb\"]}\nSELECT ?station ?stationLabel ?coords ?line ?layer ?rgb (SAMPLE(?image) AS ?image) (sample(?opening) as ?opening) WHERE {\n?station wdt:P31 wd:Q928830; \n         wdt:P131* wd:Q104231;\n         wdt:P625 ?coords;\n         p:P197 ?pred.\n   ?pred ps:P197 ?conn;\n         pq:P81 ?subwayLine.\n   MINUS { ?pred pq:P582 [] }\n   OPTIONAL { ?station wdt:P18 ?image }.\n   OPTIONAL { ?station wdt:P1619 ?opening }.  \n?conn wdt:P625 ?cds;\n      wdt:P31 wd:Q928830.\nBIND(IF(xsd:double(?depth/10) = xsd:integer(?depth/10),?cds,\"\") as ?coords)\n?conn p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\nBIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n?subwayLine wdt:P465 ?rgb .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"sv\". ?subwayLine rdfs:label ?layer. ?station rdfs:label ?stationLabel }         \n}\nGROUP BY ?station ?stationLabel ?coords ?line ?layer ?rgb", "context": "The subway network of Stockholm", "target": ""}
{"query": "SELECT DIStiNCT ?item ?itemLabel WHERE {\n  ?item wdt:P39/wdt:P279* wd:Q16707842; wdt:P106 wd:Q937857 .\n  OPTIONAL { ?item wdt:P569 ?dob }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?dob)", "context": "Former footballers who have ever been an MP in the UK parliament", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coord\"]}\n#Query by Silvia Gutiérrez (@espejolento)\n#Note: This query depends on the place of birth having a 'country (P17)' statement AND the country having 'part of (P361)' = 'Latin America'\n\nSELECT ?author ?authorLabel ?birthplaceLabel ?idGND ?coord ?country\n\n(GROUP_CONCAT(DISTINCT ?occupation_label; SEPARATOR=\", \") as ?occupations) #thanks to @NavinoEvans for this!\n\nWHERE {\n      ?author wdt:P227 ?idGND . #has a GND id\n       ?author wdt:P21 wd:Q6581072 . #is female\n       ?author wdt:P106 wd:Q36180 . #is writer\n       ?author wdt:P19 ?birthplace .   # born in a place...\n       ?birthplace wdt:P17 ?country .  # which is in a country that is...\n       ?country p:P361/ps:P361 wd:Q12585 .   # part of Latin America thanks Tony Bowden! (@tmtm)\n       ?birthplace wdt:P625 ?coord .\n       OPTIONAL {\n         ?author wdt:P106 ?occupation . #get occupation\n         ?occupation rdfs:label ?occupation_label FILTER (lang(?occupation_label) = \"en\") #get label the 'manual' way \n       }\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }        \n} GROUP BY ?author ?authorLabel ?birthplaceLabel ?idGND ?coord ?country #GROUP BY\nLIMIT 5000", "context": "Latin American Women Writers with a GND ID", "target": ""}
{"query": "SELECT ?item ?itemLabel ?personLabel ?coordinate_location WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P31/wdt:P279* wd:Q34442;\n    wdt:P138 ?person.\n  ?person wdt:P31 wd:Q5;\n    wdt:P106/wdt:P279* wd:Q2526255.\n  OPTIONAL { ?item wdt:P625 ?coordinate_location. }\n}\n", "context": "Roads that are named after film directors", "target": ""}
{"query": "#defaultView:Timeline{\"hide\":[\"?from\",\"?to\"]}\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?from ?to ?img \n{\n\t?item p:P39 [ps:P39 wd:Q11696 ; pq:P580 ?from ; pq:P582 ?to ] .\n    { ?item p:P106/ps:P106 wd:Q10076267 } UNION { VALUES ?item { wd:Q11812 wd:Q8612 wd:Q23 wd:Q11891 wd:Q11813 wd:Q11869 wd:Q11896 wd:Q11817 wd:Q11815 wd:Q11820 wd:Q34836 } }\n    OPTIONAL { ?item wdt:P18 ?img }\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}", "context": "Timeline of U.S. Presidents", "target": ""}
{"query": "SELECT ?item ?itemLabel ?partyLabel ?start ?replacesLabel ?districtLabel\nWHERE \n{\n  ?item wdt:P31 wd:Q5;\n        p:P39 ?parl.\n  ?parl ps:P39 wd:Q10655178;\n        pq:P580 ?start .\n  OPTIONAL { ?parl pq:P582 ?end }\n  OPTIONAL { ?parl pq:P1365 ?replaces }\n  OPTIONAL { ?parl pq:P4100 ?party }\n  OPTIONAL { ?parl pq:P768 ?district }\n  FILTER (!BOUND(?end))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv,en\". }\n}\nORDER BY ?itemLabel ?start ?item", "context": "List of MPs currently in the Sveriges riksdag", "target": ""}
{"query": "#defaultView:Map\nSELECT ?article ?person ?personLabel ?picGrave ?coord ?bd ?graveplot2 ?layer WHERE {\n{\n  ?person wdt:P119 wd:Q311 ;\n           p:P119 [ \n             pq:P965 ?graveplot];\n           wdt:P569 ?bd .\n  ?person wdt:P19 ?city .\n  ?city wdt:P625 ?coord .\t\t\t\n   OPTIONAL{ ?person wdt:P1442 ?picGrave .} \t\t\n   OPTIONAL {\n      ?article schema:about ?person .\n      ?article schema:inLanguage \"fr\" .\n      ?article schema:isPartOf <https://fr.wikipedia.org/> .}    \n  BIND (CONCAT(\"Division \",?graveplot) AS ?graveplot2)\n  BIND(\n    IF(?bd < \"1700-01-01T00:00:00Z\"^^xsd:dateTime, \"<1700\",\n    IF(?bd < \"1800-01-01T00:00:00Z\"^^xsd:dateTime, \"18e siècle\",\n    IF(?bd < \"1900-01-01T00:00:00Z\"^^xsd:dateTime, \"19e siècle\",\n    IF(?bd < \"2000-01-01T00:00:00Z\"^^xsd:dateTime, \"20e siècle\",\n    \"21e siècle\"))))\n    AS ?layer).\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\"}\n }\nOrder by ?personLabel", "context": "Map showing people buried in Père Lachaise by place of birth", "target": ""}
{"query": "#Cats\nSELECT ?item ?itemLabel ?geo WHERE {\n  ?item wdt:P31 wd:Q811534;\n        wdt:P625 ?geo .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Existing named trees in the world", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?fairytale ?fairytaleLabel ?illustration ?illustrationLabel ?image WHERE {\n  {\n    SELECT DISTINCT ?fairytale (SAMPLE(?work) AS ?illustration) WHERE {\n      ?work wdt:P180 ?depicted.\n      ?depicted ?property ?fairytale.\n      ?fairytale wdt:P31 wd:Q699.\n      ?work wdt:P18 [] .\n    }\n    GROUP BY ?fairytale ?illustration\n  }\n  ?illustration wdt:P18 ?image.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?fairytale ?fairytaleLabel ?illustration ?illustrationLabel ?image", "context": "Artworks depicting things related to a fairy tale", "target": ""}
{"query": "#defaultView:Map\nSELECT ?org ?orgLabel ?portal ?portalLabel (SAMPLE(?coord) as ?coord) (SAMPLE(?img) as ?img) WHERE {\n#  ?portal wdt:P31 wd:Q27031827.\n  ?org wdt:P8402 ?portal.\n  ?org wdt:P276?/wdt:P625 ?coord.\n  OPTIONAL { ?org wdt:P41 ?flag }\n  OPTIONAL { ?org wdt:P94 ?coat }\n  OPTIONAL { ?org wdt:P154 ?logo }\n  OPTIONAL { ?org wdt:P18 ?image }\n  BIND( COALESCE(?flag, ?coat, ?logo, ?image) as ?img )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?org ?orgLabel ?portal ?portalLabel", "context": "Open data portals in Wikidata", "target": ""}
{"query": "SELECT ?item ?itemLabel ?year {\n  {\n    SELECT ?item (year(?time) as ?year) {\n      ?item wdt:P138 [wdt:P31 wd:Q5];\n        wdt:P571 ?time.\n      FILTER (!isBLANK(?time)).\n    }\n    ORDER by ?year\n    LIMIT 20\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER by ?year\n", "context": "Objects that are named after people on Wikidata", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT (uri(replace(str(?l), \"L\", \"Q\")) as ?item) (MD5(CONCAT(str(?item),str(RAND()))) as ?random) ?l ?hitza ?sense ?ahotsak ?elhuyar ?audio (SAMPLE(?picture_) AS ?picture) WHERE\n{\n  ?l a ontolex:LexicalEntry ; dct:language ?language ; wikibase:lemma ?hitza ; ontolex:sense ?esanahia.\n  ?esanahia skos:definition ?sense; wdt:P5137 ?q_concept.\n  ?q_concept wdt:P18 ?picture_.\n  ?language wdt:P218 'eu'.\n  FILTER (LANG(?sense) = 'eu')\n  OPTIONAL {?l wdt:P7559 ?ahotsak.}\n  OPTIONAL {?l wdt:P6838 ?elhuyar.}\n  ?l ontolex:lexicalForm ?f .\n  ?f ontolex:representation ?form ; wdt:P443 ?audio .\n  filter (regex(str(?f), \"-F1$\")) .\n    # Exclude out of scope concepts\n  MINUS {?q_concept wdt:P31 wd:Q3624078.}. # countries\n  MINUS {?l wdt:P31 wd:Q1499602.}. #ad hoc hitzak\n  # Exclude non suitable concepts\n  VALUES ?minus {\n  wd:Q47092 # rape\n  wd:Q198   # war\n  wd:Q124490 # violence\n  wd:Q170382 # revolver\n  wd:Q1576   #cigar\n  #... add yours here\n  }.\n  MINUS {?sense wdt:P5137 ?minus.}.\n  # Exclude senses not suitable for young children:\n  MINUS {\n    VALUES ?filter { wd:Q8102 wd:Q545779 wd:Q1521634 wd:Q571571 wd:Q184439 }\n    ?esanahia wdt:P6191 ?filter.\n  }\n}\nGROUP BY ?l ?sense ?hitza ?ahotsak ?elhuyar ?audio\nORDER BY ?random\nLIMIT 1", "context": "Illustrated Basque word of the day for children", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?start ?parl_start ?age ?dob ?dod WHERE {\n    VALUES ?parl {wd:Q16707842 wd:Q18015642 wd:Q18018860}\n    ?item p:P39 ?pos_stmt .\n    ?pos_stmt ps:P39 ?pos .\n    ?pos wdt:P279 ?parl .\n    OPTIONAL {\n      ?pos_stmt pq:P580 ?start .\n    }\n    OPTIONAL {\n      ?pos wdt:P571 ?parl_start .\n    }\n    BIND(COALESCE(?start, ?parl_start) AS ?best_start) .\n  \n    ?item p:P569 ?dob_stmt .\n    ?dob_stmt ps:P569 ?dob .\n    FILTER(isLiteral(?dob)) .\n    ?dob_stmt psv:P569/wikibase:timePrecision ?dob_precision .\n    FILTER(?dob_precision > 8) .\n  \n    BIND (((?best_start - ?dob)/365.24) AS ?age) .\n    OPTIONAL {?item wdt:P570 ?dod} .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}  ORDER BY DESC(?age)\nLIMIT 200", "context": "Oldest UK MPs at the time of election", "target": ""}
{"query": "#defaultView:Map\nSELECT ?Archiv ?ArchivLabel ?archiviert_in ?archiviert_inLabel ?geographische_Koordinaten\nWHERE {SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n?Archiv wdt:P31/wdt:P279* wd:Q166118.\nOPTIONAL { ?Archiv wdt:P625 ?geographische_Koordinaten. }\n}\nLIMIT 100000", "context": "Map of archives in Wikidata", "target": ""}
{"query": "# MPs with identified mythical ancestors\nSELECT ?mp ?mpLabel ?person ?personLabel ?born ?died ?typeLabel WHERE {\n  ?mp p:P39 ?positionStatement .\n  ?positionStatement ps:P39 wd:Q77685926 .  # this person is a member of the current parliament\n  { ?mp (wdt:P25|wdt:P22)* ?person . } # mp's mother or father, or their mother or father, and so on\n  filter not exists { ?person wdt:P31 wd:Q5 } . # the ancestor is not \"a human\"\n  ?person wdt:P31 ?type .\n  OPTIONAL { ?person wdt:P569 ?born } .\n  OPTIONAL { ?person wdt:P570 ?died } .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "MPs in the current UK parliament with identified mythical ancestors", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?cd\",\"?l\",\"?rgb\"]}\nSELECT DISTINCT ?cd ?l ?i ?iLabel ?ct ?ctLabel ?rgb ?x WITH{SELECT ?i ?cd ?lat1 ?lon1 ?loc ?x(sample(?op)as?op)WHERE{VALUES ?c1{wd:Q668 wd:Q843 wd:Q902}?i wdt:P31/wdt:P279* wd:Q55488;wdt:P625 ?cd;wdt:P17 ?c1;p:P625/psv:P625 ?y.?y wikibase:geoLatitude ?lat1;wikibase:geoLongitude ?lon1.MINUS{?i wdt:P31/wdt:P279* wd:Q928830}OPTIONAL{?i wdt:P131 ?loc}OPTIONAL{?i wdt:P137 ?op}OPTIONAL{{?i wdt:P5696 ?x}UNION{?i wdt:P6785 ?x}}OPTIONAL{?i wdt:P17 wd:Q902. BIND(\"-\"as?x)}}GROUP BY ?i ?cd ?lat1 ?lon1 ?loc ?x}AS%s\nWITH{SELECT ?l ?ct ?o ?t WHERE{INCLUDE %s.?i p:P197 ?nss.?nss ps:P197 ?ns.?ns p:P625/psv:P625 ?z.?z wikibase:geoLatitude ?lat2;wikibase:geoLongitude ?lon2.OPTIONAL{?nss pq:P81 ?ct.OPTIONAL{?ct wdt:P127 ?o}}OPTIONAL{?nss pq:P5051 ?t}FILTER(STR(?i)< STR(?ns)).BIND(CONCAT('LINESTRING(',STR(?lon1),' ',STR(?lat1),',',STR(?lon2),' ',STR(?lat2),')')AS?str).BIND(STRDT(?str,geo:wktLiteral)AS?l)}}AS%l\nWHERE{{INCLUDE%s}UNION{INCLUDE%l}.BIND(IF(BOUND(?l),IF(BOUND(?ct),IF(BOUND(?t),IF(BOUND(?o),\"00f\",\"4682b4\"),\"40e0d0\"),\"0f0\"),IF(BOUND(?x),IF(BOUND(?loc),IF(BOUND(?op),\"00f\",\"4682b4\"),\"40e0d0\"),\"0f0\"))AS?rgb)SERVICE wikibase:label{bd:serviceParam wikibase:language \"en\".}}# \n", "context": "Indian subcontinent rail lines", "target": ""}
{"query": "#List of countries by age of the head of government\n#added by Jura1, rev. 2016-11-08\nSELECT DISTINCT ?countryLabel ?hgovernmentLabel ?gage ?hstateLabel ?sage WHERE {\n  ?country wdt:P31 wd:Q3624078.\n  FILTER(NOT EXISTS { ?country wdt:P31 wd:Q3024240. })\n  ?country p:P6 ?statement.\n  ?statement ps:P6 ?hgovernment.\n  ?country wdt:P6 ?hgovernment.\n  FILTER(NOT EXISTS { ?statement pq:P582 ?x. })\n  ?country p:P35 ?sstatement.\n  ?sstatement ps:P35 ?hstate.\n  ?country wdt:P35 ?hstate.\n  FILTER(NOT EXISTS { ?statement pq:P582 ?x. })\n  ?hgovernment wdt:P569 ?dobh.\n  BIND((YEAR(NOW())) - (YEAR(?dobh)) AS ?gage)\n  ?hstate wdt:P569 ?dobs.\n  BIND((YEAR(NOW())) - (YEAR(?dobs)) AS ?sage)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY (?countryLabel)", "context": "List of countries by age of the head of government", "target": ""}
{"query": "# Cities of the United States between 100,000 and 600,000 inhabitants with the counties and states they are located in\nSELECT DISTINCT ?city ?cityLabel ?county ?countyLabel ?state ?stateLabel ?pop ?coord\nWHERE \n{\n  ?city wdt:P31 wd:Q1093829 .\n\n  ?city wdt:P1082 ?pop .\n  FILTER (?pop >= 100000 && ?pop <= 600000)\n  \n  ?city wdt:P131 ?county .\n  ?county wdt:P31/wdt:P279 wd:Q47168 .\n  \n  ?county wdt:P131 ?state .\n  ?state wdt:P31 wd:Q35657 .\n  \n  ?city wdt:P625 ?coord .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?stateLabel ?countyLabel ?cityLabel", "context": "Cities of the United States between 100,000 and 600,000 inhabitants with the counties and states they are located in", "target": ""}
{"query": "SELECT DISTINCT ?person ?personLabel ?office ?officeLabel ?end1 ?start2 (FLOOR(?days/36.525)/10 AS ?gap)\nWHERE {\n  # Offices Held by Heads of Government\n  ?country wdt:P31 wd:Q3624078; p:P1313/ps:P1313 ?office .\n  \n  # People who have held the office more than once\n  ?person p:P39 [ ps:P39 ?office ; pq:P582 ?end1 ] .\n  ?person p:P39 [ ps:P39 ?office ; pq:P580 ?start2 ] .\n  FILTER (?start2 > ?end1)\n  FILTER NOT EXISTS { # but must be successive periods\n    ?person p:P39 [ ps:P39 ?office ; pq:P580 ?start3 ] .\n    FILTER((?start3 > ?end1) && (?start3 < ?start2))\n  }\n\n  # How long between end of one term and start of the next\n  BIND (?start2 - ?end1 AS ?days)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?days)\nLIMIT 10", "context": "People who have returned to being their country’s head of government after the longest gap", "target": ""}
{"query": "SELECT (COUNT(?item) AS ?count) ?electedLabel WHERE {\n  ?item p:P39 ?positionStatement .\n  ?positionStatement ps:P39 wd:Q10655178 .\n  ?positionStatement pq:P2715 ?elected\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?electedLabel\nORDER BY DESC(?electedLabel)", "context": "Number of MPs elected to the Swedish parliament for each election back to 1970", "target": ""}
{"query": "SELECT ?property ?propertyLabel (COUNT(?constraint) AS ?count) WHERE {\n  ?property a wikibase:Property.\n  OPTIONAL { ?property p:P2302 ?constraint. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?property ?propertyLabel", "context": "Properties and the number of constraint definition statements on them", "target": ""}
{"query": "# wealthiest queer people\nSELECT ?person ?personLabel ?netWorthUSD ?orientationLabel ?genderLabel WHERE {\n  ?person wdt:P31 wd:Q5;\n          p:P2218/psv:P2218 [\n            wikibase:quantityAmount ?netWorthUSD;\n            wikibase:quantityUnit wd:Q4917\n          ].\n  OPTIONAL {\n    ?person wdt:P91 ?orientation.\n    ?orientation wdt:P279* wd:Q339014.\n  }\n  OPTIONAL {\n    ?person wdt:P21 ?gender.\n    FILTER(?gender NOT IN (wd:Q6581097, wd:Q6581072))\n  }\n  FILTER(BOUND(?orientation) || BOUND(?gender))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?netWorthUSD)\nLIMIT 50", "context": "Wealthiest queer people on Wikidata", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?winner ?winnerLabel ?colour ?count ?rgb WHERE {\n  {SELECT ?winner ?count (SAMPLE(?colour) AS ?colour) WHERE {\n  {SELECT ?winner (COUNT(?winner) AS ?count) WHERE {\n    ?event wdt:P31 wd:Q4484477.\n    ?event wdt:P1346 ?winner. \n  }\n  GROUP BY ?winner  }\n    \n  OPTIONAL { ?winner wdt:P6364 ?colour }\n  BIND (IF ( BOUND(?colour), ?colour, wd:Q1003676) AS ?colour) \n  } \n  GROUP BY ?winnerLabel ?winner ?count ?rgb }\n    \n  OPTIONAL { ?colour wdt:P465 ?rgb }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\nORDER BY DESC (?count)\n\n", "context": "Bubble chart showing the winners of the FA Cup", "target": ""}
{"query": "#parker i Oslo uten bilde\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord\nWHERE \n{\n  ?item wdt:P31 wd:Q22698;\n  wdt:P17 wd:Q20;\n  wdt:P131 wd:Q5245991;\n        wdt:P625 ?coord.\n  MINUS { ?item wdt:P18 [] }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"nb,en\". }\n}", "context": "Map of parks in Oslo missing images on Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?koord ?layer WHERE {\n  wd:Q191045 ((wdt:P22|wdt:P25)*) ?item.\n  ?item wdt:P19 ?place.\n  ?place wdt:P625 ?koord.\n  ?item wdt:P21 ?sex.\n  BIND (IF(?sex = wd:Q6581097, \"gizonezkoa\",\n        IF(?sex = wd:Q6581072, \"emakumezkoa\", \n           \"\")) AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,en,fr,pl,es\". }\n}\nORDER BY (?itemLabel)", "context": "Ancestors of the current King of Spain color coded by sex or gender", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?hog ?hogLabel ?party ?partyLabel\n  (GROUP_CONCAT(?alignment_label; SEPARATOR=\" to \") AS ?partyAlignment)\nWHERE {\n  ?item wdt:P31 wd:Q3624078 ; wdt:P463 wd:Q1065 ; wdt:P6 ?hog .\n  OPTIONAL { \n    ?hog wdt:P102 ?party .\n    OPTIONAL { \n      ?party wdt:P1387 ?alignment . \n      ?alignment rdfs:label ?alignment_label FILTER (lang(?alignment_label) = 'en')\n    }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?hog ?hogLabel ?party ?partyLabel\nORDER BY ?itemLabel\n", "context": "Political alignments of the parties of world leaders", "target": ""}
{"query": "# Female Mathematicians whose birth/death anniversary is today\nSELECT ?person ?personLabel ?dob ?dod WHERE {\n  BIND(NOW() as $today)\n  BIND(YEAR($today) AS $today_year)\n  BIND(MONTH($today) AS $today_month)\n  BIND(DAY($today) AS $today_day)\n  ?person wdt:P31 wd:Q5.\n  ?person wdt:P21 wd:Q6581072 .\n  OPTIONAL { ?person wdt:P569 ?dob }\n  OPTIONAL { ?person wdt:P570 ?dod }\n  ?person wdt:P106 wd:Q170790  .\n  FILTER( $today_month = MONTH(?dob) && $today_day = DAY(?dob) ||  $today_month = MONTH(?dod) && $today_day = DAY(?dod) )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Female Mathematicians whose birth/death anniversary is today", "target": ""}
{"query": "SELECT ?collection ?collectionLabel (COUNT(*) AS ?count) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],es\". }\n  ?thing wdt:P170 wd:Q5593.\n  OPTIONAL { ?thing wdt:P195 ?collection. }\n}\nGROUP BY ?collection ?collectionLabel\nORDER BY DESC (?count)\nLIMIT 300", "context": "Largest collections of Picasso content", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?winnerLabel1 ?ballsize ?rgb WHERE {\n  # the snooker ball colours\n   BIND ( COALESCE(\n    IF(?wins = 1, \"ff0000\", 1/0),\n    IF(?wins = 2, \"ffff00\", 1/0),\n    IF(?wins = 3, \"00ff00\", 1/0),\n    IF(?wins = 4, \"8b4513\", 1/0),\n    IF(?wins = 5, \"0000ff\", 1/0),\n    IF(?wins = 6, \"ffc0cb\", 1/0),\n    IF(?wins = 7, \"000000\", 1/0),\n    IF(?wins = 8, \"ffa500\", 1/0),\n    \"800080\"\n  ) AS ?rgb )\n  BIND(1 AS ?ballsize)\n  BIND( CONCAT(?winnerLabel, \": \", STR(?wins)) AS ?winnerLabel1)\n\n  # inner select generates the winners totals\n  { SELECT ?winnerLabel (COUNT (?winner) AS ?wins) WHERE {\n  { ?p wdt:P31 wd:Q502948 }\n  UNION\n  { ?p wdt:P361 wd:Q502948 }\n  ?p wdt:P1346 ?winner .\n  ?winner rdfs:label ?winnerLabel .\n  FILTER(LANG(?winnerLabel) = \"en\")\n#  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?winnerLabel \n  ORDER BY ASC (?wins )}\n}\n", "context": "Bubble chart of snooker world champions", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?itemDescription ?subjectLabel ?coords ?image WHERE {\n?item wdt:P31 wd:Q79007 .\n?item wdt:P131 wd:Q62274582 .\n?item wdt:P138 ?subject .\n?subject wdt:P106 wd:Q189290 .\n?subject wdt:P18 ?image .\n?item wdt:P625  ?coords .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Map of streets in Tillydrone named after WW2 military leaders", "target": ""}
{"query": "SELECT DISTINCT ?item2 ?item2Label  ?item1 ?item1Label WHERE {\n  VALUES ?mp {wd:Q16707842 wd:Q18015642 wd:Q18018860} .\n  VALUES ?mp2 {wd:Q16707842 wd:Q18015642 wd:Q18018860} .\n  ?item1 wdt:P39 ?mp .\n  ?item1 wdt:P26 ?spouse .\n  ?spouse wdt:P22|wdt:P25 ?item2 .\n  ?item2  wdt:P39 ?mp2 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?item2Label ?item1Label", "context": "British MPs with sons- or daughters-in-law who were also MPs", "target": ""}
{"query": "SELECT DISTINCT ?item2 ?item2Label (count(distinct ?item1) as ?count) \n\nWITH {\n  SELECT DISTINCT ?item1 WHERE {\n     VALUES ?mp {wd:Q16707842 wd:Q18015642 wd:Q18018860} .\n     { ?item1 wdt:P39 ?p . ?p ps:P39 ?t . ?t wdt:P279 ?mp }  union { ?item1 wdt:P1614 [] }\n  }\n} AS %mps\n\nWHERE {\n  {INCLUDE %mps . ?item1 wdt:P26 ?spouse . ?spouse wdt:P22|wdt:P25 ?item2 }\n  union\n  {INCLUDE %mps . ?item1 wdt:P22|wdt:P25 ?item2 } \n  OPTIONAL {\n       VALUES ?mp {wd:Q16707842 wd:Q18015642 wd:Q18018860} .\n       ?item2 wdt:P39 ?p . ?p ps:P39 ?t . ?t wdt:P279 ?mp .\n  }\n  OPTIONAL {?item2 wdt:P1614 ?hpID} .\n  FILTER (bound(?mp) || bound(?hpID)) . \n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?item2 ?item2Label order by desc(?count) ?item2Label", "context": "MPs with the largest number of children or childen-in-law who became MPs", "target": ""}
{"query": "SELECT DISTINCT ?depth ?descendant ?descendantLabel ?descendantDescription (YEAR(?dob) as ?yob) (YEAR(?dod) as ?yod) ?ids ?spouse_ids  ?sl ?spouse_sl\nWHERE\n{\n  BIND( wd:Q75891071 as ?ancestor ) \n  SERVICE gas:service {\n    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" ;\n                gas:in wd:Q75891071 ;\n                gas:traversalDirection \"Forward\" ;\n                gas:out ?descendant ;\n                gas:out1 ?depth ;\n                gas:maxIterations 100 ;\n                gas:linkType wdt:P40 .\n  }\n  OPTIONAL { ?descendant wdt:P569 ?dob }\n  OPTIONAL { ?descendant wdt:P570 ?dod }\n  OPTIONAL { ?descendant wikibase:identifiers ?ids }\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"en,en\" }\n  OPTIONAL { ?descendant wdt:P26 / wikibase:identifiers ?spouse_ids }\n  OPTIONAL { ?descendant wikibase:sitelinks ?sl }\n  OPTIONAL { ?descendant wdt:P26 / wikibase:sitelinks ?spouse_sl }\n}\nORDER BY ASC(?depth)\nLIMIT 1000", "context": "Descendants of Robert Emett (born 1729), with counts of sitelinks and external IDs for them and their spouses", "target": ""}
{"query": "SELECT ?language ?languageLabel (GROUP_CONCAT(DISTINCT ?creatorLabel; separator = \", \") AS ?creators) ?sitelinks WHERE {\n  hint:Query hint:optimizer \"None\".\n  ?language wdt:P31?/wdt:P279* wd:Q9143; wdt:P170|wdt:P178|wdt:P61|wdt:P287 ?creator; wikibase:sitelinks ?sitelinks. ?creator wdt:P31 wd:Q5;\n           wdt:P21/wdt:P279? wd:Q6581072.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?language rdfs:label ?languageLabel.\n    ?creator rdfs:label ?creatorLabel.\n  }\n}\nGROUP BY ?language ?languageLabel ?sitelinks\nORDER BY DESC(?sitelinks)", "context": "Programming languages written by women", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?protein ?proteinLabel (COUNT(?work) AS ?count) WHERE {\n  ?protein wdt:P703 wd:Q82069695 ; wdt:P31 wd:Q8054 .\n  ?work wdt:P921 ?protein .\n  OPTIONAL { ?work wdt:P577 ?dates . }\n  OPTIONAL { ?work wdt:P356 ?doi . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,en\". }\n} GROUP BY ?protein ?proteinLabel\n  ORDER BY ASC(?proteinLabel)\n", "context": "Bar chart showing the number of research output (articles, etc) annotated with a SARSCoV2 proteins as 'main subject'", "target": ""}
{"query": "select ?item ?itemLabel ?sexeLabel ?cocLabel (concat(\"https://www.facebook.com/\",?fbc) as ?facebook) (concat('https://www.twitter.com/',?twc) as ?twitter) (concat('https://www.instagram.com/',?inc,'/') as ?insta){  {  select distinct ?item   where { ?item wdt:P106 wd:Q937857 .          ?item wdt:P21 wd:Q6581072       .           {?item wdt:P2013 ?fb} union           {?item wdt:P2002 ?tw} union            {?item wdt:P2003 ?in}              }  }   optional {?item wdt:P2013 ?fbc}   optional {?item wdt:P2002 ?twc}   optional {?item wdt:P2003 ?inc}   optional {?item wdt:P21 ?sexe}   optional {?item wdt:P27 ?coc}  SERVICE wikibase:label { bd:serviceParam wikibase:language \"nl,en\" }}", "context": "Female soccer players who have a (known) social media account", "target": ""}
{"query": "SELECT ?VilleLabel ?Population\nWHERE\n{\n  ?Ville wdt:P31/wdt:P279* wd:Q515;\n        wdt:P17 wd:Q142;\n        wdt:P1082 ?Population .\n         SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }.\n\n}\nORDER BY DESC(?Population)\nLIMIT 30\n", "context": "Largest cities in France by population", "target": ""}
{"query": "#defaultView:Map\nselect ?item ?itemLabel ?coord where {\n\n  ?item wdt:P54 wd:Q131206 .\n  ?item wdt:P19/wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs,en\". }\n  \n}", "context": "Birth places of Buffalo Sabres players", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?item ?itemLabel  ?date  WHERE {\n#?item wdt:P86 ?composer .\n?item wdt:P86 wd:Q103767 .\n#?item wdt:P144 ?source .\n?item wdt:P577 ?date .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY ?year", "context": "Timeline of all of Bird’s tunes", "target": ""}
{"query": "SELECT distinct ?parliament_start (round((?johns/?members)*100) as ?percent_johns) where\n{\n  { SELECT distinct ?term ?parliament_start where \n   { { ?term wdt:P279 wd:Q16707842 } union { ?term wdt:P279 wd:Q18015642 } union { ?term wdt:P279 wd:Q18018860 } \n     optional { ?term wdt:P571 ?parliament_start } \n  } \n }\n optional  { SELECT ?term (count(distinct ?item) as ?members) where \n    { ?item p:P39 ?positionStatement . ?positionStatement ps:P39 ?term . \n    { ?term wdt:P279 wd:Q16707842 } union { ?term wdt:P279 wd:Q18015642 } union { ?term wdt:P279 wd:Q18018860 } \n      # ?item wdt:P735 ?name #has a known forename\n    } group by ?term\n  } \n\n optional { SELECT ?term (count(distinct ?item) as ?johns) where \n    { ?item p:P39 ?positionStatement . ?positionStatement ps:P39 ?term . \n    { ?term wdt:P279 wd:Q16707842 } union { ?term wdt:P279 wd:Q18015642 } union { ?term wdt:P279 wd:Q18018860 } \n      ?item wdt:P735 wd:Q4925477 # that name is John\n    } group by ?term \n  } \n  filter ( ?members >= 25 ) # only terms with 25 or more people\n  SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n} order by ?parliament_start\n#defaultView:ScatterChart", "context": "Approximate proportion of people called \"John\" in any given UK Parliament since 1372", "target": ""}
{"query": "#defaultView:BarChart\nselect (STR(?year1) as ?year) ((count(?item) * ?sgn) as ?count) ?layer where {\n\n?item wdt:P19/wdt:P17 ?country . values ?country {wd:Q213 wd:Q183} . ?item wdt:P569 ?birthdate bind((FLOOR(YEAR(?birthdate)/10)*10) as ?year1) FILTER(?year1>=1800). \n  \noptional {?item wdt:P19/wdt:P17 wd:Q213 . ?item wdt:P20/wdt:P17 wd:Q183 .  bind(\"Born in Czechia, died in Germany\" as ?layer). bind(1 as ?sgn)} \noptional {?item wdt:P19/wdt:P17 wd:Q183 . ?item wdt:P20/wdt:P17 wd:Q213 .  bind(\"Born in Germany, died in the Czech Republic\" as ?layer). bind(-1 as ?sgn)} \n\nfilter(bound(?layer)) .\n                                                                                     \n}\ngroup by ?year1 ?layer ?sgn\norder by asc(?year1) ", "context": "People born in Czechia who died in Germany and vice versa, by decade of birth", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord1\", \"?coord2\", \"?line\"]}\nSELECT * WHERE {\n  ?comm1 wdt:P31 wd:Q55488 ; wdt:P17 wd:Q213 ; wdt:P625 ?coord1 ; wdt:P197 ?comm2.\n  MINUS { ?comm1 p:P31 [ ps:P31 wd:Q55488 ; pq:P582 [] ] . }\n  MINUS { ?comm2 p:P31 [ ps:P31 wd:Q55488 ; pq:P582 [] ] . }\n  ?comm2 wdt:P625 ?coord2 .\n  ?comm1 p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].\n  ?comm2 p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n}", "context": "Railway network in Czechia", "target": ""}
{"query": "#defaultView:BarChart\nselect (STR(?year1) as ?year) ((count(?item) * ?sgn) as ?count) ?layer where {\n\n?item wdt:P19/wdt:P17 ?country . values ?country {wd:Q213 wd:Q183} . ?item wdt:P569 ?birthdate bind((FLOOR(YEAR(?birthdate)/10)*10) as ?year1) FILTER(?year1>=1800). \n  \noptional {?item wdt:P19/wdt:P17 wd:Q213 . ?item wdt:P20/wdt:P17 wd:Q183 .  bind(\"Born in Czechia, died in Germany\" as ?layer). bind(1 as ?sgn)} \noptional {?item wdt:P19/wdt:P17 wd:Q183 . ?item wdt:P20/wdt:P17 wd:Q213 .  bind(\"Born in Germany, died in the Czech Republic\" as ?layer). bind(-1 as ?sgn)} \n\nfilter(bound(?layer)) .\n                                                                                     \n}\ngroup by ?year1 ?layer ?sgn\norder by asc(?year1) ", "context": "People born in Czechia who died in Germany and vice versa, by decade (of birth)", "target": ""}
{"query": "SELECT ?book ?bookLabel ?pubdate ?pubdateLabel ?location ?locationLabel ?coordinates\nWHERE { ?book wdt:P31 wd:Q47461344. \n        ?book wdt:P50 wd:Q40909.\n        ?book wdt:P577 ?pubdate. \n        OPTIONAL {?book wdt:P840 ?location.\n                 ?location wdt:P625 ?coordinates}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}\n", "context": "List of locations of Woolf's books", "target": ""}
{"query": "SELECT ?item ?itemLabel ?age ?end_causeLabel ?end_time ?dob WHERE {\n  ?item wdt:P569 ?dob .\n  ?item p:P39 ?mp_stmt .\n  ?mp_stmt ps:P39 ?mp .\n  ?mp_stmt pq:P582 ?end_time .\n  OPTIONAL {\n     VALUES ?excluded_cause {wd:Q1787171 wd:Q35855188 wd:Q30580660 wd:Q35867887 wd:Q52084147} .\n    ?mp_stmt pq:P1534 ?excluded_cause .\n  }\n  FILTER(!bound(?excluded_cause)) .\n  ?mp_stmt pq:P1534 ?end_cause .\n  ?mp wdt:P279 wd:Q16707842 .\n  ?mp_next_parl wdt:P155 ?mp .\n  MINUS {?item wdt:P39 ?mp_next_parl} .\n  BIND (((?end_time - ?dob) / 365.24) AS ?age) .\n  FILTER(bound(?age)) .\n  FILTER(year(?end_time) > 1899) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?age \nLIMIT 200", "context": "Youngest age of UK MPs leaving office since 1900", "target": ""}
{"query": "SELECT ?item ?itemLabel ?time ?end_causeLabel ?latest_mp ?dob1 ?dod1\n\nWITH {\n  SELECT ?item (MAX(?end_time) AS ?latest_mp) (MAX(?dod) AS ?dod1) (MAX(?dob) AS ?dob1) WHERE {\n  OPTIONAL {?item wdt:P570 ?dod} .\n  ?item p:P39 ?mp_stmt .\n  ?mp_stmt ps:P39 ?mp .\n  ?mp wdt:P279 wd:Q16707842 .\n  ?mp_stmt pq:P582 ?end_time .\n  FILTER(year(?end_time) > 1899) .\n  OPTIONAL {?item wdt:P569 ?dob}\n  \n  } GROUP BY ?item\n} AS %mps\n\nWHERE {\n  INCLUDE %mps .  \n  ?item p:P39 ?mp_stmt .\n  ?mp_stmt pq:P582 ?latest_mp .\n  ?mp_stmt pq:P1534 ?end_cause .\n  \n  BIND(COALESCE(?dod1, now()) AS ?date) .\n  BIND (((?date - ?latest_mp) / 365.24) AS ?time) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?time) \nLIMIT 200", "context": "Longest time UK MPs lived after leaving office since 1900", "target": ""}
{"query": "SELECT distinct ?item ?itemLabel ?rankLabel ?branchLabel\nWHERE \n{\n  ?item wdt:P39/wdt:P361 wd:Q112014.   #held job which is part of cabinet\n  optional {?item wdt:P410 ?rank. }    #had a military rank\n  optional {?item wdt:P241 ?branch. }  #associated with a military branch\n  bind(coalesce(?rank,?branch) as ?test)\n  filter(bound(?test))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "British cabinet members with past military service", "target": ""}
{"query": "#defaultView:Timeline\nselect ?itemLabel ?zminka ?img where {\n?item wdt:P31 wd:Q5153359 .\n?item wdt:P1249 ?zminka .\n?item wdt:P18 ?img .\n?item wdt:P1082 ?populace .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs\". }\n  }\norder by desc(?populace)\nlimit 10", "context": "Earliest written record of 10 largest Czech cities", "target": ""}
{"query": "#defaultView:Map\nselect ?item ?itemLabel ?coord ?layer where {\n  ?item wdt:P31 wd:Q16970 . \n  {?item wdt:P417 wd:Q196527 .} union {?item wdt:P417 wd:Q239925 .} \n  ?item wdt:P625 ?coord .\n  ?item wdt:P17 wd:Q213 .\n  \n    OPTIONAL {\n    ?item wdt:P417 wd:Q196527 .\n    BIND(\"St. Wenceslas\" AS ?layer).\n             }\n    OPTIONAL {\n    ?item wdt:P417 wd:Q239925 .\n    BIND(\"St. Cyril and Methodius\" AS ?layer).\n             }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,cs\". }\n  \n  }", "context": "Churches dedicated to Czech patron St. Wenceslas (blue) and Moravian patrons St. Cyril and Methodeus (orange)", "target": ""}
{"query": "#defaultView:Map\n\nselect * where {\n  {\nselect ?item ?label ?coord (IF(strends(?label,\"ingen\"),\"-ingen\",\n\tIF(strends(?label,\"ing\"),\"-ing\",\n\t\tIF(strends(?label,\"heim\"),\"-heim\",\n\t\t\tIF(strends(?label,\"ein\"),\"\",\n\t\t\t\tIF(strends(?label,\"ain\"),\"\",\n\t\t\t\t\tIF(strends(?label,\"in\"),\"-in\",\"\")))))) AS ?layer) where {\n\n  ?item wdt:P31 wd:Q262166 .\n  ?item wdt:P625 ?coord. \n  ?item rdfs:label ?label filter(lang(?label)=\"fr\").\n  \n} \n    }\n  filter(?layer != \"\") \n  }\n", "context": "German municipality names ending with -in, -ing, -heim", "target": ""}
{"query": "SELECT ?artistLabel ?languages\nWITH { \n  SELECT ?song WHERE {\n    ?song wdt:P31/wdt:P279* wd:Q7366.\n  }\n} AS %songs  \nWITH { \n  SELECT ?artist (COUNT(DISTINCT ?language) AS ?languages)\n  WHERE {\n    INCLUDE %songs .\n    ?song wdt:P175 ?artist; wdt:P407 ?language .\n  }  \n  GROUP BY ?artist\n  HAVING(?languages > 2)\n} AS %artists\nWHERE {\n  INCLUDE %artists\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC(?languages)", "context": "Artists that have recorded songs in several languages, by number of different languages used", "target": ""}
{"query": "SELECT ?itemLabel (ROUND(AVG(?age)) AS ?avg)\nWHERE\n{\n  ?item wdt:P31 wd:Q223371.\n  ?item wdt:P17 ?country.\n  ?item wdt:P527 ?components.\n  ?components wdt:P571 ?founding.\n  bind( year(now())-year(?founding) as ?age )\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nGROUP BY ?itemLabel\nORDER BY ?avg", "context": "Average age of companies listed on major indices", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord1\", \"?coord2\", \"?line\"]}\nSELECT DISTINCT * WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  { ?item wdt:P31 wd:Q2074737. }\n  UNION\n  { ?item wdt:P31 wd:Q484170. }\n  ?item (wdt:P131/(wdt:P131*)/^wdt:P527) wd:Q47588.\n  ?item wdt:P190 ?hiri_senidetuak.\n  ?item wdt:P625 ?coord1.\n  ?hiri_senidetuak wdt:P625 ?coord2.\n  ?item p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].\n  ?hiri_senidetuak p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n}", "context": "Basque Country sister cities with world municipalities", "target": ""}
{"query": "# (Thanks, James Heald, for the original query!)\n#defaultView:Map\nSELECT ?line (CEIL(?depth/10)*10 as ?layer) {\n  SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q720102 ; gas:out ?station ; gas:out1 ?depth ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . }\n  FILTER(?station != wd:Q720102)\n                      ?pred wdt:P625 ?cds.\n                      BIND(IF(xsd:double(?depth/10) = xsd:integer(?depth/10),?cds,\"\") as ?coords)\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". }\n}\n", "context": "Map of every railway station presently connected directly or indirectly to St Pancras on Wikidata", "target": ""}
{"query": "SELECT ?Frantziako_Tourra ?Frantziako_TourraLabel ?urtea ?irabazle ?irabazleLabel ?adina WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Frantziako_Tourra wdt:P31 wd:Q33881;\n    wdt:P582 ?bukaera_data;\n    p:P1346 [ ps:P1346 ?irabazle; pq:P642 wd:Q20882667 ].\n  BIND(YEAR(?bukaera_data) AS ?urtea)\n  ?irabazle wdt:P569 ?jaiotza_data.\n  BIND((?bukaera_data - ?jaiotza_data) as ?adina_egunak)\n    BIND(?adina_egunak/365.2425 AS ?adina_urteak).\n  BIND(FLOOR(?adina_urteak) AS ?adina).\n}\nORDER by ASC (?urtea)", "context": "Age of the winners of the Tour de France", "target": ""}
{"query": "# Hvor døde danske regenter henne og hvornår? \n#defaultView:TreeMap\nSELECT ?item ?itemLabel  ?layer ?coordsLabel ?DOD ?coords  WHERE {\n ?item wdt:P39 wd:Q18341329;\n    wdt:P20 ?PoD.\n   ?item rdfs:label ?itemLabel.\n   \n    ?item wdt:P570 ?DOD.\n    ?PoD wdt:P625 ?coords.\n  FILTER((LANG(?itemLabel)) = \"da\")\n  OPTIONAL {\n    ?item wdt:P1196 ?manner.\n    ?manner rdfs:label ?mannerLabel.\n    FILTER((LANG(?mannerLabel)) = \"da\")\n    BIND(?mannerLabel AS ?layer)\n  }\n}", "context": "Location and date of death of Danish rulers", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coords ?layer\nWHERE \n{\n  ?item wdt:P39 wd:Q842606.\n  ?item wdt:P20 ?PoD.\n  ?PoD wdt:P625 ?coords .\n  ?item rdfs:label ?itemLabel . filter(lang(?itemLabel)=\"en\")  \n  optional { ?item wdt:P1196 ?manner.\n            ?manner rdfs:label ?mannerLabel .  filter(lang(?mannerLabel)=\"en\")\n             bind(?mannerLabel  as ?layer) }\n}", "context": "Map of the places & causes of death of Roman Emperors", "target": ""}
{"query": "#defaultView:ImageGrid\n# members of the 16th Odisha Assembly (2019-24).\nSELECT DISTINCT ?item ?itemLabel ?constituencyLabel ?partyLabel ?image\n{\n ?item p:P39 ?positionStatement .\n ?positionStatement ps:P39 wd:Q61479103 . \n OPTIONAL { ?item wdt:P18 ?image. }\n OPTIONAL { ?positionStatement pq:P768 ?constituency . }\n OPTIONAL { ?positionStatement pq:P4100|pq:P102 ?party . }\n OPTIONAL { ?item wikibase:sitelinks ?linkcount. } # <<<<<< Link count  \n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en\". }\n}\nORDER BY DESC(?linkcount)", "context": "Images of members of the 16th Odisha Assembly (2019-24)", "target": ""}
{"query": "SELECT DISTINCT ?item ?dob ?sign {\n ?item wdt:P31 wd:Q5 ; p:P39/ps:P39/wdt:P279* wd:Q274948 . # all Japanese Prime Ministers\n ?item p:P569/psv:P569 [ # with a known birthday\n   wikibase:timeValue ?dob; wikibase:timePrecision 11 # of “day” precision\n ].\nBIND(MONTH(?dob)AS ?month)\nBIND(DAY(?dob)AS ?day)\nBIND(IF(((?month=3)&&(?day>=21))||((?month=4)&&(?day<20)),\"Aries\",\nIF(((?month=4)&&(?day>=20))||((?month=5)&&(?day<21)),\"Taurus\",\nIF(((?month=5)&&(?day>=21))||((?month=6)&&(?day<21)),\"Gemini\",\nIF(((?month=6)&&(?day>=21))||((?month=7)&&(?day<23)),\"Cancer\",\nIF(((?month=7)&&(?day>=23))||((?month=8)&&(?day<23)),\"Leo\",\nIF(((?month=8)&&(?day>=23))||((?month=9)&&(?day<23)),\"Virgo\",\nIF(((?month=9)&&(?day>=23))||((?month=10)&&(?day<23)),\"Libra\",\nIF(((?month=10)&&(?day>=23))||((?month=11)&&(?day<22)),\"Scorpio\",\nIF(((?month=11)&&(?day>=23))||((?month=12)&&(?day<22)),\"Sagittarius\",\nIF(((?month=12)&&(?day>=22))||((?month=1)&&(?day<20)),\"Capricorn\",\nIF(((?month=1)&&(?day>=20))||((?month=2)&&(?day<19)),\"Aquarius\",\nIF(((?month=2)&&(?day>=19))||((?month=3)&&(?day<21)),\"Pisces\",\n\"unknown (this should never happen)\"\n))))))))))))\n       AS ?sign)\n SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n}\n\n", "context": "Star signs of Japanese Prime Ministers", "target": ""}
{"query": "# Video game series with the longest time gap between a game and its direct sequel\nSELECT ?series ?seriesLabel ?years ?interval ?game ?gameLabel ?original_date_game ?sequel ?sequelLabel ?original_date_sequel WHERE {{\nSELECT ?game ?gameLabel (MIN(?game_date) AS ?original_date_game) ?sequel ?sequelLabel (MIN(?sequel_date) AS ?original_date_sequel) ?series ?seriesLabel WHERE {\n?game (wdt:P31/(wdt:P279*)) wd:Q7889; # All video games…\nwdt:P577 ?game_date. # with their publication date(s)…\n{?game p:P179 ?series_statement. # (Unpacking the series statement)\n?series_statement ps:P179 ?series; #   …in a series…\npq:P156 ?sequel. #   …with a sequel…\n} UNION { # …or…\n?game wdt:P156 ?sequel. #   …with a sequel…\nOPTIONAL {\n?game wdt:P179 ?series # …maybe with a series\n}}\n?sequel wdt:P577 ?sequel_date. # with its own publication date(s)\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }}\nGROUP BY ?game ?gameLabel ?sequel ?sequelLabel ?series ?seriesLabel}\nBIND(?original_date_sequel - ?original_date_game AS ?interval) # Calculating the time gap between the original releases…\nBIND(ROUND(?interval / 365 ) AS ?years) # ...and converting to years, for convenience.\nFILTER(?interval > 0 ) # Only keeping sequels published after (filtering out some bad data)\n}\nORDER BY DESC (?interval)", "context": "Video game series with the longest time gap between a game and its direct sequel", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coord\"]}\n#Query by Silvia Gutiérrez (@espejolento)\n\nSELECT ?author ?authorLabel ?birthplaceLabel ?idELEM ?coord\n\n(GROUP_CONCAT(DISTINCT ?occupation_label; SEPARATOR=\", \") as ?occupations) #thanks to @NavinoEvans for this!\n\nWHERE {\n      ?author wdt:P1565 ?idELEM . #has a ELEM id   \n       OPTIONAL {\n        ?author wdt:P19 ?birthplace .   # born in a place...\n         ?birthplace wdt:P625 ?coord .\n         ?author wdt:P106 ?occupation . #get occupation\n         ?occupation rdfs:label ?occupation_label FILTER (lang(?occupation_label) = \"es\") #get label the 'manual' way \n       }\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],es\". }        \n} GROUP BY ?author ?authorLabel ?birthplaceLabel ?idELEM ?coord #GROUP BY\nLIMIT 5000", "context": "Map of the birthplace of persons with an ELEM id on Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT ?pertsona ?pertsonaLabel ?koord  WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Mikel wdt:P460 wd:Q4927524.\n  ?pertsona wdt:P735 ?Mikel.\n  ?pertsona wdt:P19 ?jaiolekua.\n  ?jaiolekua wdt:P625 ?koord.\n}", "context": "Birthplace of people named Michael or any other variant of the name", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?count ?image WHERE {\n  ?item wikibase:sitelinks ?count.\n  ?item wdt:P31/wdt:P279* wd:Q202866;\n        wdt:P495 wd:Q15180.\n# FILTER (SUBSTR(str(?sitelink), 11, 15) = \".wikipedia.org/\") .\n  OPTIONAL {?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"ru\",\"en\" }\n}\n ORDER BY DESC(?count)", "context": "Soviet cartoons in descending order of popularity in the world", "target": ""}
{"query": "## Select Query was used to retrieve specific results (Country name, wiki data number, Synonyms)\nSELECT ?wikidata ?wikidataLabel (?code as ?_iso3166) ?wikipedia (?wikidataAltLabel as ?alt) ?synonym ?wikidataDescription  (?wikidataLabel as ?term) { \n\n## Forcing particular query execution order\n  hint:Query hint:optimizer \"None\" . \n\n## all ISO countries \n  ?wikidata wdt:P297 ?code.\n\n## Optional details about the countries like links to wikipaedia pages for each country to be presented in a seperate column\n  OPTIONAL { ?wikipedia schema:about ?wikidata; schema:isPartOf <https://en.wikipedia.org/> }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n\n## Selecting the prefered label \n    ?wikidata skos:altLabel ?wikidataAltLabel ; rdfs:label ?wikidataLabel; schema:description  ?wikidataDescription          \n  } \n\n## Making sure the RGI alphabets of the flags are not rendered as flags and they appear as simple alphabets by specifying the acceptable characters. \n  BIND (REPLACE(REPLACE(?wikidataAltLabel, \"(, )?[🇦-🇿]{2}\", \"\"), \"^, \", \"\") AS ?synonym )\n}\n\n ORDER BY (?wikidataLabel)", "context": "List of ISO countries showing each country's synonyms, labels, etc.", "target": ""}
{"query": "SELECT  ?personLabel (COUNT(?parti) AS ?partiC)  WHERE {\n  ?person wdt:P102 ?parti;\n          wdt:P27 wd:Q20.\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"nb,en\" }\n\n}\nGROUP BY ?personLabel\nHAVING (?partiC > 2)\nORDER BY DESC (?partiC)\nLIMIT 100", "context": "Norwegian politicians who have been members of at least 3 parties", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?vernoemdeLabel ?coords\nWHERE \n{\n  ?item wdt:P31 wd:Q79007 .\n  ?item wdt:P131 wd:Q43631 .\n  ?item wdt:P138 ?vernoemde .\n  ?item wdt:P625 ?coords .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Streets in Leiden named after people/things", "target": ""}
{"query": "PREFIX gas: <http://www.bigdata.com/rdf/gas#>\n\nSELECT ?item ?itemLabel ?sitelinks {\n  SERVICE gas:service {\n    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" ;\n                gas:in wd:Q291877 ;\n                gas:traversalDirection \"Forward\" ;\n                gas:out ?item ;\n                gas:linkType wdt:P40 .\n  }\n  ?item wdt:P21 wd:Q6581072.\n  ?item wdt:P569 ?jaiotza.\n  ?item wdt:P106 wd:Q33999.\n  ?item wikibase:sitelinks ?sitelinks.\n  BIND(YEAR(?jaiotza) as ?year).\n  FILTER(?year > 1950).\n  MINUS{?item wdt:P570 ?heriotza}.\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"eu,es,en\" }\n}\norder by desc(?sitelinks)", "context": "Women actress less than 70 years old who are descendants of the first king of Navarre, ordered by number of sitelinks", "target": ""}
{"query": "SELECT ?fødselsdato ?personLabel (GROUP_CONCAT(?embede_label; separator=\", \") AS ?embeder) {\n  ?person wdt:P102 wd:Q212101 ;\n          p:P39 ?embede_statement ;\n          wdt:P570 [] ;\n          wdt:P569 ?fødselsdato .\n  ?embede_statement ps:P39 ?embede ;\n                    pq:P580 ?start .\n  ?embede wdt:P361 wd:Q1503072 ;\n          rdfs:label ?embede_label .\n  FILTER (?start > \"1992-01-01\"^^xsd:dateTime)\n  FILTER (LANG(?embede_label) = 'da')\n  FILTER (wd:Q1424299 != ?person)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"da\". }\n}\nGROUP BY ?fødselsdato ?person ?personLabel ?dødsdato\nORDER BY DESC(?fødselsdato)", "context": "Dead Social Democratic Party members who have been ministers in 1992 or later", "target": ""}
{"query": "#defaultView:AreaChart\nSELECT ?year (count(?margolan) AS ?count) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?margolan wdt:P31 wd:Q3305213;\n    wdt:P571 ?sorrera_data.\n  ?margolan wdt:P18 ?irudia.\n  BIND(YEAR(?sorrera_data) as ?year).\n  FILTER(?year > 1)\n}\nGROUP by ?year\nORDER by ASC(?year)", "context": "Artworks with an image in Wikidata by year of creation", "target": ""}
{"query": "#Swiss mountains\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord ?elev ?prom ?picture WHERE {\n  ?item (p:P2044/psn:P2044/wikibase:quantityAmount) ?elev;\n    \n    wdt:P625 ?coord;\n    wdt:P17 wd:Q39;\n    wdt:P2660 ?prom;\n    wdt:P18 ?picture.\n  FILTER(?prom > 100)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?prom)", "context": "Map of mountains in Switzerland", "target": ""}
{"query": "#defaultView:Map\nSELECT ?market ?marketLabel ?coordinate_location WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id\". }\n  ?market wdt:P31 wd:Q330284;\n    wdt:P276 wd:Q11462;\n          wdt:P625 ?coordinate_location.\n}\n", "context": "Map of markets in the city of Surabaya", "target": ""}
{"query": "#defaultView:Map\nSELECT ?geo ?layer WHERE {\n  ?ereserki wdt:P31 wd:Q23691 ; wdt:P17 ?country ; wdt:P826 ?ton .\n  ?country wdt:P3896 ?geo .\n  ?ton rdfs:label ?tonLabel .\n  FILTER (lang(?tonLabel) = \"eu\"). \n  BIND ( ?tonLabel AS ?layer )\n}", "context": "National Anthems by tonality", "target": ""}
{"query": "SELECT ?item1 ?item1Label ?item2Label ?item3Label ?item4Label ?item5Label ?item6Label WHERE {\n  ?item1 wdt:P138 ?item2 .\n  ?item2 wdt:P138 ?item3 .\n  ?item3 wdt:P138 ?item4 .\n  ?item4 wdt:P138 ?item5 .\n  ?item5 wdt:P138 ?item6 .\n  \n  FILTER (?item1 != ?item2 && ?item2 != ?item3 && ?item3 != ?item4 && ?item4 != ?item5 && ?item5 != ?item6) .\n  FILTER (?item1 != ?item3 && ?item2 != ?item4 && ?item3 != ?item5 && ?item4 != ?item6) .\n  FILTER (?item1 != ?item4 && ?item2 != ?item5 && ?item3 != ?item6) .\n  FILTER (?item1 != ?item5 && ?item2 != ?item6) .\n  FILTER (?item1 != ?item6) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de,fr\". }\n}", "context": "Item named after item named after item named after item named after item named after item", "target": ""}
{"query": "PREFIX parl: <https://id.parliament.uk/schema/>\nPREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\nSELECT ?oppositionPost ?memberLabel ?postStart ?postEnd ?sitelink WHERE {\n  ?member wdt:P6213 ?o.\n  SERVICE <https://api.parliament.uk/sparql> {\n    ?member parl:wikidataResourceHasEquivalentWikidataThing ?parlMember.\n    ?parlMember parl:oppositionPersonHasOppositionIncumbency ?oppPost .\n               ?oppPost parl:incumbencyHasPosition/rdfs:label ?oppositionPost ;\n               parl:startDate ?postStart;          \n               parl:endDate ?postEnd .\n  }\n  OPTIONAL {\n  ?sitelink schema:about ?member.\n  ?sitelink schema:isPartOf <https://en.wikipedia.org/> .\n    }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n      ORDER BY ?postStart", "context": "UK parliament members' opposition posts, start dates and end dates where Wikidata has a Parliament ID for that member", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?part (STR(YEAR(?pit)) AS ?year) (COUNT(*) AS ?counts) \nWHERE \n{\n  ?item wdt:P31 wd:Q100532807.\n  ?item wdt:P179 ?part.\n  ?part wdt:P1001 wd:Q27;\n        wdt:P585 ?pit.\n}\nGROUP BY ?part ?partLabel ?pit", "context": "Count of Irish Statutory Instruments, 1948-2020", "target": ""}
{"query": "SELECT ?item ?itemLabel ?doi ?journal ?journalLabel ?date WHERE {\n  ?item wdt:P921 wd:Q98969121.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" . \n  }\n  \nOPTIONAL { ?item wdt:P18 ?_image. }\nOPTIONAL { ?item wdt:P356 ?doi. }\nOPTIONAL { ?item wdt:P577 ?date.}\nOPTIONAL { ?item wdt:P1433 ?journal. }\n}\nORDER BY DESC(?date)\nLIMIT 100", "context": "Scientific papers explaining backward contact tracing", "target": ""}
{"query": "#List of sovereign states in English, French, German, Russian\nSELECT ?country ?label_en ?label_fr ?label_de ?label_ru\nWHERE\n{\n  ?country wdt:P31 wd:Q3624078.\n  OPTIONAL { ?country rdfs:label ?label_en filter (lang(?label_en) = \"en\"). }\n  OPTIONAL { ?country rdfs:label ?label_fr filter (lang(?label_fr) = \"fr\"). }\n  OPTIONAL { ?country rdfs:label ?label_de filter (lang(?label_de) = \"de\"). }\n  OPTIONAL { ?country rdfs:label ?label_ru filter (lang(?label_ru) = \"ru\"). }\n}", "context": "List of sovereign states in en, fr, de, ru", "target": ""}
{"query": "#defaultView:Map\n\nSELECT ?item ?anotherItemLabel ?loc WHERE {\n  ?item wdt:P31 wd:Q3305213 .\n  ?item wdt:P180 ?anotherItem .\n  ?anotherItem wdt:P625 ?loc .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Locations depicted in paintings", "target": ""}
{"query": "#defaultView:BarChart\n\nSELECT (COUNT(?anotherItem) AS ?anotherItemCount) ?anotherItemLabel ?loc WHERE {\n  ?item wdt:P31 wd:Q3305213 .\n  ?item wdt:P180 ?anotherItem .\n  ?anotherItem wdt:P625 ?loc .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?anotherItemLabel ?anotherItemCount ?loc\nORDER BY DESC(?anotherItemCount)\nLIMIT 20", "context": "The most commonly depicted locations", "target": ""}
{"query": "SELECT ?i_subject ?iu_enwiki ?i_subjectLabel ?ii_pic ?rn_t_dobLabel ?i_DOB ?rn_t_dodLabel ?i_DOD ?i_place ?i_placeLabel ?i_plaque ?i_plaqueLabel \n?ii_plaqueImage ?i_occupationLabel WHERE  \n{\n ?i_plaque wdt:P31 wd:Q721747; wdt:P131 wd:Q62274582 .\n     ?i_plaque wdt:P547 ?i_subject .\n     ?i_subject wdt:P31 wd:Q5 .   \n  \nOPTIONAL { ?iu_enwiki schema:about ?i_subject; schema:isPartOf <https://en.wikipedia.org/> }\nOPTIONAL {?i_subject wdt:P18  ?ii_pic. }\nOPTIONAL {?i_plaque wdt:P18 ?ii_plaqueImage .}\nOPTIONAL {?rn_t_dob wikibase:directClaim wdt:P569. }\nOPTIONAL {?i_subject wdt:P569 ?i_DOB .}\nOPTIONAL {?rn_t_dod wikibase:directClaim wdt:P570. }\nOPTIONAL {?i_subject wdt:P570 ?i_DOD .}\nOPTIONAL {?i_subject wdt:P106  ?i_occupation} \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Commemorative plaques and their subjects in Aberdeen, Scotland; with photos of plaque and subject and Wikipedia links", "target": ""}
{"query": "#defaultView:Map\nselect * {\n  ?city wdt:P31/wdt:P279* wd:Q515 .\n  ?city wdt:P6 ?mayor .\n  ?mayor wdt:P21 ?gender .\n  ?city wdt:P625 ?loc .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n                         ?gender rdfs:label ?genderLabel}\n  bind(?genderLabel as ?layer)\n}", "context": "Cities with female mayor", "target": ""}
{"query": "#defaultView:Map\nSELECT ?country ?nameLabel ?shape ?layer WITH {\n  \n  SELECT ?item ?name ?country where {\n    values ?name {\n    wd:Q14936808\n    wd:Q1183757\n    wd:Q1440465\n    wd:Q7648632\n    wd:Q7648707\n    wd:Q13138161\n    wd:Q14936073\n    wd:Q14936243\n    wd:Q14936686\n    wd:Q14936925\n    wd:Q14937153\n    wd:Q16281769\n    wd:Q18061334\n    wd:Q18067250\n    wd:Q18067716\n    wd:Q18067726\n    wd:Q18067736\n    wd:Q18067785\n    wd:Q18067790\n    wd:Q18199125\n    wd:Q19368856\n    wd:Q62595789\n    wd:Q78165638\n    wd:Q94368759\n    wd:Q97120482\n    } .\n    ?item wdt:P735 ?name .\n    ?item wdt:P19/wdt:P17 ?country .\n\n  } LIMIT 40000\n} AS %data WITH {\n  SELECT ?country ?name (COUNT(?item) AS ?pocet) {\n    INCLUDE %data .\n  } GROUP BY ?country ?name\n} AS %counts WITH {\n  SELECT ?country (MAX(?pocet) AS ?max) {\n    INCLUDE %counts .\n  } GROUP BY ?country\n} AS %best WHERE {\n  INCLUDE %counts .\n  INCLUDE %best .\n  FILTER( ?max = ?pocet ) .\n  ?name rdfs:label ?nameLabel filter(lang(?nameLabel)=\"en\").\n  ?country wdt:P3896 ?shape .\n  BIND((?nameLabel) as ?layer ). \n}", "context": "Most common variants of the name \"Susanne\" in each country", "target": ""}
{"query": "#defaultView:Map\nSELECT ?arthistorianLabel ?coordinates WHERE {\n  ?arthistorian wdt:P31 wd:Q5 ;\n                wdt:P19 ?birthPlace ;\n                wdt:P106 wd:Q1792450 .\n  ?birthPlace wdt:P625 ?coordinates .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Birthplaces of art historians", "target": ""}
{"query": "SELECT ?taxon ?dutch ?nl ?de ?it ?en ?fr   WHERE {\nbind(if(bound(?english), \"+\", \"-\") as ?en )\nbind(if(bound(?french), \"+\", \"-\") as ?fr )\nbind(if(bound(?dutch), \"+\", \"-\") as ?nl )\nbind(if(bound(?german), \"+\", \"-\") as ?de )\nbind(if(bound(?italian), \"+\", \"-\") as ?it )\n{SELECT ?taxon  ?dutch ?italian ?english ?french ?german WHERE {\n   ?taxon wdt:P5588 wd:Q458 .\n   OPTIONAL {?english schema:about ?taxon .\n   FILTER (regex(str(?english), \"https://en.wikipedia\"))}\n   OPTIONAL {?dutch schema:about ?taxon .\n   FILTER (regex(str(?dutch), \"https://nl.wikipedia\"))}\n   OPTIONAL {?french schema:about ?taxon .\n   FILTER (regex(str(?french), \"https://fr.wikipedia\"))}\n   OPTIONAL {?german schema:about ?taxon .\n   FILTER (regex(str(?german), \"https://de.wikipedia\"))}\n   OPTIONAL {?italian schema:about ?taxon .\n   FILTER (regex(str(?italian), \"https://it.wikipedia\"))}\n}}}", "context": "Invasive species in EU and whether they have articles in different languages", "target": ""}
{"query": "#Joueurs de l'Olympique de Marseille\n#defaultView:Map\nSELECT ?item ?itemLabel ?nomLabel ?naissanceLabel ?coord ?debut ?fin ?matches WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P106 wd:Q937857.\n  OPTIONAL { ?item wdt:P734 ?nom. }       \n  ?item wdt:P19 ?naissance.\n  ?naissance wdt:P625 ?coord .\n  ?item p:P54 ?om.\n  ?om ps:P54 wd:Q132885.\n  OPTIONAL { ?om pq:P580 ?debut. }\n  OPTIONAL { ?om pq:P582 ?fin. }\n  OPTIONAL { ?om pq:P1350 ?matches. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr, en\". }\n}", "context": "Birthplace of Olympique de Marseille players", "target": ""}
{"query": "SELECT  ?month ?year (count(*) as ?count) WHERE {\n?item wdt:P31 wd:Q5.\n?item (p:P570/psv:P570) ?timenode0.\n  ?timenode0 wikibase:timeValue ?time0.\n  ?timenode0 wikibase:timePrecision ?timeprecision0.\n  FILTER('2019-01-01'^^xsd:dateTime <= ?time0 && ?time0 < '2021-12-31'^^xsd:dateTime).\n  FILTER(?timeprecision0>=8)\n?item wdt:P570 ?time.\n?item wdt:P509 wd:Q84263196.\n bind(month(?time0) as ?month) \n bind(year(?time0) as ?year) \n}\ngroup by ?month ?year", "context": "COVID-19 deaths by month", "target": ""}
{"query": "# Movies by rate of actors who studied at RADA (When distribution has at last 5 actors)\nPREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\n\nSELECT DISTINCT ?item ?itemLabel ((xsd:float(?nbRadaActors)/xsd:float(?totalNbActors)) AS ?rate)  WHERE {\n  {\n    SELECT DISTINCT ?item (COUNT(?actors) AS ?nbRadaActors) {\n      ?item wdt:P31/wdt:P279* wd:Q11424 .\n      ?item wdt:P161 ?actors .\n      ?actors wdt:P69 wd:Q523926 .\n\n    } GROUP BY ?item \n  }\n  \n  {\n    SELECT DISTINCT ?item (COUNT(?actors) AS ?totalNbActors) {\n      ?item wdt:P31/wdt:P279* wd:Q11424 .\n      ?item wdt:P161 ?actors .\n    } GROUP BY ?item HAVING (?totalNbActors >= 5)\n\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n} ORDER BY DESC(?rate)", "context": "Movies by rate of actors who studied at RADA (When distribution has at last 5 actors)", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?clCoord\", \"?blCoord\", \"?line\"]}\n#linking central libraries in California to their branch library \nSELECT ?libSystem ?libSystemLabel ?centralLibrary ?centralLibraryLabel ?clCoord ?branchLibrary ?branchLibraryLabel ?blCoord ?line ?layer WHERE {\n  ?libSystem wdt:P31/wdt:P279* wd:Q28324850 ; wdt:P131 ?loc ;\n                 wdt:P527 ?centralLibrary ;\n                 wdt:P527 ?branchLibrary .\n  ?centralLibrary wdt:P31 wd:Q12317349 ;\n                  wdt:P625 ?clCoord .\n  ?branchLibrary wdt:P31 wd:Q11396180 ; \n                 wdt:P625 ?blCoord .\n  \n  ?centralLibrary p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?clLon; wikibase:geoLatitude ?clLat; ] ].\n  ?branchLibrary p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?blLon; wikibase:geoLatitude ?blLat; ] ].\n  \n  BIND(CONCAT('LINESTRING (', STR(?clLon), ' ', STR(?clLat), ',', STR(?blLon), ' ', STR(?blLat), ')') AS ?str1) .\n  BIND(STRDT(?str1, geo:wktLiteral) AS ?line) .\n  \n  BIND(?libSystem as ?layer)\n  \n  { SELECT * { ?loc wdt:P131* wd:Q99 . } }\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\n  \n  ", "context": "Central libraries in California linked to their branch library", "target": ""}
{"query": "#defaultView:ImageGrid\n# Écrivaines dans le dictionnaire de la SIEFAR avec un portrait dans Wikimedia Commons\nSELECT ?femmeLabel ?femme (MIN(?image) AS ?pic) (CONCAT(\"http://siefar.org/dictionnaire/fr/\", MIN(?siefarId)) AS ?siefar) WHERE {\n  ?femme wdt:P7962 ?siefarId;\n    wdt:P106/wdt:P279* wd:Q36180.\n  OPTIONAL { ?femme wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\". }\n}\nGROUP BY ?femmeLabel ?femme\nORDER BY (?siefar)", "context": "Writers in the SIEFAR dictionary with a portrait in Wikimedia Commons", "target": ""}
{"query": "#defaultView:Map\nSELECT ?article ?person ?personLabel ?picGrave ?coord ?bd ?graveplot2 ?layer WHERE {\n{\n  ?person wdt:P119 wd:Q311 ;\n           p:P119 [ \n             pq:P965 ?graveplot];\n           wdt:P569 ?bd .\n  ?person wdt:P19 ?city .\n  ?city wdt:P625 ?coord .\t\t\t\n   OPTIONAL{ ?person wdt:P1442 ?picGrave .} \t\t\n   OPTIONAL {\n      ?article schema:about ?person .\n      ?article schema:inLanguage \"fr\" .\n      ?article schema:isPartOf <https://fr.wikipedia.org/> .}    \n  BIND (CONCAT(\"Division \",?graveplot) AS ?graveplot2)\n  BIND(\n    IF(?bd < \"1700-01-01T00:00:00Z\"^^xsd:dateTime, \"<1700\",\n    IF(?bd < \"1800-01-01T00:00:00Z\"^^xsd:dateTime, \"18e siècle\",\n    IF(?bd < \"1900-01-01T00:00:00Z\"^^xsd:dateTime, \"19e siècle\",\n    IF(?bd < \"2000-01-01T00:00:00Z\"^^xsd:dateTime, \"20e siècle\",\n    \"21e siècle\"))))\n    AS ?layer).\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\"}\n }\nOrder by ?personLabel", "context": "Place and date of birth of people buried at Père Lachaise Cemetery (one color per century of birth)", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?occupationLabel (COUNT (*) AS ?count) WHERE {\n?person wdt:P31 wd:Q5 .\n?person wdt:P509 wd:Q84263196 .\n ?person wdt:P106 ?occupation .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGroup BY ?occupationLabel\nORDER BY desc( ?count)\nLIMIT 100", "context": "Occupations of people on Wikidata dying of coronavirus (politician ranks higher than the next 3 occupations combined)", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?i_owner ?i_ownerLabel ?ii_owner ?rn_ownsLabel ?i_item ?i_itemLabel ?i_itemDescription ?ii_item WHERE {\n  {\n    SELECT ?i_item WHERE {\n      wd:Q22686 (wdt:P1830 | ^wdt:P1830)* ?i_item.\n    }\n  }\n  \n  OPTIONAL {\n              ?i_owner wdt:P1830 ?i_item.\n              OPTIONAL { ?i_owner wdt:P18 ?ii_owner. }\n              OPTIONAL { ?i_item wdt:P18 ?ii_item. }\n           }\n\n  BIND (\"owns/owned\" AS ?rn_owns).\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Things Donald Trump and others own / have owned", "target": ""}
{"query": "#defaultView:Map\nSELECT ?groupLabel (?groupLabel as ?layer) ?article ?person ?personLabel  ?personDescription  ?coord ?pic ?picGrave \nWHERE {\n\t?person wdt:P119 wd:Q311; #Place of burial\n            p:P119 [ \n              pq:P965 ?graveplot;\n              pq:P625 ?coord].  \n   OPTIONAL{ ?person wdt:P18 ?pic .} \t\t\t\t\t# If we have an illustration\n   OPTIONAL{ ?person wdt:P1442 ?picGrave .} \t\t\t# If we have a picture of the grave\n   OPTIONAL {\n      ?article schema:about ?person .\n      ?article schema:inLanguage \"fr\" .\n      ?article schema:isPartOf <https://fr.wikipedia.org/> .\n    }\n   OPTIONAL { ?person wdt:P21 ?group .} \t\t\t# Occupation in Layer\n\n  \n\nSERVICE wikibase:label {    bd:serviceParam wikibase:language \"fr, en\" .}\n\n}", "context": "Graveplots by gender in Père Lachaise Cemetery", "target": ""}
{"query": "#defaultView:Graph\nSELECT\n  ?author1 ?author1Label ?image1 ?rgb\n  ?author2 ?author2Label ?image2 \nWITH {\n  SELECT\n    ?author1 (SAMPLE(?image1_) AS ?image1)\n    ?author2 (SAMPLE(?image2_) AS ?image2)\n    (SAMPLE(?rgb_) AS ?rgb)\n  WHERE {\n    wd:Q784171 ^wdt:P361* / ^( wdt:P108 | wdt:P1416 | wdt:P463 ) ?author1 , ?author2 . \n    ?work wdt:P50 ?author1 , ?author2 .\n\n    # Only display co-authorship for certain types of documents\n    # Journal and conference articles, books, not (yet?) software\n    VALUES ?publication_type { wd:Q13442814 wd:Q571 wd:Q26973022 wd:Q17928402 wd:Q947859 wd:Q54670950 }\n    FILTER EXISTS { ?work wdt:P31 ?publication_type . }\n\n    # No self-links.\n    FILTER (?author1 != ?author2)\n\n    # Images\n    OPTIONAL { ?author1 wdt:P18 ?image1_ }\n    OPTIONAL { ?author2 wdt:P18 ?image2_ }\n\n    # Coloring of the nodes\n    BIND(\"FFFFFF\" AS ?rgb_)\n  }\n  GROUP BY ?author1 ?author2\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,sv,ru,zh\".\n  }\n}\n", "context": "Co-authorship between researchers from National University of La Plata", "target": ""}
{"query": "#Number of Harvard Law graduates dead by year between 1850 and 1950\n#defaultView:AreaChart\nSELECT  ?year (COUNT(?person) as ?count)\nWHERE\n{\n  ?person wdt:P31 wd:Q5;\n          wdt:P69 wd:Q49122;\n          p:P570/psv:P570 [\n                wikibase:timeValue ?date ;\n              ] .\n  BIND(STR(YEAR(?date)) as ?year).\n  filter (?date > \"1850-01-01\"^^xsd:dateTime && ?date < \"1950-01-01\"^^xsd:dateTime)\n}\nGROUP BY ?year", "context": "Number of Harvard Law graduates dead by year between 1850 and 1950", "target": ""}
{"query": "# Artists and their works depictions\n#defaultView:Graph\nSELECT ?item1 ?image1 ?item1Label ?item2 ?image2 ?item2Label ?size ?rgb \nWHERE \n{\n VALUES ?creator { wd:Q1064767 }\n { # Get works and instances\n  VALUES ?rgb { \"FFBD33\" }\n  VALUES ?size { 2 }\n  ?item1 wdt:P170 ?creator .\n  ?item1 wdt:P31 ?item2 .\n  OPTIONAL { ?item1 wdt:P18 ?image1. }\n } \n UNION\n { # Depictions\n  VALUES ?rgb { \"fff033\" }\n  VALUES ?size { 1 }\n  ?item1 wdt:P170 ?creator .\n  ?item1 wdt:P180 ?item2 .\n  OPTIONAL { ?item1 wdt:P18 ?image1. }\n }\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Visualization of the works of Charles Heaphy", "target": ""}
{"query": "#TEMPLATE={\"template\":\"Identifiers present on ?comparewith , but not on the item for Sean Connery\",\"variables\":{\"?comparewith\":{\"query\":\"SELECT DISTINCT ?id WHERE { VALUES ?id { wd:Q81520 wd:Q181917 wd:Q4547 wd:Q134333 wd:Q41233 wd:Q205599 wd:Q37459 wd:Q7374 }  } \" } } }\nSELECT\n  ?p ?pLabel ?comparewithLabel\n  (SAMPLE(?url) AS ?url_comp2) \nWHERE \n{\n  BIND(wd:Q39666 as ?comparewith)\n  hint:Query hint:optimizer \"None\".\n  {  ?comparewith ?wdt ?v.\n     ?p wikibase:directClaim ?wdt; wikibase:propertyType wikibase:ExternalId.\n     FILTER NOT EXISTS { wd:Q4573 ?wdt [] }\n        FILTER NOT EXISTS { ?p wdt:P31 wd:Q66712599 }\n        FILTER NOT EXISTS { ?p wdt:P31 wd:Q27525351 }\n        FILTER NOT EXISTS { ?p wdt:P31 wd:Q66118402 }   \n     OPTIONAL { ?p wdt:P1630 ?f. } BIND(URI(REPLACE(?f, \"\\\\$1\", ?v)) AS ?url)\n  } UNION { BIND( wd:Q4573 as ?p) }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\"}\n}\nGROUP BY ?p ?pLabel ?comparewithLabel\nORDER BY (?url_comp2)", "context": "Identifiers present on the item for Penelope Cruz (or other movie people), but not on the item for Sean Connery", "target": ""}
{"query": "#defaultView:Map\nSELECT * WHERE {\n  ?q wdt:P5375 ?id .\n  ?q wdt:P569 ?date .\n  ?q wdt:P19/wdt:P625 ?coord .\n  BIND ( floor(year(?date)/100) AS ?layer )\n}", "context": "Map of places of birth of people with an identifier Inter-university Health Library in Wikidata colored by century of birth", "target": ""}
{"query": "#defaultView:Map\nSELECT ?terminusLabel ?roadLabel ?location  WHERE {\n  ?road wdt:P31 wd:Q18019452 ;\n        wdt:P609 ?terminus .\n  ?terminus wdt:P625 ?location .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  }\n", "context": "Terminus locations of A Roads in Britain", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?date ?age ?layer WHERE {\n{\n?usPresident wdt:P31 wd:Q5;\np:P39 [\nps:P39 wd:Q11696;\npq:P580 ?usPresidentStart\n];\nwdt:P569 ?usPresidentDob.\nBIND(xsd:integer((?usPresidentStart - ?usPresidentDob) / 365.2425) AS ?usPresidentAge)\nBIND(?usPresidentStart AS ?date)\nBIND(?usPresidentAge AS ?age)\nBIND(\"US president\"@en AS ?layer)\n} UNION {\n?ukPrimeMinister wdt:P31 wd:Q5;\np:P39 [\nps:P39 wd:Q14211;\npq:P580 ?ukPrimeMinisterStart\n];\nwdt:P569 ?ukPrimeMinisterDob.\nBIND(xsd:integer((?ukPrimeMinisterStart - ?ukPrimeMinisterDob) / 365.2425) AS ?ukPrimeMinisterAge)\nBIND(?ukPrimeMinisterStart AS ?date)\nBIND(?ukPrimeMinisterAge AS ?age)\nBIND(\"UK Prime Minister\"@en AS ?layer)\n} UNION {\nwd:Q21 p:P4442 [\nps:P4442 ?englandMeanAge;\npq:P585 ?englandMeanAgeTime\n].\nBIND(?englandMeanAgeTime AS ?date)\nBIND(?englandMeanAge AS ?age)\nBIND(\"England\"@en AS ?layer)\n}\n# FILTER(?date >= \"1989\"^^xsd:dateTime)\n}", "context": "POTUS/UK PM age at inauguration/appointment and England mean age", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord\", \"?layer\"], \"markercluster\": {\"maxClusterRadius\": 50}}\nSELECT ?x ?xLabel ?coords ?location ?locationLabel ?layer ?startDate WHERE {\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n?x wdt:P31 wd:Q54875403;\nwdt:P580 ?date;\nwdt:P276 ?location.\n?location wdt:P625 ?coords.\nOPTIONAL { ?x wdt:P580 ?start. }\nBIND(YEAR(?start) AS ?startDate)\nBIND(IF(?startDate = 2013 , \"2013\", IF(?startDate = 2014 , \"2014\", IF(?startDate = 2015 , \"2015\", IF(?startDate = 2016 , \"2016\", IF(?startDate = 2017 , \"2017\", IF(?startDate = 2018 , \"2018\", IF(?startDate = 2019 , \"2019\", IF(?startDate = 2020 , \"2020\", \"other\")))))))) AS ?layer)\n?x wdt:P6195 wd:Q13583472.\n}\nORDER BY (?startDate)", "context": "Map of (public-sector) winners of H2020 projects in the Netherlands sortable by year", "target": ""}
{"query": "SELECT DISTINCT ?person ?personName ?image WHERE {\n  ?person wdt:P39 wd:Q11699.\n  ?person wdt:P39 wd:Q11696.\n  ?person wdt:P1559 ?personName.\n  OPTIONAL {?person wdt:P18 ?image.}\n          \n}", "context": "People who have held the positions of both Vice President and President of the United States", "target": ""}
{"query": "SELECT ?country ?countryLabel (count (distinct ?member) as ?count) WHERE {\n  {?item wdt:P31 wd:Q18201623.} # expedition to the International Space Station\n  UNION\n  {  ?item p:P793 ?stat .       # significant event\n  ?stat ps:P793 wd:Q557450 .    # docking and berthing of spacecraft\n  ?stat pq:P276 wd:Q25271 .     # with the International Space Station\n   }\n  ?item wdt:P1029 ?member .     # crew member\n  ?member wdt:P19 ?PoB.         # their place of birth\n  ?PoB wdt:P17 ?country.        # its country\n  SERVICE wikibase:label {   bd:serviceParam wikibase:language \"en\" . }\n  } group by ?country ?countryLabel order by desc(?count)", "context": "Crew of missions to the ISS and human spaceflights that have docked with the ISS", "target": ""}
{"query": "select (str(?decade) as ?dec) (count(distinct ?ps) as ?count) ?causeLabel   where\n{\n  ?mp p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q16707842 . \n  ?ps pq:P582 ?pend . filter not exists { ?mp p:P39 ?ps2 . ?ps2 ps:P39 ?term . ?ps2 pq:P580 ?pend } \n  # not new term starting same day - ie left Parliament\n  ?ps pq:P1534 ?cause . \n  filter not exists { ?ps pq:P1534 wd:Q741182 } # not dissolution\n  filter not exists { ?ps pq:P1534 wd:Q7649223 } # not suspension\n  filter not exists { ?ps pq:P1534 wd:Q35855188 } # tofix\n  filter not exists { ?ps pq:P1534 wd:Q36345696 } # tofix\n  filter not exists { ?ps pq:P1534 wd:Q30580660 } # tofix\n  filter not exists { ?ps pq:P1534 wd:Q52084147 } # tofix\n  filter not exists { ?ps pq:P1534 wd:Q35867887 } # tofix\n  bind(year(?pend) as ?year) . filter(?year >= 1880) . bind((floor(?year/10)*10) as ?decade) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?causeLabel ?decade order by ?causeLabel\n#defaultView:BarChart", "context": "Graph of reasons UK MPs have left Parliament, grouped by decade since the 1890s", "target": ""}
{"query": "select distinct ?start (count(distinct ?ps) as ?count) ?tLabel where\n{\n  ?t wdt:P279 wd:Q16707842 . ?t wdt:P571 ?start . filter(year(?start) > 1858)\n  optional { ?person p:P39 ?ps . ?ps ps:P39 ?t . ?ps pq:P1534 wd:Q13634247 }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?tLabel ?start order by desc(?start) \n#defaultView:BarChart", "context": "Number of successful UK election petitions by parliamentary term, 1859 onwards (includes by-elections as well as general elections)", "target": ""}
{"query": "#Longest cars\n#defaultView:Table\nSELECT ?item ?itemLabel ?length WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q1420;\n    wdt:P2043 ?length;\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?length) (?itemLabel)\nLIMIT 50", "context": "List of longest Cars", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord\"],\"layer\":\"?canalLabel\"}\nSELECT DISTINCT ?item ?label ?canal ?canalLabel ?coord WHERE { \n  {SELECT * {\n    {SELECT * { \n    ?item wdt:P17 wd:Q145 ;\n          wdt:P625 ?coord ;\n          rdfs:label ?label .\n    FILTER(LANG(?label) = \"en\").  \n    } }\n    { ?item p:P177 [ ps:P177 ?canal ] } UNION { ?item p:P2505 [ ps:P2505 ?canal ] } UNION { ?item p:P206 [ ps:P206 ?canal ] }    \n    } } \n  VALUES ?watercourse { wd:Q4022 wd:Q12284 }  \n  FILTER EXISTS { ?canal wdt:P31/wdt:P279* ?watercourse }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\nGROUP BY ?item ?label ?canal ?canalLabel ?coord\nORDER BY ?canalLabel", "context": "Rivers and canals in the United Kingdom", "target": ""}
{"query": "#defaultView:Map{\"layer\": \"?typeLabel\"}\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?coords ?image ?typeLabel\nWHERE \n{\n  ?item wdt:P5008 wd:Q101553389.\n  ?item wdt:P625 ?coords .\n  ?item wdt:P18 ?image .\n  ?item wdt:P31 ?type .\n  \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cy,en\". }\n}", "context": "Map of images taken for the Wicipics photography campaign in Wales", "target": ""}
{"query": "select ?partyLabel (count(distinct ?person) as ?people) (count(distinct ?ps) as ?terms) where\n{\n  ?person p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q16707842 .\n  ?term wdt:P571 ?s . filter(year(?s) >= 1945) .\n  ?ps pq:P4100 ?party .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?partyLabel order by desc(?people)", "context": "Number of people and terms of UK parties since the 1945 election", "target": ""}
{"query": "select distinct ?person ?personLabel ?old_seatLabel ?resigned ?new_seatLabel ?elected ?diff where\n{\n  ?term wdt:P279 wd:Q16707842 .\n  ?person p:P39 ?ps1 . ?ps1 ps:P39 ?term . ?ps1 pq:P580 ?s1 . ?ps1 pq:P582 ?resigned . \n  ?ps1 pq:P768 ?old_seat . ?ps1 pq:P1534 wd:Q796919 .\n  \n  ?person p:P39 ?ps2 . ?ps2 ps:P39 ?term . ?ps2 pq:P580 ?elected . ?ps2 pq:P768 ?new_seat .\n  \n  filter (?old_seat != ?new_seat) . \n  filter (?elected > ?resigned) . \n  bind(round(?elected - ?resigned) as ?diff) .\n  \n  filter not exists { \n    ?person p:P39 ?ps3 . ?ps3 ps:P39 ?term . ?ps3 pq:P580 ?s3 . ?ps3 pq:P582 ?e3 .\n    filter (?s3 > ?resigned ) . filter (?elected > ?e3 ) }  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  \n} order by desc(?elected)", "context": "UK MPs who have voluntarily resigned their seats and then come back for a different seat in the same Parliament", "target": ""}
{"query": "SELECT ?item ?itemLabel ?doi ?journal ?journalLabel WHERE {\n  ?item wdt:P921 wd:Q98969121.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" . \n  }\n  \nOPTIONAL { ?item wdt:P18 ?_image. }\nOPTIONAL { ?item wdt:P356 ?doi. }\nOPTIONAL { ?item wdt:P1433 ?journal. }\n}\nLIMIT 100", "context": "Media articles, public documents and academic references on backward/cluster-focused contact tracing (including models)", "target": ""}
{"query": "SELECT distinct  ?itemLabel ?date ?sourcePropertyLabel ?referenceLabel ?reference \nWHERE \n{\n  ?item wdt:P39/wdt:P279 wd:Q1285463.\n  ?item p:P569 ?dateStatement.\n  ?dateStatement a wikibase:BestRank; # simulate wdt:\n                 ps:P569 ?date.\n  OPTIONAL{\n    ?dateStatement prov:wasDerivedFrom [ ?source ?reference ].\n    ?sourceProperty wikibase:reference ?source.\n  }\n  \n  ?item p:P39 ?decla.\n  ?decla pq:P580 ?ddebut.\n  FILTER NOT EXISTS {?decla pq:P582 ?dfin. }\n  FILTER (?date > \"1952-01-01T00:00:00+00:00\"^^xsd:dateTime)\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\". }.\n\n}", "context": "Date of birth of current French prefects and their sources", "target": ""}
{"query": "select ?countryLabel (count(distinct ?person) as ?count) ?pop ((?count/?pop)*100000 as ?per100k) where\n{\n  ?person wdt:P735 wd:Q1221747 . ?person wdt:P27 ?country .\n  filter not exists { ?person wdt:P570 ?d }\n  ?country wdt:P1082 ?pop . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?countryLabel ?pop order by desc(?per100k)", "context": "Living 'notable' people with the given name \"Karen\" per 100k per country", "target": ""}
{"query": "#Paintings in the Suter Art Gallery\n#defaultView:ImageGrid\n\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?image ?collection WHERE {\n    ?item wdt:P31 wd:Q3305213 ; # Get items that are instances of painting\n          wdt:P195/wdt:P361* ?collection . # That are in some collection\n\n    # Only return results where 'collection' is in the Suter Art Gallery\n    FILTER ( ?collection = wd:Q61797555 )\n \n    OPTIONAL { ?item wdt:P18 ?image } # Optionally with an image\n\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\" }\n}", "context": "Paintings in the Suter Art Gallery", "target": ""}
{"query": "SELECT distinct ?politician ?politicianLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?politician wdt:P27 wd:Q298;\n    wdt:P39 ?position.\n  ?position wdt:P279* wd:Q3304818.\n  MINUS { ?politician wdt:P570 ?death. }\n  MINUS {\n    ?politician wdt:P69 ?institution.\n    ?institution wdt:P31 wd:Q9826.\n  }\n}", "context": "Chilean ministers who were not educated at any high school", "target": ""}
{"query": "SELECT ?item ?itemLabel ?dob ?start ?age1 ?end ?age2 ?rest WHERE {\n  ?item wdt:P31 wd:Q5.\n  ?item p:P39 [ps:P39 wd:Q11696; pq:P580 ?start].\n  BIND ((?start-?dob)/365.25 AS ?age1)\n  OPTIONAL {?item p:P39 [ps:P39 wd:Q11696; pq:P582 ?end].}\n  ?item wdt:P569 ?dob.\n  BIND ((?end-?dob)/365.25 AS ?age2).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL {?item wdt:P570 ?dod.}\n  BIND ((?dod-?end)/365.25 AS ?rest)\n}\nORDER BY DESC(?age1)", "context": "Age of US presidents start and end time", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?cds\",\"?line\",\"?layer\"],\"layer\":\"?trackLabel\"}\nSELECT ?station ?stationLabel ?cds ?line ?trackLabel ?track ?image {\n{SELECT * {\n  SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q1990205 ; gas:out ?station ; gas:out1 ?depth ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . }\n  FILTER(?station != wd:Q1990205)\n                      ?station wdt:P625 ?cds ;\n                               wdt:P17 wd:Q145 .\n                             \n  ?station p:P197 ?adjST .\n  ?adjST ps:P197 ?adj ;\n                pq:P81 ?track .\n\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line)}\n \n}\n  OPTIONAL { ?station wdt:P18 ?image .}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Map of Welsh Railways", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?cds\",\"?line\",\"?layer\"],\"layer\":\"?trackLabel\"}\nSELECT ?station ?stationLabel ?cds ?line ?trackLabel ?track ?image {\n{SELECT * {\n  SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q1990205 ; gas:out ?station ; gas:out1 ?depth ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . }\n  FILTER(?station != wd:Q1990205)\n                      ?station wdt:P625 ?cds ;\n                               wdt:P17 wd:Q55 .\n                             \n  ?station p:P197 ?adjST .\n  ?adjST ps:P197 ?adj ;\n                pq:P81 ?track .\n\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line)}\n \n}\n  OPTIONAL { ?station wdt:P18 ?image .}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Connections of railways of the Netherlands", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?language ?languageLabel ?count\nWITH {                             \n  SELECT ?language (COUNT(DISTINCT ?lexeme) AS ?count) { \n    ?lexeme dct:language ?language.\n  }\n  GROUP by ?language\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER by DESC (?count)", "context": "Distinct languages of Wikidata lexemes", "target": ""}
{"query": "SELECT DISTINCT ?gizaki ?gizakiLabel ?jaiolekuaLabel ?heriotzalekuaLabel ?year ?layer ?data (CONCAT(STR(?year), \"-01-01 00:00:00\") as ?concat) ?lat ?lon ?lat2 ?lon2 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?gizaki wdt:P31 wd:Q5;\n    (wdt:P19/(wdt:P131*)/^wdt:P527) wd:Q47588.\n  ?gizaki wdt:P19 ?jaiolekua.\n  ?jaiolekua p:P625 ?koordenatuak. \n  ?koordenatuak psv:P625 ?koord. \n  ?koord wikibase:geoLatitude ?lat. \n  ?koord wikibase:geoLongitude ?lon. \n  ?gizaki wdt:P570 ?data.\n  BIND(YEAR(?data) as ?year).\n  BIND(FLOOR((?year-1)/100)+1 AS ?mende).\n  BIND(?mende as ?layer)\n  ?gizaki wdt:P20 ?heriotzalekua.\n  ?heriotzalekua p:P625 ?koordenatuak2. \n  ?koordenatuak2 psv:P625 ?koord2. \n  ?koord2 wikibase:geoLatitude ?lat2. \n  ?koord2 wikibase:geoLongitude ?lon2. \n}", "context": "Death place of all Basque people with known birthplace on Wikipedia, by century", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?itemDescription ?coords WHERE {\nVALUES ?o { wd:Q47520309 wd:Q27106436 } \n?item  wdt:P131 wd:Q62274582.\n?item wdt:P31 ?o .\n?item wdt:P625 ?coords .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Aberdeen recycling centers and recycling points", "target": ""}
{"query": "#defaultView:Map\n#\n# SWIB20 participants' institutions, mapped to Wikidata and enriched from there\n#\nPREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>\n#\n# from possibly multi-valued fields, use an arbitrary value\nselect distinct ?item ?itemLabel (sample(?logo1) as ?logo) (sample(?img) as ?image) (sample(?pos) as ?coord) ?participants\nwhere {\n  # institutions' items and count of participants from custom WDQS-enabled endpoint\n  service <http://zbw.eu/beta/sparql/econ_corp/query> {\n    ?item zbwext:swib20participants ?count .\n    bind(concat(str(?count), ' participant', if(?count > 1, 's', '')) as ?participants)\n  }\n  optional { ?item wdt:P18 ?img . }\n  optional { ?item wdt:P154 ?logo1 . }\n  optional { ?item wdt:P625 ?pos1 . }\n  optional { ?item wdt:P159/wdt:P625 ?pos2 . }\n  optional { ?item wdt:P131/wdt:P625 ?pos3 . }\n  optional { ?item wdt:P17/wdt:P625 ?pos4 . }\n  # use the most precise position\n  bind(coalesce(?pos1, ?pos2, ?pos3, ?pos4) as ?pos)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,[AUTO_LANGUAGE],de,fr,es\". }\n}\ngroup by ?item ?itemLabel ?participants", "context": "Map of Semantic Web in Libraries 2020 participant's institutions", "target": ""}
{"query": "\nSELECT DISTINCT ?ajedrez ?aj_pag WHERE {\n  ?ajedrez wdt:P106 wd:Q10873124 ;\n              wdt:P21 wd:Q6581072 . \n  ?art schema:about ?ajedrez;\n       schema:isPartOf <https://es.wikipedia.org/> ;  \n       schema:name ?aj_pag . \n}\nLIMIT 500", "context": "Women who play chess and have a Wikipedia article in Spanish", "target": ""}
{"query": "#defaultView:Timeline\nPREFIX dct: <http://purl.org/dc/terms/>\nPREFIX skos: <http://www.w3.org/2004/02/skos/core#>\nPREFIX dbc: <http://dbpedia.org/resource/Category:>\nPREFIX dbo: <http://dbpedia.org/ontology/>\n\nSELECT DISTINCT  ?wditem ?wditemLabel ?date ?image ?work ?workLabel ?coord ?description WHERE {  \n    SERVICE <http://dbpedia.org/sparql> {\n        ?item dct:subject dbc:Diet_food_advocates ;\n        #?item dct:subject dbc:Sewing_machines;\n        #?item dct:subject|dct:subject/skos:broader|dct:subject/skos:broader/skos:broader dbc:Ancient_Greek_philosophers ; \n        #?item dct:subject|dct:subject/skos:broader dbc:Cryptography ;\n              owl:sameAs ?wditem FILTER regex (?wditem, \"wikidata.org\") .\n        #?item dct:subject|dct:subject/skos:broader|dct:subject/skos:broader/skos:broader dbc:Duelling_fatalities .\n    }        \n    SERVICE <https://query.wikidata.org/sparql> {\n\t  {?wditem wdt:P569 ?date FILTER (YEAR(?date) >= 500).}\n      #{?wditem wdt:P570 ?date .}\n      OPTIONAL {?wditem wdt:P18 ?image } .  \n      ?wditem rdfs:label ?wditemLabel FILTER (LANG(?wditemLabel)=\"en\") .\n    }\n}", "context": "Timeline of Dietary Advocates", "target": ""}
{"query": "SELECT DISTINCT ?i_p ?i_pLabel ?i_pDescription ?iu_p ?ii_p ?rn_t_dobLabel ?ic_dob ?rn_t_dodLabel ?ic_dod ?ic_milyear ?ic_cent ?ic_dec ?rn_country ?i_country ?i_countryLabel ?i_countryDecription\nWHERE{{\nSELECT ?ic_dec ?ic_cent WHERE{\n?p wdt:P106 wd:Q4964182; wdt:P569 ?dob.\nFILTER ((YEAR(?dob) > 100) && (YEAR(?dob) < 500)).\nBIND (xsd:integer(FLOOR(YEAR(?dob) / 10) * 10) AS ?ic_dec).\nBIND (xsd:integer(FLOOR(YEAR(?dob) / 100) * 100) AS ?ic_cent).\n}}UNION{\nSELECT ?ic_cent ?ic_milyear WHERE{\n?p wdt:P106 wd:Q4964182; wdt:P569 ?dob.\nFILTER ((YEAR(?dob) > 100) && (YEAR(?dob) < 500)).\nBIND (xsd:integer(FLOOR(YEAR(?dob) / 100) * 100) AS ?ic_cent).\nBIND (xsd:integer(FLOOR(YEAR(?dob) / 1000) * 1000) AS ?ic_milyear).\n}}UNION{\nSELECT ?i_p ?iu_p ?ii_p ?ic_dec ?rn_t_dob ?ic_dob ?rn_t_dod ?ic_dod ?rn_country ?i_country WHERE{\n?i_p wdt:P106 wd:Q4964182; wdt:P569 ?ic_dob.\nFILTER ((YEAR(?ic_dob) > 100) && (YEAR(?ic_dob) < 500)).\nBIND (xsd:integer(FLOOR(YEAR(?ic_dob) / 10) * 10) AS ?ic_dec).\n?rn_t_dob wikibase:directClaim wdt:P569.\nOPTIONAL { ?i_p wdt:P570 ?ic_dod. ?rn_t_dod wikibase:directClaim wdt:P570. }\nBIND (CONCAT (STR (?i_p), \"§if\") AS ?iu_p).\nOPTIONAL { ?i_p wdt:P18 ?ii_p. }\nBIND (\"country\" AS ?rn_country).\nOPTIONAL { ?i_p wdt:P27 ?i_country. }\n}}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Philosophers born between 0 and 500 grouped by the decade where they lived", "target": ""}
{"query": "#TEMPLATE={\"template\":\"Video games from ?country\",\"variables\":{\"?country\":{\"query\":\" SELECT ?id WHERE { ?id wdt:P31 wd:Q6256 . }\"} } }\nSELECT DISTINCT ?item ?itemLabel (GROUP_CONCAT(DISTINCT ?developerLabel;separator=\", \") AS ?devs) ?publisherLabel (GROUP_CONCAT(DISTINCT ?genreLabel;separator=\", \") AS ?genres) WHERE {\n  BIND(wd:Q40 AS ?country)\n  ?item wdt:P31 wd:Q7889.  # it’s a video game \n  { # Game has country of origin \n    ?item wdt:P495 ?country.\n  } UNION { # OR Dev studio has country\n    ?item wdt:P178 ?dev.\n    ?dev wdt:P17 ?country.\n  }\n  OPTIONAL { ?item wdt:P178 ?developer}\n  OPTIONAL { ?item wdt:P123 ?publisher}\n  OPTIONAL { ?item wdt:P136 ?genre }\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                         ?item rdfs:label ?itemLabel .\n                         ?country rdfs:label ?countryLabel.\n                         ?genre rdfs:label ?genreLabel.\n                         ?developer rdfs:label ?developerLabel.\n                         ?publisher rdfs:label ?publisherLabel}\n} GROUP BY ?item ?itemLabel ?publisherLabel", "context": "Video games from ?country", "target": ""}
{"query": "# Politicians with acount at GitHub, CPAN, or Wikimedia\nSELECT DISTINCT ?person ?personLabel ?countryLabel ?cpan ?github ?wikimedia {\n  ?person wdt:P31 wd:Q5 .\n  {\n    { ?person wdt:P8124 ?cpan } UNION\n    { ?person wdt:P2037 ?github } UNION\n    { ?person wdt:P4174 ?wikimedia }\n  }\n  {\n    { ?person wdt:P106 wd:Q83307 } # occupation minister\n   UNION {                        \n     ?person wdt:P39 ?pos .        # position held: minister\n     ?pos wdt:P279* wd:Q83307 \n     }\n  }\n  OPTIONAL { ?person wdt:P27 ?country }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Politicians with an account at GitHub, CPAN, or Wikimedia", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?decade\n(100*SUM(?tuberculosiscount)/COUNT(?decade) AS ?percentage_of_deaths_cause_by_tuberculosis)\nWHERE \n{\n  ?item wdt:P509 ?causeofdeath.\n  ?item wdt:P570 ?sterbedatum.\n  BIND(CONCAT(SUBSTR(CONCAT(STR(YEAR(?sterbedatum))),0,4),\"0\") as ?decade).\n  FILTER( ?sterbedatum >= \"1750-01-01T00:00:00\"^^xsd:dateTime )\n  FILTER( ?sterbedatum <= \"2019-12-31T00:00:00\"^^xsd:dateTime )\n    BIND(\n    IF(?causeofdeath = wd:Q12204,1,0)\n    AS ?tuberculosiscount). \n}\nGROUP BY ?decade #?ref\nORDER BY ?decade", "context": "Percentage of deaths caused by tuberculosis by decade", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?paradigmLabel (count(?proglang) as ?count)\n{\n  ?proglang wdt:P31/wdt:P279* wd:Q9143;\n            wdt:P3966 ?paradigm.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGroup by ?paradigmLabel\n", "context": "Number of programming languages and their paradigms", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?image ?distanceMiles ?coords WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  wd:Q188740 wdt:P625 ?MoMA.\n  SERVICE wikibase:around {\n    ?item wdt:P625 ?coords.\n    bd:serviceParam wikibase:center ?MoMA;\n      wikibase:radius \"3.21869\";\n      wikibase:distance ?distance.\n  }\n  FILTER(EXISTS { ?item (wdt:P31/(wdt:P279*)) wd:Q33506. })\n  OPTIONAL { ?item wdt:P18 ?image. }\n  BIND(CONCAT(STR((ROUND(?distance * \"62.1371192\"^^xsd:decimal)) / 100 ), \" miles FROM MoMA\") AS ?distanceMiles)\n}\nORDER BY (?distance)", "context": "Museums within 2 miles of MoMA", "target": ""}
{"query": "#Largest cities of the world\nSELECT DISTINCT ?cityLabel ?population ?gps\nWHERE\n{\n  ?city wdt:P31/wdt:P279* wd:Q515 .\n  ?city wdt:P1082 ?population .\n  ?city wdt:P625 ?gps .\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"es\" .\n  }\n}\nORDER BY DESC(?population) LIMIT 100", "context": "Largest cities of the world ordered by population", "target": ""}
{"query": "# Italian politicians who served as a minister in more than one cabinet since 1990\n# Query by Andrea Mignone (https://twitter.com/i_m_andrea)\n#defaultView:Graph\nSELECT distinct ?person ?personLabel ?gov ?govLabel ?rgb ?size\nWHERE {\n\n  ?person p:P39 ?statement.\n  ?person p:P39 ?statement2.\n\n  ?statement pq:P5054 ?gov;\n             pq:P580 ?date;\n             ps:P39 ?edge.\n\n  ?statement2 pq:P5054 ?gov2;\n              pq:P580 ?date2;\n              ps:P39 ?edge2.\n\n  ?edge wdt:P31 | wdt:P279 wd:Q3858501.\n  ?edge2 wdt:P31 | wdt:P279 wd:Q3858501.\n\n  FILTER(YEAR(?date) >= 1990)\n  FILTER(YEAR(?date2) >= 1990)\n  FILTER(?gov != ?gov2)\n\n  # cosmetic stuff\n  BIND('8F2539' as ?rgb)\n  BIND(2000 as ?size)\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\n\n\n\n", "context": "Italian politicians who served as a minister in more than one cabinet since 1990", "target": ""}
{"query": "# people born after 1800 whose “name in native language” is in Latin\nSELECT DISTINCT ?person ?personLabel ?personDescription ?name WHERE {\n  ?person wdt:P31 wd:Q5;\n          wdt:P569 ?dob. hint:Prior hint:rangeSafe true.\n  FILTER(?dob >= \"1800-00-00\"^^xsd:dateTime)\n  ?person wdt:P1559 ?name.\n  FILTER(LANG(?name) = \"la\")\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "People born after 1800 whose “name in native language” is in Latin", "target": ""}
{"query": "# UK statues and whether they are of nobles and/or women\n#defaultView:Map{\"hide\": [\"?layer\"]}\nSELECT\n?statue ?statueLabel (SAMPLE(?statueLocation) AS ?statueLocation_) (SAMPLE(?statueImage) AS ?statueImage_)\n?person ?personLabel\n(IF(?female, \"female\"@en, \"not female\"@en) AS ?femaleLabel)\n(IF(?hasNobleTitle, \"has noble title\"@en, \"has no noble title\"@en) AS ?hasNobleTitleLabel)\n(CONCAT(?femaleLabel, \", \"@en, ?hasNobleTitleLabel) AS ?layer)\nWHERE {\n  ?statue wdt:P31/wdt:P279* wd:Q860861;\n          wdt:P17 wd:Q145;\n          wdt:P180|wdt:P547 ?person.\n  ?person wdt:P31 wd:Q5.\n  BIND(EXISTS { ?person wdt:P21/wdt:P279* wd:Q6581072. } AS ?female)\n  BIND(EXISTS { ?person wdt:P97 ?nobleTitle. } AS ?hasNobleTitle)\n  OPTIONAL { ?statue wdt:P625 ?statueLocation. }\n  OPTIONAL { ?statue wdt:P18 ?statueImage. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?statue ?statueLabel ?person ?personLabel ?female ?hasNobleTitle", "context": "UK statues and whether they are of nobles and/or women", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?city ?cityLabel ?population ?location ?lon ?lat WHERE {\n  # get cities (or classes based on city) in germany\n  ?city wdt:P31/wdt:P279* wd:Q515 ;\n        wdt:P17 wd:Q183 .\n  \n  # get population and location of the city\n  ?city wdt:P1082 ?population ;\n        wdt:P625 ?location .\n  \n  # we're only interested in cities with a population count > 50.000\n  FILTER (abs(?population) > 50000)\n  \n  # extract lat and lon\n  ?city p:P625 ?coordinate.\n  ?coordinate psv:P625 ?coordinate_node.\n  ?coordinate_node wikibase:geoLongitude ?lon.\n  ?coordinate_node wikibase:geoLatitude ?lat. \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de,en\" }\n}", "context": "Location of German cities with a population count greater than 50.000", "target": ""}
{"query": "#added before 2016-10\n#defaultView:Graph\nSELECT DISTINCT ?item ?itemLabel ?rgb ?link ?link2\nWHERE\n{\n  VALUES ?toggle { true false }\n  ?disease wdt:P699 ?doid;\n           wdt:P279* wd:Q18123741;\n           wdt:P2176 ?drug.\n  ?drug wdt:P3780 ?active.\n  ?drug rdfs:label ?drugLabel.\n\t\tFILTER(LANG(?drugLabel) = \"en\").\n  ?disease rdfs:label ?diseaseLabel.\n\t\tFILTER(LANG(?diseaseLabel) = \"eu\").\n  BIND(IF(?toggle,?disease,?drug) AS ?item).\n  BIND(IF(?toggle,?diseaseLabel,?drugLabel) AS ?itemLabel).\n  BIND(IF(?toggle,\"FFA500\",\"7FFF00\") AS ?rgb).\n  BIND(IF(?toggle,\"\",?disease) AS ?link).\n}", "context": "Map of medicines used (green) to fight infectious diseases (orange)", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?image {\n  ?item wdt:P361 wd:Q12505 ; wdt:P18 ?image.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Russian cuisine", "target": ""}
{"query": "SELECT ?name ?nameLabel ?dod ?image (GROUP_CONCAT(DISTINCT ?occupationLabel;separator=\", \") AS ?occupations) (GROUP_CONCAT(DISTINCT ?countryLabel;separator=\", \") AS ?countries)  WHERE {\n  ?name wdt:P27 wd:Q31 .\n  ?name wdt:P27 ?country .\n  ?name wdt:P570 ?dod .\n  ?name wdt:P106/wdt:P279* wd:Q483501 .\n  ?name wdt:P106 ?occupation .\n  OPTIONAL {?name wdt:P18 ?image} .\n  FILTER (YEAR(?dod) = 1950)  .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                       ?occupation rdfs:label ?occupationLabel .\n                       ?country rdfs:label ?countryLabel .\n                       ?name rdfs:label ?nameLabel .}\n}\nGROUP BY ?name ?dod ?image ?nameLabel", "context": "Belgian artists that rise to the public domain in 2021", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?lang (COUNT(?sujet) AS ?compte) WHERE {\n  ?page schema:about ?sujet .\n  ?page schema:isPartOf ?wiki .\n  ?page schema:inLanguage ?lang .\n  ?sujet wdt:P195/wdt:P361* wd:Q1429003 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?lang", "context": "Bubble diagram by wiki page numbers in each language on objects kept at the Bardo National Museum (note: Commons is counted as English)", "target": ""}
{"query": "CONSTRUCT {\n  ?item p:P31 wd:Q194195 .\n  ?item dc:title ?itemLabel .\n  ?item dc:spatial ?coord .\n  ?item foaf:depiction ?image\n}\nWHERE \n{\n  ?item p:P31/ps:P31/wdt:P279* wd:Q194195 .\n  OPTIONAL { ?item wdt:P625 ?coord }\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n}", "context": "Amusement parks around the world", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?geographische_Koordinaten\", \"?layer\"]}\nSELECT ?Stolperstein ?StolpersteinLabel ?geographische_Koordinaten ?Bild ?layer\nWHERE {\n  ?Stolperstein wdt:P31 wd:Q26703203;\n                wdt:P276 wd:Q1715;\n                wdt:P18 ?Bild;\n                wdt:P625 ?geographische_Koordinaten.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\"  }\n  }\nORDER BY ?StolpersteinLabel", "context": "All stumbling stones in the city of Hannover", "target": ""}
{"query": "SELECT ?item ?itemLabel ?website ?facebook ?twitter ?instagram \nWHERE \n{\n  ?item wdt:P31 wd:Q5124370.\n  ?item wdt:P17 wd:Q145.\n  OPTIONAL {?item wdt:P856 ?ws . BIND(IRI(?ws) as ?website) }\n  OPTIONAL {?item wdt:P2013 ?fb . BIND(IRI(CONCAT(\"https://www.facebook.com/\",?fb)) as ?facebook) } \n  OPTIONAL {?item wdt:P2002 ?tw . BIND(IRI(CONCAT(\"https://twitter.com/\",?tw)) as ?twitter) }\n  OPTIONAL {?item wdt:P2003 ?in . BIND(IRI(CONCAT(\"https://www.instagram.com/\",?in,\"/\")) as ?instagram) } \n#  OPTIONAL {?item wdt:P3267 ?fl . BIND(IRI(CONCAT(\"https://www.flickr.com/people/\",?fl)) as ?flickr) }\n#  OPTIONAL {?item wdt:P2397 ?yt . BIND(IRI(CONCAT(\"https://www.youtube.com/channel/\",?yt)) as ?youtube) }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by ?itemLabel", "context": "Web & social media URLs for UK Civic Societies", "target": ""}
{"query": "#Accused witches in Scotland and the ritual objects they are said to have used\n#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?accusedurl ?coords ?residenceLabel ?image (?ritual as ?layer) \nWHERE \n{\n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P4478 ?accused .\n  wd:P4478 wdt:P1630 ?formatterurl .\n  BIND(IRI(REPLACE(?accused, '^(.+)$', ?formatterurl)) AS ?accusedurl).\n  ?item wdt:P551 ?residence .\n  ?residence wdt:P625 ?coords.\n  ?item wdt:P8706 ?RitualObject . \n  OPTIONAL { ?RitualObject wdt:P18 ?image } \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".  \n                  \n                          ?charge rdfs:label ?chargeLabel .\n                          ?RitualObject rdfs:label ?ritual .\n                          ?residence rdfs:label ?residenceLabel .\n                          ?item rdfs:label ?itemLabel .\n                          ?accused rdfs:label ?accusedLabel .}\n}", "context": "Map of the ritual objects said to be used by accused witches in Scotland (colourcoded layer accessible on top right to see the list of objects)", "target": ""}
{"query": "select ?mp ?mpLabel ?seat ?seatLabel (year(?first_date) as ?first_year) where\n{\n  ?mp p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q18018860 .\n  ?term wdt:P571 ?first_date . ?ps pq:P768 ?seat . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  { select ?seat (MIN(?termstart) as ?first_date) where\n    {\n      ?person p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q18018860 .\n      ?ps pq:P768 ?seat . ?term wdt:P571 ?termstart .\n      ?seat wdt:P31 wd:Q27990982 .\n    } group by ?seat }\n} order by ?first_year ?seatLabel ?mpLabel\n", "context": "Seats of the Parliament of England, and the first year we have a named person listed for them", "target": ""}
{"query": "\nSELECT DISTINCT ?auto ?auto_pag WHERE {\n  ?auto wdt:P106 wd:Q10349745 ;\n              wdt:P21 wd:Q6581072 . \n  ?art schema:about ?auto;\n       schema:isPartOf <https://es.wikipedia.org/> ;  \n       schema:name ?auto_pag . \n}\nLIMIT 500", "context": "Female race car drivers on Spanish Wikipedia", "target": ""}
{"query": "#defaultView:Map\n\nSELECT ?Alamedilla ?AlamedillaLabel ?coordenadas ?imagen WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Alamedilla wdt:P131 wd:Q1631944.\n  ?Alamedilla wdt:P31 wd:Q56528396.\n  OPTIONAL { ?Alamedilla wdt:P625 ?coordenadas. }\n  OPTIONAL { ?Alamedilla wdt:P18 ?imagen. }\n}\nLIMIT 100", "context": "Places of Geological Interest in Alamedilla", "target": ""}
{"query": "SELECT ?year (COUNT(?i) as ?is) {\n  ?i wdt:P1433 wd:Q180445 ; wdt:P577 ?date.\n  bind((floor(\"2020-12-06\"^^xsd:dateTime - ?date)/7) as ?weeks) .\n  bind ((?weeks-floor(?weeks)) as ?fractional) .\n  bind (YEAR(?date) as ?year)\n  filter (?fractional = 0) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?year\n\n", "context": "Number of Nature articles that were published on a Sunday in a given year", "target": ""}
{"query": "SELECT DISTINCT ?human ?humanLabel \nWHERE\n{\n    VALUES ?professions {wd:Q177220 wd:Q639669 wd:Q488205}\n    ?human wdt:P31 wd:Q5 .\n    ?human wdt:P106 ?professions .\n    ?human wdt:P136 wd:Q431415 .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nLIMIT 100", "context": "Tex-Mex singers", "target": ""}
{"query": "#defaultView:Map{\"layer\": \"?typeLabel\"}\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?coords ?image ?typeLabel\nWHERE \n{\n  ?item wdt:P5008 wd:Q101553389.\n  ?item wdt:P625 ?coords .\n  ?item wdt:P18 ?image .\n  ?item wdt:P31 ?type .\n  \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cy,en\". }\n}", "context": "Map of images from Wicipics project (photography campaign in Wales)", "target": ""}
{"query": "#Vito Russo Test result for each film along with the score, if in Wikidata:\n#0/3: no identifiably LGBTQIA+ characters\n#1/3: at least one character that is identifiably LGBTQIA+\n#2/3: at least one character that is identifiably LGBTQIA+ (not predominantly defined by their orientation/identity)\n#passing: at least one character that is identifiably LGBTQIA+ (not predominantly defined by their orientation/identity)\n#whose removal would have a significant effect on the plot\nSELECT ?item ?itemLabel (MIN(YEAR(?date)) AS ?year) (GROUP_CONCAT(DISTINCT ?country_label; separator = \", \") AS ?countries) ?VRTresult ?score ?nature_of_statement WHERE {\n{?item p:P1552 ?st. ?st ps:P1552 wd:Q93615557. BIND(\"passing\" AS ?VRTresult)}\nUNION\n{?item p:P1552 ?st. ?st ps:P1552 wd:Q93985027. BIND(\"failing\" AS ?VRTresult)}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\nOPTIONAL{?st pq:P444 ?score}\nOPTIONAL{?st pq:P5102 ?noc. ?noc rdfs:label ?nature_of_statement. FILTER(LANG(?nature_of_statement)=\"en\")}\nOPTIONAL{?item wdt:P577 ?date}\nOPTIONAL{?item wdt:P495 ?country. ?country rdfs:label ?country_label. FILTER(LANG(?country_label)=\"en\")}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?VRTresult ?score ?nature_of_statement\nORDER BY ?year\n", "context": "Vito Russo Test in Wikidata", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?language ?languageLabel ?count\nWITH {                             \n  SELECT ?language (COUNT(DISTINCT ?lexeme) AS ?count) { \n    ?lexeme dct:language ?language.\n    ?language wdt:P31 wd:Q33384\n  }\n  GROUP by ?language\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER by desc(?count)", "context": "Distinct dialects of Wikidata lexemes", "target": ""}
{"query": "SELECT ?metro ?metroLabel ?locationLabel ?clientelaLabel\nWHERE {\n  ?metro wdt:P31 wd:Q5503.\n  ?metro wdt:P131 ?location .\n  ?metro wdt:P3872 ?clientela .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". } }\norder by ?clientela\nlimit 200", "context": "Patronage of rapid-transit transportation", "target": ""}
{"query": "# people who played Caesar, and what \"version\" of him they played\nselect distinct  ?pub ?caesar ?caesarLabel ?item ?itemLabel ?itemDescription ?version where\n{\n  { ?item p:P161 ?cs . ?cs ps:P161 ?caesar . ?cs pq:P453 wd:Q1048 . } union\n  { ?item p:P161 ?cs . ?cs ps:P161 ?caesar . ?cs pq:P453 ?version . ?version wdt:P1074 wd:Q1048. }\n  # played Caesar, or played a character based on him\n  optional { { ?item wdt:P577 ?pub . } union  { ?item wdt:P580 ?pub . }  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "People who played Julius Caesar, and what \"version\" of him they played", "target": ""}
{"query": "# Find works with a public domain date (P3893) in 2021\nSELECT ?work ?workLabel ?type ?typeLabel ?date ?articles WHERE {\n  ?work p:P3893/psv:P3893 ?date_node .\n  ?date_node wikibase:timeValue ?date .\n  FILTER( ?date >= \"2021-01-01T00:00:00\"^^xsd:dateTime && ?date < \"2022-01-01T00:00:00\"^^xsd:dateTime )\n  ?work wdt:P31 ?type ;\n           wikibase:sitelinks ?articles .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr,en,de,it,es,nl\" . }\n}\n# Order by number or articles on Wikimedia sites\nORDER BY DESC(?articles)\n", "context": "Works identified (via dedicated property P3893) as entering the public domain on 1/1/2021, ordered by number of articles on Wikimedia sites", "target": ""}
{"query": "SELECT distinct ?oeuvre ?oeuvreLabel ?artisteLabel ?muséeLabel WHERE {\n  ?oeuvre wdt:P31 wd:Q3305213.\n  ?oeuvre wdt:P195/wdt:P1416* wd:Q3365279.          \n  ?oeuvre wdt:P195 ?musée.\n  ?oeuvre wdt:P571 ?date0.\n  ?oeuvre wdt:P170 ?artiste.\n  ?artiste wdt:P570 ?death .\n  BIND(year(?death) AS ?deces)\n\n\n  OPTIONAL{?oeuvre wdt:P217 ?inventaire.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr, en\". }\n  \n FILTER (?deces =1950)    \n   \n}\n\nORDER BY ?artiste", "context": "Painters in the collections of Paris Musées which enter the public domain on January 1", "target": ""}
{"query": "#defaultView:Map\nSELECT ?i ?iLabel ?coords ?url WHERE {\n  ?i wdt:P31/wdt:P279* wd:Q57821 .\n  ?i wdt:P973 ?url .\n  ?i wdt:P625 ?coords .\n  FILTER (REGEX(STR(?url),\"atlasofmutualheritage\"))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of forts related to Dutch trading companies VOC and WIC", "target": ""}
{"query": "#defaultView:Map{\"hide\":\"?coords\",\"layer\":\"?fortTypeLabel\"}\nSELECT ?fort ?fortLabel ?coords ?commons ?img ?inception ?fortTypeLabel WHERE {\n\n  ?fort wdt:P361 wd:Q1409040;\n        wdt:P625 ?coords.\n  \n  ?fort wdt:P31 ?fortType.\n       # ?item schema:description ?itemdesc.\n  OPTIONAL {\n    ?fort wdt:P571 ?inception\n  }    \n  OPTIONAL {\n    ?fort wdt:P18 ?img\n  }\n        \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de\". }\n}", "context": "Map of forts in the Cologne fortress ring", "target": ""}
{"query": "# Query to get first level administrative region of a country\nSELECT DISTINCT ?region ?regionLabel ?pop ?ended\nWHERE {\n  # Instance of first administrative division\n  ?region p:P31 ?stat.\n  ?stat a wikibase:BestRank.\n  FILTER NOT EXISTS { ?stat pq:P582 []}\n  ?stat ps:P31/wdt:P279* wd:Q10864048.\n  \n#  ?region wdt:P31/wdt:P279* wd:Q10864048.\n  #UNION { ?city wdt:P31/wdt:P279* wd:Q515. }\n\n  ?region wdt:P1082 ?pop.\n  ?region wdt:P150 ?regionContained.\n      \n  # only in Germany\n  ?region wdt:P17 wd:Q183.\n  \n  MINUS { ?region wdt:P576 ?x. } # remove region that has been disolved\n  # remove region that is nowdays not a first level administrative region\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?pop)", "context": "First-level administrative divisions in Germany", "target": ""}
{"query": "SELECT DISTINCT ?city ?cityLabel ?population WHERE {\n  ?city wdt:P31/wdt:P279* wd:Q486972;\n    \twdt:P17 wd:Q183;\n        wdt:P1082 ?population;\n  FILTER (abs(?population - 41962) < 100)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],nl\" }\n}", "context": "Population of cities in Germany corresponds to the total number of vaccinations in Germany", "target": ""}
{"query": "SELECT ?theorem ?theoremLabel (COUNT(DISTINCT ?article) AS ?wikipedia) ?sitelinks {\n  ?theorem wdt:P31/wdt:P279* wd:Q65943 ; wikibase:sitelinks ?sitelinks .\n  ?article schema:about ?theorem ; schema:isPartOf [ wikibase:wikiGroup \"wikipedia\" ] .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" . }\n}\nGROUP BY ?theorem ?theoremLabel ?sitelinks\nORDER BY DESC(?wikipedia)\nLIMIT 100", "context": "Most popular theorems according to their number of articles on Wikipedia", "target": ""}
{"query": "SELECT ?occupationLabel (COUNT(DISTINCT ?person) as ?count) WHERE {\n  {?person wdt:P509 wd:Q84263196} # died of COVID-19\n  UNION\n  {?person wdt:P1050 wd:Q84263196} # diagnosed with COVID-19\n  ?person wdt:P106 ?occupation\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?occupationLabel\nORDER BY DESC(?count)", "context": "Number of people who have died, or been diagnosed, of COVID-19, grouped by occupation", "target": ""}
{"query": "SELECT (COUNT(DISTINCT ?item) AS ?count) {\n\t?item wdt:P570 ?fecha .\n\tFILTER ( ?fecha >= \"2020-01-01T00:00:00Z\"^^xsd:dateTime && ?fecha < \"2021-01-01T00:00:00Z\"^^xsd:dateTime )\n  \t?item wdt:P31 wd:Q5 .\n}", "context": "Deaths in 2020 - date of birth, date of death, cause of death and country of nationality", "target": ""}
{"query": "SELECT distinct ?mp ?mpLabel ?pLabel ?sLabel ?start (min(?end)as ?end2)(round(?end2- ?start)as ?days)(concat(?sLabel,' (',?pLabel,'), ',str(year(?start)),'-',str(year(?end2))) as ?string)\nwhere{VALUES ?mp{wd:Q160852}#set MP here\n#seat-party-start pairs for each continuing office period\n{SELECT distinct ?mp ?mpLabel ?pLabel ?sLabel ?start\nWHERE{?mp p:P39 ?ps.?ps ps:P39 ?t;pq:P768 ?s;pq:P4100 ?p;pq:P580 ?start.?t wdt:P279 wd:Q16707842. \nminus{?ps pq:P2715 ?elec.?elec wdt:P31 wd:Q15283424.?mp p:P39[ps:P39 ?t0; pq:P4100 ?p; pq:P768 ?s; pq:P1534 wd:Q741182].?t0 wdt:P156 ?t}\nminus{?ps pq:P1534 wd:Q50393121} # omit untaken double-return seats\nSERVICE wikibase:label{bd:serviceParam wikibase:language \"en\"}}ORDER BY ?mp ?start}\n#corresponding seat-party-end pairs\n{SELECT distinct ?mp ?mpLabel ?pLabel ?sLabel ?end\nWHERE{?mp p:P39 ?ps.?ps ps:P39 ?t;pq:P768 ?s;pq:P4100 ?p;pq:P582 ?end.?t wdt:P279 wd:Q16707842.\nminus{?ps pq:P1534 wd:Q741182.?mp p:P39[ps:P39 ?t2;pq:P4100 ?p;pq:P768 ?s;pq:P2715 ?elec].?t2 wdt:P155 ?t.?elec wdt:P31 wd:Q15283424}\nminus{?ps pq:P1534 wd:Q50393121}#omit untaken double-return seats\nSERVICE wikibase:label{bd:serviceParam wikibase:language \"en\"}}ORDER BY ?mp ?end}\nfilter(?end > ?start)#> not >=\n}group by ?mp ?mpLabel ?pLabel ?sLabel ?start order by ?start", "context": "Summary of a UK parliamentary career seat-party-start pairs", "target": ""}
{"query": "SELECT ?pesawat ?pesawatLabel ?tgl (YEAR(?tanggal) as ?tahun) ?gambar WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?pesawat wdt:P31/wdt:P279 wd:Q15056995.\n  ?pesawat wdt:P606 ?tanggal.\n  ?pesawat wdt:P606 ?tgl.\n  FILTER ( YEAR(?tanggal) >= 1903 )\n  ?pesawat wdt:P18 ?gambar.\n}\nLIMIT 500\n#defaultView:Timeline\n#view:Timeline{\"hide\": \"?tgl\"}", "context": "Timeline of aircraft types around the world and the first time a type of aircraft made a flight", "target": ""}
{"query": "SELECT ?hukum ?hukumLabel ?daerahLabel ?koord WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id\". }\n  ?hukum wdt:P921 wd:Q2986880.\n  ?hukum wdt:P31/wdt:P279* wd:Q12504878.\n  ?hukum wdt:P1001 ?daerah.\n  ?daerah wdt:P625 ?koord.\n}", "context": "Indonesian legislation related to indigenous people", "target": ""}
{"query": "# search for artist D.K. active in 1964\n# Source: https://www.wikidata.org/wiki/Property_talk:P7457#Related_queries:_Who_could_be_%22J.B._1670%22_and_%22D.K._1964%22?\n# query by Jura1, 2021-01-02, suggestion by https://www.wikidata.org/wiki/User:Herzi_Pinki\n\nSELECT DISTINCT ?item ?d1 ?d2 ?nl0 ?nl1 ?itemDescription ?sig \nWHERE\n{\n    ?item wdt:P569 ?d1 ; wdt:P570 ?d2 .\n    hint:Prior hint:rangeSafe true .\n    FILTER( ?d1 > \"1880-00-00\"^^xsd:dateTime &&\n            ?d2 > \"1964-00-00\"^^xsd:dateTime &&\n            ?d1 < \"1944-00-00\"^^xsd:dateTime\n          )\n    ?item wdt:P735 / wdt:P1705 ?nl0 . FILTER( REGEX(?nl0, \"^D\" ) )\n    ?item wdt:P734 / wdt:P1705 ?nl1 . FILTER( REGEX(?nl1, \"^K\" ) )\n    ?item wdt:P106/wdt:P279* wd:Q3391743 .\n     hint:Prior hint:gearing \"forward\".   \n    OPTIONAL { ?item wdt:P109 ?sig }\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}", "context": "Which artist could be \"D.K. 1964\"?", "target": ""}
{"query": "#defaultView:LineChart\nSELECT (CONCAT(STR(?year),\"-01-01T00:00:00Z\") as ?year_) (COUNT(?i) as ?is) {\n  ?i wdt:P6392 [] ; wdt:P571 ?date. BIND(YEAR(?date) as ?year)\n}\nGROUP BY ?year", "context": "Line graph of colleges and universities in India by year of establishment", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?i ?iLabel ?j ?jLabel {\n  ?i wdt:P31 wd:Q11424 ; wdt:P495 wd:Q668 ; ^wdt:P144 ?j.\n  ?j wdt:P31 wd:Q11424 ; wdt:P495 wd:Q668.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of Indian films that are remakes of other Indian films", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?i ?iLabel ?img ?j ?jLabel {\n  VALUES ?source { wd:Q711237 wd:Q11115439 }\n  ?i wdt:P69 ?place1 ; wdt:P184 ?j. ?j wdt:P69 ?place2 ; wdt:P184* ?source.\n  wd:Q668 ^wdt:P17 ?place1, ?place2.\n  OPTIONAL { ?i wdt:P18 ?img }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of academic descendants of C. R. Rao and K. Ananda Rau", "target": ""}
{"query": "#defaultView:Map{\"hide\": \"?rgb\"}\nSELECT ?d ?dLabel ?shape ?litrate ?rgb WHERE {\n  ?d wdt:P31 wd:Q2732840; p:P6897 ?litratestmt ; wdt:P3896 ?shape.\n  ?litratestmt ps:P6897 ?litrate ; pq:P585 \"+2001-01-01T00:00:00Z\"^^xsd:dateTime.\n  BIND(IF(?litrate < 80, \"00008b\", IF(?litrate < 85, \"006400\", IF(?litrate < 90, \"008000\", IF(?litrate < 95, \"32cd32\", \"7fff00\")))) AS ?rgb)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,ml\". }\n}\n", "context": "Map of gram panchayats in Kerala by literacy rate in 2001", "target": ""}
{"query": "#defaultView:Map\nSELECT ?i ?iLabel ?layer ?cds {\n  VALUES ?layer { \"abad\" \"pukur\" \"tali\" \"guri\" \"bazar\" \"gaon\" \"hat\" \"gola\" \"shwar\" \"bag\" }\n  ?i wdt:P131/wdt:P131* wd:Q1356; wdt:P625 ?cds; wdt:P5578 []; rdfs:label ?iLabel. FILTER(LANG(?iLabel)=\"en\")\n  FILTER(STRENDS(?iLabel,?layer))\n}", "context": "Maps of different places in West Bengal according to suffix", "target": ""}
{"query": "#defaultView:TreeMap\nSELECT ?date ?poem ?poemLabel\n{\n  ?poem wdt:P31 wd:Q7725634 ; wdt:P7937 wd:Q482 ; wdt:P50 wd:Q7241 ; wdt:P571 ?date_.\n  BIND(MONTH(?date_) as ?month)\n  BIND(YEAR(?date_) as ?year)\n  FILTER(?year > 1936)\n  BIND(CONCAT(STR(?month),\"-\",STR(?year)) as ?date)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". }\n}\n", "context": "Tree map of Rabindranath's poems according to the month of writing", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?loc ?locLabel (count(?i) as ?nr)  WHERE {\n  ?i wdt:P31/wdt:P279* wd:Q464980 .\n  ?i wdt:P276 ?loc .\n  #?loc wdt:P17 wd:Q55.\n SERVICE wikibase:label {\n bd:serviceParam wikibase:language \"nl\" . \n }\n}\ngroup by ?loc ?locLabel\norder by desc(?nr) ", "context": "Count of locations of exhibitions on Wikidata", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?languageLabel ?formCount {   \n  {   SELECT ?language (COUNT (?form) as ?formCount) {   ?lexeme dct:language ?language . ?lexeme ontolex:lexicalForm ?form. } GROUP BY ?language }  \n  SERVICE wikibase:label {    bd:serviceParam wikibase:language \"en\". } \n}\nORDER BY DESC(?formCount)", "context": "Wikidata lexeme forms by language", "target": ""}
{"query": "SELECT ?item ?itemLabel ?bdate\nWITH { \n  SELECT (GROUP_CONCAT(?num; separator=\"|\") AS ?primes) { ?prime wdt:P31 wd:Q49008 ; wdt:P1181 ?num FILTER(?num < 2021) }\n} AS %primes\n{\n  INCLUDE %primes\n  ?item wdt:P106 wd:Q170790 .\n  ?item wdt:P569 ?bdate FILTER(REGEX(STR(?bdate), CONCAT(\"^(\", ?primes, \")-\"))) .\n  ?item wikibase:sitelinks ?sitelinks .\n \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?sitelinks)", "context": "Mathematicians born in prime number years featuring Albert Einstein, Pierre de Fermat, Georg Ohm, Carl Gauss, Évariste Galois, Augustin-Louis Cauchy, James Maxwell et al.", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?coup ?coupLabel (MIN(?date) AS ?date_) (SAMPLE(?image) AS ?image_) WHERE {\n  ?coup wdt:P31/wdt:P279* wd:Q45382;\n        wdt:P585 ?date. hint:Prior hint:rangeSafe true.\n  FILTER(?date >= \"2001-00-00\"^^xsd:dateTime\n        && ?date < \"2101-00-00\"^^xsd:dateTime)\n  OPTIONAL { ?coup wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?coup ?coupLabel", "context": "Timeline of coups d'état of the 21st century", "target": ""}
{"query": "#Children of which were born with by a person, and their step child.\nSELECT ?item ?itemLabel ?parentLabel ?spousesChildLabel\nWHERE \n{\n  ?item wdt:P31 wd:Q5.\n  ?parent wdt:P40 ?item.\n  ?parent wdt:P26 ?spouse.\n  ?spouse wdt:P40 ?spousesChild .\n  ?spousesChild wdt:P40 ?item. \n      \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Children of which were born with by a person, and their step child", "target": ""}
{"query": "# (Thanks, James Heald, for the original query!)\n#defaultView:Map\nSELECT ?station ?stationLabel ?cds ?line ?layer WHERE { \n  { SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q15213944 ; gas:target wd:Q2784062 ; gas:out ?station ; gas:out1 [] ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . } }\n  FILTER(?station != wd:Q15213944)\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Map of the shortest railway path (in terms of number of stations) between Dibrugarh and the Scottish Highlands", "target": ""}
{"query": "#Joueurs de l'Olympique de Marseille\n#defaultView:Map\nSELECT ?item ?itemLabel ?nomLabel ?naissanceLabel ?DateNaissanceLabel ?img ?coord WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P106 wd:Q937857.\n  OPTIONAL { ?item wdt:P734 ?nom. } \n  OPTIONAL { ?item wdt:P569  ?DateNaissance. } \n  OPTIONAL { ?item wdt:P18 ?img. }\n  ?item wdt:P19 ?naissance.\n  ?naissance wdt:P625 ?coord.\n  ?item p:P54 ?om.\n  ?om ps:P54 wd:Q2378143.\n  OPTIONAL { ?om pq:P580 ?debut. }\n  OPTIONAL { ?om pq:P582 ?fin. }\n  OPTIONAL { ?om pq:P1350 ?matches. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr, en\". }\n}", "context": "Map of place of birth of Olympique de Marseille football players", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?shape\", \"?rgb\"]}\nSELECT ?ward ?wardLabel ?shape ?rgb ?is {\n  { SELECT ?ward ?shape (COUNT(?i) as ?is) {\n    ?i wdt:P6391 [] ; wdt:P131 ?ward. ?ward wdt:P31 wd:Q1195098 ; wdt:P131/wdt:P131 wd:Q1348 ; wdt:P3896 ?shape.\n  } GROUP BY ?ward ?shape }\n  BIND(IF(?is > 25, \"006400\", IF(?is > 20, \"32cd32\", IF(?is > 15, \"7cfc00\", IF(?is > 10, \"adff2f\", \"00ff7f\")))) as ?rgb)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of Kolkata wards by number of schools", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?beach ?beachLabel ?coord WHERE {\n  ?beach wdt:P17 wd:Q96 ;\n        wdt:P31 wd:Q40080 ;\n        wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es, en\". }\n}\n", "context": "Map of beaches in Mexico", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?i ?iLabel ?j ?jLabel {\n  ?i wdt:P31/wdt:P279* wd:Q1149652; wdt:P47 ?j. ?j wdt:P31/wdt:P279* wd:Q1149652.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of shared borders between Indian districts", "target": ""}
{"query": "#defaultView:Graph\nPREFIX gas: <http://www.bigdata.com/rdf/gas#>\nSELECT ?item ?itemLabel ?image ?linkTo WHERE\n{\n  SERVICE gas:service {\n    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" ;\n    gas:in wd:Q737942;\n    gas:traversalDirection \"Forward\" ;\n    gas:out ?item ;\n    gas:out1 ?depth ;\n    gas:maxIterations 15 ;\n    gas:linkType wdt:P40 .\n  }\n  OPTIONAL { ?item wdt:P40 ?linkTo }\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"bn,en\" }\n}\n\n#", "context": "Descendants of Upendrakishore Roy", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?i ?iLabel ?img ?p580 ?p582 {\n  ?i p:P39 ?pstmt. ?pstmt ps:P39 wd:Q92212615 ; pq:P580 ?p580. OPTIONAL { ?pstmt pq:P582 ?end_ }\n  BIND(IF(BOUND(?end_),?end_,NOW()) as ?p582)\n  OPTIONAL { ?i wdt:P18 ?img }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Timeline of Indian High Commissioners to Britain", "target": ""}
{"query": "#defaultView:TreeMap\nSELECT ?item ?itemLabel ?painting ?paintingLabel WHERE {\n  ?painting wdt:P31 wd:Q3305213 ; wdt:P180 ?item ; wdt:P195 ?collection .\n  ?collection wdt:P17 wd:Q668.\n  ?item wdt:P39 wd:Q15390704.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}", "context": "A tree map of items for artworks depicting Mughal emperors", "target": ""}
{"query": "SELECT ?lexeme ?example ?formstmt (SAMPLE(?form_) as ?form) ?sensestmt ?sense ?source ?sourceLabel {\n  ?lexeme dct:language/wdt:P4913* wd:Q9610 ; p:P5831 ?examplestmt.\n  ?examplestmt ps:P5831 ?example.\n  OPTIONAL { ?examplestmt pq:P5830 ?formstmt . ?formstmt ontolex:representation ?form_. }\n  OPTIONAL { ?examplestmt pq:P6072 ?sensestmt . ?sensestmt skos:definition ?sense. FILTER(LANG(?sense)=\"bn\") }\n  OPTIONAL { ?examplestmt prov:wasDerivedFrom/pr:P248 ?source }\n  OPTIONAL { ?examplestmt prov:wasDerivedFrom/pr:P854 ?source }\n  OPTIONAL { ?examplestmt prov:wasDerivedFrom/pr:P996 ?source }\n  OPTIONAL { ?examplestmt prov:wasDerivedFrom/pr:P4656 ?source }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". }\n}\nGROUP BY ?lexeme ?example ?formstmt ?sensestmt ?sense ?source ?sourceLabel\nORDER BY ?lexeme", "context": "Example of documented usage of different Bengali words", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?label ?coord WHERE {\n  ?item wdt:P17 wd:Q145.\n  ?item wdt:P625 ?coord.\n  ?item rdfs:label ?labelWithLanguage.\n  BIND(STR(?labelWithLanguage) AS ?label)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  FILTER(STRSTARTS(?labelWithLanguage, \"Llan\"))\n  FILTER((LANG(?labelWithLanguage)) = \"en\")\n}", "context": "'Llan' place names in the UK", "target": ""}
{"query": "# Organisationen nach Spenden\nSELECT ?Orga ?Organisation ?Spenden (YEAR(?Spenden_Zeit) AS ?Bezugsjahr)\nWHERE \n{\n  ?Orga wdt:P166 wd:Q62019748 . # gGmbH\n  \n  ?Orga p:P8093 ?Spenden_statement . # Statement Spenden\n  ?Spenden_statement psv:P8093 ?Spenden_Wert . # Wert Spenden\n  ?Spenden_Wert wikibase:quantityAmount ?Spenden . # Summe Spenden\n  ?Spenden_statement pq:P585 ?Spenden_Zeit . # Zeitpunk Spenden\n  FILTER NOT EXISTS # Nur neuesten Wert herausfiltern\n  {\n    ?Orga p:P8093/pq:P585 ?Spenden_Zeit2 .\n    FILTER(?Spenden_Zeit2 > ?Spenden_Zeit)\n  }\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en\". ?Orga rdfs:label ?Organisation . } # OrgaLabel umbennen\n}\n\nORDER BY DESC(?Spenden) # Sortieren nach höchstem Spenden", "context": "Organizations with DZI donation seal approval according to donation amount", "target": ""}
{"query": "# Red Cross and Red Crescent societies\nSELECT \n  ?cLabel ?c_pop ?item ?itemLabel ?itemDescription ?start\n  (SAMPLE(?w) as ?w_) \n  (GROUP_CONCAT(DISTINCT ?t) as ?t_) \n  (SAMPLE(?fb) as ?fb_)\n  ?sts ?ids ?sls\n  ?end\nWHERE\n{\n\t?item p:P31 / ps:P31 wd:Q1968122 . \n    OPTIONAL { ?item wdt:P571 ?start }\n    OPTIONAL { ?item wdt:P856 ?w }\n    OPTIONAL { ?item wdt:P2002 ?t }\n    OPTIONAL { ?item wdt:P2013 ?fb }  \n    OPTIONAL { ?item wdt:P17 ?c . OPTIONAL { ?c wdt:P1082 ?c_pop } }\n    ?item wikibase:statements ?sts ; wikibase:identifiers ?ids ; wikibase:sitelinks ?sls .\n    OPTIONAL { ?item wdt:P576 ?end }  \n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nGROUP BY ?cLabel ?c_pop ?item ?itemLabel ?itemDescription ?sts ?ids ?sls ?end ?start\nORDER BY (BOUND(?cLabel)) (BOUND(?end)) DESC(?c_pop) ?itemLabel", "context": "Red Cross and Red Crescent societies", "target": ""}
{"query": "SELECT ?i ?iLabel ?pos ?posLabel ?const ?constLabel {\n  ?i p:P39 ?posstmt. ?posstmt ps:P39 ?pos. ?pos wdt:P17 wd:Q902.\n  ?posstmt pq:P580 ?start.\n  OPTIONAL { ?posstmt pq:P582 ?end_ }\n  BIND(IF(BOUND(?end_),?end_,NOW()) as ?end)\n  OPTIONAL { ?posstmt pq:P768 ?const }\n  FILTER(?pos != wd:Q61945113)\n  FILTER(?start <= \"+2005-01-13T00:00:00Z\"^^xsd:dateTime)\n  FILTER(?end >= \"+2005-01-13T00:00:00Z\"^^xsd:dateTime)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". ?i rdfs:label ?iLabel. ?pos rdfs:label ?posLabel. ?const rdfs:label ?constLabel }\n}\nORDER BY ?posLabel ?constLabel", "context": "The people of the government of Bangladesh on 13 January 2005", "target": ""}
{"query": "#defaultView:Map\nSELECT ?Roke_Montpellierkoa ?Roke_MontpellierkoaLabel ?koordenatuak WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Roke_Montpellierkoa wdt:P825 wd:Q152457;\n    ((wdt:P131*)/^wdt:P527) wd:Q47588.\n  OPTIONAL { ?Roke_Montpellierkoa wdt:P625 ?koordenatuak. }\n}", "context": "Buildings in the Basque Country dedicated to San Roque", "target": ""}
{"query": "SELECT ?langLabel ?lemma {\n  ?lexeme ontolex:sense/wdt:P5137 wd:Q42804 ; wikibase:lemma ?lemma; dct:language ?lang. wd:Q12414903 wdt:P527 ?lang.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "List of words for \"beard\" in Eighth Schedule languages", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?cds\", \"?line\", \"?layer\"]}\nSELECT DISTINCT ?line ?ns ?nsLabel ?connect ?connectLabel (?connectLabel as ?layer)\nWITH {\nSELECT ?item (sample(?lat1) as ?lat1) (sample(?lon1) as ?lon1) WHERE {\n?item wdt:P31/wdt:P279* wd:Q55488; wdt:P625 ?cds. MINUS { ?item wdt:P31/wdt:P279* wd:Q928830 }\n{?item wdt:P17 wd:Q668}\n?connect wdt:P31 wd:Q15141321 ; wdt:P17 wd:Q668 ; wdt:P1427 wd:Q986105 .\n?item wdt:P1192 ?connect.\n?item p:P625/psv:P625/wikibase:geoLatitude ?lat1 .\n?item p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n} GROUP BY ?item\n} AS %stations\nWITH {\nSELECT ?line ?connect ?ns WHERE {\nINCLUDE %stations .\n?item p:P1192 [ ps:P1192 ?connect ; pq:P156 ?ns ] .\n?connect wdt:P31 wd:Q15141321 ; wdt:P17 wd:Q668 ; wdt:P1427 wd:Q986105 .\n?ns p:P625/psv:P625/wikibase:geoLatitude ?lat2 .\n?ns p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\nBIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) .\nBIND(STRDT(?str, geo:wktLiteral) AS ?line) \n}\n} AS %lines\nWHERE {\n{ INCLUDE %lines  }.\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". ?connect rdfs:label ?connectLabel. ?ns rdfs:label ?nsLabel}\n} ORDER BY ?connectLabel", "context": "Map of train services starting at Howrah station", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?label ?_image ?coordinate_location WHERE {\n  ?item wdt:P31 wd:Q100323579.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n    ?item rdfs:label ?label.\n  }\n  OPTIONAL { ?item wdt:P18 ?_image. }\n  OPTIONAL { ?item wdt:P625 ?coordinate_location. }\n}\n", "context": "Map of big sculptures of New Zealand", "target": ""}
{"query": "#defaultView:Map\nSELECT ?i ?iLabel ?cds ?layer {\n  ?i wdt:P31/wdt:P279* wd:Q32815 ; wdt:P17 wd:Q902 ; wdt:P625 ?cds ; wdt:P149 ?type.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". ?i rdfs:label ?iLabel. ?type rdfs:label ?layer }\n}", "context": "Map of mosques in Bangladesh according to architectural style", "target": ""}
{"query": "#defaultView:ScatterChart\nSELECT ?deathYYYY_MM  (COUNT(?gender) AS ?count) ?genderLabel WHERE {\n  ?human wdt:P31 wd:Q5 .\n  ?human wdt:P27 wd:Q145 .\n  ?human wdt:P21 ?gender . \n  ?human p:P570 [\n    a wikibase:BestRank ;\n    psv:P570 [\n      wikibase:timeValue ?dateOfDeath ;\n      wikibase:timePrecision ?precision\n    ]\n  ]\n  FILTER (?precision >= 10 ) # month precision or better\n  BIND(YEAR(?dateOfDeath) as ?year) .\n  BIND(MONTH(?dateOfDeath) as ?month) .\n  BIND ( IF(?month < 10, CONCAT(\"0\",STR(?month) ), STR(?month)) AS ?MM)\n  BIND(CONCAT(STR(?year), \"-\", ?MM) as ?deathYYYY_MM).\n  FILTER (?year >= 2005)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} \nGROUP BY ?deathYYYY_MM  ?genderLabel\n", "context": "UK deaths per month since 2005 split by sex/gender", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?i ?iLabel ?start ?end {\n  ?i wdt:P31 wd:Q50846468 ; wdt:P641 wd:Q5375 ; wdt:P1923 wd:Q1143793 ; wdt:P580 ?start ; wdt:P582 ?end. MINUS { ?i wdt:P17 wd:Q668 }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Timeline of Indian cricket tours abroad", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?universidad ?universidadLabel ?fecha WHERE {\n  ?universidad wdt:P17 wd:Q96 ;\n        wdt:P31 wd:Q3918 ;\n        wdt:P571 ?fecha.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es, en\". }\n}\n", "context": "Timeline of inception of Mexican universities", "target": ""}
{"query": "#Kueri spesies-spesies Mangifera dan kultivar-kultivar mangga\nSELECT distinct ?butir ?nama ?deskripsi ?gambar WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id\".\n          ?mangga rdfs:label         ?nama.\n          ?mangga schema:description ?deskripsi.\n  }\n  { ?mangga wdt:P171 wd:Q161807. BIND (?mangga AS ?butir) }\n  UNION\n  { ?mangga wdt:P31 wd:Q27068180. BIND (?mangga AS ?butir) }\n  OPTIONAL { ?mangga wdt:P18 ?gambar. }\n}\n#defaultView:ImageGrid", "context": "Mangifera species and mango cultivars", "target": ""}
{"query": "#defaultView:Map\nSELECT * WHERE {\n   wd:Q104863295 p:P276 ?stat . \n    ?stat ps:P276 ?location .\n    ?location p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].\n   ?stat pq:P1545 ?series .\n  \n   wd:Q104863295 p:P276 ?stat2 . \n    ?stat2 ps:P276 ?location2 .\n    ?location2 p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].\n   ?stat2 pq:P1545 ?series2 .\n  \n  filter (xsd:integer(?series) = xsd:integer(?series2) -1)\n  \n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n    BIND(\"ff0033\" as ?rgb)\n   BIND(\"voyage\" AS ?layer) \n}", "context": "Locations where The Antartic Circumpolar Voyage expedition passed", "target": ""}
{"query": "# All english forms missing an audio pronounciation\n# affixes excluded\nSELECT distinct ?l ?lemma ?audio WHERE {\n  ?l dct:language wd:Q1860;\n    wikibase:lemma ?lemma; \n    ontolex:lexicalForm ?form .\n  ?form ontolex:representation ?lemma .\n  minus {?form wdt:P443 ?audio.}\n  # Exclude affixes\n  minus {?l wikibase:lexicalCategory wd:Q62155.}\n}\n", "context": "English Language forms missing an audio pronunciation", "target": ""}
{"query": "#defaultView:ImageGrid\nselect ?item ?itemLabel ?img where {\n\n  ?item wdt:P31 wd:Q1195942 .\n  ?item wdt:P31/wdt:P279* wd:Q24398318 .\n  optional {?item wdt:P18 ?img .}\n\n}", "context": "Czech religious buildings used also as a fire station", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?ingredienteLabel (COUNT (?ingrediente) AS ?count)\nWHERE\n{\n  ?sandwich wdt:P31?/wdt:P279* wd:Q28803;\n            wdt:P527 ?ingrediente.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es\", \"en\". }\n}\nGROUP BY  ?ingredienteLabel\nORDER BY DESC (?count)\n\n", "context": "Common ingredients to prepare a sandwich", "target": ""}
{"query": "#OpenGLAM Survey\nSELECT ?item ?itemLabel ?coordinate_location ?image WHERE {\n  ?item wdt:P1343 wd:Q73357989.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL {\n    { ?item wdt:P625 ?coordinate_location. }\n    UNION\n    {\n      ?item p:P159 ?headquarters.\n      ?headquarters pq:P625 ?coordinate_location.\n    }\n  }\n  OPTIONAL { ?item wdt:P18 ?image. }\n}", "context": "Location of GLAMs with open access policies", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?presidente ?presidenteLabel ?firma  WHERE { \n?presidente wdt:P39 wd:Q628004 ;\n   wdt:P109 ?firma .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es\". }\n}\n", "context": "Mexican presidents who have an image of their signature on Wikidata", "target": ""}
{"query": "#defaultView:Timeline\n\nselect distinct ?item ?itemLabel ?itemDescription ?birth where {\n    ?item wdt:P106 wd:Q36180 ;\n          wdt:P27 wd:Q96 ;\n          wdt:P570 ?death;\n          wdt:P569 ?birth;            \n    filter (?birth > \"1800-01-01\"^^xsd:dateTime && ?birth < \"1900-01-01\"^^xsd:dateTime)\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\" }\n}\n", "context": "Timeline of Mexican writers who were born between 1800 and 1900", "target": ""}
{"query": "#TV-Series in Spain by Decade\nSELECT DISTINCT ?item ?itemLabel ?start_time ?end_time ?original_broadcasterLabel ?itemDescription ?sitelinks ?number_of_episodes ?duration ?number_of_seasons WHERE {\n  ?item wdt:P31 wd:Q5398426;\n    wdt:P495 wd:Q29;\n    wikibase:sitelinks ?sitelinks.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n  OPTIONAL { ?item wdt:P449 ?original_broadcaster. }\n  OPTIONAL { ?item wdt:P580 ?start_time. }\n  OPTIONAL { ?item wdt:P582 ?end_time. }\n  OPTIONAL { ?item wdt:P1113 ?number_of_episodes. }\n  OPTIONAL { ?item wdt:P2047 ?duration. }\n  OPTIONAL { ?item wdt:P2437 ?number_of_seasons. }\n}\nORDER BY DESC (?start_time)", "context": "TV-Series in Spain by Decade", "target": ""}
{"query": "#Diplomats who are also art collectors\nSELECT ?item ?itemLabel\nWHERE \n{\n  ?item wdt:P106 wd:Q193391.\n  ?item wdt:P106 wd:Q10732476 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Diplomats who are also art collectors", "target": ""}
{"query": "# Euskal Lexemak Wikidatan: Larramendiren hiztegira lotura dutenak\n# Query for demonstration (Lindemann/Alonso 2021)\n# ikus http://lexbib.org/larramendi\n\nselect ?lexemeId ?lemaBatua (group_concat(distinct ?attestedAs; separator=\"; \") as ?attestedLemmata)\n        (group_concat(distinct (strafter(str(?url), \"#\")); separator=\"; \") as ?spanishHeadwords) \n        (group_concat(distinct ?url; separator=\"; \") as ?wikisourceURLs) \nwhere \n{\n  ?lexemeId dct:language wd:Q8752;\n            wikibase:lemma ?lemaBatua;\n            p:P5323  [pq:P7855 ?attestedAs ;\n                      pq:P973 ?url ;\n                     ] .\n} group by ?lemaBatua ?lexemeId\n  order by ?attestedLemmata\n", "context": "Basque lexemes and their attestations in a 1745 Spanish-Basque dictionary, with links to dictionary entries in Wikisource", "target": ""}
{"query": "#Lexemes describing a color\n# Lexemes describing a color\n# By Vesihiisi\n\nSELECT ?l ?lemma ?languageLabel  WHERE {\n  ?l a ontolex:LexicalEntry; \n       dct:language ?language; \n       wikibase:lemma ?lemma .\n  ?l wdt:P31 wd:Q376431.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?languageLabel", "context": "Lexemes describing a color", "target": ""}
{"query": "SELECT DISTINCT ?lexeme ?lemma ?foreignlexeme ?foreignlemma {\n  ?lexeme wikibase:lemma ?lemma ; \n          dct:language wd:Q9035 ;\n          ontolex:sense / wdt:P5137 / ^wdt:P5137 / ^ontolex:sense ?foreignlexeme .\n  ?foreignlexeme dct:language wd:Q9288 ;\n                     wikibase:lemma ?foreignlemma .\n  FILTER (LANG(?foreignlemma) = 'he')\n}", "context": "Danish-Hebrew Lexeme pairs", "target": ""}
{"query": "# Lexemes in Swedish with usage example that demonstrates both a form and a sense and a reference\n# So9q 2020-02-01\nselect ?lexemeId ?lemma ?example ?sourceLabel ?source\nWHERE {\n  ?lexemeId <http://purl.org/dc/terms/language> wd:Q9027;\n            wikibase:lemma ?lemma.\n  ?lexemeId p:P5831 ?statement.\n  ?statement ps:P5831 ?example;\n             pq:P6072 [];\n             pq:P5830 [].\n  ?lexemeId p:P5831 [ prov:wasDerivedFrom [ pr:P248 ?source ] ].\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv,en\". }\n}", "context": "Lexemes in Swedish with usage example that demonstrates both a form and a sense and a reference", "target": ""}
{"query": "SELECT ?episode_nb ?item ?itemLabel ?original_title ?season_nb ?episode_nb_in_season ?season ?sl ?st\nWHERE\n{\n  ?item wdt:P31 wd:Q21191270 ; wdt:P4908 ?season ; wdt:P179 wd:Q204228 .\n  OPTIONAL { ?season p:P179 [ pq:P1545 ?season_nb ] }\n  OPTIONAL { ?item p:P4908 [ pq:P1545 ?episode_nb_in_season ] }\n  OPTIONAL { ?item p:P179 [ pq:P1545 ?episode_nb ] } \n  OPTIONAL { ?item wdt:P1476 ?original_title . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL { ?item wikibase:sitelinks ?sl ; wikibase:statements ?st }\n}\nORDER BY xsd:integer(?season_nb) xsd:integer(?episode_nb)", "context": "Episodes of the TV series \"The Mentalist\"", "target": ""}
{"query": "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\nSELECT DISTINCT \n?citizenship ?citizenship_label (COUNT(?citizenship_label) AS ?count) \nWHERE\n{ ?work wdt:P6982 ?afd .\n  \n  \n    ?work wdt:P50 ?author .\n    ?author rdfs:label ?name .\n    FILTER (lang(?name) = 'en')\n     ?author wdt:P27 ?citizenship .\n    ?citizenship rdfs:label ?citizenship_label .\n    FILTER (lang(?citizenship_label) = 'en')    \n   \n   \n}\nGROUP BY ?citizenship ?citizenship_label", "context": "Nationality of authors publishing papers relevant to the Australian fauna", "target": ""}
{"query": "SELECT ?citizenshipLabel (COUNT(?s) AS ?cnt)\n{\n  ?s wdt:P31 wd:Q5 . # human\n  ?s wdt:P106 wd:Q639669 . # musician\n  ?s wdt:P27 ?citizenship .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n} GROUP BY ?citizenshipLabel ORDER BY DESC(?cnt)\n", "context": "Number of musicians on Wikidata by country", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord\"], \"layer\": \"?countryLabel\"}\nSELECT  ?region ?ISO3166_2 ?countryLabel ?ISO3166_1 ?regionLabel ?regionDescription ?coord WHERE {\n  ?region wdt:P300 ?ISO3166_2.\n  ?region wdt:P17 ?country.\n  OPTIONAL {?region wdt:P625 ?coord}.\n  OPTIONAL{?country wdt:P298 ?ISO3166_1}\n#  OPTIONAL {?Wiki schema:about ?region;\n#           schema:isPartOf <https://en.wikipedia.org/>}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"sv,en\". }\n} order by ?countryLabel ?ISO3166_2", "context": "Map of ISO 3166-2 codes", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?peli ?peliLabel ?lugar ?lugarLabel ?coord\nWHERE \n{\n  ?lugar wdt:P17 wd:Q96 ;\n         wdt:P625 ?coord .\n  ?peli wdt:P31 wd:Q11424 ;\n         wdt:P915 ?lugar .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es\". }\n}", "context": "Map films that have been filmed in Mexico", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord\"], \"layer\": \"?cidLabel\"}\nSELECT ?item ?itemLabel ?itemDescription ?GITHUBTopic ?url ?Wikipedia ?coord ?country ?img ?cidLabel\nWHERE \n{\n  ?item wdt:P9100 ?GITHUBTopic.\n  ?item wdt:P625 ?coord.\n  OPTIONAL {?item wdt:P2716 ?img}\n  OPTIONAL {?item wdt:P18 ?img}\n  OPTIONAL {?item wdt:P17 ?cid}\n  BIND(URI(CONCAT(\"https://github.com/topics/\",?GITHUBTopic)) AS ?url)\n  OPTIONAL { ?Wikipedia schema:about ?item;\n                      schema:isPartOf <https://en.wikipedia.org/>}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} order by ?itemLabel", "context": "Map of Githubtopics on Wikidata", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?jo ?joLabel ?date WHERE { \n   ?jo wdt:P31 wd:Q159821 ;\n      wdt:P580 ?date .\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"es\". }\n}", "context": "Timeline with the start date of the Summer Olympics", "target": ""}
{"query": "SELECT\n  ?director ?directorLabel ?directorDescription \n  (COUNT(?item) as ?count_episodes) \n  (SAMPLE(?item) as ?sample_episode)\n  (SAMPLE(?original_title) as ?sample_title)\nWHERE\n{\n  ?item wdt:P31 wd:Q21191270  ; wdt:P179 wd:Q204228 .\n  ?item wdt:P57 ?director .\n  OPTIONAL { ?item wdt:P1476 ?original_title }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?director ?directorLabel ?directorDescription \nORDER BY DESC(?count_episodes) ?directorLabel", "context": "directors of episodes of The Mentalist", "target": ""}
{"query": "SELECT ?scholarly_article ?title WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?scholarly_article wdt:P31 wd:Q13442814;\n    wdt:P1433 wd:Q21385466.\n  OPTIONAL {  }\n  OPTIONAL { ?scholarly_article wdt:P1476 ?title. }\n}\nLIMIT 1000", "context": "Articles from The Wētā entomology journal", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?townhall ?townhallLabel ?communeLabel ?coords ?image\nWHERE \n{\n  ?commune p:P31 ?cSta ;\n           wdt:P47 ?centre .\n  ?cSta ps:P31 wd:Q484170 .\n  VALUES ?centre { wd:Q12191 }\n  FILTER NOT EXISTS { ?cSta pq:P582 ?x }\n  OPTIONAL {\n    ?townhall wdt:P31/wdt:P279* wd:Q543654 ;\n              wdt:P131 ?commune .\n    OPTIONAL { ?townhall wdt:P625 ?coords }\n    OPTIONAL { ?townhall wdt:P18 ?image }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?communeLabel", "context": "Town halls of the municipalities that surround Nantes", "target": ""}
{"query": "#defaultView:Map \nSELECT DISTINCT ?author ?authorLabel ?image ?coord WHERE {\n  ?author wdt:P31 wd:Q11269813 .\n  ?author wdt:P625 ?coord .\n  OPTIONAL {?author wdt:P18 ?image .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de, en, fr, es, it, cz\" }\n} \n", "context": "Caves containing prehistoric art", "target": ""}
{"query": "SELECT ?item ?itemLabel ?place ?placeLabel ?image\nWHERE \n{\n  ?item wdt:P183 ?place .\n  ?item wdt:P171+ wd:Q5113 .\n  ?place wdt:P17 wd:Q142 .\n  OPTIONAL { ?item wdt:P18 ?image . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Endemic birds in a place located in France", "target": ""}
{"query": "# works in the Women's + Gender History DH list \n# (bit.ly/2RVz7ks)\n\nSELECT ?item ?itemLabel ?official_website WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P1343 wd:Q76510930.\n  OPTIONAL { ?item wdt:P856 ?official_website. }\n}\nORDER BY ASC(?itemLabel)", "context": "Websites related to works in the Women's & Gender History DH list", "target": ""}
{"query": "SELECT DISTINCT ?q ?qLabel WHERE {\n  ?q wdt:P31 wd:Q3624078 ; wdt:P37 ?l1 ; wdt:P37 ?l2 .\n  FILTER ( STR(?l1) < STR(?l2) ) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?qLabel", "context": "List of states with more than one official language", "target": ""}
{"query": "#Orgs registered in the NAAN registry with their location.\n#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?geo ?image\nWHERE\n{\n  ?item wdt:P1662 ?doiPrefix ;\n       wdt:P625 ?geo.\n  OPTIONAL {?item wdt:P18 ?image.}\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\"  } \n  }", "context": "Location of the organizations in the Name Assigning Authority Number (NAAN) registry", "target": ""}
{"query": "#Using mwapi to base a query on articles in a Wikipedia category\n# Use the MediaWiki API to get the articles from a specific category in a specific Wikipedia language version.\n# Then use this results as usual in a query, in this example a simple query for the gender.\n# By User:Ainali with the help of User:Dipsacus fullonum  2021-02\nSELECT ?item ?itemLabel ?genderLabel WHERE {\n  SERVICE wikibase:mwapi {\n     bd:serviceParam wikibase:endpoint \"sv.wikipedia.org\";     # Set the project here\n                     wikibase:api \"Generator\";\n                     mwapi:generator \"categorymembers\";        # Selects the content from a category\n                     mwapi:gcmtitle \"Category:Födda 2001\";.    # Specifies the category (Born in 2001)\n     ?item wikibase:apiOutputItem mwapi:item.\n  } \n  FILTER BOUND (?item)                                         # Safeguard to not get a timeout from unbound items when using ?item below\n  ?item wdt:P21 ?gender .                                      # Example retrieval of a value\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,sv\". }    \n}", "context": "Use the MediaWiki API to get the content of a Wikipedia category into a query", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT  (str(?year) as ?yearstr) (count (?item) as ?count ){\n ?item wdt:P1325 ?dataset;\n       wdt:P31 wd:Q13442814;\n       wdt:P577 ?date.\n  BIND(year(?date) as ?year)\n}\nGROUP By ?year\nORDER by ?count", "context": "Number of scholarly articles sharing their datasets", "target": ""}
{"query": "#Find parent taxon shared by two taxa:\n\n# Alocasia cuprea (Q15314291)\nPREFIX TAX1: <http://www.wikidata.org/entity/Q15314291>\n\n# Codiaeum variegatum (Q1345646)\nPREFIX TAX2: <http://www.wikidata.org/entity/Q1345646>\n\nSELECT ?via ?viaLabel ?rank ?rankLabel ?pic\nWHERE {\n\n?via \n      ^wdt:P171* TAX1: ;\n      ^wdt:P171*  TAX2: .\n  \n  \n  \nFILTER NOT EXISTS {\n?via2 ^wdt:P171* TAX1: ;\n      ^wdt:P171* TAX2: ;\n      wdt:P171 ?via .\n}\n  \nOPTIONAL { ?via       wdt:P18 ?pic ;          wdt:P105 ?rank .  } \n  \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Most specific taxon that includes two given species", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  { SELECT DISTINCT ?item WHERE { ?item (p:P31/ps:P31/(wdt:P279*)) wd:Q61960067. } }\n}", "context": "List of vegan restaurants", "target": ""}
{"query": "#defaultView:Map{ \"hide\" : \"?coord\" }\nSELECT DISTINCT ?item ?itemLabel ?pic ?coord ?denkmallink ?wikipedia WHERE {\n             ?item wdt:P7900 ?denkmalatlas .\n  OPTIONAL { ?item wdt:P625  ?coord . }\n  OPTIONAL { ?item wdt:P18   ?pic . }\n  OPTIONAL {\n    ?wikipedia schema:about ?item ;\n               schema:inLanguage \"de\" ;\n               schema:isPartOf <https://de.wikipedia.org/> . }\n  BIND(URI(CONCAT(\"https://denkmalatlas.niedersachsen.de/viewer/metadata/\", STR(?denkmalatlas))) AS ?denkmallink)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en\" . }\n}", "context": "Map of monuments in Lower Saxony, Germany", "target": ""}
{"query": "#Wat\n#defaultView:Map\nSELECT DISTINCT ?wat ?watLabel ?coord WHERE {\n  ?wat wdt:P31 wd:Q427287.\n  ?wat wdt:P17 wd:Q869.\n  OPTIONAL { ?wat wdt:P625 ?coord. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],th\". }\n}\nORDER BY ?watLabel", "context": "Map of Wats in Thailand", "target": ""}
{"query": "SELECT DISTINCT ?localidad ?localidadLabel WHERE {\n  ?localidad wdt:P31 wd:Q20202352 ;\n        rdfs:label ?itemLabel;\n  FILTER (lang(?itemLabel) = \"es\") .\n  FILTER regex (?itemLabel, \"^San\") .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es\". }\n}\n", "context": "Localities in Mexico and contain 𝐒𝐚n in their name", "target": ""}
{"query": "#female mycologists born<1900, sorted by number of sitelinks, with some related info\n#\nSELECT ?item ?itemLabel (SAMPLE(?occname) AS ?occname) ?sitelinks ?WDstatements ?article (SAMPLE(?birth) AS ?birth) (SAMPLE(?death) AS ?death) (SAMPLE(?bloodhound) AS ?bloodhound) (SAMPLE(?harvard) AS ?harvard) WHERE {\n  #?occ wdt:P279|wdt:P279/wdt:P279* wd:Q2374149.\n  VALUES ?occ { wd:Q2487799 }\n  ?item wdt:P106 ?occ .\n  OPTIONAL { ?item wdt:P108 ?emp . }\n  ?item wdt:P21 wd:Q6581072;\n    wikibase:statements ?WDstatements;\n    wikibase:sitelinks ?sitelinks.\n  OPTIONAL {\n   ?article schema:about ?item;\n      schema:inLanguage \"en\";\n      schema:isPartOf <https://en.wikipedia.org/>.\n  }\n  ?occ rdfs:label ?occname .\n  OPTIONAL {?item wdt:P569 ?birth .}\n  OPTIONAL {?item wdt:P570 ?death .}\n  OPTIONAL {?item wdt:P6944 ?bloodhound .}\n  OPTIONAL {?item wdt:P6264 ?harvard .}\n  FILTER(LANG(?occname) = \"en\").\n  FILTER((?birth<\"1900-01-01T00:00:00Z\"^^xsd:dateTime)||(?death<\"1920-01-01T00:00:00Z\"^^xsd:dateTime)).\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n  }\n}\nGROUP BY ?item ?itemLabel ?sitelinks ?WDstatements ?article\nORDER BY DESC (?sitelinks)", "context": "Female mycologists born pre-1900, sorted by number of sitelinks", "target": ""}
{"query": "SELECT ?item ?itemLabel (GROUP_CONCAT(DISTINCT ?occupationLabel; SEPARATOR = \", \") AS ?occupations) (GROUP_CONCAT(DISTINCT ?countryLabel; SEPARATOR = \", \") AS ?countries) ?birth ?articles WHERE {\n  VALUES ?occ {wd:Q901}\n  ?item wdt:P31 wd:Q5;\n    wdt:P21 wd:Q6581072;\n    wdt:P27 ?country;\n    (wdt:P106/(wdt:P279*)) ?occ;\n    wdt:P106 ?occupation;\n    wikibase:sitelinks ?articles;\n    wdt:P569 ?birth.\n  hint:Prior hint:rangeSafe \"true\"^^xsd:boolean.\n  FILTER((?birth >= \"1500-01-01T00:00:00\"^^xsd:dateTime) && (?birth < \"1900-01-01T00:00:00\"^^xsd:dateTime))\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"es,en\".\n    ?item rdfs:label ?itemLabel.\n    ?gender rdfs:label ?genderLabel.\n    ?occupation rdfs:label ?occupationLabel.\n    ?country rdfs:label ?countryLabel.\n  }\n}\nGROUP BY ?item ?itemLabel ?genderLabel ?birth ?articles\nORDER BY DESC (?articles)", "context": "Women scientists who were born between the year 1500 and 1900", "target": ""}
{"query": "# Hungarian church buildigs sorted by confessions \n#defaultView:Map{\"hide\": [\"?layer\"]}\nSELECT\n?templom ?templomLabel (SAMPLE(?templomLocation) AS ?templomLocation_) (SAMPLE(?templomImage) AS ?templomImage_)\n\n(IF(?ref, \"Calvinist\" , \" \") AS ?refLabel)\n(IF(?evang, \"Lutheran\", \" \") AS ?evangLabel)\n(IF(?kat, \"Catholic\", \" \") AS ?katLabel)\n(IF(?ort, \"Eastern Orthodox\", \" \") AS ?ortLabel)\n(CONCAT(?refLabel,  ?evangLabel,  ?katLabel, ?ortLabel) AS ?layer)\nWHERE {\n  ?templom wdt:P31/wdt:P279* wd:Q16970;\n          wdt:P17 wd:Q28;\n  BIND(EXISTS { ?templom wdt:P31 wd:Q103842783. } AS ?ref)\n  BIND(EXISTS { ?templom wdt:P31 wd:Q56242275. } AS ?evang)\n  BIND(EXISTS { ?templom wdt:P31/wdt:P279* wd:Q2031836. } AS ?ort)\n  BIND(EXISTS { ?templom wdt:P31/wdt:P279* wd:Q1088552. } AS ?kat)\n  OPTIONAL { ?templom wdt:P625 ?templomLocation. }\n  OPTIONAL { ?templom wdt:P18 ?templomImage. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?templom ?templomLabel ?person ?personLabel ?ref ?evang ?kat ?ort", "context": "Hungarian church buildings sorted by confessions", "target": ""}
{"query": "#defaultView:Map\nSELECT ?misto_narozeni ?misto_umrti ?line WITH {\n  SELECT ?item ?misto_narozeni ?misto_umrti where {\n  ?item wdt:P19/wdt:P17 wd:Q213 .  \n  ?item wdt:P20/wdt:P17 wd:Q213 . \n  ?item wdt:P19* ?misto_narozeni . ?misto_narozeni wdt:P31 wd:Q5153359 .?misto_narozeni wdt:P1082 ?obyvatele filter(?obyvatele > 2500 ) .\n  ?item wdt:P20* ?misto_umrti . ?misto_umrti wdt:P31 wd:Q5153359 filter(?misto_narozeni != ?misto_umrti).\n  } \n} AS %data WITH {\n  SELECT ?misto_narozeni ?misto_umrti (COUNT(?item) AS ?pocet) {\n    INCLUDE %data .\n  } GROUP BY ?misto_narozeni ?misto_umrti \n} AS %counts WITH {\n  SELECT ?misto_narozeni (MAX(?pocet) AS ?nej) {\n    INCLUDE %counts .\n  } GROUP BY ?misto_narozeni\n} AS %best WHERE {\n  INCLUDE %counts .\n  INCLUDE %best .\n  ?misto_narozeni p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].\n  ?misto_umrti p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  FILTER( ?nej = ?pocet ) .                  \n} order by asc(?misto_narozeni)", "context": "Where Czechs most died if not in their place of birth", "target": ""}
{"query": "#Most popular tonality\nSELECT ?tonalityLabel (COUNT(?tonalityLabel) as ?count)\nWHERE\n{\n  ?work wdt:P826 ?tonality.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nGROUP BY ?tonalityLabel\nORDER BY DESC(?count)", "context": "Most popular tonalities", "target": ""}
{"query": "#find al deceased people with a verified twitter account\n#author: https://github.com/grafst\n#date: 2021-03-04\n\nSELECT ?humanLabel ?handle ?death ?statement WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?human wdt:P31 wd:Q5. # all people\n  ?human wdt:P570 ?death. # who have a death date\n  ?human wdt:P2002 ?handle. # and a twitter account\n  ?human p:P2002 ?statement.\n  ?statement pq:P1552 wd:Q28378282. # which is accessible\n}\nLIMIT 10000", "context": "Deceased people with verified Twitter accounts", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription ?named ?img ?sts ?sls ?ids\nWHERE\n{\n\t?item wdt:P463 wd:Q1702801 . \n    OPTIONAL { ?item p:P463 [ ps:P463 wd:Q1702801 ; pq:P1810 ?named ] }\n    OPTIONAL { ?item wdt:P18 ?img }\n    ?item wikibase:sitelinks ?sls ; wikibase:statements ?sts ; wikibase:identifiers ?ids \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de\" }\n}", "context": "members of Ludlamshöhle, a society in Vienna (1819-1826)", "target": ""}
{"query": "SELECT ?item ?itemLabel (GROUP_CONCAT(DISTINCT ?occupationLabel; SEPARATOR = \", \") AS ?occupations) (GROUP_CONCAT(DISTINCT ?countryLabel; SEPARATOR = \", \") AS ?countries) ?birth ?articles WHERE {\n  VALUES ?occ {wd:Q15253558}\n  ?item wdt:P31 wd:Q5;\n    wdt:P21 wd:Q6581072;\n    wdt:P27 ?country;\n    (wdt:P106/(wdt:P279*)) ?occ;\n    wdt:P106 ?occupation;\n    wikibase:sitelinks ?articles;\n    wdt:P569 ?birth.\n    ?country p:P361/ps:P361 wd:Q12585 .\n  hint:Prior hint:rangeSafe \"true\"^^xsd:boolean.\n  FILTER((?birth >= \"1900-01-01T00:00:00\"^^xsd:dateTime) && (?birth < \"1999-01-01T00:00:00\"^^xsd:dateTime))\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"es,en\".\n    ?item rdfs:label ?itemLabel.\n    ?gender rdfs:label ?genderLabel.\n    ?occupation rdfs:label ?occupationLabel.\n    ?country rdfs:label ?countryLabel.\n  }\n}\nGROUP BY ?item ?itemLabel ?genderLabel ?birth ?articles\nORDER BY DESC (?articles)", "context": "Latin American women who are activists and were born between 1900-1999", "target": ""}
{"query": "#Rues nommées d’après une personne\n#defaultView:Map\nSELECT ?street ?streetLabel ?cityLabel ?personLabel ?loc\nWHERE\n{\n    ?street (wdt:P31/(wdt:P279*)) wd:Q83620 .\n    ?street wdt:P131 wd:Q72 .\n  ?street wdt:P625 ?loc.\n    ?street wdt:P138 ?person .\n    ?person wdt:P31 wd:Q5.\n  ?person wdt:P21 wd:Q6581072.\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" }\n}\n", "context": "Transportation routes in Zurich named after women", "target": ""}
{"query": "#defaultView:Map\nselect ?building ?buildingLabel ?coord ?image WHERE \n{\n ?building wdt:P84 wd:Q47780 ;\n           wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n  }\n", "context": "Location of buildings designed by", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?_jaiolekuaLabel (COUNT(?_genre) AS ?count) (SAMPLE(?_genreLabel) AS ?label) WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P1412 wd:Q8752;\n    wdt:P19 ?_jaiolekua;\n    wdt:P21 ?_genre.\n  ?_jaiolekua wdt:P131* wd:Q2602877.\n  ?_genre rdfs:label ?_genreLabel.\n  ?_jaiolekua rdfs:label ?_jaiolekuaLabel.\n  FILTER((LANG(?_genreLabel)) = \"eu\")\n  FILTER((LANG(?_jaiolekuaLabel)) = \"eu\")\n\n}\nGROUP BY ?_genreLabel ?_jaiolekuaLabel", "context": "Proportion of Basque men / women in Donostialdea region", "target": ""}
{"query": "SELECT ?P452 ?P452Label ?count WITH {\n  SELECT ?P452 (COUNT (DISTINCT ?item) as ?count)\n  WHERE \n         {\n  ?item wdt:P31/wdt:P279* wd:Q783794 .\n  ?item wdt:P452 ?P452  .\n  } group by ?P452} as %i\nWHERE \n{include %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  } \nORDER by desc (?count)", "context": "Count of the industry companies on Wikidata belong to", "target": ""}
{"query": "SELECT ?verb ?lemma ?conjugation_class ?conjugation_classLabel {\n  ?verb wikibase:lexicalCategory wd:Q24905 ;\n        wikibase:lemma ?lemma ;\n        dct:language wd:Q9035 ;\n        wdt:P5186 ?conjugation_class ;\n        wdt:P5191 ?lexeme .\n  FILTER NOT EXISTS { ?lexeme wikibase:lexicalCategory wd:Q24905 }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da\". }\n}", "context": "Danish verbs-derived-from-non-verbs", "target": ""}
{"query": "#Euskal Herriko Mendiak\n#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coor ?image WHERE {\n  ?item wdt:P131* ?non.\n  wd:Q47588 wdt:P527 ?non.\n  ?item wdt:P31 wd:Q8502.\n  OPTIONAL { ?item wdt:P625 ?coor. }\n  OPTIONAL { ?item wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es\". }\n}\nORDER BY ?itemLabel", "context": "Mountains of the Basque Country", "target": ""}
{"query": "#defaultView:Map\nSELECT distinct ?item ?itemLabel ?coord \nWHERE \n{\n  ?item wdt:P31 wd:Q1311696 . hint:Prior hint:runFirst true.\n  ?item wdt:P131* wd:Q22. hint:Prior hint:gearing \"forward\".\n  ?item wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n                         ?item rdfs:label ?itemLabel .}\n}", "context": "Railway junctions in Scotland", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?metroline ?metrolineLabel ?endpoints ?carte_d_itin_raire WHERE {\n  {\n    SELECT ?metroline (COUNT(?endpoint_statement) AS ?endpoints) WHERE {\n      ?metroline wdt:P31 wd:Q105967897;\n        p:P559 ?endpoint_statement.\n      ?endpoint_statement rdf:type wikibase:BestRank.\n      FILTER(NOT EXISTS { ?endpoint_statement pq:P582 _:b2. })\n    }\n    GROUP BY ?metroline\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n   ?metroline wdt:P15 ?carte_d_itin_raire.\n}", "context": "Maps of Y-shaped subway lines", "target": ""}
{"query": "SELECT ?typeLabel (COUNT(?thing) AS ?count) WHERE {\n?thing wdt:P6108 ?iiif; wdt:P31 ?type\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?typeLabel\nORDER BY DESC(?count)", "context": "Count of type of things with IIIF links known to Wikidata", "target": ""}
{"query": "# Created by User:Gnoeee\n# Successful candidates during 2016 Kerala Legislative Assembly in each constituencies\nSELECT ?constituency_No ?jurisdictionLabel ?successful_candidateLabel ?electorate ?total_valid_votes ?votes_received ?political_partyLabel (100*?votes_received/?total_valid_votes AS ?candidate_Vote_percent) WHERE {\n  ?item wdt:P31 wd:Q40231;\n    wdt:P541 wd:Q99709336.\n  ?item wdt:P1001 ?jurisdiction.\n  ?jurisdiction p:P31 ?id.\n  ?id ps:P31 wd:Q54375461 .\n  ?id pq:P1545 ?constituency_No.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,ml\". }\n  OPTIONAL { ?item wdt:P1697 ?total_valid_votes. }\n  OPTIONAL { ?item wdt:P991 ?successful_candidate. }\n  OPTIONAL { ?item wdt:P1831 ?electorate. }\n  ?item p:P991 ?sc.\n  ?sc pq:P1111 ?votes_received;\n    pq:P102 ?political_party.\n}\nORDER BY ASC(xsd:integer(?constituency_No) )", "context": "Successful candidates during 2016 Kerala Legislative Assembly in each constituencies", "target": ""}
{"query": "#Covid-19 identifiers\nSELECT \n  ?p ?pLabel\n  (SAMPLE(?url) as ?url_comp2)\nWHERE\n{\n    hint:Query hint:optimizer \"None\".\n    BIND( wd:Q84263196 as ?comp2)\n    { \n        ?comp2 ?wdt ?v . \n        ?p wikibase:directClaim ?wdt ; wikibase:propertyType wikibase:ExternalId .\n        OPTIONAL { ?p  wdt:P1630 ?f }\n        BIND(uri(REPLACE(?f,\"\\\\$1\",?v)) as ?url)\n    }\n    UNION { BIND(wd:Q84263196 as ?p) }\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}\nGROUP BY ?p ?pLabel\nORDER BY ?url_comp2", "context": "COVID-19 external Identifier URLs in Wikidata", "target": ""}
{"query": "SELECT ?countryLabel ?item ?itemLabel ?itemDescription ?person ?personLabel ?personDescription (sample(?coor) as ?coor)\n{\n  \t?item wdt:P31/wdt:P279* wd:Q294440.\n    ?item p:P138/ps:P138 ?person .\n  \t?person wdt:P31 wd:Q5 ;\n            wdt:P21 wd:Q6581072 .\n  \t?item wdt:P17 ?country .\n  \tOPTIONAL { ?item wdt:P625 ?coor } .\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"es, en\" . }\n}\nGROUP BY ?countryLabel ?item ?itemLabel ?itemDescription ?person ?personLabel ?personDescription\nORDER BY ?countryLabel ?itemLabel\n", "context": "Public spaces whose names are of women", "target": ""}
{"query": "#defaultView:Map\nSELECT ?gizakiLabel ?koord WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?gizaki wdt:P31 wd:Q5;\n    wdt:P21 wd:Q6581072.\n  ?wikilink schema:about ?gizaki;\n    schema:isPartOf <https://eu.wikipedia.org/>.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu\". }\n  ?gizaki wdt:P19 ?jaiolekua.\n  ?jaiolekua wdt:P625 ?koord.\n}", "context": "Place of birth of females with biography articles on Basque Wikipedia", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?pic ?coord ?iNatlink WHERE {\n  ?item wdt:P7471 ?iNat.\n  ?item wdt:P625 ?coord.\n  OPTIONAL { ?item wdt:P18 ?pic. }\n  BIND(URI(CONCAT(\"https://www.inaturalist.org/observations?subview=grid&view=species&place_id=\", STR(?iNat))) AS ?iNatlink)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of places in iNaturalist", "target": ""}
{"query": "SELECT ?creditLabel ?item ?itemLabel ?itemDescription ?typeLabel ?series ?seriesLabel (MIN(?date) as ?date_)\nWHERE\n{\n\t{ ?item wdt:P58 wd:Q710739 . BIND ( wd:Q28389 as ?credit ) } \n    UNION { ?item wdt:P57 wd:Q710739 . BIND ( wd:Q2526255 as ?credit ) } \n    OPTIONAL { ?item wdt:P31 ?type }\n    OPTIONAL { ?item wdt:P179 ?series }\n    OPTIONAL { ?item wdt:P577 ?date }\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription ?typeLabel ?series ?seriesLabel ?creditLabel\nORDER BY (!BOUND(?date_)) DESC(?date_)", "context": "Donald P. Bellisario credits as screenwriter or director", "target": ""}
{"query": "# Interwiki table from P6720, with end date for prefixes that are no longer in use\n# by Jura1, 2021-03-26 https://www.wikidata.org/wiki/Property_talk:P6720\nSELECT ?item ?itemLabel ?itemDescription ?interwiki ?formatter ?added ?ended\n{\n  ?item p:P6720 ?st .\n  ?st ps:P6720 ?interwiki .\n  ?st wikibase:rank ?goodRanks.\n  OPTIONAL { ?st pq:P1630 ?formatter }\n  OPTIONAL { ?st pq:P580 ?added }\n  OPTIONAL { ?st pq:P582 ?ended }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Interwiki table from P6720, with end date for prefixes that are no longer in use", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?subj ?subjLabel ?coord ?elev ?pic\nWHERE\n{\n  ?subj wdt:P1459 ?cadw .\n  ?subj wdt:P2044 ?elev filter(?elev < 70) .\n  ?subj wdt:P625 ?coord .\n    OPTIONAL { ?subj wdt:P18 ?pic }\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Listed buildings in Wales that would be lost if the sea level rose by 70 metres", "target": ""}
{"query": "#music releases with incomplete tracklists and diff count (via CamelCaseNick)\nselect ?release ?title ?listedTracks (sum(?trackAmount) as ?totalTracks) (?totalTracks - ?listedTracks as ?diff) where {\n  {\n    select ?release (count(distinct ?track) as ?listedTracks) where {\n      {\n        select distinct ?release where {\n          ?release wdt:P31/wdt:P279* wd:Q2031291.\n\n          filter exists {\n            ?release wdt:P2635 [].\n          }\n        }\n      }\n\n      ?release wdt:P658 ?track.\n    }\n    group by ?release\n  }\n\n  ?release p:P2635/ps:P2635 ?trackAmount.\n\n  service wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\".\n    ?release rdfs:label ?title.\n  }\n}\ngroup by ?release ?title ?listedTracks\nhaving(?listedTracks != ?totalTracks)", "context": "Music releases with incomplete track lists, showing diff count", "target": ""}
{"query": "#list of female musicians in nigeria\n\nSELECT DISTINCT ?item ?itemLabel ?dob ?birthPlaceLabel\n\nWHERE {\n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P21 wd:Q6581072.\n  ?item wdt:P27 wd:Q1033.\n  ?item wdt:P106/wdt:P279* wd:Q639669.\n  OPTIONAL {?item wdt:P569 ?dob.}\n  OPTIONAL {?item wdt:P19 ?birthPlace.}\n  \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\nORDER BY DESC(?dob)", "context": "Date and place of birth of female musicians in Nigeria", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (str(?year) as ?yearString) (count(DISTINCT ?article) as ?count) {\n  #topics on multilingualism, multiple languages and bilingualism\n  VALUES ?subject {wd:Q30081 wd:Q20923490 wd:Q10779529}\n  ?article wdt:P921 ?subject;\n           wdt:P577 ?date.\n  BIND (year(?date) as ?year).\n}\nGROUP by ?year\nORDER by DESC(?count)", "context": "Scholarly articles on multilingualism and bilingualism", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?line WHERE {\n\n  ?item wdt:P17 wd:Q213 .\n  ?item wdt:P625 [] .\n  ?item p:P625 ?statement1, ?statement2 filter(?statement1 != ?statement2).\n  {?statement1 pq:P580 [] .} union {?statement1 pq:P582 [] .}  \n  {?statement1 pq:P580 [] .} union {?statement2 pq:P582 [] .}  \n  ?statement1 ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ].\n  ?statement2 ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ].\n\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  \n}", "context": "Things in Czechia which changed coordinate location at some point", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?img  WHERE {\n  ?item wdt:P31 wd:Q5;\n        wdt:P21 wd:Q6581072 .\n  ?item wdt:P106/wdt:P279* wd:Q5482740.\n  ?item wdt:P18 ?img.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n\n}\n", "context": "Images about women programmers", "target": ""}
{"query": "# UK railway stations connected from Topsham railway station (Q1990205) \n#defaultView:Map{\"hide\":[\"?cds\",\"?line\",\"?layer\"],\"layer\":\"?trackLabel\"}\nSELECT ?station ?stationLabel ?cds ?line ?trackLabel ?track ?image {\n{SELECT * {\n  SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q1990205 ; gas:linkType wdt:P197 ; gas:out ?station ; gas:out1 ?depth ; gas:out2 ?pred . }\n  FILTER(?station != wd:Q1990205)\n                      ?station wdt:P625 ?cds ;\n                               wdt:P17 wd:Q145 .\n                             \n  ?station p:P197 ?adjST .\n  ?adjST ps:P197 ?adj ;\n                pq:P81 ?track .\n\n  ?adj p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line)}\n \n}\n  OPTIONAL { ?station wdt:P18 ?image .}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "UK railway stations connected from Topsham railway station", "target": ""}
{"query": "SELECT ?sitelink ?itemLabel ?nacionalidadLabel WHERE {   \n   ?item wdt:P21 wd:Q6581072 ;\n         wdt:P27 ?nacionalidad .\n  ?sitelink schema:isPartOf <https://en.wikipedia.org/>;\n     schema:about ?item;\n     wikibase:badge wd:Q17437796 . \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" } .\n} ORDER BY ?itemLabel", "context": "Women whose article on English Wikipedia has the featured article badge", "target": ""}
{"query": "#defaultView:Map\nSELECT ?street ?streetLabel ?layer ?coords\nWHERE \n{\n  ?street wdt:P31 wd:Q79007.\n  ?street wdt:P131 wd:Q43631 .\n  ?street wdt:P138 ?namedafter .\n  ?street wdt:P625 ?coords .\n  ?namedafter wdt:P21 ?gender  .\n  BIND(IF(?gender = wd:Q6581072, \"vrouwelijk\",\"mannelijk\") AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of males and females after which streets are named (Leiden, NL)", "target": ""}
{"query": "#defaultView:Map\nSELECT ?gizakiLabel ?koord WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?gizaki wdt:P31 wd:Q5.\n  { ?gizaki wdt:P21 wd:Q1052281. }\n  UNION\n  { ?gizaki wdt:P21 wd:Q2449503. }\n  ?wikilink schema:about ?gizaki;\n    schema:isPartOf <https://eu.wikipedia.org/>.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu\". }\n  ?gizaki wdt:P19 ?jaiolekua.\n  ?jaiolekua wdt:P625 ?koord.\n}", "context": "Map of birthplaces of trans women and men with articles in the Basque Wikipedia", "target": ""}
{"query": "#q2 - flat roof in Acre\nSELECT ?item ?itemLabel \nWHERE {\n    ?item wdt:P31 wd:Q81274644; \n          wdt:P527 wd:Q149292\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of institutions where UNR faculty got their academic degrees", "target": ""}
{"query": "#Botanists with signatures and optional IPNI author ID\nselect distinct ?item ?itemLabel ?itemDescription ?sig ?ipniid\nwhere {\n    ?item wdt:P31 wd:Q5 .\n    ?item wdt:P109 ?sig .\n    ?item wdt:P106 wd:Q2374149 .\n    OPTIONAL {?item wdt:P586 ?ipniid} .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Botanists with image of signature and optional IPNI author ID", "target": ""}
{"query": "# subclasses of musical instrument with the most individual instances\nSELECT ?class ?classLabel ?classDescription ?count WITH {\n  SELECT ?class (COUNT(*) AS ?count) WHERE {\n    ?class wdt:P279+ wd:Q34379.\n    ?instance wdt:P31 ?class.\n  }\n  GROUP BY ?class\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?count)", "context": "Subclasses of musical instrument with the most individual instances", "target": ""}
{"query": "#Ordinals in all languages/notations: 0th to 100th\n#Jura1, 2021-04-06\nSELECT \n  ?nv ?item ?itemLabel \n  (GROUP_CONCAT(DISTINCT ?l0; SEPARATOR = \", \") AS ?all) \n  (GROUP_CONCAT(DISTINCT ?l1; SEPARATOR = \", \") AS ?all_ad) \n  ?enl \nWHERE\n{\n  {\n    SELECT DISTINCT ?item ?nv ?enl (STR(?lt) AS ?l0)  (STR(?ln) AS ?l1) \n    WHERE\n    {\n      ?item wdt:P31 wd:Q191780; wdt:P1181 ?nv . FILTER( ?nv < 101 ) \n      ?item rdfs:label ?enl.                    FILTER( LANG(?enl) = \"en\" )\n      ?item (rdfs:label|skos:altLabel) ?lt . FILTER( STR(?lt) != STR(?enl) && !CONTAINS(?lt, str(?nv) ) )\n      ?item (rdfs:label|skos:altLabel) ?ln . FILTER( STR(?ln) != STR(?enl) && CONTAINS(?ln, str(?nv) ) )\n    }\n    ORDER BY ?nv ?l0\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}\nGROUP BY ?nv ?item ?itemLabel ?enl\nORDER BY ?nv", "context": "Ordinals in all languages/notations: 0th to 100th", "target": ""}
{"query": "#TEMPLATE={ \"template\": { \"en\": \"Items using genre (P136) = locked room mystery (Q1141200). Please help expand. \" } }\nSELECT DISTINCT ?item ?itemLabel ?itemDescription (YEAR(?d) as ?published) ?authorLabel ?title\nWHERE\n{\n\t?item p:P136/ps:P136 wd:Q1141200 . \n    OPTIONAL { ?item wdt:P577 ?d }\n    OPTIONAL { ?item wdt:P50 ?author }\n    OPTIONAL { ?item wdt:P1476 ?title }\n    FILTER NOT EXISTS { ?item wdt:P577 ?d2 . FILTER(?d2 < ?d && BOUND(?d) && BOUND(?d2) ) }   \n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY ?published", "context": "locked-room mysteries", "target": ""}
{"query": "SELECT ?lexeme ?offset ?representation ?qLabel ?example {\n  ?lexeme p:P5831 ?example_statement .\n  ?example_statement ps:P5831 ?example .\n  ?example_statement pq:P5830 ?form .\n  ?form ontolex:representation ?representation . \n  ?lexeme wikibase:lemma ?lemma . \n  BIND(STRLEN(STRBEFORE(?example, ?representation)) AS ?offset)\n  ?lexeme dct:language wd:Q9035 .\n  OPTIONAL {\n    ?example_statement pq:P6072 / wdt:P5137 ?q \n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Danish lexemes matched to sentences", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?itemDescription ?image ?collection ?creadorLabel WHERE {\n    ?item wdt:P31 wd:Q3305213 ;\n          wdt:P195/wdt:P361* ?collection .\n  ?item wdt:P170 ?creador . \n    FILTER ( ?collection = wd:Q1138147) \n    OPTIONAL { ?item wdt:P18 ?image } \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\" }\n}", "context": "Twitter usernames and number of subscribers", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?num ?itemDescription  WHERE {\n  VALUES ?occ {wd:Q639669}\n  ?item wdt:P31 wd:Q5 ;\n    wdt:P21 wd:Q6581072 ;\n   (wdt:P106/(wdt:P279*)) ?occ;\n    wdt:P106 ?occupation .    \n  ?item wikibase:sitelinks ?num.  \n  \n  ?item wdt:P27 ?nacionalidad .  \n  ?nacionalidad wdt:P17 ?country .  \n  ?country (wdt:P361/(wdt:P361*)) wd:Q12585 .\n  FILTER(?num >= 1)\n  MINUS {\n    ?art schema:about ?item.\n    ?art schema:isPartOf <https://es.wikipedia.org/>. \n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n}\nORDER BY DESC(?num)\n", "context": "Latin American musicians without a Wikipedia article in Spanish but with an article in another language", "target": ""}
{"query": "#Performers of tracks (P2207) or releases (P2205) with a Spotify ID, that lack a Spotify Artist ID (P1902). Via User:Mahir256\nSELECT DISTINCT ?artist ?artistLabel {\n  ?i wdt:P2205|wdt:P2207 [] . ?i wdt:P175 ?artist. \n  MINUS { ?artist wdt:P1902 [] }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Artists on Spotify that lack a Spotify Artist ID (P1902) on Wikidata", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?itemLabel ?item ?legendLabel ?date ?image WHERE {\n  ?item wdt:P31 wd:Q11344 ; \n   wdt:P575 ?date .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\" } .\n}\n\n", "context": "Timeline of the discovery of elements in the periodic table", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?painting ?paintingLabel ?pic WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?painting wdt:P31 wd:Q3305213;\n    wdt:P180 wd:Q171497;\n    wdt:P18 ?pic;\n    wdt:P571 ?dob.\n  FILTER((\"1800-01-01\"^^xsd:dateTime <= ?dob) && (?dob < \"1899-01-01\"^^xsd:dateTime))\n}", "context": "19th-century depictions of sunflowers", "target": ""}
{"query": "#defaultView:BubbleChart\nselect distinct ?itemLabel ?size ?percentage_online ?rgb where {\n\n  ?item wdt:P17 wd:Q213 .\n  ?item p:P1436 ?statement. \n    ?statement ps:P1436 ?size ; \n               pq:P585 ?date filter(year(?date)= 2021 ) \n    minus {?statement pq:P518 [] .}\n  optional {?item p:P1436 [ps:P1436 ?size_online ; pq:P518 wd:Q106474029 ; pq:P585 ?date ] .}\n  bind ((100 * ?size_online / ?size) as ?percentage_online)\n  BIND(if \n    (?percentage_online < 10, \"FF0000\", \n      if (?percentage_online < 20, \"E21C00\",\n        if (?percentage_online < 30,\"C63800\", \n          if (?percentage_online < 40, \"AA5500\", \n            if (?percentage_online < 50, \"8D7100\", \n              if (?percentage_online < 60, \"718D00\", \n\t\t\t    if (?percentage_online < 70, \"55AA00\", \n\t\t\t\t  if (?percentage_online < 80, \"38C600\", \n\t\t\t\t    if (?percentage_online < 90, \"1CE200\", \"00FF00\"))))))))) as ?rgb) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,cs\". }\n}", "context": "Czech art collections by size", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?event_date WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  BIND(wd:Q60749688 AS ?Montague_Street_Bridge).\n  ?Montague_Street_Bridge p:P793 ?event.\n  ?event ps:P793 wd:Q9687.\n  ?event pq:P585 ?event_date.\n}\nORDER BY ?event_date", "context": "Timeline of road vehicles crashing into Montague Street Bridge, Melbourne, Victoria, Australia", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coords\nWHERE \n{\n  { ?item wdt:P31 wd:Q6558431 } UNION\n  { ?item wdt:P31 wd:Q53951823 }\n  ?item wdt:P17 wd:Q183 .\n  OPTIONAL { ?item wdt:P625 ?coords }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Coal-fired power plants in Germany", "target": ""}
{"query": "#Durée de vie moyenne par profession\n# Select the desired columns and get labels.\nSELECT ?occ ?occLabel ?avgAge ?avgBirthYear ?count\nWHERE\n{\n{\n# Group the people by their occupation and calculate age\nSELECT\n?occ\n(count(?p) as ?count)\n(round(avg(?birthYear)) as ?avgBirthYear)\n(avg(?deathYear - ?birthYear) as ?avgAge)\nWHERE {\n{\n# Get people with occupation + birth/death dates; combine multiple birth/death dates using avg\nSELECT\n?p\n?occ\n(avg(year(?birth)) as ?birthYear)\n(avg(year(?death)) as ?deathYear)\nWHERE {\n?p  wdt:P31 wd:Q5 ;\nwdt:P106 ?occ ;\np:P569/psv:P569 [\nwikibase:timePrecision \"9\"^^xsd:integer ; # precision of at least year\nwikibase:timeValue ?birth ;\n] ;\np:P570/psv:P570 [\nwikibase:timePrecision \"9\"^^xsd:integer ; # precision of at least year\nwikibase:timeValue ?death ;\n] .\n}\nGROUP BY ?p ?occ\n}\n}\nGROUP BY ?occ\nLIMIT 200\n}\n\nFILTER (?count > 300) # arbitrary number to weed out values with 'few' observations\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" . }\n}\nORDER BY ASC(?avgAge)", "context": "Average lifespan by profession", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription ?somePositionLabel ?died ?kind WITH {\n  SELECT ?item (MAX(?died_) AS ?died) (SAMPLE(?kind_) AS ?kind) (SAMPLE(?position) AS ?somePosition) {\n    hint:SubQuery hint:optimizer \"None\".\n    # death in battle or killed in action\n    { ?item wdt:P1196 wd:Q18663901. BIND(\"death in battle\"@en AS ?kind_) } UNION\n    { ?item wdt:P509|wdt:P1347 wd:Q210392. BIND(\"killed in action\"@en AS ?kind_) }\n    # suitable item\n    ?item wdt:P31 wd:Q5;\n          wdt:P570 ?died_.\n    # head of state or government\n    {\n      VALUES ?class { wd:Q2285706 wd:Q48352 }\n      ?item wdt:P39 ?position.\n      ?position wdt:P279* ?class.\n    } UNION {\n      ?state wdt:P6 ?item.\n      OPTIONAL { ?state wdt:P1313 ?position_. }\n      BIND(COALESCE(?position_, wd:Q2285706) AS ?position)\n    } UNION {\n      ?state wdt:P35 ?item.\n      OPTIONAL { ?state wdt:P1906 ?position_. }\n      BIND(COALESCE(?position_, wd:Q48352) AS ?position)\n    }\n  }\n  GROUP BY ?item ?died_\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?died)\nLIMIT 100", "context": "Heads of states/governments whose cause of death was a military casualty", "target": ""}
{"query": "#defaultView:ImageGrid\n\nSELECT ?item ?itemLabel ?country ?pic ?cityLabel WHERE {\n  ?item wdt:P31 wd:Q55488;\n    wdt:P17 wd:Q142;\n    wdt:P18 ?pic;\n    wdt:P131 ?city;\n        wdt:P1103 ?nbVoies .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n\n}   ORDER BY desc(?nbVoies)\n", "context": "List of French stations sorted by number of tracks", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?parent ?parentLabel ?virus ?virusLabel ?ncbi WHERE {\n  ?virus wdt:P171+ wd:Q57751738 .\n  ?virus wdt:P171 ?parent .\n  OPTIONAL { ?virus wdt:P685 ?ncbi }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Relationship of the different Corona viruses", "target": ""}
{"query": "#defaultView:Map\nselect ?label ?layer ?x ?coords {\n  ?x wdt:P31 wd:Q811534 ; wdt:P625 ?coords ; wdt:P31 ?species ; wdt:P17 wd:Q219 .\n  ?species wdt:P31 wd:Q16521 . \n     SERVICE wikibase:label { bd:serviceParam wikibase:language \"bg\". \n                                ?x rdfs:label ?label .\n                                ?species rdfs:label ?layer . \n                            }\n}", "context": "Map of about one third of the ancient trees in Bulgaria", "target": ""}
{"query": "select *\nwith {\n  select ?lang (count(*) as ?meaningless) {\n    ?l dct:language ?lang;\n       wikibase:statements \"0\"^^xsd:integer.\n    minus { ?l ontolex:sense [] }\n  } group by ?lang\n} as %meaningless\nwith {\n  select ?lang (count(*) as ?total) {\n    ?l dct:language ?lang.\n  } group by ?lang\n} as %totals\nwhere {\n  include %meaningless.\n  include %totals.\n  filter (?total >= 10).\n  bind(?meaningless/?total*100 as ?pc).\n  ?lang rdfs:label ?en filter(lang(?en) = \"en\").\n} order by desc(?pc) desc(?total) ?en", "context": "Lexemes without statements or senses by language", "target": ""}
{"query": "#WHAT  : list number of albums (Q482994) by language (P407) in descending order\n#WHEN  : 2021:05-01\n#WHO:  : CamelCaseNick\n#WHY   : Ex Música\n#WHERE : https://t.me/exmusica\n\nSELECT (count(?album) as ?albums) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC(?albums) ASC(?P407)\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus\n\n", "context": "List number of albums (Q482994) by language (P407) in descending order", "target": ""}
{"query": "# places in Scotland according to P131, having a single P131\n\nselect ?item ?itemLabel ?coords ?elseLabel (?elseLabel as ?layer)\nwith { select ?item ?else ?coords where {\n   ?item wdt:P131 ?else  .\n  ?else wdt:P31 wd:Q15060255  .\n  ?item wdt:P625 ?coords .\n  } } as %i\nwhere\n{\n  include %i\n \n  filter not exists { ?item wdt:P131 ?else2 .   {?else2 wdt:P31 wd:Q15060255 .} UNION {?else2 wdt:P131* wd:Q21. hint:Prior hint:gearing \"forward\".} UNION {?else2 wdt:P131* wd:Q26. hint:Prior hint:gearing \"forward\".} filter(str(?else2) != str(?else)) } \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                         ?else rdfs:label ?elseLabel.\n                         ?item rdfs:label ?itemLabel .} \n}\n#defaultView:Map\n\n", "context": "Map of Scottish council areas", "target": ""}
{"query": "# Databases with a SPARQL endpoint, sorted by number of ids known by Wikidata\nSELECT ?service ?serviceLabel ?property ?propertyLabel ?endpoint (COUNT(?id) AS ?ids_count)  {\n  ?service wdt:P5305 ?endpoint ;\n           wdt:P1687 ?property .\n  ?property wikibase:directClaim ?prop .\n  ?entity ?prop ?id .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?service ?serviceLabel ?property ?propertyLabel ?endpoint\nORDER BY DESC(?ids_count)", "context": "Databases with a SPARQL endpoint, sorted by number of ids known by Wikidata", "target": ""}
{"query": "SELECT ?item ?itemLabel ?fecha  ?age ?itemDescription ?nacimiento \n\n(GROUP_CONCAT(DISTINCT ?nacionalidad_label; SEPARATOR = \", \") AS ?nacionalidades) \n\nWHERE {\n  ?item wdt:P570 ?fecha.\n  FILTER((?fecha >= \"2020-01-01T00:00:00Z\"^^xsd:dateTime) && (?fecha <= \"2020-12-31T00:00:00Z\"^^xsd:dateTime))\n  ?item wdt:P31 wd:Q5;\n    wdt:P106 wd:Q33999.\n  OPTIONAL {\n    ?item wdt:P569 ?nacimiento.\n    BIND((2020  - (YEAR(?nacimiento))) + 1  AS ?age)\n  }\n\n  OPTIONAL {\n    ?item wdt:P27 ?nacionalidad.\n    ?nacionalidad rdfs:label ?nacionalidad_label.\n    FILTER((LANG(?nacionalidad_label)) = \"es\")\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en,fr\". }\n}\nGROUP BY ?item ?itemLabel ?fecha  ?age ?itemDescription ?nacimiento \n", "context": "List of actors and actresses who died during 2020", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?itemDescription ?siegeLabel ?secteurLabel ?search WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q6881511;\n    wdt:P17 wd:Q142;\n    rdfs:label ?itemLabel;\n    wikibase:sitelinks ?sitelinks.\n  FILTER((LANG(?itemLabel)) = \"fr\")\n  OPTIONAL { ?item wdt:P159 ?siege. }\n  OPTIONAL { ?item wdt:P452 ?secteur. }\n  MINUS { ?item wdt:P576 _:b10. }\n  MINUS { ?item wdt:P1616 _:b11. }\n  BIND(URI(CONCAT(\" https://annuaire-entreprises.data.gouv.fr/rechercher?terme=\", ENCODE_FOR_URI(?itemLabel))) AS ?search)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}\nORDER BY DESC (?sitelinks)", "context": "List of French firms without SIREN id (P1616) ordered by number of sitelinks with search links in annuaire-entreprises‧data‧gouv.fr", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?year ?number_of_works ?author_label WHERE {\n  {\n    SELECT ?year ?number_of_works ?author ?author_label_\n    WHERE {\n      {\n        SELECT ?author ?year (COUNT(DISTINCT ?work) AS ?number_of_works) WHERE {\n          hint:Query hint:optimizer \"None\".\n          ?author (wdt:P184 | ^wdt:P185)+ wd:Q20984804 .\n          ?work wdt:P50 ?author .\n          ?work wdt:P577 ?publication_date .\n          BIND(STR(YEAR(?publication_date)) AS ?year)\n        }\n        GROUP BY ?author ?year \n      } \n      ?author rdfs:label ?author_label_ .\n      FILTER (LANG(?author_label_) = 'en')\n    }\n  }\n  # Represent the author by name and Q identifier\n  BIND (CONCAT(?author_label_, \" (\", SUBSTR(STR(?author), 32), \")\") AS ?author_label)\n}\nORDER BY ?year", "context": "Number of works per year by researchers supervised by Lars Kai Hansen", "target": ""}
{"query": "# List of Nigerians on Wikipedia\nSELECT DISTINCT ?item ?itemLabel ?genderLabel ?linkcount WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P21 ?gender;\n    wdt:P27 wd:Q1033;\n    wikibase:sitelinks ?linkcount.\n  FILTER(?linkcount > 0 )\n  OPTIONAL { ?item wdt:P734 ?lastname. }\n  OPTIONAL { ?item wdt:P569 ?birthdate. }\n  OPTIONAL { ?item wdt:P570 ?deathdate. }\n  OPTIONAL { ?item wdt:P27 ?nationality. }\n  OPTIONAL { ?item wdt:P106 ?occupation. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\".\n                         ?item rdfs:label ?itemLabel.\n                         ?gender rdfs:label ?genderLabel.\n                         }\n}\nORDER BY (?itemLabel)\n\n", "context": "Count of sitelinks of Nigerians on Wikidata", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?mayorImage ?mayor ?mayorLabel ?city ?cityLabel ?population\n{\n  ?city (wdt:P31/(wdt:P279*)) wd:Q515;\n    p:P6 ?statement.\n  ?statement ps:P6 ?mayor.\n  ?mayor wdt:P21 wd:Q6581072.\n  FILTER(NOT EXISTS { ?statement pq:P582 ?x. })\n  ?mayor wdt:P18 ?mayorImage.\n  ?city wdt:P1082 ?population.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?population)\nLIMIT 30", "context": "30 largest cities with female mayor", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coordinate WHERE {\n  wd:Q93286 wdt:P625 ?Center.\n  ?item wdt:P31 wd:Q921099 .  # ist ein Steinkreuz\n  SERVICE wikibase:around {\n    ?item wdt:P625 ?coordinate.\n    bd:serviceParam wikibase:center ?Center.\n    bd:serviceParam wikibase:radius \"25\". # 25 Kilometer around\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"hsb,de,[AUTO_LANGUAGE]\". }\n  #FILTER(EXISTS { ?place (wdt:P31/wdt:P279*) wd:Q1248784. })\n}", "context": "Recent locations of boundary stones in Upper Lusatia (see pictures by clicking on the dots)", "target": ""}
{"query": "# common (non-internal Wikimedia) types of item with no identifiers\n# added by User:99of9, 2019-05-14\nSELECT ?type ?typeLabel (COUNT(*) AS ?count) (SAMPLE(?link) AS ?link)\n{\n  {\n       SELECT *\n       {\n         #hint:Query hint:optimizer \"None\".\n         ?item wikibase:identifiers 0 .\n         ?item wdt:P17 wd:Q408 .\n         ?item wdt:P31 ?type .\n         #MINUS {?type wdt:P279 wd:Q17442446 .} I can't get this to work in time\n       }\n       LIMIT 200000\n  }\n  BIND (URI(CONCAT(\"https://query.wikidata.org/#SELECT%20%3Fitem%20%3FitemLabel%0A%20%7B%0A%20%20%20%20%3Fitem%20wikibase%3Aidentifiers%200%20.%0A%20%20%20%20%3Fitem%20wdt%3AP17%20wd%3AQ408%20.%0A%20%20%20%20%3Fitem%20wdt%3AP31%20wd%3A\",SUBSTR(str(?type),32, 250),\"%20.%0A%20%20%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22%5BAUTO_LANGUAGE%5D%2Cen%22.%20%7D%0A%20%7D%0A\")) as ?link)  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?type ?typeLabel\nORDER BY DESC(?count)", "context": "Types of items in Australia that don't have identifier properties", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (count(DISTINCT ?species ) as ?count) ?status ?statusLabel{\n  VALUES ?status {wd:Q11394 wd:Q219127 wd:Q237350 wd:Q239509 wd:Q278113 wd:Q719675}\n  ?species wdt:P141 ?status.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP by ?status ?statusLabel", "context": "Number of species belonging to the different IUCN conservation categories", "target": ""}
{"query": "SELECT DISTINCT ?lang ?tongue WHERE {\n  hint:Query hint:optimizer \"None\".\n  ?tongueitem wdt:P424 ?lang .\n  ?tongueitem wdt:P279*/wdt:P31 wd:Q34770 .\n  wd:Q52 rdfs:label ?pedia .\n    FILTER(LANG(?pedia)=?lang)\n  OPTIONAL{wd:Q2013 rdfs:label ?data .\n    FILTER(LANG(?data)=?lang)}\n  OPTIONAL{wd:Q35120 rdfs:label ?entity .\n    FILTER(LANG(?entity)=?lang)}\n  OPTIONAL{wd:Q34770 rdfs:label ?language .\n    FILTER(LANG(?language)=?lang)}\n  OPTIONAL{wd:Q565 rdfs:label ?commons .\n    FILTER(LANG(?commons)=?lang)}\n  OPTIONAL{wd:Q367 rdfs:label ?books .\n    FILTER(LANG(?books)=?lang)}\n  OPTIONAL{wd:Q964 rdfs:label ?news .\n    FILTER(LANG(?news)=?lang)}\n  OPTIONAL{wd:Q369 rdfs:label ?quote .\n    FILTER(LANG(?quote)=?lang)}\n  OPTIONAL{wd:Q263 rdfs:label ?source .\n    FILTER(LANG(?source)=?lang)}\n  OPTIONAL{wd:Q370 rdfs:label ?versity .\n    FILTER(LANG(?versity)=?lang)}\n  OPTIONAL{wd:Q373 rdfs:label ?voyage .\n    FILTER(LANG(?voyage)=?lang)}\n  OPTIONAL{wd:Q151 rdfs:label ?tionary .\n    FILTER(LANG(?tionary)=?lang)}\n  OPTIONAL{wd:Q13679 rdfs:label ?species .\n    FILTER(LANG(?species)=?lang)}\n  ?tongueitem rdfs:label ?tongue .\n  FILTER(LANG(?tongue)=?lang)\n}\n", "context": "List of Wikimedia language codes", "target": ""}
{"query": "select distinct ?item ?parentLabel ?itemLabel ?womenLabel ?women\nwhere {\n    ?item wdt:P31 wd:Q16521 .  # Any instance of a taxon.\n    ?item wdt:P105 wd:Q34740 .  #  Taxon rank is genus.\n    ?item wdt:P171 ?parent .\n    ?item wdt:P138 ?women . # Named after\n    ?women wdt:P21 wd:Q6581072 . # Female\n    { ?women wdt:P106/wdt:P279* wd:Q2374149 } UNION #botanist\n    { ?women wdt:P106/wdt:P279* wd:Q2083925 } UNION #botanical collector\n    { ?women wdt:P106/wdt:P279* wd:Q3148760 } . #botanical illustrator\n\n          SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?itemLabel)", "context": "Plant genera that honor women", "target": ""}
{"query": "#poets whose fathers are lawyers\nSELECT * WHERE {\n  ?personA wdt:P106 wd:Q49757.\n  ?personB wdt:P106 wd:Q40348.\n  ?personA wdt:P22 ?personB.\n}", "context": "Poets whose fathers are lawyers", "target": ""}
{"query": "SELECT ?lang1 ?desc ?lang2 WHERE {\n?item wdt:P279 wd:Q2095.\n ?item rdfs:label ?lang1 FILTER(lang(?lang1) = \"en\").\n ?item schema:description ?desc FILTER(lang(?desc) = \"en\").\n ?item rdfs:label ?lang2 FILTER(lang(?lang2) = \"ca\")\n} ORDER BY ?lang1", "context": "Phrase-book for types of food", "target": ""}
{"query": "#Danish idioms\n# Danish idioms\n# So9q 2021-04-11\nSELECT ?l ?lemma  WHERE {\n  ?l dct:language wd:Q9035;\n     wikibase:lemma ?lemma;\n     wikibase:lexicalCategory wd:Q184511.\n}", "context": "Danish idioms", "target": ""}
{"query": "#defaultView:Map\nSELECT\n  ?count ?layer\n  ?organization ?organizationLabel\n  ?example_author ?example_authorLabel\n  ?example_work ?example_workLabel\n  ?geo\nWITH {\n  SELECT\n    (COUNT(*) AS ?count) \n    ?organization\n    (SAMPLE(?author) AS ?example_author)\n    (SAMPLE(?work) AS ?example_work)\n  {\n    ?taxon wdt:P171+ wd:Q1134583 .\n    ?work wdt:P921 ?taxon ;\n          wdt:P577 ?date ;\n          wdt:P50 ?author .\n    ?author (wdt:P108 | wdt:P1416) ?organization . \n    FILTER(YEAR(?date) < 2020)\n  }\n  GROUP BY ?organization\n  LIMIT 100\n} AS %result\nWHERE {\n  INCLUDE %result\n  ?organization wdt:P159? / wdt:P625 ?geo\n  BIND(IF(?count > 10, IF(?count >100, \">100\", \">10\"), \"<=10\") AS ?layer) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of coronavirus research organizations active from before 2020", "target": ""}
{"query": "#title: Scholarly articles about COVID-19 with \"Wikipedia\" in the title\nselect ?paper ?title {\n  ?paper wdt:P31 wd:Q13442814; # is an academic article\n         wdt:P921 wd:Q84263196; # with the main subject COVID-19\n         wdt:P1476 ?title filter contains(lcase(?title), \"wikipedia\").\n} order by ?title", "context": "Scholarly articles about COVID-19 with \"Wikipedia\" in the title", "target": ""}
{"query": "SELECT distinct ?term ?termLabel ?start ?end ?members ?father_was_mp ?mother_was_mp where\n{\n  { SELECT distinct ?term ?start ?end where \n   { ?term wdt:P279 wd:Q16707842 . ?term wdt:P571 ?start; wdt:P576 ?end .\n     filter(year(?start) >= 1900 ) } } \n optional  { SELECT ?term (count(distinct ?item) as ?members) where \n    { ?item p:P39 [ ps:P39 ?term; pq:P768 ?const] . ?term wdt:P279 wd:Q16707842 . } group by ?term } \n optional  { SELECT ?term (count(distinct ?item) as ?father_was_mp) where \n    { ?item p:P39/ps:P39 ?term . ?term wdt:P279 wd:Q16707842 .\n      ?item wdt:P22/p:P39/ps:P39 ?term2 . \n      { ?term2 wdt:P279* wd:Q16707842 } union { ?term2 wdt:P279* wd:Q18015642 }\n    } group by ?term }\n optional  { SELECT ?term (count(distinct ?item) as ?mother_was_mp) where \n    { ?item p:P39/ps:P39 ?term . ?term wdt:P279 wd:Q16707842 .\n      ?item wdt:P25/p:P39/ps:P39 ?term2 . \n      { ?term2 wdt:P279* wd:Q16707842 } union { ?term2 wdt:P279* wd:Q18015642 }\n    } group by ?term }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n} order by ?start", "context": "20th-century UK parliaments with count of father/mother who were MPs", "target": ""}
{"query": "SELECT ?person ?personLabel ?height\nWHERE\n{\n  ?person wdt:P31 wd:Q5 ;\n          # wdt:P39 wd:Q10655178 ;      #riksdagsledamot\n          wdt:P106 wd:Q82955 ;        #politiker\n          wdt:P21 wd:Q6581072 ;       #kvinna\n          # wdt:P27 wd:Q34 ;            #svensk\n          p:P2048/psn:P2048/wikibase:quantityAmount ?height .\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n} ORDER BY DESC(?height)\nLIMIT 100\n\n", "context": "The tallest known female politicians", "target": ""}
{"query": "#aksara dengan tahun kemunculannya\nPREFIX xsd: <http://www.w3.org/2001/XMLSchema#>\nSELECT distinct ?aksara ?aksaraLabel ?tgl ?tahun (sample(?ga) as ?gam) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id,en\". }\n  ?aksara wdt:P31/wdt:P279* wd:Q8192.\n  MINUS { ?aksara wdt:P31/wdt:P279* wd:Q17451.} #bukan rupa huruf\n  MINUS { ?aksara wdt:P31 wd:Q53764738.}\n  MINUS { ?aksara wdt:P31 wd:Q16744570.}\n  MINUS { ?aksara wdt:P31 wd:Q29575627.}\n  MINUS { ?aksara wdt:P31/wdt:P279* wd:Q12681.}\n  OPTIONAL { ?aksara wdt:P571 ?tgl. }\n  OPTIONAL { ?aksara wdt:P18 ?ga. }\n  BIND ( year(?tgl) AS ?th ).\n  BIND ( xsd:integer(?th) - IF(?th<0,IF(STRENDS(STR(?th),\"9\"),1,0),0) AS ?th2). #kurangi satu tahun jika tahun SM karena tampilan tahun SM default bertambah 1\n  BIND ( IF(?th2<0,CONCAT(STR(?th2 * -1),\" SM\"),?th2) AS ?tahun).\n}\nGROUP BY ?aksara ?aksaraLabel ?tgl ?tahun\n#defaultView:Timeline\n#view:Timeline{\"hide\": \"?tgl\"}", "context": "Writing systems/characters and their year of appearance", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?itemDescription (SAMPLE(?img) as ?image)\n{\n  ?item wdt:P9566 wd:Q101029366 .\n  OPTIONAL { ?item wdt:P18 ?img }  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription\nLIMIT 100", "context": "Nocturnal animals based on diel cycle", "target": ""}
{"query": "SELECT DISTINCT ?prijmeni ?prijmeniLabel (count(distinct(?item)) as ?pocet) WITH {\n  \n  SELECT ?prijmeni ?prijmenilabel where {\n    \n    ?prijmeni wdt:P5323 wd:Q81945591 .\n    ?prijmeni wdt:P31 wd:Q101352 .   \n    ?prijmeni rdfs:label ?prijmenilabel filter(lang(?prijmenilabel)=\"cs\") .\n    \n  }\n} AS %prijmeni WITH {\n   SELECT ?prijmeni ?prijmenilabel ?ptaklabel WHERE {\n  \n    ?ptak wdt:P31 wd:Q16521 .\n    ?ptak wdt:P105 wd:Q34740 .\n    ?ptak wdt:P171* wd:Q5113 .\n    ?ptak rdfs:label ?ptaklabel filter(lang(?ptaklabel)=\"cs\" && substr(str(?ptaklabel),1,1) = substr(lcase(str(?ptaklabel)),1,1) ).\n  }\n   }\n  AS %ptaci WITH {\n  SELECT ?prijmeni ?prijmenilabel WHERE {\n    \n  INCLUDE %prijmeni .\n  INCLUDE %ptaci .\n  filter(str(?ptaklabel) = str(lcase(?prijmenilabel))) .\n    \n  } \n} AS %ptaciprijmeni WHERE {\n  \n  INCLUDE %ptaciprijmeni .\n  ?item wdt:P734 ?prijmeni .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs,en\". }\n \n} group by ?prijmeni ?prijmeniLabel ", "context": "People with bird-like surnames in Czech", "target": ""}
{"query": "SELECT ?property ?propertyLabel ?uses ?example_preprint ?example_preprint_title ?example_article ?example_article_title\n  WITH {\n    SELECT ?directProperty (COUNT(?preprint) as ?uses) (SAMPLE(?preprint) as ?example_preprint) (SAMPLE(?article) as ?example_article) WHERE {\n      ?preprint wdt:P31 wd:Q580922 .\n      ?article wdt:P31 wd:Q13442814 .\n      ?preprint ?directProperty ?article .\n      FILTER(STRSTARTS(STR(?directProperty), STR(wdt:)))\n    } GROUP BY ?directProperty\n  } AS %properties\n  WHERE {\n    INCLUDE %properties .\n    ?property wikibase:directClaim ?directProperty .\n    ?example_preprint wdt:P1476 ?example_preprint_title .\n    ?example_article wdt:P1476 ?example_article_title .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  } ORDER BY desc(?uses)\n", "context": "Properties used to link", "target": ""}
{"query": "SELECT distinct ?parliament ?parliamentLabel ?start ?end ?members ?distinct_terms \n(?distinct_terms - ?with_constituencies as ?missing_constituencies)\n(?distinct_terms - ?with_parties as ?missing_parties) where\n{\n  { SELECT distinct ?parliament ?start ?end where \n   { ?parliament wdt:P279 wd:Q16707842 . optional { ?parliament wdt:P571 ?start } optional { ?parliament wdt:P576 ?end } } } \n\n optional  { SELECT ?parliament (count(distinct ?item) as ?members) (count(distinct ?ps) as ?distinct_terms) where \n    { ?item p:P39 ?ps . ?ps ps:P39 ?parliament . ?parliament wdt:P279 wd:Q16707842 . } group by ?parliament } \n\n optional { SELECT ?parliament (count(distinct ?ps) as ?with_constituencies) where \n    { ?item p:P39 ?ps . ?ps ps:P39 ?parliament . ?parliament wdt:P279 wd:Q16707842 . ?ps pq:P768 ?const } group by ?parliament } \n\n optional { SELECT ?parliament (count(distinct ?ps) as ?with_parties) where \n    { ?item p:P39 ?ps . ?ps ps:P39 ?parliament . ?parliament wdt:P279 wd:Q16707842 . ?ps pq:P4100 ?party } group by ?parliament } \n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n} order by ?start", "context": "UK Parliaments since 1801 with number of members, number of distinct member-terms, and count of missing seats/parties.", "target": ""}
{"query": "SELECT ?property ?propertyLabel ?predicate ?value ?valueLabel WHERE { \n  ?property ?predicate ?value . \n  ?property wikibase:directClaim ?predicate .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Properties used to describe themselves", "target": ""}
{"query": "SELECT ?year ?label (SUM(?count) AS ?total) WITH {\n  SELECT ?name ?date (COUNT(DISTINCT ?human) AS ?count) WHERE {\n    ?human wdt:P31 wd:Q5;\n           wdt:P27 wd:Q183;\n           wdt:P569 ?date;\n           wdt:P735 ?name.\n    MINUS { ?human wdt:P570 ?died. }\n  }\n  GROUP BY ?name ?date\n} AS %humans WHERE {\n  INCLUDE %humans.\n  ?name wdt:P1705 ?label.\n  BIND(YEAR(?date) AS ?year)\n}\nGROUP BY ?year ?label", "context": "Most common name in Germany by year of birth", "target": ""}
{"query": "SELECT ?item ?itemLabel ?nat ?natLabel ?pob ?pobLabel ?coor\n{\n    hint:Query hint:optimizer \"None\".\n    ?item wdt:P19 / wdt:P17 wd:Q142 .\n    MINUS { ?item wdt:P27 wd:Q142 } .\n    ?item wdt:P27 ?nat .\n    ?item wdt:P19 ?pob . \n    ?pob wdt:P17 wd:Q142 . OPTIONAL { ?pob wdt:P625 ?coor }\n    ?item wdt:P31 wd:Q5 .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\". }\n}", "context": "Famous (not French) people born in France", "target": ""}
{"query": "#defaultView:Map\nSELECT ?placeLabel ?placeDescription ?image ?coord WHERE {\n  wd:Q64 wdt:P625 ?berlinLoc .\n  SERVICE wikibase:around {\n    ?place wdt:P625 ?location .\n    bd:serviceParam wikibase:center ?berlinLoc .\n    bd:serviceParam wikibase:radius \"10\" .\n  } .\n  FILTER EXISTS { ?place wdt:P31/wdt:P279* wd:Q23397 }\n  OPTIONAL { ?place wdt:P18 ?image.}\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n  }\n}", "context": "Lakes within 10 km of Berlin", "target": ""}
{"query": "#defaultView:Map\n     SELECT DISTINCT ?a ?coord ?authorityLabel ?official ?language WHERE {\n        { ?a p:P1448/prov:wasDerivedFrom/pr:P248 wd:Q107011559 }\n       \n?a wdt:P625 ?coord .\n       ?a wdt:P1448 ?official .\n              BIND( LANG(?official) AS ?language). \nOPTIONAL  { ?a wdt:P131 ?authority .}\n\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"cy\". }\n}\nORDER BY ?aLabel ", "context": "Map of the standard English and Welsh spellings of all places in the database", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?year WHERE {\n  ?item wdt:P57 wd:Q229990;\n    wdt:P577 ?date.\n  BIND(YEAR(?date) AS ?year)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en,fr\". }\n}\nORDER BY (?year)", "context": "Films directed by Agnès Varda and their publication date", "target": ""}
{"query": "SELECT DISTINCT  ?item ?itemLabel ?rec ?recLabel {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q3918;\n    p:P1075 ?prop.\n  ?prop ps:P1075 ?rec .\n  ?rec wdt:P21 wd:Q6581072 .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n}\n", "context": "List of women who hold or have held the position of rector at a university", "target": ""}
{"query": "#defaultView:Map\n\nSELECT ?public_bookcaseLabel ?coordinate_location ?image WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?public_bookcase wdt:P31 wd:Q294297.\n  OPTIONAL { ?public_bookcase wdt:P625 ?coordinate_location. }\n  OPTIONAL { ?public_bookcase wdt:P18 ?image. }\n}", "context": "Map of public bookcases", "target": ""}
{"query": "SELECT DISTINCT ?wgwID ?item ?itemLabel (GROUP_CONCAT(?inLang; SEPARATOR = \"; \") AS ?inWPLangVersions) WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P7842 ?wgwID.\n  ?wikiPage schema:about ?item;\n    schema:isPartOf ?wikiUrl;\n    schema:inLanguage ?inLang;\n    schema:name ?pageTitle.\n  FILTER(CONTAINS(STR(?wikiUrl), \"wikipedia.org\"))\n  MINUS { ?wikiPage schema:isPartOf <https://de.wikipedia.org/>. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?wgwID ?item ?itemLabel\nORDER BY (?itemLabel)", "context": "Biographies that can be linked in different Wikipedias (apart from deWP)", "target": ""}
{"query": "SELECT ?language ?languageLabel ?statusLabel ?lexemes WITH {\n  SELECT ?language ?status (COUNT(?lexeme) AS ?lexemes) WHERE {\n    VALUES ?status {\n      wd:Q20672087 # 2 vulnerable\n      wd:Q20672088 # 3 definitely endangered\n      wd:Q20672089 # 4 severely endangered\n      wd:Q20672090 # 5 critically endangered\n      wd:Q20672091 # 6 extinct\n    }\n    ?language wdt:P1999 ?status.\n    OPTIONAL { ?lexeme dct:language ?language. }\n  }\n  GROUP BY ?language ?status\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?lexemes) DESC(?statusLabel)", "context": "Vulnerable, endangered or extinct languages with number of lexemes in Wikidata", "target": ""}
{"query": "PREFIX schema: <http://schema.org/>\nSELECT DISTINCT ?item ?itemLabel WHERE {\n  ?featuredArticle schema:about ?item.\n  ?featuredArticle schema:inLanguage \"en\".\n  ?featuredArticle wikibase:badge ?insignia.\n  ?item wdt:P31 wd:Q5 ;\n   wdt:P21 wd:Q6581072 .\n  VALUES (?insignia) {\n    (wd:Q17437796)\n    (wd:Q17437798)\n  }\n  OPTIONAL {\n    ?esp schema:about ?item.\n    ?esp schema:inLanguage \"es\".\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es,en\". }\n  FILTER(!BOUND(?esp))\n}\nORDER BY ?itemLabel", "context": "Articles in English Wikipedia about women that have the good article badge but do not have an article written in Spanish", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?_image ?plaque_fr ?plaque_als WHERE {\n  ?item wdt:P912 wd:Q104123038;\n        wdt:P131 wd:Q6602; \n        wdt:P1766 ?_image;\n        wdt:P1448 ?plaque_fr;\n        wdt:P1448 ?plaque_als. \n filter (lang(?plaque_fr) = \"fr\"). \n filter (lang(?plaque_als) = \"gsw-fr\") \n}", "context": "Street names in Alsatian", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT DISTINCT ?type ?typeLabel (COUNT(DISTINCT ?item) AS ?count)\nWHERE \n{\n  {?item wdt:P131 wd:Q25 }\nUNION { ?item wdt:P131 ?place . ?place wdt:P131* wd:Q25 }\n  ?item wdt:P912 ?loo .\n  ?loo wdt:P31 ?Q813966 .\n  ?item wdt:P31 ?type .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cy\". }\n}\nGROUP BY ?type ?typeLabel\nORDER BY DESC(?count) ?typeLabel", "context": "What kind of places in Wales host public toilets?", "target": ""}
{"query": "#title: UK Health Ministers and their occupations on Wikidata\nSELECT ?x ?xLabel ?start (GROUP_CONCAT(?occLabel ;separator=\"; \") as ?occupations)  WITH {\n  \nSELECT DISTINCT ?x (SAMPLE(?start) AS ?start) WHERE {  # Using sample as ministers may have held the position multiple times but we're only interested in one\n  ?x wdt:P31 wd:Q5 .\n  ?x p:P39 ?node .\n  ?node ps:P39 ?position ;\n        pq:P580 ?start .\n  ?position wdt:P361 wd:Q112014 .   # member of the UK Cabinet\n  ?position wdt:P279 wd:Q3273722 .  # health minister\n} GROUP BY ?x } AS %health_ministers\nWHERE {\n  INCLUDE %health_ministers\n  OPTIONAL {?x wdt:P106 ?occ . ?occ rdfs:label ?occLabel.   FILTER (LANG(?occLabel) = \"en\") }\n  BIND(COALESCE(?occLabel, \"None\") AS ?occLabel )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?x ?xLabel ?start\nORDER BY DESC (?start)\n          ", "context": "UK Health Ministers and their occupations on Wikidata", "target": ""}
{"query": "#title:count of statements by property for theatrical plays\nSELECT ?property ?propertyLabel ?count WITH {\n  SELECT ?property (count(?property) as ?count)\nWHERE \n{\n  ?item wdt:P31 wd:Q25379.\n  hint:Prior hint:runFirst true.\n  ?item ?predicate [].\n  ?property wikibase:directClaim ?predicate .\n} group by ?property ?propertyLabel } as %i\nWHERE\n{\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?count)", "context": "Count of statements by property for theatrical plays", "target": ""}
{"query": "#defaultView:Map\nSELECT ?Joan_Bataiatzailea ?Joan_BataiatzaileaLabel ?koordenatuak WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Joan_Bataiatzailea wdt:P825 wd:Q40662.\n  ?Joan_Bataiatzailea wdt:P625 ?koordenatuak.\n}", "context": "Things dedicated to St. John the Baptist", "target": ""}
{"query": "select ?i ?lemma ?langLabel ?sense1 ?sense2 ?gloss {\n  ?i dct:language ?lang ; wikibase:lemma ?lemma ; ontolex:sense ?sense1, ?sense2 .\n  ?gloss ^skos:definition ?sense1, ?sense2 .\n  FILTER(STR(?sense1) < STR(?sense2))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "List of pairs of senses on the same lexeme that have the same gloss in at least one language", "target": ""}
{"query": "#Tableaux peignant des instruments à vent en bois\n# added in 2017-06\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?object ?objectLabel ?image\nWHERE\n{\n  ?item wdt:P31/wdt:P279* wd:Q3305213 .  \n  ?item wdt:P180 ?object .\n  ?object wdt:P279* wd:Q3305213 . \n  ?item wdt:P18 ?image .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}", "context": "All paintings (Q3305213) that depict (P180) a painting", "target": ""}
{"query": "#title:count of qualifiers used in P175 statements\nSELECT ?property ?propertyLabel ?count WITH {\n  SELECT ?property (count(?property) as ?count)\nWHERE \n{\n  ?item p:P175 ?stat . \n  hint:Prior hint:runFirst true.\n?stat ?predicate [].\n  ?property wikibase:qualifier ?predicate .\n} group by ?property ?propertyLabel } as %i\nWHERE\n{\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?count)", "context": "Count of qualifiers used in performer (P175) statements", "target": ""}
{"query": "# Animals which are Threatened as per IUCN Red List \n# https://en.wikipedia.org/wiki/Conservation_status\n# Common names are in English only\n\nSELECT ?animal ?scientific_names ?common_names ?statusLabel where\n{\n  # hint:Prior hint:runLast true\n   {\n      SELECT DISTINCT ?animal (GROUP_CONCAT(distinct ?scientific_name; separator=\", \") as ?scientific_names)  (GROUP_CONCAT(distinct ?common_name; separator=\", \") as ?common_names) WHERE \n      {\n \n        ?animal wdt:P141 ?status;\n                wdt:P225 ?scientific_name;\n                wdt:P1843 ?common_name.\n        filter( ?status\n           IN (\n               wd:Q11394,  #Endangered\n               wd:Q219127, #critcally endangered\n               wd:Q278113  #vulnurable\n              ) \n          ).\n  \n        # Only return common names in English\n        FILTER(LANGMATCHES(LANG(?common_name), \"en\")) \n      }\n      group by ?animal\n   }.\n   ?animal wdt:P141 ?status.\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Scientific and Commons names of animals which are threatened as per IUCN Red List", "target": ""}
{"query": "SELECT ?gov ?govLabel ?religionLabel ?stateLabel \nWHERE \n{\n  ?office wdt:P279* wd:Q889821.\n  ?gov p:P39 ?instanceOf .\n  ?instanceOf ps:P39 ?office .\n  ?instanceOf pq:P580 ?startTime .\n  ?office wdt:P1001 ?state .\n  minus { ?instanceOf pq:P582 ?endTime. }\n  optional { ?gov wdt:P140 ?religion . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\norder by ?stateLabel ", "context": "US Governors and their religion, if listed", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription (SAMPLE(?dob) AS ?dob) (SAMPLE(?countryForSport) AS ?countryForSport)\nWHERE {\n  # occupation association football player\n  ?item wdt:P106 wd:Q937857 .\n  ?item wdt:P569 ?dob .\n  FILTER (DAY(?dob) = 1 && MONTH(?dob) = 1)\n  # not dead\n  MINUS { ?item wdt:P570 [] }\n  OPTIONAL { ?item wdt:P1532 ?countryForSport }\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\"}\n} GROUP BY ?item ?itemLabel ?itemDescription\nORDER BY DESC(?dob)", "context": "Living soccer players whose birthday is on January 1st", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coordinate ?bild WHERE {\n  wd:Q107582 wdt:P625 ?Center.\n  SERVICE wikibase:around {\n    ?item wdt:P625 ?coordinate.\n    bd:serviceParam wikibase:center ?Center;\n      wikibase:radius \"30\".\n  }\n  FILTER(EXISTS { ?item (wdt:P31/(wdt:P279*)) wd:Q33506. })\n  OPTIONAL { ?item wdt:P18 ?bild. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }\n}", "context": "Museums nearby Cēsis (Q107582)", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT DISTINCT ?place ?placeLabel (COUNT(DISTINCT ?item) AS ?count)\nWHERE \n{\n  {?item wdt:P131 wd:Q25 }\nUNION { ?item wdt:P131 ?place . ?place wdt:P131* wd:Q25 }\n  ?item wdt:P912 ?loo .\n  ?loo wdt:P31 ?Q813966 .\n  ?item wdt:P31 ?type .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?place ?placeLabel\nORDER BY DESC(?count) ?placeLabel", "context": "Distribution of public toilets per administrative units in Wales", "target": ""}
{"query": "#title: subclasses of Q732577 'publication' found by the Gather, Apply & Scatter service \nSELECT ?subclass ?subclassLabel ?class ?classLabel ?depth WHERE\n {\n  SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q732577 ; gas:linkType wdt:P279 ; gas:traversalDirection \"Reverse\"; gas:out ?subclass ; gas:out1 ?depth ; gas:out2 ?class . \n  }\n\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} order by ?depth", "context": "Subclasses of Q732577 'publication' found by the Gather, Apply & Scatter service", "target": ""}
{"query": "#title:Places more or less on Papa Stour\n#defaultView:Map\nSELECT ?place ?placeLabel ?location (GROUP_CONCAT(?typeLabel_;separator=\"; \") as ?typeLabel) WHERE {\n  hint:Query hint:optimizer \"None\".\n  VALUES ?island {wd:Q1552121}      \n  ?island wdt:P5140 ?point . \n  SERVICE wikibase:around { \n      ?place wdt:P625 ?location . \n      bd:serviceParam wikibase:center ?point . \n      bd:serviceParam wikibase:radius \"10\" . \n  } \n  ?place p:P625/psv:P625/wikibase:geoLatitude ?lat .\n  ?place p:P625/psv:P625/wikibase:geoLongitude ?long . \n  ?island p:P1332/psv:P1332/wikibase:geoLatitude ?north .\n  ?island p:P1333/psv:P1333/wikibase:geoLatitude ?south .\n  ?island p:P1334/psv:P1334/wikibase:geoLongitude ?east .\n  ?island p:P1335/psv:P1335/wikibase:geoLongitude ?west .\n  FILTER ( ?lat <= ?north )\n  FILTER ( ?lat >= ?south )\n  FILTER ( ?long <= ?east )\n  FILTER ( ?long >= ?west )\n  OPTIONAL { ?place wdt:P31 ?type . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" .\n                         ?place rdfs:label ?placeLabel . \n                         ?type rdfs:label ?typeLabel_ .}\n} group by ?place ?placeLabel ?location", "context": "Places more or less on Papa Stour", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord\"], \"layer\": \"?adminLabel\"}\nSELECT ?id  ?item ?itemLabel ?itemDescription ?img ?coord ?www ?adminLabel WHERE {\n  ?item wdt:P6104 wd:Q107186275.\n\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en,sv,de\" . \n  }\n  \nOPTIONAL { ?item wdt:P18 ?img. }\nOPTIONAL { ?item wdt:P131 ?admin. }\nOPTIONAL { ?item wdt:P856 ?www. }\nOPTIONAL { ?item wdt:P625 ?coord. }\nOPTIONAL { ?item wdt:P217 ?id. }\n\n} order by ?adminLabel\n", "context": "Map of Sweden's outdoor gyms", "target": ""}
{"query": "#title: Justice that cites reports by commissions they self took part in\nSELECT ?Justice ?Case ?Rapporteur ?SOU ?Report ?Commission ?Role WHERE {\n  ?justice wdt:P39 wd:Q96336792.\n  ?commission p:P710 [ ps:P710 ?justice; pq:P3831 ?role ];\n             wdt:P800 ?report.\n  ?report wdt:P1031 ?SOU.\n  ?case wdt:P1594 ?justice;\n       wdt:P2860 ?report.\n  OPTIONAL {\n    ?case p:P1594 ?statement.\n    ?statement ps:P1594 ?justice; pq:P3831 wd:Q96971913.\n  }\n  BIND ( IF ( BOUND (?statement) , 'yes', 'no' ) AS ?Rapporteur )\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en,sv\".\n    ?justice rdfs:label ?Justice.\n    ?case rdfs:label ?Case.\n    ?role rdfs:label ?Role.\n    ?commission rdfs:label ?Commission.\n    ?report rdfs:label ?Report.\n  }\n}\nORDER BY ?justice ?report", "context": "Swedish Supreme Court judges who citing reports by commissions they themselves took part in", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?rgb\", \"?coord\", \"?layer\"]}\n\nSELECT ?org ?orgLabel ?url (SAMPLE(?coord) as ?coord) ?layer WHERE {\n  ?org wdt:P9732 ?url .\n  ?org wdt:P17 ?layer .\n  ?org wdt:P276?/wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?org ?orgLabel ?layer ?url", "context": "Map public organizations that allow one to fill citizen's initiatives online", "target": ""}
{"query": "#unemployment rate Sweden\n#defaultView:LineChart\nSELECT ?year ?unemployment {\n  wd:Q34 p:P1198 ?p .\n  ?p pq:P585 ?year ;\n     ps:P1198 ?unemployment .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY ?year", "context": "Unemployment rate in Sweden by year", "target": ""}
{"query": "SELECT ?accident ?accidentLabel ?operatorLabel ?deaths ?survivors ?date WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?accident wdt:P31 wd:Q744913;\n    wdt:P1120 ?deaths;\n    wdt:P1561 ?survivors;\n    wdt:P137 ?operator.\n  FILTER(?deaths > 50 )\n  Filter(?survivors = 1)\n  OPTIONAL { ?accident wdt:P585 ?date. }\n  OPTIONAL { ?accident wdt:P828 ?cause. }\n  OPTIONAL { ?accident wdt:P276 ?Ort. }\n}\nORDER BY DESC (?date)", "context": "Serious plane crashes with only one survivor", "target": ""}
{"query": "SELECT ?city ?cityLabel ?mayor ?mayorLabel ?genderLabel\nWHERE \n{\n  ?city wdt:P31/wdt:P279* wd:Q7930989.\n  ?city wdt:P17 wd:Q30.\n  ?city wdt:P1082 ?pop.\n  ?city wdt:P6 ?mayor.\n  ?mayor wdt:P21 ?gender.\n  FILTER ( ?pop > 100000 )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?city ?cityLabel ?mayor ?mayorLabel ?genderLabel", "context": "Mayors' genders of U.S. cities above 100k population", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?pronoun ?pronounLabel (count(?person) as ?count)\n{\n  ?person wdt:P6553 ?pronoun;\n          wdt:P31 wd:Q5.\n  ?pronoun wikibase:lemma ?pronounLabel.\n}\nGROUP by ?pronoun ?pronounLabel\nORDER by DESC(?count)", "context": "Global usage of pronouns", "target": ""}
{"query": "SELECT ?p ?ct (COUNT(?i) as ?jumlah) WHERE {\n  OPTIONAL { SERVICE wikibase:label { bd:serviceParam wikibase:language \"id\".\n                                     ?p rdfs:label ?namap.} }\n  ?i wdt:P31 wd:Q5;\n     wdt:P570 ?dod.\n  FILTER(YEAR(?dod)>2018)\n  ?i wdt:P509 ?p.\n  BIND(YEAR(?dod) as ?t)\n  BIND(CONCAT(?namap, \" \", STR(?t)) as ?ct)\n  }\nGROUP BY ?p ?ct\n#defaultView:BubbleChart", "context": "Number of causes of death (P509) 2019-2021 separated by year so that a comparison of the causes of death before and during the pandemic can also be seen", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel \nWHERE \n{\n  ?item wdt:P31 wd:Q484170 .\n  ?item wdt:P1448 ?name . FILTER(LANG(?name) = \"fr\")\n  FILTER REGEX(str(?name), \"ville\") .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} LIMIT 10000", "context": "French municipalities containing the word \"ville\"", "target": ""}
{"query": "#title:Most frequently occuring titles found on audio tracks\n#credit: Mahir256 on Telegram\n\nselect ?label (LANG(?label) as ?lang) (count(?i) as ?is) {\n  ?i wdt:P31/wdt:P279* wd:Q7302866; wdt:P1476 ?label .\n}\ngroup by ?label ?lang\nhaving(?is > 5)\norder by desc(?is) ?lang", "context": "Most frequently occurring titles found on audio tracks", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coordinate ?bild WHERE {\n  wd:Q1731 wdt:P625 ?Center.                          # Lage von Dresden\n  SERVICE wikibase:around {\n    ?item wdt:P625 ?coordinate.\n    bd:serviceParam wikibase:center ?Center.\n    bd:serviceParam wikibase:radius \"70\".                 # 70 Kilometer around\n  }\n  FILTER(EXISTS { ?item (wdt:P31/wdt:P279*) wd:Q33506. })  # nur Museen\n  optional { ?item wdt:P18 ?bild }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }\n}", "context": "Museums within 70 km of Dresden", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coord \n(SAMPLE(?type_label) as ?typeLabel) \nWHERE \n{\n \n  ?item wdt:P31/wdt:P279* wd:Q337567 .\n\n ?item wdt:P7959/wdt:P31 wd:Q1857731 .\n  ?item wdt:P625 ?coord .\n  \n  { ?item wdt:P31 ?type  .\n             ?type rdfs:label ?type_label . #manual way of getting label\n            FILTER (lang(?type_label) = \"cy\") #manual way of getting label  \n            }\n\n\n\n \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cy\". }\n}\ngroup by ?item ?itemLabel ?coord ", "context": "Map of Welsh lakes and reservoirs", "target": ""}
{"query": "#title: Number of featured articles by gender in fr.wikipedia.org \n# query written by https://m.wikidata.org/wiki/User:PAC2\nSELECT ?gender ?genderLabel (COUNT(*) AS ?count) WHERE {\nSERVICE wikibase:mwapi {\nbd:serviceParam wikibase:endpoint \"fr.wikipedia.org\";\nwikibase:api \"Generator\";\nmwapi:generator \"categorymembers\";\nmwapi:gcmtitle \"Catégorie:Article de qualité\";\nmwapi:gcmprop \"title\".\n?item wikibase:apiOutputItem mwapi:item.\n?title wikibase:apiOutput mwapi:title .\n?namespace wikibase:apiOutput \"@ns\" .\n}\nFILTER (?namespace = \"0\")\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n ?item wdt:P31 wd:Q5; \n wdt:P21 ?gender. \n}\nGROUP BY ?gender ?genderLabel", "context": "Gender statistics about featured articles in fr.wikipedia.org", "target": ""}
{"query": "#title: Number of featured articles by \"instance of\" in fr.wikipedia.org \n# query written by https://m.wikidata.org/wiki/User:PAC2\nSELECT ?instance ?instanceLabel (COUNT(*) AS ?count) WHERE {\nSERVICE wikibase:mwapi {\nbd:serviceParam wikibase:endpoint \"fr.wikipedia.org\";\nwikibase:api \"Generator\";\nmwapi:generator \"categorymembers\";\nmwapi:gcmtitle \"Catégorie:Article de qualité\";\nmwapi:gcmprop \"title\".\n?item wikibase:apiOutputItem mwapi:item.\n?title wikibase:apiOutput mwapi:title .\n?namespace wikibase:apiOutput \"@ns\" .\n}\nFILTER (?namespace = \"0\")\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n ?item wdt:P31 ?instance . \n\n}\nGROUP BY ?instance ?instanceLabel \nORDER BY DESC(?count ) \n", "context": "Statistics  about featured articles in fr.wikipedia.org by their value of instance of property", "target": ""}
{"query": "#defaultView:Map\nSELECT ?person ?personLabel ?personDescription ?birth ?sportLabel ?SPA ?coord ?coord2 ?coord3 ?img WHERE {\n  ?person wdt:P1344 wd:Q8118.\n  ?person wdt:P27 wd:Q34.\n  ?person wdt:P569 ?birth.\n  OPTIONAL {?person wdt:P2416 ?sport}\n  OPTIONAL { ?person wdt:P4819 ?SPAID}               \t\t# Svenskt Porträttarkiv \n   BIND (URI(CONCAT(\"https://xn--portrttarkiv-kcb.se/details/\",?SPAID)) AS ?SPA)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv\",\"en\" .   }\n  OPTIONAL { ?person wdt:P18 ?img. }\n\n   OPTIONAL {  ?person wdt:P19 ?loc . \t\t\t# P19 Place of Birth\n    ?loc wdt:P625 ?coord } .    \t\t\t\t# Get coordinates from Place of Birth\n   OPTIONAL {  ?person wdt:P20 ?loc2 . \t\t\t# P19 Place of Death\n    ?loc2 wdt:P625 ?coord2 } .    \t\t\t\t# Get coordinates from Place of Death\n   OPTIONAL {  ?person wdt:P119 ?loc3 . \t\t\t# P19 Place of Burial\n    ?loc3 wdt:P625 ?coord3 } .    \t\t\t\t# Get coordinates from Place of Burial\n\n}\norder by ?personLabel ", "context": "Place of birth of Swedish 1912 Summer Olympics participants", "target": ""}
{"query": "#title: List of properties which have instances or subclass of organisations as possible value \nSELECT ?property ?propertyLabel ?relation ?relationLabel WHERE {\n  ?property p:P2302 ?statement.\n  ?statement ps:P2302 wd:Q21510865;\n    pq:P2308 wd:Q43229;\n    pq:P2309 ?relation. \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "List of properties which have instances or subclass of organisations as possible value", "target": ""}
{"query": "#title: List of classes which are used as value constraints for the properties \nSELECT ?class ?classLabel (COUNT(*) AS ?count) WHERE {\n  ?property p:P2302 ?statement.\n  ?statement ps:P2302 wd:Q21510865;\n    pq:P2308 ?class ;\n    pq:P2309 [] . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?class ?classLabel\nORDER BY DESC(?count)", "context": "List of classes which are used as value constraints for the properties", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT  ?item ?itemLabel ?pic ?linkTo ?linkToLabel # ?instance ?instanceLabel ?subClass ?subClassLabel\nWHERE\n{\n  wd:Q18340964 ^wdt:P279* ?item .\n  ?item ^wdt:P279 ?linkTo .\n  ?linkTo wdt:P31 ?instance.\n  ?linkTo wdt:P279* ?subClass.\n  OPTIONAL { ?item wdt:P18 ?pic }\n  MINUS {?linkTo wdt:P17 | wdt:P1001 ?jurisdiction}\n  MINUS {?item wdt:P17 | wdt:P1001 ?jurisdiction}\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"en\" }\n  FILTER (?instance NOT IN (wd:Q28640, wd:Q14204246, wd:Q28057030, wd:Q7810129, wd:Q11488158, wd:Q1900326, wd:Q4164871, wd:Q4671286, wd:Q241317,wd:Q1665984, wd:Q11862829, wd:Q11773926, wd:Q294414, wd:Q815823, wd:Q159535, wd:Q66439731, wd:Q2465832, wd:Q5157582,wd:Q1093434, wd:Q1789452, wd:Q6055843, wd:Q2424752, wd:Q104822033, wd:Q1220872, wd:Q336, wd:Q12737077, wd:Q1714118,wd:Q13406463 , wd:Q12812139, wd:Q49773))\n  FILTER (?subClass NOT IN (wd:Q30185, wd:Q11902879, wd:Q59512604, wd:Q59240317, wd:Q99766277, wd:Q35749, wd:Q621261, wd:Q35749, wd:Q239463, wd:Q1153777, wd:Q7603708, wd:Q15284, wd:Q1632136, wd:Q206814, wd:Q206814, wd:Q5185982, wd:Q7930989, wd:Q50592854, wd:Q4671162 , wd:Q275969))\n}", "context": "Concepts, processes, practices, etc. linked to the philosophical concept of power", "target": ""}
{"query": "#title:Twelve Times table using Wikidata items (no numeric literals)\nSELECT ?a $TIMES ?b $EQUALS ?ab WITH {\n\n# equivalent to VALUES ?a { 1 2 3 4 5 6 7 8 9 10 11 12 }\nSELECT ?a WHERE {\n# we could use numeric literals 0, 1, 12 but why do that when we can get the values from Wikidata items! \n  wd:Q199 wdt:P1181 ?one .      # 1\n  wd:Q36977 wdt:P1181 ?twelve . # 12\n  BIND (?one AS ?start )\n  BIND (?twelve AS ?end )\n  BIND ( (?end - ?start ) + ?one AS ?x )\n  [] p:P31 [\n     ps:P31 wd:Q21199 ;\n     pq:P155 ?prev\n            ] .\n  ?prev wdt:P1181 ?a .\n  FILTER (?a >= ?start && ?a <= ?x)\n } ORDER BY ?a \n} AS %a\n\n# equivalent to VALUES ?b { 1 2 3 4 5 6 7 8 9 10 11 12 }\nWITH { SELECT ?b WHERE {\n  INCLUDE %a\n  BIND (?a AS ?b)\n}} AS %b\n# this is where the multiplication happens, pairing every digit in a with every digit in b\nWHERE {\n  INCLUDE %a\n  INCLUDE %b\n  BIND (?a * ?b AS ?ab )\n  BIND (\"TIMES\" AS $TIMES)\n  BIND (\"EQUALS\" AS $EQUALS)\n}\nORDER BY ASC (?b) ASC (?a)", "context": "Twelve Times table using Wikidata items", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?part ?partLabel ?countryLabel ?coords ?image ?code\nWHERE \n{\n  ?item p:P1435 ?statement ;\n        wdt:P757 ?code .\n  ?statement ps:P1435 wd:Q9259 .\n  { \n    ?statement pq:P580 ?date .\n    FILTER (YEAR(?date) = 2021)\n    {\n      ?item wdt:P527 ?part .\n      ?part wdt:P17 ?country .\n      OPTIONAL { ?part wdt:P625 ?coords }\n      OPTIONAL { ?part wdt:P18 ?image }\n    } UNION {\n      FILTER NOT EXISTS { ?item wdt:P527 ?x }\n      ?item wdt:P17 ?country .\n      OPTIONAL { ?item wdt:P625 ?coords }\n      OPTIONAL { ?item wdt:P18 ?image }\n    }\n  } UNION {\n    ?item p:P527 ?partStatement .\n    ?partStatement ps:P527 ?part ;\n                   pq:P580 ?partDate .\n    FILTER (YEAR(?partDate) = 2021)\n    ?part wdt:P17 ?country .\n    OPTIONAL { ?part wdt:P625 ?coords }\n    OPTIONAL { ?part wdt:P18 ?image }\n  }\n  FILTER NOT EXISTS { ?country wdt:P31 wd:Q3024240 }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY lcase(?itemLabel) ?countryLabel lcase(?partLabel)", "context": "Newly protected World Heritage sites in 2021", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?winnerLabel1 ?ballsize ?rgb WHERE {\n  # the snooker ball colours\n   BIND ( COALESCE(\n    IF(?wins = 1, \"ff0000\", 1/0),\n    IF(?wins = 2, \"ffff00\", 1/0),\n    IF(?wins = 3, \"00ff00\", 1/0),\n    IF(?wins = 4, \"8b4513\", 1/0),\n    IF(?wins = 5, \"0000ff\", 1/0),\n    IF(?wins = 6, \"ffc0cb\", 1/0),\n    IF(?wins = 7, \"000000\", 1/0),\n    IF(?wins = 8, \"ffa500\", 1/0),\n    \"800080\"\n  ) AS ?rgb )\n  BIND(1 AS ?ballsize)\n  BIND( CONCAT(?winnerLabel, \": \", STR(?wins)) AS ?winnerLabel1)\n\n  # inner select generates the winners totals\n  { SELECT ?winnerLabel (COUNT (?winner) AS ?wins) WHERE {\n  { ?p wdt:P31 wd:Q502948 }\n  UNION\n  { ?p wdt:P361 wd:Q502948 }\n  ?p wdt:P1346 ?winner .\n  ?winner rdfs:label ?winnerLabel .\n  FILTER(LANG(?winnerLabel) = \"en\")\n#  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?winnerLabel \n  ORDER BY ASC (?wins )}\n}\n", "context": "Total number of wins for each world snooker champion", "target": ""}
{"query": "#title: Who was UK Prime Minister when each of the English Premier League/First Division title winners last won the title\n#defaultView:ImageGrid{\"hide\":[\"?pm_image\" ]}\nSELECT ?date ?winner ?winnerLabel ?pm ?pmLabel ?pm_image WITH {\n\nSELECT DISTINCT ?winner ?d WHERE { # all the winners of the English Premier League or First Division\n  VALUES $ENGLISH_FIRST { wd:Q754839 wd:Q9448 wd:Q213347 }\n  ?x wdt:P3450 $ENGLISH_FIRST .\n  ?x wdt:P1346 ?winner .\n  ?x wdt:P582 ?d \n  MINUS { ?x wdt:P2499 [] } # make sure this is really the Top league\n}} AS %winners \nWITH {\n  SELECT (MAX(?d) AS ?date) ?winner WHERE { # get the latest (MAX) date\n  INCLUDE %winners\n} GROUP BY ?winner} AS %winner_and_date\n\nWHERE { # determine who was the Prime minister on a particular date\n  INCLUDE %winner_and_date\n  ?pm p:P39 ?ph_stmt .\n  OPTIONAL { ?pm wdt:P18 ?pm_image }\n  ?ph_stmt ps:P39 wd:Q14211 .\n  ?ph_stmt pq:P580 ?pm_start .\n  OPTIONAL { ?ph_stmt pq:P582 ?pm_end }\n  BIND(COALESCE(?pm_end, NOW()) AS ?pm_end)\n  FILTER (?pm_start <= ?date && ?pm_end >= ?date)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?date\n", "context": "Who was the UK Prime Minister when each of the English Premier League/First Division title winners last won the title", "target": ""}
{"query": "SELECT ?item ?itemLabel ?statements ?property ?propertyLabel ?predicateCount WITH {\n  SELECT ?statements WHERE {                 # get numbers from 1k to 10k\n  ?nn wdt:P31 wd:Q21199; wdt:P1181 ?statements .\n  FILTER (?statements >= 1000 && ?statements <= 10000)\n} } AS %numbers\nWITH { SELECT ?item ?statements WHERE {     # get items having that many statements; exclude scholarly articles\n  INCLUDE %numbers\n  BIND(xsd:integer(?statements) as ?b)\n  ?item wikibase:statements ?b.\n  FILTER NOT EXISTS {?item wdt:P31 wd:Q13442814 .}\n } } as %items\nWITH { SELECT ?item ?statements ?property (COUNT(?predicate) as ?predicateCount) WHERE {  # get & count claims for the items\n  INCLUDE %items\n  ?item ?predicate [].\n  ?property wikibase:claim ?predicate . \n} GROUP BY ?item ?statements ?property  } as %counts\nWITH { SELECT  ?item (MAX(?predicateCount) AS ?maxcount) {\n  INCLUDE %counts\n} GROUP BY ?item } AS %maxCounts\nWHERE {\n# filter for ?predicateCount = ?maxcount to try to identify most-used predicates per item. (It's a bit of a kludge but...)\n  INCLUDE %counts\n  INCLUDE %maxCounts\n  FILTER (?predicateCount = ?maxcount) \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}  order by desc(?statements)", "context": "Predicates implicated in non-scholarly-article item bloat", "target": ""}
{"query": "#Church of Scotland Synods as Geoshapes\n#defaultView:Map{\"hide\":[\"?layer\"]}\nSELECT ?itemLabel ?shape (?itemLabel as ?layer)\nWHERE \n{\n  ?item wdt:P31 wd:Q51041800 ;\n        wdt:P361 wd:Q922480 ;\n        wdt:P3896 ?shape .\n\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Church of Scotland Synods as Geoshapes", "target": ""}
{"query": "#defaultView:Map{\"layer\": \"?typeLabel\"}\nSELECT ?label ?coord ?subj WHERE {\n\n  ?subj wdt:P31 wd:Q1641020 .\n OPTIONAL {?subj wdt:P291 ?loc .\n   ?loc wdt:P625 ?coord } .   \n  ?subj rdfs:label ?label filter (lang(?label) = \"en\")\n}", "context": "Map of the origin of Balinese palm-leaf manuscripts", "target": ""}
{"query": "#title: All current principal Local Authorities in England\nSELECT ?LALabel ?official_website WHERE {\n\n#find all instances of a non-metropolitan county, district, unitary authority or metropolitan borough council  \n{?LA wdt:P31 wd:Q769603. \n}\nUNION {?LA wdt:P31 wd:Q1187580.\n}\nUNION {?LA wdt:P31 wd:Q1136601.\n}\n UNION {?LA wdt:P31 wd:Q19414242.}\n\n  #get all things these are instances of & then get end date for that (i.e. LA is current)\nOPTIONAL {?LA p:P31 ?type.\n  ?type pq:P582 ?end_date.}\n  #get website \nOPTIONAL { ?LA wdt:P856 ?official_website. }\n  \n  #deduplicate (assumes LA cannot be isntance of both these things at same time)\n  OPTIONAL { ?LA wdt:P31 ?instance_of. }\n  FILTER((((?instance_of = wd:Q1187580) || (?instance_of = wd:Q1136601)) || (?instance_of = wd:Q769603)) || (?instance_of = wd:Q19414242))\n  \n  #create value ?test_date. If instance statement had an end date use it, if not use today\n BIND(IF(BOUND(?end_date),?end_date,NOW()) AS ?test_date).\n \n  #only show LAs with test date today or in future (this means we will show local authorities that exist but are planned to cease in the future)\n FILTER(?test_date >= NOW()) .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }}\nORDER BY (?LALabel)", "context": "Current principal local authorities in England", "target": ""}
{"query": "#title: All current principal Local Authorities in England\nSELECT ?LALabel ?official_website WHERE {\n\n#find all instances of a non-metropolitan county, district, unitary authority or metropolitan borough council  \n{?LA wdt:P31 wd:Q769603. \n}\nUNION {?LA wdt:P31 wd:Q1187580.\n}\nUNION {?LA wdt:P31 wd:Q1136601.\n}\n UNION {?LA wdt:P31 wd:Q19414242.}\n\n  #get all things these are instances of & then get end date for that (i.e. LA is current)\nOPTIONAL {?LA p:P31 ?type.\n  ?type pq:P582 ?end_date.}\n  #get website \nOPTIONAL { ?LA wdt:P856 ?official_website. }\n  \n  #deduplicate (assumes LA cannot be isntance of both these things at same time)\n  OPTIONAL { ?LA wdt:P31 ?instance_of. }\n  FILTER((((?instance_of = wd:Q1187580) || (?instance_of = wd:Q1136601)) || (?instance_of = wd:Q769603)) || (?instance_of = wd:Q19414242))\n  \n  #create value ?test_date. If instance statement had an end date use it, if not use today\n BIND(IF(BOUND(?end_date),?end_date,NOW()) AS ?test_date).\n \n  #only show LAs with test date today or in future (this means we will show local authorities that exist but are planned to cease in the future)\n FILTER(?test_date >= NOW()) .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }}\nORDER BY (?LALabel)", "context": "All current principal Local Authorities in England", "target": ""}
{"query": "#title: List of movies with Jean-Paul Belmondo, by number of sitelinks, with sitelinks to your preferred wiki\nSELECT DISTINCT\n  ?item ?itemLabel\n  ?sitelinks\n  (YEAR(?d) AS ?year)\n  (GROUP_CONCAT(DISTINCT ?directorLabel; SEPARATOR = \", \") AS ?directors) \n  ?url\nWHERE \n{\n  ?item wdt:P161 wd:Q106255;\n    wikibase:sitelinks ?sitelinks.\n  OPTIONAL {\n    ?item wdt:P57 ?director.\n    ?director rdfs:label ?directorLabel.\n    FILTER((LANG(?directorLabel)) = \"[AUTO_LANGUAGE]\")\n  }\n  ?item wdt:P577 ?d.\n  FILTER(NOT EXISTS {\n    ?item wdt:P577 ?d2.\n    FILTER(?d2 < ?d)\n  })\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\". }\n  OPTIONAL { ?url schema:about ?item ; schema:isPartOf / wikibase:wikiGroup \"wikipedia\" ; schema:inLanguage \"[AUTO_LANGUAGE]\" }\n}\nGROUP BY ?item ?itemLabel ?sitelinks ?d ?url\nORDER BY DESC (?sitelinks) ?item", "context": "List of movies with Jean-Paul Belmondo ordered by number of sitelinks", "target": ""}
{"query": "SELECT\n  ?distribution ?distributionLabel ?distributionDescription \n  ?img \n  (COUNT(*) AS ?count) \n  (GROUP_CONCAT(DISTINCT CONCAT(?t,\" (\",str(YEAR(?d)),\")\"); separator=\"; \") as ?titles) \n  (MIN(YEAR(?d)) as ?from)\n  (MAX(YEAR(?d)) as ?to)\nWHERE \n{\n  ?item wdt:P161 wd:Q106255, ?distribution.\n  FILTER(?distribution != wd:Q106255)\n  OPTIONAL { ?distribution wdt:P18 ?img }\n  ?item wdt:P1476 ?t .\n  ?item wdt:P577 ?d . \n  FILTER NOT EXISTS { ?item wdt:P577 ?d2 . FILTER( ?d2 < ?d) }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?distribution ?distributionLabel ?distributionDescription ?img\nORDER BY DESC (?count)", "context": "List of actors with whom Jean-Paul Belmondo has played the most", "target": ""}
{"query": "#title: Count of movies with Jean-Paul Belmondo, by decade\nSELECT DISTINCT\n  ?decade\n  (COUNT(*) as ?count)\n  (SAMPLE(?item) as ?sample_movie)\n  (SAMPLE(?itemLabel) as ?sample_movieLabel)\n  (SAMPLE(?d) as ?sample_date)\nWHERE \n{\n  ?item wdt:P161 wd:Q106255; wdt:P31/wdt:P279? wd:Q11424 .\n  ?item wdt:P577 ?d.\n  FILTER(NOT EXISTS {\n    ?item wdt:P577 ?d2.\n    FILTER(?d2 < ?d)\n  })\n  BIND(FLOOR(YEAR(?d)/10)*10 AS ?decade)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,cs,it,en\".\n                         ?item rdfs:label ?itemLabel\n                         }\n}\nGROUP BY ?decade\nORDER BY ?decade", "context": "count of movies with Jean-Paul Belmondo by decade", "target": ""}
{"query": "#title: List of characters in Tintin\nSELECT ?character ?characterLabel (COUNT(*) AS ?count)  WHERE {\n  ?book wdt:P179 wd:Q744536;\n        wdt:P674 ?character.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?character ?characterLabel\nORDER BY DESC(?count)", "context": "List of characters in Tintin ordered by number of apparitions", "target": ""}
{"query": "# People with no birth/death/floruit dates but which have a position from which dates can be inferred\n\nSELECT DISTINCT ?person (min(year(?start)) as ?from) (max(year(?end)) as ?to) (?to - ?from as ?span) \nWHERE {\n  ?person wdt:P31 wd:Q5 . \n  ?person p:P39 ?ps1 . ?ps1 ps:P39 ?position . ?position wdt:P279* wd:Q30185 . # for example they are a mayor\n  ?person p:P39 ?ps2 . ?ps2 pq:P580 ?start . ?ps2 pq:P582 ?end . # find start & end of all their other positions\n  filter(year(?end) < 1950) # avoid anyone recent\n  filter not exists { ?person wdt:P569 ?b }\n  filter not exists { ?person wdt:P570 ?d }\n  filter not exists { ?person wdt:P1317 ?fl }\n  filter not exists { ?person wdt:P2031 ?wps }\n  filter not exists { ?person wdt:P2032 ?wpe }\n} group by ?person", "context": "People with no birth/death/floruit dates but which have a position from which dates can be inferred", "target": ""}
{"query": "#defaultView:Map\nSELECT ?diamond ?diamondLabel ?location ?origin ?originLabel ?twitter ?publisher ?publisherLabel WHERE {\n  ?diamond wdt:P31 wd:Q108440863 .\n  OPTIONAL { ?diamond wdt:P495 ?origin . ?origin wdt:P625 ?location }\n  OPTIONAL { ?diamond wdt:P2002 ?twitter }\n  OPTIONAL { ?diamond wdt:P123 ?publisher }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of the origin of diamond open access journal (Q108440863) publications", "target": ""}
{"query": "#name of place names in Ghana ending with \"li\", \"ti\", \"om\"\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q486972;\n    wdt:P17 wd:Q117;\n    rdfs:label ?itemLabel;\n    wdt:P625 ?coord.\n  FILTER((LANG(?itemLabel)) = \"en\")\n  FILTER(REGEX(?itemLabel, \"(li|ti|om)$\"))\n}", "context": "Map of place names in Ghana ending with \"li\", \"ti\", \"om\"", "target": ""}
{"query": "SELECT ?taxon ?taxonLabel ?band ?bandLabel WHERE {\n   ?taxon wdt:P31 wd:Q16521 ;\n          wdt:P138 ?band .\n  ?band wdt:P31 wd:Q215380 ;\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }        \n   \n}", "context": "Biological taxa named after (metal (and other) music bands", "target": ""}
{"query": "#title:Dante Alighieri: items with Q1067 as author\nSELECT \n  ?item ?title (YEAR(?date) as ?year)\n  ?itemLabel ?itemDescription \n  ?instanceof ?instanceofLabel \n  ?sts ?sls ?ids \n  (COALESCE(?myurl, ?iturl) as ?Wikipedia)\n  ?Wikisource\nWHERE\n{\n    ?item  wdt:P50 wd:Q1067 .\n    OPTIONAL { ?item wdt:P31 ?instanceof }\n    OPTIONAL { ?item wdt:P577 ?date }\n    OPTIONAL { ?item wdt:P1476 ?title }\n    ?item wikibase:statements ?sts ; wikibase:sitelinks ?sls ; wikibase:identifiers ?ids \n    OPTIONAL { ?myurl schema:about ?item ; schema:isPartOf / wikibase:wikiGroup \"wikipedia\" ; schema:inLanguage \"[AUTO_LANGUAGE]\" }              \n    OPTIONAL { ?iturl schema:about ?item ; schema:isPartOf <https://it.wikipedia.org/> ; schema:inLanguage \"it\" }                \n    OPTIONAL { ?Wikisource schema:about ?item ; schema:isPartOf <https://it.wikisource.org/> ; schema:inLanguage \"it\" }                  \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],it,en\". }\n}\nORDER BY (!BOUND(?date)) ?date ?item", "context": "Works by Dante Alighieri (1265 - September 13/14, 1321)", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT DISTINCT ?party_label (SUM(?views) as ?sum_views) WHERE {\n    \n    ?item p:P3602 [ ps:P3602 wd:Q48834983 ; pq:P1268 ?party ] .\n    ?sitelink schema:about ?item . \n    ?sitelink schema:isPartOf <https://cs.wikipedia.org/> . \n    ?sitelink schema:name ?name .\n    SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:api \"Generator\";\n                    wikibase:endpoint \"cs.wikipedia.org\";\n                    wikibase:limit \"once\";\n                    mwapi:generator \"allpages\";\n                    mwapi:gapfrom ?name;\n                    mwapi:gaplimit \"1\";\n                    mwapi:prop \"pageviews\";\n                    mwapi:pvipdays \"2\".\n     ?pageviews wikibase:apiOutput \"pageviews/pvip/text()\".\n  }\n  .\n  BIND ((xsd:integer(?pageviews)) AS ?views)\n  FILTER(?pageviews != \"1\") \n  ?party rdfs:label ?party_label filter(lang(?party_label)=\"en\") .\n\n} group by ?party ?party_label", "context": "Cumulative page views for candidates in Czech parliamentary election 2021, split by party", "target": ""}
{"query": "PREFIX wdname: <http://www.wikidata.org/entity/Q108636690>\nSELECT ?here ?count ?rank ?native_label ?itemLabel ?item ?itemDescription ?scriptLabel\nWITH  { SELECT DISTINCT ?item (COUNT(DISTINCT ?p) as ?count) \n        WHERE { { ?item wdt:P460* wdname: . ?p wdt:P735 ?item } UNION { ?item wdt:P460* wdname: }  } \n        GROUP BY ?item ORDER BY DESC(?count) LIMIT 1000\n      } AS %all\nWITH  { SELECT (?item as ?item2) (?count AS ?count2) WHERE { INCLUDE %all }\n      } AS %all2\nWITH  { SELECT ?item (SUM(?x) AS ?rank) ?count WHERE { INCLUDE %all . INCLUDE %all2 . BIND( IF( ?count  <= ?count2 ,1,0) AS ?x ) } GROUP BY ?item ?count\n      } AS %main \nWHERE\n{ hint:Query hint:optimizer \"None\".    \n  INCLUDE %main .\n  OPTIONAL { FILTER( ?item = wdname: )  BIND(\" ==> \" as ?here) }\n  OPTIONAL { ?item wdt:P1705 ?native_label }\n  OPTIONAL { ?item wdt:P282 ?script  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?count)", "context": "Given name \"Filaret\" ranked within similar names", "target": ""}
{"query": "SELECT ?journal ?journalLabel ?started\nWHERE {\n  ?journal wdt:P31 wd:Q773668;\n    (wdt:P275/wdt:P31) wd:Q196294.\n  ?journal p:P31/pq:P580 ?started.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Start time of open access journals known to Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?length ?coords\nWHERE \n{\n  ?item wdt:P31 wd:Q4022;\n        wdt:P403/wdt:P361* wd:Q788 ;\n        p:P2043/psv:P2043 [\n        wikibase:quantityAmount ?baseLength;\n        wikibase:quantityUnit [\n          p:P2370/psv:P2370 [\n            wikibase:quantityAmount ?unitLength;\n            wikibase:quantityUnit wd:Q11573\n          ]\n        ]\n      ].\n  BIND(ROUND(?baseLength * ?unitLength / 1000) AS ?length) .\n  OPTIONAL {\n    {\n      ?item p:P625 ?coordsStatement .\n      ?coordsStatement ps:P625 ?coords ;\n                       pq:P518 wd:Q1233637 .\n    } UNION {\n      ?item p:P625 ?coordsStatement .\n      ?coordsStatement ps:P625 ?coords .\n      FILTER NOT EXISTS { ?coordsStatement pq:P518 ?x }\n    }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?length)", "context": "Mouths of rivers flowing into the Arctic", "target": ""}
{"query": "#title: Names of food in more languages Q1381277\nSELECT DISTINCT ?meailID ?label (lang(?label) as ?langcode) WHERE {\n   VALUES ?meailID { wd:Q1381277}\n  ?meailID rdfs:label ?label . \n  OPTIONAL {?meailID schema:inLanguage ?code} . \n}\n", "context": "What chocolate mousse is called in different languages", "target": ""}
{"query": "#title:Economists with an enwiki articles about themselves, but not their wife ;)\n# adapted from https://twitter.com/wikigamaliel/status/1440678869539844110\nSELECT ?item ?itemLabel ?sls ?wife ?wifeLabel ?wifeDescription ?slswife \nWITH\n{ \n  SELECT ?item  ?article\n  WHERE\n  {\n  ?item wdt:P21 wd:Q6581097;\n        wdt:P106 wd:Q188094.\n  ?article schema:about ?item ; \n           schema:isPartOf <https://en.wikipedia.org/> .\n  } \n} AS %i\nWHERE\n{\n  INCLUDE %i\n  ?item wdt:P26 ?wife . \n   FILTER NOT EXISTS { ?wen schema:about ?wife; schema:isPartOf <https://en.wikipedia.org/> . } \n  ?item wikibase:sitelinks ?sls .\n  ?wife wikibase:sitelinks ?slswife .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Women in Wikidata whose husbands are economists with articles on the English Wikipedia, but they themselves lack articles", "target": ""}
{"query": "#title:Economists with an enwiki articles about themselves, but not about their husband ;)\n# adapted from https://twitter.com/wikigamaliel/status/1440678869539844110\nSELECT ?item ?itemLabel ?sls ?husband ?husbandLabel ?husbandDescription ?slshusband \nWITH\n{ \n  SELECT ?item  ?article\n  WHERE\n  {\n  ?item wdt:P21 wd:Q6581072 ;\n        wdt:P106 wd:Q188094.\n  ?article schema:about ?item ; \n           schema:isPartOf <https://en.wikipedia.org/> .\n  } \n} AS %i\nWHERE\n{\n  INCLUDE %i\n  ?item wdt:P26 ?husband . \n   FILTER NOT EXISTS { ?wen schema:about ?husband; schema:isPartOf <https://en.wikipedia.org/> . } \n  ?item wikibase:sitelinks ?sls .\n  ?husband wikibase:sitelinks ?slshusband ; wdt:P21 wd:Q6581097 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Same with husbands", "target": ""}
{"query": "#Items with a Wikispecies sitelink\n#illustrates sitelink selection, \";\" notation\nSELECT ?item ?itemLabel ?image ?article\nWHERE\n{\n  ?item wdt:P31 wd:Q5;\n        wdt:P108 wd:Q48989.\n  ?article  schema:about ?item ;\n      schema:isPartOf <https://de.wikipedia.org/> .\n OPTIONAL{?item wdt:P18 ?image}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"nl\" }\n}\n", "context": "People employed by the ZDF with an image on Commons and a link to the German language Wikipedia", "target": ""}
{"query": "# Coordinates of the birth places of people named Antoine\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord\nWHERE\n{\n  ?item wdt:P31 wd:Q5 .   # human\n  ?item wdt:P735 wd:Q15235674.\n  ?item wdt:P19 ?place.\n  ?place wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Coordinates of the birth places of people named Antoine", "target": ""}
{"query": "#title: Place of birth of people named Antoine, Tony, Antonio, etc\n#defaultView:Map\nSELECT ?item ?itemLabel ?layer ?coord\nWITH\n{\n  SELECT DISTINCT ?gn (str(?layer0) as ?layer) { ?gn wdt:P460* wd:Q15235674 ; wdt:P31 wd:Q12308941 ; wdt:P1705 ?layer0  }\n} as %a  \nWHERE\n{\n  INCLUDE %a\n  ?item wdt:P31 wd:Q5 . \n  ?item wdt:P735 ?gn .\n  ?item wdt:P19/wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}", "context": "Place of birth of people named Antoine, Tony, Antonio, etc", "target": ""}
{"query": "SELECT ?member ?memberLabel ?descendants ?descendants_were_MPs\nWITH { \n  select distinct ?member WHERE \n     { ?member wdt:P31 wd:Q5 . ?member p:P39 ?pss . ?pss ps:P39 ?st . ?st wdt:P279 wd:Q16707842 .\n     } } AS %members \nWHERE {\n { SELECT distinct ?member (count(distinct ?child) as ?descendants) where \n    { INCLUDE %members . ?member wdt:P40+ ?child } group by ?member } \n optional { SELECT distinct ?member (count(distinct ?MPchild) as ?descendants_were_MPs) where \n    { INCLUDE %members . ?member wdt:P40+ ?MPchild .\n      ?MPchild p:P39 ?psc . ?psc ps:P39 ?ct . ?ct wdt:P279 wd:Q16707842 .\n    } group by ?member } \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by desc(?descendants_were_MPs) desc(?children)", "context": "UK MPs who had the most identified descendants who were themselves MPs", "target": ""}
{"query": "#Women born in Nigeria with an article in Basque for Listeria\n#Listeria can only handle up to 5000\nSELECT DISTINCT ?item ?itemLabel ?image ?jarduera ?jardueraLabel ?jaiotza_data ?heriotza_data WHERE {\n  ?item wdt:P21 wd:Q6581072.\n  { ?item wdt:P17 wd:Q1033. }\n  UNION\n  {\n    ?item wdt:P19 ?pob.\n    ?pob (wdt:P131*) wd:Q1033.\n  }\n  OPTIONAL {\n    ?sitelink schema:about ?item;\n      schema:inLanguage \"eu\".\n  }\n  ?item wdt:P31 wd:Q5.\n  FILTER(BOUND(?sitelink))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es, en\". }\n  OPTIONAL { ?item wdt:P106 ?jarduera. }\n  OPTIONAL { ?item wdt:P569 ?jaiotza_data. }\n  OPTIONAL { ?item wdt:P570 ?heriotza_data. }\n}\nLIMIT 1000", "context": "Women born in Nigeria with an article in Basque for Listeria", "target": ""}
{"query": "#Still life paintings by women\nSELECT ?item ?itemLabel ?artistLabel ?pic\nWHERE \n{\n  ?item wdt:P31 wd:Q3305213.\n  ?item wdt:P136/wdt:P279* wd:Q170571.\n  ?item wdt:P170 ?artist.\n  ?item wdt:P18 ?pic.\n  ?artist wdt:P21 wd:Q6581072.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Still life paintings by women", "target": ""}
{"query": "#defaultView:Map\nSELECT ?cemetery ?cemeteryLabel ?districtLabel ?operatorLabel ?coords\nWHERE {\n  ?cemetery wdt:P31/wdt:P279* wd:Q39614 .\n  ?cemetery wdt:P17 wd:Q664 .\n  ?cemetery wdt:P131 ?district .\n  OPTIONAL { ?cemetery wdt:P137 ?operator } .\n  ?cemetery wdt:P625 ?coords\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY ?districtLabel ?cemeteryLabel", "context": "Map of cemeteries in New Zealand", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?label ?_image WHERE {\n  ?item wdt:P166 wd:Q152770.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" . \n    ?item rdfs:label ?label\n  }\n  \nOPTIONAL { ?item wdt:P18 ?_image. }\n}", "context": "Right Livelihood Award laureates", "target": ""}
{"query": "#Chelsea FC players\n#defaultView:ImageGrid\nSELECT DISTINCT ?playerItem ?player ?startDate ?endDate ?position ?games ?goals ?image ?article ?rank\n\nWHERE\n{\n  ?playerItem p:P54 ?teamStat .        \n  ?playerItem wdt:P18 ?image .\n  ?playerItem wikibase:sitelinks ?rank .\n  OPTIONAL {?playerItem wdt:P413 ?positionItem}\n  \n  ?teamStat ps:P54 wd:Q9616 .\n  OPTIONAL {?teamStat pq:P1350 ?games}\n  OPTIONAL {?teamStat pq:P1351 ?goals}\n  #FILTER (?games > 0)\n  \n  ?teamStat pqv:P580 ?startStatement .\n  ?startStatement wikibase:timeValue ?startDate .\n  ?startStatement wikibase:timePrecision ?startPrecision .\n\n  OPTIONAL {\n    ?teamStat pqv:P582 ?endStatement .\n    ?endStatement wikibase:timeValue ?endDate .\n    ?endStatement wikibase:timePrecision ?endPrecision \n  }\n  \n  OPTIONAL {\n    ?article schema:about ?playerItem .\n    ?article schema:isPartOf <https://en.wikipedia.org/> .\n    }\n  SERVICE wikibase:label {\n  \tbd:serviceParam wikibase:language \"en\" .\n    ?playerItem rdfs:label ?player .\n    ?positionItem rdfs:label ?position .\n  }\n}\nORDER BY DESC(?rank)", "context": "Chelsea FC players, with English Wikipedia article, Commons image, etc", "target": ""}
{"query": "SELECT ?award ?awardLabel (SUM(?female) AS ?count_females) (SUM(?male) AS ?count_males) (COUNT(*) AS ?count) ((100 * ?count_females / ?count) AS ?share_female) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?award wdt:P279 wd:Q7191.\n  ?item wdt:P166 ?award;\n    wdt:P21 ?gender.\n  BIND(IF(?gender IN(wd:Q6581097, wd:Q2449503), 1 , 0 ) AS ?male)\n  BIND(IF(?gender IN(wd:Q6581072, wd:Q1052281), 1 , 0 ) AS ?female)\n}\nGROUP BY ?award ?awardLabel\nORDER BY DESC(?share_female)", "context": "Share of women for each Nobel prize", "target": ""}
{"query": "# tool: scholia\n#defaultView:ScatterChart\n# Statistics on number of recipients for award wrt. gender\nSELECT ?men ?women ?award ?awardLabel \nWITH {\n  SELECT (COUNT(?recipient_male) AS ?men) (COUNT(?recipient_female) AS ?women) ?award WHERE {\n    ?award wdt:P31 wd:Q11448906 .\n    { \n      ?recipient_male wdt:P166 ?award .\n      ?recipient_male wdt:P21 wd:Q6581097 .\n    }\n    UNION \n    {\n      ?recipient_female wdt:P166 ?award .\n      ?recipient_female wdt:P21 wd:Q6581072 .\n    }  \n  }\n  GROUP BY ?award\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,da,de,es,fr,jp,nl,no,ru,sv,zh\". }\n} \nORDER BY DESC(?men) DESC(?women)", "context": "Nobel Prize statistics on number of recipients for award wrt. gender (interactive plot)", "target": ""}
{"query": "# query made User:PAC2\n#title: Share of women among Nobel prize winners by decade\nSELECT ?decade (SUM(?female) AS ?count_females) (SUM(?male) AS ?count_males) (COUNT(*) AS ?count) ((100  * ?count_females) / ?count AS ?share_female) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?award wdt:P279 wd:Q7191.\n  ?item p:P166 _:b1;\n    wdt:P21 ?gender.\n  _:b1 ps:P166 ?award;\n    pq:P585 ?date.\n  BIND(CONCAT(STR(ROUND((YEAR(?date)) / 10 )), \"0\") AS ?decade)\n  BIND(IF(?gender IN(wd:Q6581097, wd:Q2449503), 1 , 0 ) AS ?male)\n  BIND(IF(?gender IN(wd:Q6581072, wd:Q1052281), 1 , 0 ) AS ?female)\n}\nGROUP BY ?decade\nORDER BY DESC (?decade)", "context": "Share of women among Nobel prize winners by decade", "target": ""}
{"query": "#get the list of notable books on linked data, semantic technologies, and semantic knowledge graphs. (issue: langauge of wdt:P2093)\n \nSELECT DISTINCT ?book ?title (GROUP_CONCAT (DISTINCT ?author; SEPARATOR = \", \") AS ?authors) ?pubDate\n{\n  VALUES ?book {wd:Q17744504 wd:Q17744476 wd:Q61718117 wd:Q59784970 wd:Q17744339 wd:Q51601248 wd:Q17744488 wd:Q27942623  wd:Q108378522}\n         \n         ?book wdt:P1476 ?title.\n         OPTIONAL {?book (wdt:P50/rdfs:label)|(wdt:P98/rdfs:label)|wdt:P2093 ?author\n                   FILTER (LANG(?author)=\"\"||LANG(?author)=\"en\")}\n         OPTIONAL {?book wdt:P577 ?pubDate}\n \n\n}\nGROUP BY ?book ?title ?pubDate", "context": "Notable books on linked data, semantic technologies, and semantic knowledge graphs", "target": ""}
{"query": "# UK railway stations connected from Brussels-South railway station (Q800587) \n#defaultView:Map{\"hide\":[\"?cds\",\"?line\",\"?layer\"],\"layer\":\"?trackLabel\"}\nSELECT ?station ?stationLabel ?cds ?line ?trackLabel ?track ?image {\n{SELECT * { SERVICE gas:service { gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q800587 ; gas:linkType wdt:P197 ; gas:out ?station ; gas:out1 ?depth ; gas:out2 ?pred . } FILTER(?station != wd:Q800587)\n  ?station wdt:P625 ?cds ; wdt:P17 ?country .\n  VALUES ?country { wd:Q31 wd:Q32 wd:Q55 }#Be, Lu, NL\n  ?station p:P197 ?adjST . ?adjST ps:P197 ?adj .\n  OPTIONAL { ?adjST pq:P81 ?track .}\n  MINUS { ?station p:P31 [ ps:P31 wd:Q55488 ; pq:P582 [] ] . } #connections that are no longer \n  MINUS { ?adj p:P31 [ ps:P31 wd:Q55488 ; pq:P582 [] ] . }\n  ?adj p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line)}\n}\n  OPTIONAL { ?station wdt:P18 ?image .}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Benelux railway stations connected from", "target": ""}
{"query": "SELECT ?station ?stationLabel ?coordinate_location ?start ?end WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"da,en\". }\n  ?station (wdt:P31/(wdt:P279*)) wd:Q55488;\n    wdt:P17 wd:Q35;\n    wdt:P625 ?coordinate_location.\n  FILTER((BOUND(?start)) || (BOUND(?end)))\n  OPTIONAL { ?station wdt:P1619 ?date_of_official_opening. }\n  OPTIONAL { ?station wdt:P571 ?inception. }\n  OPTIONAL { ?station wdt:P3999 ?closure. }\n  OPTIONAL { ?station wdt:P582 ?endtime. }\n  BIND(IF(BOUND(?date_of_official_opening), ?date_of_official_opening, ?inception) AS ?start)\n  BIND(IF(BOUND(?closure), ?closure, ?endtime) AS ?end)\n}", "context": "Date of opening and closing of railway stations in Denmark", "target": ""}
{"query": "#Cultura Talaiòtica a Menorca\n#defaultView:Map\nSELECT ?item ?itemLabel ?coordenades ?imatge ?localitzat_a_l_entitat_territorial_administrativaLabel ?inst_ncia_de ?inst_ncia_deLabel WHERE {\n  ?item wdt:P31 wd:Q530894;\n    (wdt:P131+) wd:Q52636.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],ca\". }\n  OPTIONAL { ?item wdt:P625 ?coordenades. }\n  OPTIONAL { ?item wdt:P18 ?imatge. }\n  OPTIONAL { ?item wdt:P131 ?localitzat_a_l_entitat_territorial_administrativa. }\n}", "context": "Map or Talaiotic archeological culture sites located in the Menorca island in Spain", "target": ""}
{"query": "#defaultView:Map\nSELECT ?estazio_meteorologiko ?estazio_metereologikoLabel ?koordenatuak ?itsas_mailaren_gainetiko_altuera WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es\". }\n  ?estazio_meteorologiko wdt:P31 wd:Q190107;\n    (wdt:P131/(wdt:P131+)) wd:Q4018;\n    wdt:P625 ?koordenatuak.\n  ?estazio_meteorologiko wdt:P2044 ?itsas_mailaren_gainetiko_altuera.\n}", "context": "Location of all the meteorological stations in Navarre", "target": ""}
{"query": "#defaultView:ImageGrid{\"hide\": \"?photo\"}\nSELECT ?personLabel ?personDescription ?countryLabel (SAMPLE(?photo) as ?photo)\nWHERE {\n  wd:Q108782773 wdt:P710 ?person . \n  ?person wdt:P27 ?country .\n  ?person wdt:P18 ?photo .\n  \n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n  }\n}\nGROUP BY ?personLabel ?personDescription ?countryLabel\nORDER BY ?countryLabel", "context": "Photos of participants in the Pandora Papers", "target": ""}
{"query": "#defaultView:Map{\"hide\" :[\"?layer\"]}\n#title: Map of French departments with shape\nSELECT ?itemLabel ?shape (?itemLabel AS ?layer) WHERE {\n  ?item wdt:P31 wd:Q6465;\n    wdt:P3896 ?shape.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "French departments with their shape", "target": ""}
{"query": "SELECT ?river ?riverLabel ?length ?continent ?continentLabel WITH {\n  SELECT DISTINCT ?river ?length {\n    ?river wdt:P31 wd:Q4022 ; wdt:P17 wd:Q142 ; wdt:P2043 ?length .\n    FILTER NOT EXISTS { ?department wdt:P31/wdt:P279* wd:Q6465 ; wdt:P138 ?river }\n  }\n  ORDER BY DESC(?length)\n  LIMIT 10\n} AS %i WHERE {\n  INCLUDE %i\n  OPTIONAL { ?river wdt:P4614/wdt:P30 ?continent }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" }\n}\nORDER BY DESC(?length)", "context": "Longest rivers located in France and which did not gave their name to a department", "target": ""}
{"query": "# some repetitive taxon names\nSELECT ?taxon ?taxonName WHERE {\n  ?taxon wdt:P225 ?taxonName.\n  # e.g. gorilla gorilla or gorilla gorilla gorilla\n  FILTER(REGEX(?taxonName, \"^([^ ]*) \\\\1( \\\\1)?$\", \"i\"))\n}\nLIMIT 500", "context": "Repetitive taxon names", "target": ""}
{"query": "SELECT DISTINCT ?item ?labelFr ?nomCOG ?pop ?image ?coord WHERE \n{\n  ?item wdt:P31 wd:Q484170. #Commune française\n  FILTER NOT EXISTS { ?item wdt:P576 ?end_date . } # Qui n'a pas de date de fin (on veut les communes actuelles)\n  \n  ?item rdfs:label ?labelFr . FILTER(LANG(?labelFr) = \"fr\") # Nom en français\n\n  ?item wdt:P1448 ?nomCOG . # Nom officiel dans le code officiel géographique  \n\n  BIND(STRLEN(STR(?labelFr)) AS ?nameLength) # Nombre de caractères\n  FILTER (?nameLength > 30) # Fitre pour ne garder que les valeurs supérieures à 30\n\n  OPTIONAL { ?item wdt:P1082 ?pop . } # Dernières données de population\n  OPTIONAL { ?item wdt:P625 ?coord . } # Coordonnées\n  OPTIONAL { ?item wdt:P18 ?image . } # Et juste là, un petit geranium.\n} ORDER BY DESC(?nameLength)", "context": "Map of French communes that are >30 characters long", "target": ""}
{"query": "#title:Lexemes in Swedish that are missing in SAOB\n# 2021-10-12\nselect ?lemma\nWHERE {\n  ?lexemeId dct:language wd:Q9027;\n            wikibase:lemma ?lemma;\n            wdt:P9660 wd:Q1935308.\n}", "context": "Lexemes in Swedish that are missing in Svenska Akademiens Ordbok (Q1935308)", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?musee ?annee ?visiteurs ?museeLabel WHERE {\n  {\n    SELECT ?musee ?annee (AVG(?nb) AS ?visiteurs) WHERE {\n      {\n        SELECT ?musee (STR(YEAR(?date)) AS ?annee) ?nb WHERE {\n          VALUES ?musee { wd:Q3330860 wd:Q1495869 wd:Q3329089 }\n          ?musee p:P1174 ?node.\n          ?node ps:P1174 ?nb ; pq:P585 ?date.\n        }\n      }\n    }\n    GROUP BY ?musee ?annee\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Attendance at the Bargoin, Roger-Quilliot and Henri-Lecoq museums per year", "target": ""}
{"query": "select ?item1 ?item1Label ?item2 ?item2Label where {\n\n  ?item1 wdt:P39 wd:Q19803234  .\n  ?item2 wdt:P39 wd:Q19803234  .\n  ?item1 wdt:P1038| wdt:P3373|wdt:P22|wdt:P25|wdt:P40|wdt:P26 ?item2 . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs,en\". }\n  \n} ", "context": "Present and past members of Czech Chamber of Deputies who are relatives", "target": ""}
{"query": "#defaultView:Map\nSELECT ?library ?libraryLabel ?coords WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?library (wdt:P31/wdt:P279*) wd:Q32815.\n  ?library wdt:P625 ?coords.\n}", "context": "Map of mosques", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?décennie (COUNT(?item) AS ?compte) WHERE {\n  ?item wdt:P195 wd:Q108427678 ; wdt:P577 ?date .\n  BIND ( STR(FLOOR(YEAR(?date)/10)) AS ?décennie )\n}\nGROUP BY ?décennie", "context": "Documents from the Boyer collection by decades", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?pronoun ?pronounLabel (count(?person) as ?count)\n{\n  ?person wdt:P6553 ?pronoun;\n          wdt:P31 wd:Q5.\n  ?pronoun wikibase:lemma ?pronounLabel.\n}\nGROUP by ?pronoun ?pronounLabel\nORDER by DESC(?count)", "context": "Pronouns of people in different languages", "target": ""}
{"query": "#defaultView:Map{\"hide\" : \"?coords\"}\nSELECT DISTINCT ?bib ?bibLabel (IRI(CONCAT(\"https://twitter.com/\", ?twitterUser)) AS ?twitter) ?logo ?coords WHERE {\n  ?bib (wdt:P31/(wdt:P279*)) wd:Q7075;\n    wdt:P17 wd:Q39;\n    wdt:P2002 ?twitterUser.\n  OPTIONAL { ?bib wdt:P154 ?logo. }\n  OPTIONAL { ?bib wdt:P625 ?coords. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Twitter accounts of libraries in Switzerland", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?image ?cl\nWHERE \n{\n  ?item wdt:P7963 ?cl . \n  \n  ?item wdt:P18 ?image .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Images of cell types in Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT ?person ?personLabel ?locLabel ?coord WHERE {\n  ?person wdt:P19/wdt:P131* wd:Q12694 ;\n          wdt:P20 ?loc .\n  ?loc wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of the place of death of people born in Puy-de-Dôme", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?p ?pLabel (\"EC0000\" AS ?rgb) ?w ?wLabel\nWHERE \n{\n  ?p wdt:P31 wd:Q98270496 . \n  ?p wdt:P101 ?w .\n  OPTIONAL { ?w wdt:P154 ?wpicture }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n} ORDER BY ASC(?wLabel)", "context": "Graph of NFDI consortia with their fields of work", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?author ?authorLabel ?organisation ?organisationLabel (COALESCE(?orcid_, CONCAT(\"orcid-search/quick-search/?searchQuery=\", ?authorLabel)) AS ?orcid) \n                ?exampleWork ?exampleWorkLabel (SAMPLE(?location) AS ?where)\nWITH {\n  SELECT (SAMPLE(?work) AS ?exampleWork) ?author WHERE {\n    { ?work wdt:P921/wdt:P31*/wdt:P279* wd:Q2013 . }\n    union { ?work wdt:P921/wdt:P361+ wd:Q2013 . }\n    union { ?work wdt:P921/wdt:P1269+ wd:Q2013 . }\n    ?work wdt:P50 ?author .\n  } GROUP BY ?author\n} AS %result\nWHERE {\n  INCLUDE %result\n  VALUES ?COUNTRY { wd:Q155 wd:Q414 wd:Q298 wd:Q419 wd:Q739 wd:Q750 wd:Q717 wd:Q733 wd:Q736 wd:Q734 wd:Q77 wd:Q730 }\n  ?author wdt:P108 | wdt:P1416 ?organisation .\n  { ?organisation wdt:P131 ?COUNTRY ; wdt:P625 ?location . }\n  UNION\n  { ?organisation wdt:P159 ?hq . ?hq wdt:P17 ?COUNTRY . ?hq wdt:P625 ?location . }\n  UNION\n  { ?organisation wdt:P17 ?COUNTRY ; wdt:P625 ?location . }\n  # Include optional ORCID iD\n  OPTIONAL { ?author wdt:P496 ?orcid_ . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,nl,no,ru,sv,zh\" . } \n} GROUP BY ?author ?authorLabel ?organisation ?organisationLabel ?orcid_ ?exampleWork ?exampleWorkLabel\nORDER BY DESC(?authorLabel)\n\n", "context": "Places where people in South-America work who published on Wikidata", "target": ""}
{"query": "# (Thanks, James Heald, for the original query!)\n#defaultView:Map\nSELECT ?station ?stationLabel ?cds ?line ?layer WHERE { \n  { SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q3097173 ; gas:target wd:Q6132101 ; gas:out ?station ; gas:out1 [] ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . } }\n  FILTER(?station != wd:Q3097173)\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,th\". }\n}\n", "context": "Rail link between Narvik and Singapore", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?ouvrage ?ouvrageLabel ?ligne ?ligneLabel ?coordonnees ?image WHERE {\n  ?ouvrage wdt:P2505 ?ligne;\n    wdt:P17 wd:Q142;\n    wdt:P625 ?coordonnees.\n  OPTIONAL { ?ouvrage wdt:P18 ?image. }\n  ?ligne wdt:P31 wd:Q858485.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Map of French bridges and tunnels carrying an high-speed railway line", "target": ""}
{"query": "# See https://m.wikidata.org/wiki/User:PAC2/Gender_neutral_labels\n#title: Items with male form as a generic form in French\nSELECT DISTINCT ?item ?itemLabel ?femaleform ?maleform ?lang WHERE {\n  wd:Q12737077 (^(wdt:P279*)/^wdt:P31) ?item.\n  ?item wdt:P2521 ?femaleform;\n    wdt:P3321 ?maleform;\n    rdfs:label ?itemLabel.\n  BIND(LANG(?itemLabel) AS ?lang)\n  FILTER((LANG(?itemLabel)) = \"fr\")\n  FILTER((LANG(?femaleform)) = ?lang)\n  FILTER((LANG(?maleform)) = ?lang)\n  FILTER(?itemLabel = ?maleform)\n  FILTER(?itemLabel != ?femaleform)\n}\nORDER BY ?item\nLIMIT 100", "context": "List of Wikidata item's labels which are same as male form and different from female form in French (ie using the generic as male form)", "target": ""}
{"query": "# See https://m.wikidata.org/wiki/User:PAC2/Gender_neutral_labels\n#title: Items with male form as a generic form in English \nSELECT ?item ?itemLabel ?femaleform ?maleform ?lang WHERE {\n  wd:Q12737077 ^wdt:P279*/^wdt:P31 ?item. \n ?item wdt:P2521 ?femaleform;\n    wdt:P3321 ?maleform;\n    rdfs:label ?itemLabel.\n  BIND(LANG(?itemLabel) AS ?lang) \n  FILTER((LANG(?itemLabel)) = \"en\")\n  FILTER((LANG(?femaleform)) = ?lang)\n  FILTER((LANG(?maleform)) = ?lang)\n  FILTER(?itemLabel = ?maleform) \n  FILTER(?itemLabel != ?femaleform) \n}\nLIMIT 100", "context": "List of Wikidata item's labels which are same as male form and different from female form in English (ie using the generic as male form)", "target": ""}
{"query": "SELECT\n  ?ord\n  ?consort ?consortLabel ?consortDescription \n  ?died \n  ?placeofdeath ?placeofdeathLabel \n  ?placeofburial ?placeofburialLabel\nWHERE\n{\n  wd:Q199633 p:P26 ?st .\n  ?st ps:P26 ?consort .\n  OPTIONAL { ?st pq:P1545 ?ord }\n  OPTIONAL { ?consort wdt:P570 ?died }\n  OPTIONAL { ?consort wdt:P20 ?placeofdeath }\n  OPTIONAL { ?consort wdt:P119 ?placeofburial }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "consorts of Selim III", "target": ""}
{"query": "#Lieux de naissance des personnes dont le nom de famille est « Autrique »\n# Coordinates of the birth places of people with family name Autrique\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P734 wd:Q11134722;\n    wdt:P19 ?place.\n  ?place wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Coordinates of the birthplaces of people with family name Autrique", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?point_in_time ?coords\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q645883. \n  optional {?item wdt:P585 ?point_in_time .}\n  optional {?item wdt:P625 ?coords .}\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}", "context": "Coordinates of battles", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?communeLabel ?placeLabel ?id ?image ?coords\nWHERE \n{\n  ?item wdt:P31 wd:Q4405227 ;\n        wdt:P31 wd:Q6498959 ;\n        wdt:P1435/wdt:P279* wd:Q61058374 ;\n        wdt:P481 ?id ;\n        p:P131 ?communeStatement ;\n        wdt:P276 ?place .\n  ?communeStatement ps:P131 ?commune .\n  FILTER NOT EXISTS { ?communeStatement pq:P582 ?x }\n  ?place wdt:P625 ?coords .\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Geographical distribution of votive boats historic monuments in France", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?p ?pLabel (\"EC0000\" AS ?rgb) ?w ?wLabel ?w2 ?w2Label #?w3 ?w3Label\nWHERE\n{\n  ?p wdt:P31 wd:Q98270496 .\n  ?p wdt:P101 ?w .\n  OPTIONAL { ?p wdt:P2652 ?w2 }.\n  #OPTIONAL { ?p wdt:P1416 ?w3 }.\n  OPTIONAL { ?w wdt:P154 ?wpicture }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en\" . }\n} ORDER BY ASC(?wLabel)", "context": "Relationship among accepted NFDI consortia", "target": ""}
{"query": "# by Jura1, 2021-11-04\n#title:Number of virtual twins (pair with the same dates of birth and death) in Wikidata at a given milestone, from [[d:Wikidata:WikiProject Virtual Twins]]\n#defaultView:AreaChart\nSELECT ?date ?cumulativecount \n{\n    hint:Query hint:optimizer \"None\".\n    {   SELECT ?milestonep (COUNT(?item) as ?cumulativecount)\n        WHERE\n        {\n          { SELECT ?item { {\n  \t?item p:P1889 [ pq:P1013 wd:Q20978290 ; ps:P1889 ?item2 ]\n    FILTER( xsd:integer(strafter(str(?item),\"Q\")) >  xsd:integer(strafter(str(?item2),\"Q\"))  )         \n  } }   }\n          BIND( xsd:integer( substr(str(?item), 33)) as ?qid)\n          wd:Q38074555 p:P1114 ?milestonep .\n          ?milestonep ps:P1114 ?milestone .\n          FILTER( ?milestone > ?qid ) \n        }\n        GROUP BY ?milestonep\n    }          \n    ?milestonep pq:P585 ?date\n}", "context": "Number of virtual twins (pair with the same dates of birth and death) in Wikidata", "target": ""}
{"query": "#title:People recently buried in a cemetery nearby without an image of the grave (P1442) on Wikidata\n#defaultView:Map{\"hide\":[\"?coor\"]}\n#by Jura1, 2021-11-0, source: https://www.wikidata.org/wiki/Property_talk:P1442#Queries\nSELECT\n  ?item ?itemLabel ?itemDescription \n  ?date_of_death \n  ?cemetery ?cemeteryLabel ?coor \n  ?commons_category_which_may_include_one ?findagrave \n  ?img_person ?img_cemetery\n{\n  ?item wdt:P119 ?cemetery.\n  ?cemetery wdt:P31 wd:Q39614.\n  ?item wdt:P570 ?date_of_death.\n  FILTER (?date_of_death >\"2015-01-01\"^^xsd:dateTime)\n  FILTER NOT EXISTS {?item p:P1442 []}\n  ?cemetery wdt:P625 ?coor.\n  FILTER NOT EXISTS {?cemetery wdt:P625 ?c2.FILTER(str(?coor)>str(?c2))}\n  OPTIONAL{?item wdt:P373 ?cat.BIND(URI(CONCAT(\"https://commons.wikimedia.org/wiki/Category:\",?cat)) as ?commons_category_which_may_include_one)}\n  OPTIONAL{?item wdt:P535 ?fid.wd:P535 wdt:P1630 ?ff.BIND(URI(REPLACE(?ff,\"\\\\$1\",?fid)) as ?findagrave)}\n  OPTIONAL{?item wdt:P18 ?img_person}\n  FILTER NOT EXISTS {?item wdt:P18 ?i2.FILTER(str(?i2)>str(?img_person))}\n  OPTIONAL{?cemetery wdt:P18 ?img_cemetery}\n  FILTER NOT EXISTS {?cemetery wdt:P18 ?i3.FILTER(str(?i3)>str(?img_cemetery))}\n  SERVICE wikibase:label{ bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,fr\"}\n}", "context": "People recently buried in a cemetery nearby without an image of the grave (P1442) on Wikidata", "target": ""}
{"query": "#title: List of lexemes in French about occupation items with gender\nSELECT DISTINCT ?item ?itemLabel ?sense ?lexeme ?lemma ?genre ?genreLabel WHERE {\n  ?lexeme rdf:type ontolex:LexicalEntry;\n    dct:language wd:Q150;\n    wikibase:lemma ?lemma;\n    ontolex:sense ?sense;\n    wdt:P5185 ?genre.\n  ?sense wdt:P5137 ?item.\n  ?item (wdt:P31/(wdt:P279*)) wd:Q12737077;\n    rdfs:label ?itemLabel.\n  FILTER((LANG(?itemLabel)) = \"fr\")\n  ?genre rdfs:label ?genreLabel.\n  FILTER((LANG(?genreLabel)) = \"fr\")\n}\nORDER BY (?item) (?lemma) (?genre)", "context": "List of lexemes in French associated with occupation items", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?place ?placeLabel ?population ?countryLabel ?coords ?image\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q2977 ;\n        wdt:P131 ?place .\n  ?place wdt:P1082 ?population ;\n         wdt:P17 ?country .\n  OPTIONAL { ?item wdt:P625 ?coords }\n  OPTIONAL { ?item wdt:P18 ?image }\n  FILTER ( ?population < 1000 )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?population  ", "context": "Towns with less than 1000 people that have a cathedral", "target": ""}
{"query": "SELECT distinct ?song ?songLabel ?code\nWHERE\n{\n  ?song wdt:P941 ?book. hint:Prior hint:runFirst true.\n  ?book wdt:P31/wdt:P279* wd:Q7725634. hint:Prior hint:gearing \"forward\".\n  ?song wdt:P31/wdt:P279* wd:Q2188189.  hint:Prior hint:gearing \"forward\".\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC(STRLEN(REPLACE(?code, \" \", \"\")))\n", "context": "Musical works based on literary works", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?nom ?nomLabel (COUNT(?q) AS ?ct) WHERE {\n  ?q (wdt:P19/(wdt:P131*)) wd:Q12694;\n    wdt:P734 ?nom.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?nom ?nomLabel\nLIMIT 30", "context": "Main surnames of people born in the Puy-de-Dôme, France", "target": ""}
{"query": "#title: List of properties which have instances or subclass of humans as possible value with male and female form in French\nSELECT ?property ?propertyLabel ?relation ?relationLabel ?maleform ?femaleform WHERE {\n  ?property p:P2302 ?statement.\n  ?statement ps:P2302 wd:Q21510865;\n    pq:P2308 wd:Q5;\n    pq:P2309 ?relation.\n  ?property rdfs:label ?propertyLabel.\n  ?relation rdfs:label ?relationLabel.\n  FILTER((LANG(?propertyLabel)) = \"fr\")\n  FILTER((LANG(?relationLabel)) = \"fr\")\n  OPTIONAL {\n    ?property wdt:P2521 ?femaleform.\n    FILTER((LANG(?femaleform)) = \"fr\")\n  }\n  OPTIONAL {\n    ?property wdt:P3321 ?maleform.\n    FILTER((LANG(?maleform)) = \"fr\")\n  }\n}", "context": "List of properties which have instances or subclass of humans as possible value with male and female form in French", "target": ""}
{"query": "select distinct ?person1 ?person2 ?Label ?birthday ?occupation where\n{\n  ?person1 wdt:P569 ?birthday . ?person2 wdt:P569 ?birthday . \n  filter (?birthday >= \"1706-01-01\"^^xsd:dateTime) .   filter (?birthday < \"1707-01-01\"^^xsd:dateTime) .\n  ?person1 rdfs:label ?Label . filter(lang(?Label) = \"en\") .\n  ?person2 rdfs:label ?Label . \n  ?person1 wdt:P106 ?occ . ?person2 wdt:P106 ?occ . \n  ?occ rdfs:label ?occupation . filter(lang(?occupation) = \"en\") .\n  filter (str(?person1) > str(?person2))\n}", "context": "Different people with same label / birthday / occupation in a specified year", "target": ""}
{"query": "#defaultView:Map\nSELECT ?lieu ?lieuLabel ?layer ?coord WHERE {\n  wd:Q108366183 p:P840 ?node .\n  ?node ps:P840 ?lieu .\n  ?node pq:P585 ?layer .\n  ?lieu wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Narrative locations of the book Itinerary from Paris to Mont-d'Or", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?countryLabel ?coords ?image\nWHERE \n{\n  ?item wdt:P31 wd:Q1541043 ;\n        wdt:P17 ?country .\n  FILTER NOT EXISTS { ?country wdt:P576 ?x }\n  OPTIONAL { ?item wdt:P625 ?coords }\n  OPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Maps of tombs of unknown soldiers", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?prenomLabel (COUNT(?q) AS ?compte) WHERE {\n  ?q wdt:P19/wdt:P131* ?dpt ; wdt:P735 ?prenom .\n  ?dpt p:P131/ps:P131 wd:Q1152 ; wdt:P31 wd:Q6465 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?prenomLabel\nHAVING ( ?compte > 5 )", "context": "Most common first names (> 5) among people with a Wikidata item and born in Auvergne", "target": ""}
{"query": "#title: Student-Teacher relationship based on entries in 'Das Geistige Berlin' (1897)\n#defaultView:Graph\nSELECT ?student ?studentLabel ?teacher ?teacherLabel ?edgeLabel ?rgb WHERE {\n  ?student p:P1066 ?studentStmt.\n  ?studentStmt ps:P1066 ?teacher;\n    prov:wasDerivedFrom ?studentNode.\n  ?studentNode pr:P248 wd:Q109118295.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  BIND(\"student of\" AS ?edgeLabel)\n  BIND(\"cbcbcb\" AS ?rgb)\n}", "context": "Student-Teacher relationship based on entries in 'Das Geistige Berlin' (1897)", "target": ""}
{"query": "SELECT ?value ?lemma {\n  ?number wdt:P31 wd:Q21199 ; wdt:P1181 ?value .\n  FILTER (?value >= 0 && ?value <= 20) .\n  ?lexeme dct:language wd:Q150 ; wikibase:lemma ?lemma ; ontolex:sense ?sense .\n  ?sense wdt:P5137 ?number .\n}\nORDER BY ?lemma", "context": "Numbers from 0 to 20 sorted alphabetically thanks to lexicographical data", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?givenName ?givenNameLabel (COUNT(DISTINCT ?character) AS ?count) ?authorClassification WHERE {\n  BIND(wd:Q150 AS ?language)\n  ?character wdt:P31/wdt:P279* wd:Q95074;\n             (wdt:P27/wdt:P37)|wdt:P103|wdt:P1412 ?language;\n             wdt:P735 ?givenName;\n             wdt:P170 ?author.\n  OPTIONAL { ?author (wdt:P27/wdt:P37)|wdt:P103|wdt:P1412 ?language. BIND(1 AS ?authorSpeaksLanguage) }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n    ?givenName rdfs:label ?givenNameLabel.\n    ?language rdfs:label ?languageLabel.\n  } hint:Prior hint:runLast false.\n  BIND(IF(BOUND(?authorSpeaksLanguage), ?languageLabel, \"other\"@en) AS ?authorClassification)\n}\nGROUP BY ?givenName ?givenNameLabel ?authorClassification\nORDER BY DESC(?count)", "context": "Given names of French fictional characters, as chosen by French / non-French authors", "target": ""}
{"query": "#defaultView:Map\n#title: Map of members of the International Society of Biocuration (historical and current; affiliation and education institutions)\nSELECT ?autor ?autorLabel ?organization ?organizationLabel ?geo\nWHERE {\n    ?autor wdt:P463 * wd:Q23809291 . \n    ?autor ( wdt:P108 | wdt:P463 | wdt:P1416 | wdt:P69) / wdt:P361* ?organization . \n    ?organization wdt:P625 ?geo .\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }        \n}\n\n\n\n  ", "context": "Map of members of the International Society of Biocuration (historical and current; affiliation and education institutions)", "target": ""}
{"query": "# UK MPs and their reason for *finally* leaving office\nSELECT distinct ?dec ((?count/?decade_total)*100 as ?percent) ?causeLabel ?count\nWITH {\n   select (str(?decade) as ?dec) ?item ?cause where {\n   ?term wdt:P279 wd:Q16707842 . ?item p:P39 ?ps . ?ps ps:P39 ?term .  \n   ?ps pq:P580 ?start . ?ps pq:P582 ?end . ?ps pq:P1534 ?cause .\n   FILTER NOT EXISTS { ?term2 wdt:P279 wd:Q16707842 .\n     ?item p:P39 ?ps2 . ?ps2 ps:P39 ?term2 . ?ps2 pq:P580 ?start2 . filter (?ps != ?ps2) \n     optional { ?ps2 pq:P582 ?end2 } BIND(COALESCE(?end2, ?start2) AS ?check) . filter (?check >= ?end) }\n  # filter out any where they were still serving later on\n   bind(year(?end) as ?year) . filter(?year >= 1870) . filter(?year < 2020) . bind((floor(?year/10)*10) as ?decade) .\n  } \n} AS %members\n\nWHERE {\n  { SELECT ?dec (count(distinct ?item) as ?count) ?cause where \n    { INCLUDE %members } group by ?dec ?cause } \n  { SELECT ?dec (count(distinct ?item) as ?decade_total) where \n    { INCLUDE %members } group by ?dec } \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n#defaultView:BarChart", "context": "UK MPs and their reason for *finally* leaving office", "target": ""}
{"query": "#number of MPs at each general election who had never served before\nselect (concat(str(year(?start)),\"-\",str(month(?start))) as ?date) (count(distinct ?item) as ?count)where\n{ \n  { select distinct ?item (min(?incep) as ?start) \n    where {\n      ?term wdt:P279 wd:Q16707842 . ?item p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P571 ?incep . \n    } group by ?item \n  }\n  filter (year(?start) >= 1870) \n  ?item p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q16707842 . ?term wdt:P571 ?incep .\n  ?ps pq:P2715 ?elec . ?term wdt:P2715 ?elec . \n} group by ?start \n#defaultView:BarChart", "context": "Number of new UK MPs at each general election since 1870 (new = never served before)", "target": ""}
{"query": "#Crewe Alexandra players from Crewe\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?itemDescription ?image WHERE {\n  ?item wdt:P19 wd:Q648810 .\n  ?item wdt:P106 wd:Q937857 .\n  ?item wdt:P54 wd:Q19587 .\n  ?item wdt:P18 ?image\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n", "context": "Crewe Alexandra players from Crewe", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?person ?personLabel ?dateN ?dateM WHERE {\n  ?person wdt:P119 wd:Q2972588 .\n  ?person wdt:P569 ?dateN .\n  ?person wdt:P570 ?dateM .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Chronological timeline of people buried in Cimetière des Carmes", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?coord1\", \"?coord2\", \"?line\", \"?str\", \"?coord1lon\", \"?coord2lon\", \"?coord1lat\", \"?coord2lat\", \"?schlagwortStmt\", \"?ort2\",\"?ort1\", \"?refNode\"]}\nSELECT * WHERE {\n  VALUES ?reisebericht {\n    wd:Q109590352\n  }\n  ?reisebericht rdfs:label ?reiseberichtLabel.\n  FILTER((LANG(?reiseberichtLabel)) = \"de\")\n  ?reisebericht p:P921 ?schlagwortStmt.\n  ?schlagwortStmt ps:P921 ?ort1;\n    pq:P156 ?ort2;\n    prov:wasDerivedFrom ?refNode.\n  ?refNode pr:P854 ?Volltext.\n  ?ort1 wdt:P625 ?coord1.\n  ?ort2 wdt:P625 ?coord2.\n  ?ort1 p:P625 _:b20.\n  _:b20 ps:P625 _:b18;\n    psv:P625 _:b19.\n  _:b19 wikibase:geoLongitude ?coord1lon;\n    wikibase:geoLatitude ?coord1lat.\n  ?ort2 p:P625 _:b23.\n  _:b23 ps:P625 _:b21;\n    psv:P625 _:b22.\n  _:b22 wikibase:geoLongitude ?coord2lon;\n    wikibase:geoLatitude ?coord2lat.\n  BIND(CONCAT(\"LINESTRING (\", STR(?coord1lon), \" \", STR(?coord1lat), \",\", STR(?coord2lon), \" \", STR(?coord2lat), \")\") AS ?str)\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line)\n  ?ort1 rdfs:label ?ort1Label.\n  FILTER((LANG(?ort1Label)) = \"de\")\n}", "context": "Stops in the travel routes of Wilhelm Müller on his way from Leipzig to Bad Schandau in 1820/21", "target": ""}
{"query": "SELECT (COUNT(*) as ?count) { ?item wikibase:statements 0 }", "context": "Number of items without any statements", "target": ""}
{"query": "#title:items without claims count by wiki in sitelinks\nSELECT ?partof (COUNT(*) as ?count)\n{\n  ?item wikibase:statements 0 . \n  [] schema:about ?item ; schema:isPartOf ?partof . \n}\nGROUP BY ?partof", "context": "Number of items without any statements by sitelinks", "target": ""}
{"query": "#title: Taxa nomeados em homenagem a uma mulher (por lugar de nascimento)\n#defaultView:Map\nSELECT ?taxon ?taxonLabel ?pessoa ?pessoaLabel ?lugar ?lugarLabel ?coordenadas\nWHERE \n{\n  ?taxon wdt:P31 wd:Q16521 . \n  ?taxon wdt:P138 ?pessoa . \n  ?pessoa wdt:P21 wd:Q6581072 . \n  ?pessoa wdt:P19 ?lugar .\n  ?lugar wdt:P625 ?coordenadas . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"pt,en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Taxa named after women (by place of birth)", "target": ""}
{"query": "#title: Liste des articles sur Blaise Pascal dans toutes les Wikipédia, classés par taille décroissante de l’article\nSELECT ?sitelink ?site ?title ?taille\nWHERE\n{\n  ?sitelink schema:about wd:Q1290.\n  ?sitelink schema:isPartOf ?site.\n  ?sitelink schema:isPartOf/wikibase:wikiGroup \"wikipedia\" .\n  ?sitelink schema:name ?title.\n  BIND (STRBEFORE(STRAFTER(STR(?site), \"https://\"), \"/\") AS ?endpoint)\n  BIND (STR(?title) AS ?title_nolang)\n  SERVICE wikibase:mwapi\n  {\n    bd:serviceParam wikibase:endpoint ?endpoint.\n    bd:serviceParam wikibase:api \"Generator\".\n    bd:serviceParam mwapi:generator \"revisions\".\n    bd:serviceParam mwapi:titles ?title_nolang.\n    bd:serviceParam mwapi:prop \"revisions\".\n    bd:serviceParam mwapi:rvprop \"size\".\n    ?size wikibase:apiOutput \"revisions/rev[last()]/@size\".\n  }\n  BIND ( xsd:integer(?size) AS ?taille )\n}\nORDER BY DESC(?taille)", "context": "List of articles on Blaise Pascal in all Wikipedias, sorted by decreasing article size", "target": ""}
{"query": "#title:Textes sur la Wikisource en français par des personnes nées dans le Puy-de-Dôme\nSELECT DISTINCT ?personLabel ?bookLabel ?page WHERE {\n    ?book wdt:P50 ?person.\n    ?person wdt:P19 ?place.\n    ?place wdt:P131+ wd:Q12694. \n    ?page schema:about ?book .\n    ?page schema:isPartOf <https://fr.wikisource.org/> .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}\nORDER BY ?personLabel", "context": "Texts on French Wikisource by people born in Puy-de-Dôme", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?highest ?highestLabel ?elevation\nWHERE \n{\n  ?item wdt:P31 wd:Q3624078. # is a sovereign country\n  ?item wdt:P610 ?highest. # has \"highest point\" statement\n  OPTIONAL {?highest wdt:P2044 ?elevation} # elevation of highest point\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv,en\". } \n}\nORDER BY DESC(?elevation)", "context": "List of countries by the highest elevation point", "target": ""}
{"query": "# number of distinct seats returning someone who chose to sit elsewhere each year\n# probably missing Brougham in 1830 (became a peer before making the decision)\nSELECT distinct (concat(str(year(?start)),\"-\",str(month(?start))) as ?date) (count(distinct ?ps) as ?count) where\n{\n  ?term wdt:P279 wd:Q16707842 . \n  ?term wdt:P2715 ?elec . ?term wdt:P571 ?start . \n  filter(year(?start) >= 1830) . filter(year(?start) <= 1910) \n  optional { ?mp p:P39 ?ps . ?ps ps:P39 ?term . ?ps pq:P2715 ?elec . ?ps pq:P1534 wd:Q50393121 }\n} group by ?start order by ?start\n#defaultView:BarChart", "context": "Number of distinct (UK parliament) seats returning someone who chose to sit elsewhere each year, 1830-1910", "target": ""}
{"query": "SELECT ?service ?serviceLabel ?endpoint ?documentation ?source WHERE {\n  ?service p:P6269 ?statement.\n  ?statement ps:P6269 ?endpoint ;\n     pq:P2700 wd:Q64490175.\n  OPTIONAL { ?statement (pq:P973 | pq:P2078) ?documentation }\n  OPTIONAL { ?statement pq:P1324 ?source }\n  FILTER NOT EXISTS { ?statement wikibase:rank wikibase:DeprecatedRank }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?endpoint)\n", "context": "List of services that support the OpenRefine Reconciliation service", "target": ""}
{"query": "#defaultView:ImageGrid{\"hide\" :[\"?img\"]}\n#title: Things depicted in art works of the Khalili Collections\nSELECT (?link as ?q) (?nameplusdesc AS ?qLabel) (SAMPLE(?image) AS ?img) WHERE {\n  ?work wdt:P195/wdt:P361 wd:Q63160499; wdt:P18 ?image ; wdt:P973 ?link; wdt:P180 ?depicted.  \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                            ?depicted rdfs:label ?name . ?depicted schema:description ?desc }\n  BIND(IF(BOUND(?desc), CONCAT(?name, \", \", ?desc), ?name) AS ?nameplusdesc)\n} GROUP BY ?link ?nameplusdesc\nORDER BY UCASE(?qLabel)", "context": "Things depicted in art works in the Khalili Collections", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?countryLabel ?area ?population ?density ?coords\nWHERE \n{\n  ?item p:P31 ?statement ;\n        wdt:P17 ?country ;\n        wdt:P625 ?coords .\n  ?statement ps:P31/wdt:P279* wd:Q10864048 .\n  FILTER NOT EXISTS { ?statement pq:P582 ?x }\n  FILTER NOT EXISTS { ?country wdt:P31 wd:Q3024240 }\n  FILTER NOT EXISTS { ?item wdt:P576 ?x }\n  \n  ?item p:P2046 ?areaStatement .\n  { FILTER NOT EXISTS { ?areaStatement pq:P518 ?x } } #UNION\n  #{ ?areaStatement pq:P518 wd:Q16868672 }\n  ?areaStatement psv:P2046 [\n        wikibase:quantityAmount ?baseArea;\n        wikibase:quantityUnit [\n          p:P2370/psv:P2370 [\n            wikibase:quantityAmount ?unitArea;\n            wikibase:quantityUnit wd:Q25343\n          ]\n        ]\n      ].\n  BIND(ROUND(?baseArea * ?unitArea / 1000000) AS ?area) .\n  FILTER (?area >= 100000)\n  \n  ?item wdt:P1082 ?population .\n  BIND(ROUND(100 * ?population / ?area) / 100 AS ?density)\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?area)", "context": "Population density of administrative subdivisions exceeding 100,000 km² and 100 inhabitants / km²", "target": ""}
{"query": "#title: Query fo all Scottish agencies (should be 9+45+6+7+11+32+14+31+56+28 = 239 )\nSELECT ?item ?itemLabel (SAMPLE(?typeLabel) AS ?_type) WHERE {\n  { VALUES ?type { wd:Q108838260 wd:Q3343298 wd:Q55657615 wd:Q524778 wd:Q169180 } # Exec agencies (9) + NDPBs (45) + commissioners (7)\n  ?item wdt:P31 ?type ;\n        wdt:P1001 wd:Q22 .\n  MINUS { ?item wdt:P31 wd:Q5532360 }  \n   }\n  UNION\n  { ?item wdt:P31 wd:Q270791; # Public corps (6)\n          wdt:P127 wd:Q32521 .\n  }\n  UNION\n  { VALUES ?type { wd:Q7139410 wd:Q1412224 wd:Q3044788 wd:Q327333 wd:Q16931297 } # Tribunals (11) + other (28)\n  ?item wdt:P31 ?type ;\n        wdt:P131|wdt:P1001 wd:Q22 .\n  MINUS { ?item wdt:P1001 wd:Q1156248 }\n  MINUS { ?item p:P1001 ?js . ?js pq:P582 [] }\n  }\n  UNION\n  { VALUES ?type { wd:Q21451686 wd:Q83425186 wd:Q20667323 } # Local authorities (32) + Health boards (14)\n    ?item wdt:P31 ?type .\n  }\n  UNION\n  { ?item wdt:P361 wd:Q4993654 . # Courts (56)\n  }\n  MINUS { ?item wdt:P576 [] }\n  OPTIONAL { ?item wdt:P31 ?type }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". ?type rdfs:label ?typeLabel . ?item rdfs:label ?itemLabel }\n} GROUP BY ?item ?itemLabel", "context": "List of government agencies in Scotland", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?weekday (count(distinct(?item)) as ?count) WHERE {\n  wd:Q362 wdt:P580 ?od ; wdt:P582 ?do. \n  ?item wdt:P20 wd:Q160139 .\n  ?item p:P570 [ps:P570 ?date ; psv:P570/wikibase:timePrecision ?precision ] filter(?precision = 11 && day(?date) != 1 && ?date > ?od && ?date < ?do ). \n  BIND( DAY( ?date ) AS ?q ) .\n  BIND( MONTH( ?date ) + IF( MONTH( ?date ) < 3, 12, 0 ) AS ?m ) .\n  BIND( YEAR( ?date ) - IF( MONTH( ?date ) < 3, 1, 0 ) AS ?_year ) .\n  BIND( ?_year - FLOOR( ?_year / 100 ) * 100 AS ?K ) . # modulo\n  BIND( FLOOR( YEAR( ?date ) / 100 ) AS ?J ) .\n  BIND( ?q + FLOOR( 13 * ( ?m + 1 ) / 5 ) + ?K + FLOOR( ?K / 4 ) + FLOOR( ?J / 4 ) - 2 * ?J AS ?_h ) .\n  BIND(( ?_h - FLOOR( ?_h / 7 ) * 7) as ?day) . \n  BIND(if(?day=0,\"06 (Saturday)\",if(?day=1,\"07 (Sunday)\",if(?day=2,\"01 (Monday)\",if(?day=3,\"02 (Tuesday)\",if(?day=4,\"03 (Wednesday)\",if(?day=5,\"04 (Thursday)\",if(?day=6,\"05 (Friday)\",\"\"))))))) as ?weekday) . \n} group by ?weekday order by asc(?weekday) ", "context": "Which day of the week people died on in the Mauthausen concentration camp", "target": ""}
{"query": "#title:word cloud articles without statements MyLangWikipedia (switch to \"Table\" view to list, at left border of screen)\n#defaultView:BubbleChart\nSELECT\n  ?word\n  (COUNT(*) as ?count) \n  (URI(CONCAT(\"https://www.wikidata.org/w/index.php?search=inlabel%3A\",?word,\"%40[AUTO_LANGUAGE]+-haswbstatement%3A%2A&profile=advanced&fulltext=1&ns=1\")) as ?search)\nWITH\n{\n  SELECT ?item (CONCAT(?n, \" 1 1 1 1 1 1 1 1 \") as ?string) \n  {\n    ?item wikibase:statements 0 .\n    [] schema:about ?item ; schema:name ?n ; schema:isPartOf <https://[AUTO_LANGUAGE].wikipedia.org/>\n  }\n  LIMIT 20000\n} as %aa\nWITH\n{\n  SELECT (str(?nv-1) as ?ord) { [] wdt:P5176 ?i ; wdt:P1181 ?nv . FILTER( ?nv < 8) } \n} as %i  \nWHERE\n{\n  INCLUDE %i\n  INCLUDE %aa\n  BIND(  CONCAT(\"^([^ ]+ ){\",?ord,\"}([^ ]+) .+$\") as ?re)         \n  BIND(  REPLACE(?string, ?re, \"$2\") as ?word)\n  # FILTER( ?word != \"1\" && )\n  FILTER( strlen(?word) > 4)           \n} \nGROUP BY ?word\nORDER BY DESC(?count) \nLIMIT 100", "context": "Wordcloud articles without statements of MyLang Wikipedia", "target": ""}
{"query": "#title:Œuvres représentant la neige\n#defaultView:ImageGrid\nSELECT ?q ?qLabel (SAMPLE(?img) AS ?image) WHERE {\n  ?q wdt:P180 ?depeint ; wdt:P18 ?img .\n  ?depeint ?prop wd:Q7561 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?q ?qLabel", "context": "Artworks picturing snow", "target": ""}
{"query": "SELECT (?year_d as ?year) (COUNT(?futbolari)as ?count) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?futbolari wdt:P106 wd:Q937857.\n  ?futbolari wdt:P570 ?heriotza_data.\n  ?futbolari wdt:P569 ?jaiotza_data.\n  BIND (year(?heriotza_data) as ?year_d).\n  BIND (year(?jaiotza_data) as ?year_j).\n  BIND (?year_d - ?year_j as ?age).\n  FILTER (?age < 40)\n}\nGROUP by ?year_d", "context": "Graph of football players who died per year", "target": ""}
{"query": "#defaultView:ImageGrid{\"hide\" :[\"?img\"]}\n#title: Things depicted in art works of the Khalili Collections\nSELECT (?link as ?q) (?nameplusdesc AS ?qLabel) (SAMPLE(?image) AS ?img) WHERE {\n  ?work wdt:P195/wdt:P361 wd:Q63160499; wdt:P18 ?image ; wdt:P973 ?link; wdt:P180 ?depicted.  \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                            ?depicted rdfs:label ?name . ?depicted schema:description ?desc }\n  BIND(IF(BOUND(?desc), CONCAT(?name, \", \", ?desc), ?name) AS ?nameplusdesc)\n} GROUP BY ?link ?nameplusdesc\nORDER BY UCASE(?qLabel)", "context": "Things depicted in art works of the Khalili Collections", "target": ""}
{"query": "#title:Personnes représentées sur le tableau \"Le Sacre de Napoléon\" du 2 décembre 1804 par Jacques-Louis David\n#defaultView:ImageGrid\nSELECT ?img ?depeint ?depeintLabel ?depeintDescription WHERE {\n  wd:Q1231009 wdt:P180 ?depeint .\n  ?depeint wdt:P31 wd:Q5 ; wdt:P18 ?img\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "People represented on the painting \"Le Sacre de Napoléon\" from 1804 by Jacques-Louis David", "target": ""}
{"query": "SELECT * WHERE {\n    ?place wdt:P31 wd:Q42744322 ;               # German municipalities\n           wdt:P1448 ?name .                    # their official names\n    BIND(lcase(?name) as ?lemma)                # use the names in lower case as lemma\n    ?lexeme wikibase:lemma ?lemma ;             # lexemes with that lemma\n            wikibase:lexicalCategory wd:Q24905; # verbs\n            dct:language wd:Q188                # in German\n}\n", "context": "German cities whose names are also verbs (using Lexemes)", "target": ""}
{"query": "# (Thanks, James Heald, for the original query!)\n#defaultView:Map\nSELECT ?station ?line (CEIL(?depth/10)*10 as ?layer) ?coord {\n  SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q2660080 ; gas:out ?station ; gas:out1 ?depth ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . }\n  FILTER(?station != wd:Q2660080)\n                      ?pred wdt:P625 ?cds.\n                      BIND(IF(xsd:double(?depth/10) = xsd:integer(?depth/10),?cds,\"\") as ?coords)\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  values ?country { wd:Q211 wd:Q37 wd:Q191 wd:Q159 wd:Q184 } ?station wdt:P17 ?country . \n  # only Baltics or Russia or Belarus\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". }\n  ?station wdt:P625 ?coord.\n}\n", "context": "Railway stations in the Baltics", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?libellé ?coord ?image ?layer WHERE {\n  ?item wdt:P138 ?type ; wdt:P625 ?coord .\n  ?type wdt:P279* wd:Q726 .\n  OPTIONAL { ?item wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" .\n                         ?type rdfs:label ?layer .\n                         ?item rdfs:label ?libellé } \n}", "context": "Map of places names after horses", "target": ""}
{"query": "#Latvian citizens by occupation\n\n#defaultView:BubbleChart\nSELECT ?cid ?cause (count(*) as ?count)\nWHERE\n{\n\t?pid wdt:P31 wd:Q5 .\n    ?pid wdt:P27 wd:Q211 .\n\t?pid wdt:P106 ?cid .\n\tOPTIONAL {\n\t\t?cid rdfs:label ?cause \n\t\tfilter (lang(?cause) = \"lv\")\n\t}\n}\nGROUP BY ?cid ?cause\nORDER BY DESC(?count) ASC(?cause)", "context": "Latvian citizens by occupation", "target": ""}
{"query": "#defaultView:Map\nSELECT ?hobi_komun ?hobi_komunLabel ?lat ?lon ?coord ?pertsona_hilak WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?hobi_komun wdt:P31 wd:Q734271;\n    wdt:P607 wd:Q10859;\n    p:P625 _:b5.\n  _:b5 psv:P625 _:b4.\n  _:b4 wikibase:geoLatitude ?lat;\n    wikibase:geoLongitude ?lon;\n    wikibase:geoGlobe ?globe.\n  _:b5 ps:P625 ?coord.\n  ?hobi_komun wdt:P1120 ?pertsona_hilak.\n}", "context": "Map of the mass graves from the Spanish Civil War", "target": ""}
{"query": "#title:Shijian (SJ) satellites \nSELECT ?item ?itemLabel ?ord ?itemDescription ?launchdt ?launchvehLabel ?launchpointLabel ?orbitLabel ?mass ?cospar ?sts ?sls \n{\n\t?item p:P179 [pq:P1545 ?ord ; ps:P179 wd:Q11452851 ]\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n    OPTIONAL { ?item wdt:P619 ?launchdt }\n    OPTIONAL { ?item wdt:P375 ?launchveh }\n    OPTIONAL { ?item wdt:P1427 ?launchpoint }\n    OPTIONAL { ?item wdt:P247 ?cospar }   \n    OPTIONAL { ?item wdt:P2067 ?mass }\n    OPTIONAL { ?item wdt:P522 ?orbit }\n  \n    ?item wikibase:statements ?sts ; wikibase:sitelinks ?sls \n}\nORDER BY xsd:integer(REPLACE(?ord,\"^(\\\\d+)(-\\\\d+|[A-Z])?$\",\"$1\")) ?ord", "context": "Shijian (SJ 实践) experimental satellites", "target": ""}
{"query": "#Deaths\nSELECT DISTINCT ?item ?itemLabel ?age ?birth_date WHERE {\n  ?item wdt:P31 wd:Q5;\n    (p:P569/psv:P569) _:b8.\n  _:b8 wikibase:timeValue ?birth_date;\n    wikibase:timePrecision ?precision.\n  ?item wdt:P106 wd:Q13414980;\n    wdt:P54 wd:Q1034556.\n  FILTER(NOT EXISTS { ?item wdt:P570 ?dead. })\n  BIND(((YEAR(NOW())) - (YEAR(?birth_date))) - (IF(((MONTH(NOW())) < (MONTH(?birth_date))) || (((MONTH(NOW())) = (MONTH(?birth_date))) && ((DAY(NOW())) < (DAY(?birth_date)))), 1 , 0 )) AS ?age)\n  FILTER(?age >= 80 )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?age)", "context": "Age of living Hawthorn FC players", "target": ""}
{"query": "#title:5 people died on their 50th birthday\nSELECT ?item ?itemLabel ?dob ?dod WHERE {\n  ?item (p:P569/psv:P569) _:b44.\n  _:b44 wikibase:timePrecision 11;\n    wikibase:timeValue ?dob.\n  ?item (p:P570/psv:P570) _:b45.\n  _:b45 wikibase:timePrecision 11;\n    wikibase:timeValue ?dod.\n  #FILTER(?precisionDob = 11 )\n  #FILTER(?precisionDod = 11 )\n  FILTER((DAY(?dob)) = (DAY(?dod)))\n  FILTER((MONTH(?dob)) = (MONTH(?dod)))\n  FILTER( ((YEAR(?dod)) - (YEAR(?dob))) = 50)\n  ?item rdfs:label ?itemLabel. FILTER(LANG(?itemLabel)=\"de\")\n}\nLIMIT 5", "context": "People died on their 50th birthday", "target": ""}
{"query": "#title:Carte des villes jumelées (ou partenaires) de communes du Puy-de-Dôme\n#defaultView:Map\nSELECT ?commune ?twin ?twinLabel ?layer ?coord WHERE {\n  ?commune wdt:P131+ wd:Q12694 ; wdt:P190 ?twin .\n  ?twin wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                         ?commune rdfs:label ?layer }\n}", "context": "Map of twinned towns (or partners) of municipalities of Puy-de-Dôme", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?libellé ?coord ?image ?layer WHERE {\n  ?item wdt:P138 ?type ; wdt:P625 ?coord .\n  ?type wdt:P279* wd:Q726 .\n  OPTIONAL { ?item wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" .\n                         ?type rdfs:label ?layer .\n                         ?item rdfs:label ?libellé } \n}", "context": "Map of the places named in reference the horse", "target": ""}
{"query": "#defaultView:Map\nSELECT ?bridge ?bridgeLabel ?coords\nWHERE \n{\n  ?bridge wdt:P31/wdt:P279* wd:Q12280 ;\n          wdt:P131 wd:Q1829 ;\n          wdt:P625 ?coords .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of bridges in Kaliningrad", "target": ""}
{"query": "SELECT ?item ?itemLabel (COUNT(DISTINCT ?sense) AS ?count) {\n  ?sense wdt:P5137 ?item .\n  OPTIONAL { ?item rdfs:label ?itemLabel . FILTER(LANG(?itemLabel) = \"en\") }\n}\nGROUP BY ?item ?itemLabel\nORDER BY DESC(?count)\nLIMIT 100", "context": "Wikidata items which are the most linked by lexemes using the property “item for this sense”", "target": ""}
{"query": "# by Jura1; December 20, 2021 - Merry Xmas\n#title:actors in Christmas movies: by frequency, with film titles and sample film item\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?itemDescription \n  (SAMPLE(?img) AS ?image) \n  (COUNT(DISTINCT ?film) AS ?count) \n  (GROUP_CONCAT(DISTINCT ?title; SEPARATOR = \"; \") AS ?films)\n  (SAMPLE( ?film ) as ?sample_film)\nWHERE \n{\n  { ?film wdt:P136 wd:Q28026639 } UNION { ?film wdt:P9215 wd:Q3517772 ; wdt:P31 wd:Q11424 } \n  ?film wdt:P161 ?item.\n  OPTIONAL { ?item wdt:P18 ?img }\n  OPTIONAL { ?film wdt:P1476 ?title }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription\nORDER BY DESC (?count)\nLIMIT 100", "context": "Actors in Christmas movies: by frequency, with film titles and sample film item", "target": ""}
{"query": "# by Jura1; December 20, 2021 - Merry Xmas\n#title:filming locations of Christmas movies, with film titles and sample film item\n#defaultView:Map{\"hide\":[\"?coor\"]}\nSELECT \n  ?item ?itemLabel ?itemDescription \n  ?coor  \n  (SAMPLE ( ?img ) as ?image)\n  (COUNT(DISTINCT ?film) AS ?count) \n  (GROUP_CONCAT(DISTINCT ?title; SEPARATOR = \"; \") AS ?films)\n  (SAMPLE( ?film ) as ?sample_film)\nWHERE \n{\n  { ?film wdt:P136 wd:Q28026639 } UNION { ?film wdt:P9215 wd:Q3517772 ; wdt:P31 wd:Q11424 } \n  ?film wdt:P915 ?item .\n  ?item wdt:P625 ?coor .\n  OPTIONAL { ?item wdt:P18 ?img }\n  OPTIONAL { ?film wdt:P1476 ?title }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription ?coor\nORDER BY DESC (?count)", "context": "Filming locations of Christmas movies, with film titles and sample film item", "target": ""}
{"query": "# by Jura1; December 20, 2021 - Merry Xmas\n#title:recent Christmas movies lacking filming location\nSELECT DISTINCT\n  ?item ?itemLabel ?itemDescription (MIN(YEAR(?pub)) as ?year)\nWHERE \n{\n  { ?item wdt:P136 wd:Q28026639 } UNION { ?item wdt:P9215 wd:Q3517772 ; wdt:P31 wd:Q11424 } \n  MINUS { ?item wdt:P915 [] }\n  ?item wdt:P577 ?pub .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription\nORDER BY DESC(?year)\nLIMIT 200", "context": "Recent Christmas movies lacking filming location", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (COUNT(?item) as ?count) ?herrialdeaLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P1296 ?cat .\n  ?item wdt:P1417 ?brit. \n  ?item wdt:P2924 ?rus. \n  ?item wdt:P4342 ?norske.\n  ?item wdt:P17 ?herrialdea .\n}\nGROUP by ?herrialdeaLabel\nORDER by DESC(?count)\n", "context": "Places that appear in the Encyclopaedia Britannica, the Great Russian Encyclopaedia, the Great Catalan Encyclopaedia and the Store Norske Leksikon", "target": ""}
{"query": "#defaultView:ImageGrid\n#title: Christmas traditions around the world \nSELECT DISTINCT ?item ?itemLabel ?image ?article\nWHERE \n{\n  # Christmas around the world\n  {?item wdt:P31?/wdt:P279* wd:Q19809 . } \n  UNION\n  {?item wdt:P31?/wdt:P279* wd:Q78189229 . }\n  UNION\n  #Christmas traditions\n  {?item wdt:P31?/wdt:P279* wd:Q717040 . } \n  \n  ?item wdt:P18 ?image .\n  OPTIONAL { ?article schema:about ?item ;\n                      schema:inLanguage \"en\" ;\n                      schema:isPartOf [ wikibase:wikiGroup \"wikipedia\" ] }\n\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } # Helps get the label in your language, if not, then en language\n}\n\n", "context": "Christmas traditions around the world", "target": ""}
{"query": "SELECT ?party ?partyLabel ?firstDate ?total_MPs WITH {\n  SELECT ?party (MIN(?date) AS ?firstDate) (count(distinct ?mp) as ?total_MPs) WHERE {\n    ?mp p:P39 ?mpStatement . ?mpStatement ps:P39 ?term . ?term wdt:P279 wd:Q16707842.\n    ?mpStatement pq:P580 ?date. \n    ?mpStatement pq:P4100 ?party.\n    wd:Q918378 wdt:P585 ?1935elec . filter(?date >= ?1935elec)\n  }\n  GROUP BY ?party\n} AS %parties WHERE {\n  INCLUDE %parties.\n  filter(?total_MPs = 1)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY desc(?firstDate) ?partyLabel", "context": "UK parties since 1935 that have only ever been represented by a single MP", "target": ""}
{"query": "SELECT \n(GROUP_CONCAT(?occupationLabel; SEPARATOR= \", \") as ?occupations)\n?item ?linkcount ?itemLabel\nWHERE \n{\n  ?item wdt:P27 wd:Q155 . \n  ?item wikibase:sitelinks ?linkcount .\n  \n  OPTIONAL{\n    ?item wdt:P106 ?occupation .\n    ?occupation rdfs:label ?occupationLabel . \n    FILTER(LANG(?occupationLabel)=\"pt\") .\n    } .\n  \n  ?item rdfs:label ?itemLabel . \n  FILTER(LANG(?itemLabel)=\"pt\") .\n}\nGROUP BY ?item ?linkcount ?itemLabel\nORDER BY DESC(?linkcount)\nLIMIT 100", "context": "Brazilians with most Wikipedia pages across languages", "target": ""}
{"query": "SELECT ?suburb ?suburbLabel ?coordinate_location WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?suburb wdt:P31 wd:Q188509;\n    wdt:P17 wd:Q664.\n  OPTIONAL { ?suburb wdt:P625 ?coordinate_location. }\n}\nLIMIT 10000", "context": "Map of New Zealand suburbs", "target": ""}
{"query": "#defaultView:Graph\n\nSELECT DISTINCT ?a ?aLabel ?b ?bLabel\n\n{?a wdt:P31 wd:Q5;\n    wdt:P569 ?birth;\n    wdt:P737 ?b .\n ?b wdt:P31 wd:Q5 .\n \n FILTER (?birth > \"1590-01-01\"^^xsd:dateTime && ?birth < \"1750-01-01\"^^xsd:dateTime)   \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n \n }\n", "context": "Graph of influences in the Age of Enlightenment", "target": ""}
{"query": "SELECT (SAMPLE(?familyNameLabel_) AS ?familyNameLabel) ?astronauts ?items (CONCAT(SUBSTR(STR(?astronauts / ?items * 100), 1, 5), \"%\") AS ?percent) WITH {\n  SELECT ?familyName (COUNT(DISTINCT ?astronaut) AS ?astronauts) WHERE {\n    ?astronaut wdt:P31 wd:Q5;\n               wdt:P450 ?mission;\n               wdt:P734 ?familyName.\n  }\n  GROUP BY ?familyName\n  HAVING(?astronauts > 1)\n} AS %astronautNames WITH {\n  SELECT ?familyName (COUNT(*) AS ?items) WHERE {\n    INCLUDE %astronautNames.\n    ?item wdt:P734 ?familyName.\n  }\n  GROUP BY ?familyName\n} AS %names WHERE {\n  INCLUDE %astronautNames.\n  INCLUDE %names.\n  ?familyName wdt:P1705 ?familyNameLabel_.\n}\nGROUP BY ?familyName ?astronauts ?items\nORDER BY DESC(?astronauts / ?items) ?familyNameLabel", "context": "Family names shared by astronauts (along with how often they occur in Wikidata)", "target": ""}
{"query": "#defaultView:ImageGrid\n#title: Bills and coins of Brazilian Real\nSELECT ?item ?itemLabel ?pic\nWHERE \n{\n  {?item wdt:P279* wd:Q110313058 .} \n  UNION\n  {?item wdt:P279* wd:Q107626735 .} \n\n   ?item wdt:P18 ?pic .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}\n# Query by TiagoLubiana; License: CC0", "context": "Bills and coins of Brazilian Real (with pictures)", "target": ""}
{"query": "SELECT DISTINCT ?rank ?rankLabel ?personne ?personneLabel WITH {\n  SELECT ?rank (COUNT(DISTINCT ?inferior) AS ?rankOrder) {\n    ?rank wdt:P361 wd:Q163700 ; wdt:P3729+ ?inferior .\n  }\n  GROUP BY ?rank\n} AS %i\nWHERE {\n  INCLUDE %i\n  ?personne wdt:P31 wd:Q5 ; p:P166 [ ps:P166 ?rank ; pq:P585 \"2021-12-31T00:00:00Z\"^^xsd:dateTime ] .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\" }\n}\nORDER BY ?rankOrder ?personneLabel", "context": "People awarded by the French Legion of Honour on Dec 31, 2021", "target": ""}
{"query": "# 7030 Joes, but only 389 with heights\nselect ?joe ?joeLabel ?height ?average_height ?difference {\n  ?joe wdt:P735 wd:Q17862013 .\n  ?joe p:P2048 ?statement .\n  ?statement a wikibase:BestRank .\n  ?statement psn:P2048/wikibase:quantityAmount ?height .\n  bind (abs(?height - ?average_height) as ?difference)\n  { \n    select (avg(?height) AS ?average_height)  {\n      ?joe wdt:P735 wd:Q17862013 .\n      ?joe p:P2048 ?statement .\n      ?statement a wikibase:BestRank .\n      ?statement psn:P2048/wikibase:quantityAmount ?height .\n    }\n  }\n  service wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by asc (?difference) limit 30", "context": "Average height of people named Joe", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?item WHERE {\n      ?item p:P570 ?statement_0.\n      ?statement_0 psv:P570 ?statementValue_0.\n      ?statementValue_0 wikibase:timePrecision ?precision_0.\n      hint:Prior hint:rangeSafe \"true\"^^xsd:boolean.\n      FILTER(?precision_0 >= 9 )\n      ?statementValue_0 wikibase:timeValue ?P570_0.\n      hint:Prior hint:rangeSafe \"true\"^^xsd:boolean.\n      FILTER((\"+1961-00-00T00:00:00Z\"^^xsd:dateTime <= ?P570_0) && (?P570_0 < \"+1962-00-00T00:00:00Z\"^^xsd:dateTime))\n      ?item p:P27 ?statement1.\n      ?statement1 (ps:P27/(wdt:P279*)) wd:Q668.\n      ?item p:P106 ?statement2.\n      ?statement2 (ps:P106/(wdt:P279*)) wd:Q36180.\n    }\n    LIMIT 100\n  }\n}", "context": "Indian writers who died in 1961", "target": ""}
{"query": "SELECT ?sitelinks ?author ?authorLabel  WHERE {\n  SERVICE bd:sample { ?work wdt:P921 ?topic . bd:serviceParam bd:sample.limit 1000 }     \n  ?work wdt:P50 ?author .\n  FILTER NOT EXISTS {?author wdt:P101 ?fow }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?author wikibase:sitelinks ?sitelinks .\n}\nGROUP BY ?sitelinks ?score ?author ?authorLabel \nORDER BY DESC(?sitelinks) \nLIMIT 200\n\n", "context": "Authors without field of work but with topic-tagged publications", "target": ""}
{"query": "SELECT DISTINCT ?name ?nameLabel (COUNT(DISTINCT ?person) AS ?count)\nWHERE \n{\n  ?name wdt:P31/wdt:P279* wd:Q202444 ;\n        p:P527 ?sJean ;\n        p:P527 ?sSecondName .\n  ?sJean ps:P527 wd:Q7521081 ;\n         pq:P1545 \"1\" .\n  ?sSecondName ps:P527 ?secondName ;\n               pq:P1545 \"3\" .\n  OPTIONAL { ?person wdt:P735 ?name }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?name ?nameLabel\nORDER BY DESC(?count) ?secondNameLabel ?nameLabel", "context": "Compound first names starting with \"John\" (and the number of uses on Wikidata)", "target": ""}
{"query": "#title: Presidentes do Brasil com mais premiações\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?image \n(\"Exemplo de premiação:\" as ?s)\n?award_label ?sample_award ?sample_awardLabel\nWHERE\n{\n  \n {\n  SELECT ?item ?image\n  (COUNT(DISTINCT ?award) as ?award_count)\n  (CONCAT(\"Número de premiações: \", STR( ?award_count )) AS ?award_label )\n  (SAMPLE(?award) as ?sample_award)\n  WHERE \n  {\n    ?item wdt:P31 wd:Q5 .\n    ?item wdt:P39 wd:Q5176750 .\n    ?item wdt:P166 ?award .\n    ?item wdt:P18 ?image .\n  }\n   GROUP BY  ?item ?itemLabel ?image ?award_label\n\n }\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"pt-br\",\"pt\". } # Helps get the label in your language, if not, then en language\n}\n  \nORDER BY DESC(?award_count)\n", "context": "Presidents of Brazil with the most awards", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?parish\", \"?coords\", \"?layer\"]}\nSELECT ?parishLabel ?coords ?partLabel ?synodLabel (?partLabel as ?layer)\nWHERE \n{\n  ?parish wdt:P31 wd:Q5124673. \n  ?parish wdt:P361 ?part .\n  ?part wdt:P361 ?synod .\n  \n      wd:Q68815318 wdt:P625 ?center. # Place that is the centre point of the search\n  \n    SERVICE wikibase:around { \n      ?parish wdt:P625 ?coords . \n      bd:serviceParam wikibase:center ?center . \n      bd:serviceParam wikibase:radius \"500\" \n  } \n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Locations of parishes across Scotland", "target": ""}
{"query": "#defaultView:Map\nselect distinct ?item ?itemLabel ?itemDescription ?pob ?coord where {\n    ?item wdt:P31 wd:Q5;  \n          wdt:P39 wd:Q19546;\n          wdt:P19 ?pob.\n  ?pob wdt:P625 ?coord.\n\n   \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?sitelinks)", "context": "Map of where Roman Catholic Popes were born", "target": ""}
{"query": "#defaultView:Map\nSELECT ?koord ?item ?itemLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P1296 ?GEC. hint:Prior hint:runFirst true.\n  ?item wdt:P1417 ?EB.\n  ?item wdt:P2924 ?GRE. \n  ?item wdt:P4342 ?norske.\n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P19 ?jaio. \n  ?jaio wdt:P625 ?koord.\n}", "context": "Birth place of people who are described in the Encyclopaedia Britannica, the Great Russian Encyclopedia, the Great Catalan Encyclopedia and the Store Norske Leksikon", "target": ""}
{"query": "SELECT ?New_Year ?New_YearLabel (SAMPLE(?image) as ?img) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id,en\". }\n  ?New_Year wdt:P279/wdt:P279* wd:Q34812.\n  OPTIONAL { ?New_Year wdt:P18 ?image. }\n}\nGROUP BY ?New_Year ?New_YearLabel\n#defaultView:ImageGrid", "context": "Various kinds of New Year's celebrations in the world", "target": ""}
{"query": "#title:World map of recent censuses known at Wikidata for each decade (select decade on the right side)\n#defaultView:Map{\"hide\":[\"?loc\",\"?locLabel\",\"?dg\",\"?geoshape\",\"?decade\"], \"layer\": \"?decade\"}\n#from https://www.wikidata.org/wiki/Property_talk:P8701 query, by Jura1, 2022-01-10\nSELECT \n  ?loc ?locLabel ?geoshape ?decade \n  ?dg\n  (URI(concat(str(?dg),\"#P8701\")) as ?available_census) \n  (CONCAT(?locLabel, \": \", GROUP_CONCAT(DISTINCT ?year; separator=\", \")) as ?available_censusLabel) \nWHERE\n{\n  ?dg wdt:P8701 / wdt:P585 ?d . \n  FILTER( year(?d) > 1959) \n  BIND( str(YEAR(?d)) as ?year)\n  BIND( CONCAT(str(FLOOR(YEAR(?d)/10)*10),\"s\") as ?decade) \n  ?dg wdt:P276 ?loc .\n  OPTIONAL { ?loc wdt:P3896 ?geoshape }\n  BIND(URI(concat(str(?dg),\"#P8701\")) as ?available_census) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}  \nGROUP BY ?loc ?locLabel ?geoshape ?decade ?dg\nORDER BY DESC(?decade)", "context": "World map of recent censuses known at Wikidata for each decade", "target": ""}
{"query": "#title:Non-English labels for Khalili Collections items\nSELECT ?lang ?count WITH {\n  SELECT ?work ?label WHERE {\n    ?work wdt:P195?/wdt:P361? wd:Q63160499. # Khalili Collections, or individual collections, or objects within collections\n    ?work (rdfs:label|skos:altLabel) ?label\n    FILTER(lang(?label) != \"en\") } } AS %core\nWHERE { {\n  SELECT ?lang (COUNT(?label) AS ?count) WHERE {\n    INCLUDE %core.\n    BIND(lang(?label) AS ?langcode).\n    OPTIONAL { ?language wdt:P218 ?langcode; rdfs:label ?langname FILTER(lang(?langname) = \"en\")}\n    BIND(IF (BOUND(?langname), ?langname, ?langcode) AS ?lang) \n    } GROUP BY ?lang\n } UNION {\n    SELECT (\"TOTAL\" AS ?lang) (COUNT(?label) AS ?count) WHERE {\n    INCLUDE %core.\n    } }\n} order BY DESC(?count) ?lang", "context": "Non-English labels for a set of objects, with the names of the languages", "target": ""}
{"query": "SELECT ?thing ?thingLabel WHERE {\n  hint:Query hint:optimizer \"None\".\n  BIND(NOW() - \"P20Y\"^^xsd:duration AS ?thenPrecise)\n  BIND(STRDT(REPLACE(STR(?thenPrecise), \"T.*\", \"T00:00:00Z\"), xsd:dateTime) AS ?then)\n  ?thing p:P571|p:P580 [\n    psv:P571|psv:P580 [\n      wikibase:timeValue ?then;\n      wikibase:timePrecision 11 # day\n    ];\n    a wikibase:BestRank\n  ].\n  MINUS { ?thing wdt:P576|wdt:P582 ?end. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Things that turned 20 years old today", "target": ""}
{"query": "\n#defaultView:Map\nSELECT ?place ?placeLabel ?location WHERE {\n  SERVICE wikibase:around {\n    ?place wdt:P625 ?location.\n    bd:serviceParam wikibase:center \"[AUTO_COORDINATES]\" ;\n                    wikibase:radius \"1.5\";\n                    wikibase:distance ?dist.\n  }\n\n\n  ?place wdt:P31/wdt:P279* wd:Q88985865\n         \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Eating or drinking establishments near you (1.5 radius)", "target": ""}
{"query": "select  (concat(str(?day), \"/\", str(?month)) as ?date)  (count(distinct ?ps) as ?count) \nwhere {\n  ?MP p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q16707842 .\n  ?ps pq:P2715 ?election .\n  ?ps pq:P580 ?elected .\n  ?election wdt:P31* wd:Q7864918 . \n  bind(day(?elected) as ?day) . bind(month(?elected) as ?month) .\n  filter(year(?elected) >= 1880) . \n} group by ?day ?month order by desc(?count)", "context": "Most common day for UK by-elections since 1880", "target": ""}
{"query": "#title: Carte des tramways en France\n#defaultView:Map{\"hide\":[\"?coords\",\"?line\",\"?rgb\"]}\nSELECT ?st ?stLabel ?sl ?coords ?line ?layer ?rgb WHERE {\n?search wdt:P31 wd:Q15640053;wdt:P527 ?lignes.\n?lignes wdt:P559 ?termini.\n?st wdt:P31 wd:Q2175765;wdt:P17 wd:Q142;(wdt:P361|wdt:P16) ?search;wdt:P625 ?coords;(wdt:P81|wdt:P1192) ?sl;wdt:P197 ?pred;p:P197[ps:P197 ?pred;pq:P5051 ?towards;(pq:P81|pq:P1192) ?line_pq].\n?pred wdt:P625 ?coords_pred;(wdt:P81|wdt:P1192) ?sl_pred.\nOPTIONAL{?sl wdt:P1671 ?ln.}\nFILTER(?sl_pred = ?lignes)FILTER(?sl = ?lignes)FILTER(?towards = ?termini)\nMINUS{?st (wdt:P576|wdt:P582|wdt:P3999) ?dispar.}\nMINUS{?st (wdt:P5817|wdt:P5816) ?interdit.VALUES ?interdit{wd:Q811683 wd:Q63065035 wd:Q12377751 wd:Q97317113 wd:Q55653430 wd:Q30108381 wd:Q55570340 wd:Q11639308 wd:Q104664889}}\n?pred p:P625[psv:P625[wikibase:geoLatitude ?lat1;wikibase:geoLongitude ?lon1]; rdf:type wikibase:BestRank].\n?st p:P625[psv:P625[wikibase:geoLatitude ?lat2;wikibase:geoLongitude ?lon2]; rdf:type wikibase:BestRank].\nBIND(STRDT(CONCAT(\"LINESTRING(\",STR(?lon1),\" \",STR(?lat1),\",\",STR(?lon2),\" \",STR(?lat2),\")\"),geo:wktLiteral)AS ?line)\n?sl wdt:P465 ?rgb.\nSERVICE wikibase:label{bd:serviceParam wikibase:language \"fr\". ?sl rdfs:label ?layer. ?st rdfs:label ?stLabel} \n}\nGROUP BY ?st ?stLabel ?sl ?rgb ?layer ?coords ?line\nORDER BY ?ln ?sl", "context": "Map of tramways in France", "target": ""}
{"query": "#title:location of items with property P10280 statements\n#defaultView:Map{\"hide\":[\"?coordinates\"]}\nSELECT DISTINCT ?site ?siteLabel ?coordinates ?image ?value\nWHERE \n{\n  ?site wdt:P10280 ?value; wdt:P625 ?coordinates.\n  OPTIONAL { ?site wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of categories for honorary citizens", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?location\"]}\nSELECT ?uni ?uniLabel (SAMPLE(?location) AS ?location) (SAMPLE(?inception) AS ?inception) WHERE {\n  ?uni wdt:P31/wdt:P279* wd:Q3918 ;\n       wdt:P571 ?inception .\n  {\n    ?uni wdt:P625 ?location .\n  } UNION {\n    ?uni p:P159/pq:P625 ?location .\n  }\n     \n  FILTER (YEAR(?inception) < 1500)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?uni ?uniLabel\n", "context": "Map of the Medieval universities and its founding date", "target": ""}
{"query": "#defaultView:Map\n\nSELECT ?place ?placeLabel ?location WHERE {\n\nSERVICE wikibase:around {\n\n?place wdt:P625 ?location.\n\nbd:serviceParam wikibase:center \"[AUTO_COORDINATES]\" ;\n\nwikibase:radius \"20\";\n\nwikibase:distance ?dist.\n\n}\n\n \n?place wdt:P31/wdt:P279* wd:Q152081\n       \n\n \n\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n\n}", "context": "Camps/subcamps within a 20km radius of your location", "target": ""}
{"query": "#title: Taxa found at Fazenda Tamanduá (Sertão of Paraíba - Brazil)\n#defaultView:ImageGrid\nSELECT \n?item ?itemLabel ?pic\nWHERE\n{\nwd:Q109639255 wdt:P6803 ?item . \nOPTIONAL {?item wdt:P18 ?pic}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}", "context": "Taxa found at Fazenda Tamanduá", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?naissance ?geoloc ?image WHERE {\n  ?item wdt:P106/wdt:P279* wd:Q133485. \n?item wdt:P19 ?lieudenaissance. \n?lieudenaissance wdt:P625 ?geoloc . \n\n    ?item wdt:P569 ?naissance, ?date.\nOPTIONAL { ?item wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,fr,he,ru,de\". }\n}\n ORDER BY ASC(?naissance)", "context": "Birthplace of Rabbis", "target": ""}
{"query": "# Parlamentari e ministri 50-80enni\nSELECT DISTINCT ?item ?itemLabel (COUNT(DISTINCT ?position_statement) AS ?ministerCount)\nWHERE \n{\n  # Essere umano di cittadinanza italiana\n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P27 wd:Q38.\n  # Non più vecchio/a di Mattarella\n  ?item wdt:P569 ?birth.\n  filter (?birth > \"1941-07-23\"^^xsd:dateTime).\n  filter (?birth < \"1972-01-24\"^^xsd:dateTime).\n  # Non morto/a\n  MINUS { ?item wdt:P570 ?death. }.\n  # Ex parlamentari\n  ?item wdt:P39/wdt:P361/wdt:P361 wd:Q1117578.\n  MINUS {  ?item wdt:P39/wdt:P361/wdt:P361 wd:Q1117578 ; \n                 pq:P580 ?start FILTER (?start >= \"2018-01-01T00:00:00Z\"^^xsd:dateTime) . }\n  # Ex ministri\n  ?item p:P39 ?position_statement .\n  ?position_statement ps:P39/wdt:P361 wd:Q3687318.\n  # Ma non ministri recenti\n  MINUS { ?item p:P39 ?position_recent .\n         ?position_recent ps:P39/wdt:P361 wd:Q3687318.\n         ?position_recent pq:P582 ?position_end\n                   FILTER (?position_end >= \"2014-01-01T00:00:00Z\"^^xsd:dateTime). }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],it\". }\n}\nGROUP BY ?item ?itemLabel", "context": "Italian parliamentarians and ministers aged between 50 and 80", "target": ""}
{"query": "SELECT ?item ?itemLabel ?occLabel WHERE {\n  ?item wdt:P31 wd:Q5;\n        wdt:P27 wd:Q27;\n        wdt:P21 wd:Q6581072;\n        wdt:P106/wdt:P279* wd:Q901 .\n  MINUS { ?article schema:about ?item }\n  OPTIONAL { ?item wdt:P106 ?occ }\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ", "context": "Female Irish scientists in Wikidata without a Wikipedia article", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel (count(distinct ?article) as ?count) ?coords ?image WITH\n{\n  SELECT ?item WHERE {\n    ?item wdt:P131* wd:Q12709 .\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q1496967 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q2385804 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q271669 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q35145263 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q728937 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q12819564 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q1292279 }\n    #FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q27096213 }\n    FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q83620 }\n  }\n} AS %i\nWHERE \n{\n  INCLUDE %i .\n  ?item wdt:P625 ?coords .\n  OPTIONAL { ?item wdt:P18 ?image }\n  ?article schema:about ?item\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?coords ?image\nHAVING (?count >= 10)\nORDER BY DESC(?count)", "context": "Places in the Hautes-Alpes that are the subject of an article on Wikipedia in at least 10 languages", "target": ""}
{"query": "SELECT ?state ?stateLabel (COUNT(DISTINCT(?band)) AS ?bandCount)\n  (GROUP_CONCAT(DISTINCT ?bandLabel; SEPARATOR=\", \") AS ?bands)\nWHERE {\n  ?band p:P31 ?statement1.\n  ?statement1 (ps:P31/(wdt:P279*)) wd:Q215380. # item is instance of musical group or subclass (ie. rock group)\n  ?band p:P136 ?statement0.\n  ?statement0 (ps:P136/(wdt:P279*)) wd:Q25554481. # band genre is punk music or subclass\n  ?band wdt:P740 ?city. # band formed in city\n  ?city wdt:P131* ?state. # county in admin body (state)\n  ?state wdt:P31 wd:Q35657. # state is instance of US State\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\".\n                         ?state rdfs:label ?stateLabel.\n                          ?band rdfs:label ?bandLabel.}\n  }\nGROUP BY ?state ?stateLabel\nORDER BY DESC(?bandCount)\n", "context": "US states with the most punk bands", "target": ""}
{"query": "#Ghanaian scientists by citation count\n#defaultView:BubbleChart\nSELECT\n  ?number_of_citing_works\n  ?author ?authorLabel\n  ?organization ?organizationLabel\n  ?example_work ?example_workLabel\nWITH {\n  SELECT DISTINCT ?author WHERE {\n    ?author wdt:P27| wdt:P1416/wdt:P17 | wdt:P108/wdt:P17 wd:Q117 .\n  }\n} AS %authors\nWITH {\n  SELECT\n    ?author \n    (COUNT(DISTINCT ?citing_work) AS ?number_of_citing_works)\n    (SAMPLE(?organization_) AS ?organization)\n    (SAMPLE(?work) AS ?example_work)\n  WHERE {\n    INCLUDE %authors\n    ?work wdt:P50 ?author .\n    OPTIONAL { ?citing_work wdt:P2860 ?work . }\n    OPTIONAL {\n      ?author wdt:P1416 | wdt:P108 ?organization_ .\n      ?organization_ wdt:P17 wd:Q43\n    }\n  }\n  GROUP BY ?author \n} AS %results\nWHERE {\n  INCLUDE %results\n  service wikibase:label { bd:serviceParam wikibase:language \"en\" . } \n}\nORDER BY DESC(?number_of_citing_works)", "context": "List of Ghanaian scientists by citation count", "target": ""}
{"query": "#productions of Pride and Prejudice and who played Elizabeth Bennet and what kind of thing they were\nSELECT ?production ?productionLabel ?actress ?actressLabel (group_concat(?instanceofLabel ;separator=\", \") as ?typeofproduction)\n\nWHERE {\n  \n  ?production wdt:P144 wd:Q170583 ; # thing based on Pride & Prejudice\n              p:P161 [ ps:P161 ?actress; pq:P453 wd:Q2223341 ] . # cast member has character role elizabeth bennet\n       ?production wdt:P31 ?instanceof     \n                               \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                              ?instanceof rdfs:label ?instanceofLabel . # i don't know what this bit does except that it's needed\n                            ?production rdfs:label ?productionLabel .\n                            ?actress rdfs:label ?actressLabel .\n                            \n                         }\n}\n\ngroup by ?production ?productionLabel ?actress ?actressLabel", "context": "Actresses who have played Elizabeth Bennett in Pride and Prejudice, with type of production", "target": ""}
{"query": "# MPs with identified mythical ancestors\nSELECT distinct ?person ?personLabel ?born ?died \n(group_concat(distinct ?classname;separator=\", \") as ?class)  (count(distinct ?mp) as ?descendant_mp_count) WHERE {\n  ?mp p:P39 ?positionStatement .\n  ?positionStatement ps:P39 wd:Q77685926 .  # this person is a member of the current parliament\n  { ?mp (wdt:P25|wdt:P22)* ?person . } # mp's mother or father, or their mother or father, and so on\n  filter not exists { ?person wdt:P31 wd:Q5 } . # the ancestor is not \"a human\"\n  ?person wdt:P31 ?type . ?type rdfs:label ?classname . filter(lang(?classname) = \"en\") .\n  OPTIONAL { ?person wdt:P569 ?born } .\n  OPTIONAL { ?person wdt:P570 ?died } .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} group by ?person ?personLabel ?born ?died order by ?personLabel", "context": "MPs with identified mythical ancestors", "target": ""}
{"query": "#defaultView:Timeline\nselect ?sportswoman ?sportswomanLabel ?professionLabel ?first with {\n  select distinct ?profession where {\n   ?profession wdt:P279* wd:Q2066131 .\n  } } as %sport_professions with {\n  select ?sportswoman ?profession ?date_of_birth where {\n   include %sport_professions\n   ?sportswoman wdt:P21 wd:Q6581072 .\n   ?sportswoman wdt:P569 ?date_of_birth .\n   ?sportswoman wdt:P106 ?profession .     \n  } } as %sportswomen with {\n  select ?profession (MIN(?date_of_birth) as ?first)  where {\n  include %sportswomen\n  } group by ?profession } as %world_firsts with {\n  select ?sportswoman ?profession ?first where {\n    include %sportswomen\n    include %world_firsts\n    FILTER( ?first = ?date_of_birth && BOUND(?profession))      \n  } } as %final_results where {\n  include %final_results\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Timeline of 1st women practising a given sports discipline", "target": ""}
{"query": "#title: water boards in the Netherlands (should be 21)\nSELECT ?item ?itemLabel WHERE {\n  ?item wdt:P31 wd:Q702081 .\n  MINUS { ?item wdt:P576 [] }\n  MINUS { ?item wdt:P1366 [] }\n  MINUS { \n    ?item p:P31 ?inStatement .\n    ?inStatement pq:P582 [] .\n    MINUS { ?item p:P31 ?p.\n            ?p ps:P31 wd:Q702081; pq:P580 ?start .\n            FILTER NOT EXISTS { ?p pq:P582 []}\n    }             \n  }\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"nl,en\". }\n} ORDER BY ?itemLabel", "context": "Water boards in the Netherlands", "target": ""}
{"query": "#title: Age of the actress when she played \"Elizabeth Bennet\"\n#productions of Pride and Prejudice and who played Elizabeth Bennet and what kind of thing they were\nSELECT DISTINCT ?production ?productionLabel ?actressLabel ?dateBirth ?dateProduction ?age WHERE {\n  ?production wdt:P144 wd:Q170583 ;  # thing based on Pride & Prejudice\n              p:P161 [ ps:P161 ?actress; pq:P453 wd:Q2223341 ] . # cast member has character role Elizabeth Bennet\n  ?actress wdt:P569 ?dateBirth .\n  ?production wdt:P577 ?dateProduction .\n  BIND ( ((?dateProduction - ?dateBirth)/365.24) AS ?age )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?age", "context": "Age of the actress when she played \"Elizabeth Bennet\"", "target": ""}
{"query": "#title: people with dates of birth and death on January 1st (day precision dates)\nSELECT ?item ?itemLabel ?itemDescription ?born ?died\n{\n  ?item p:P570 [ a wikibase:BestRank; psv:P570 [ wikibase:timeValue ?born; wikibase:timePrecision 11 ]] . \n  FILTER( MONTH(?born) = 1 && DAY(?born) = 1 ) \n  ?item p:P569 [ a wikibase:BestRank; psv:P569 [ wikibase:timeValue ?died; wikibase:timePrecision 11 ]] . \n  FILTER( MONTH(?died) = 1 && DAY(?died) = 1 ) \n  ?item wdt:P31 wd:Q5 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nLIMIT 1000", "context": "People with dates of birth and death on January 1st (day precision dates)", "target": ""}
{"query": "SELECT DISTINCT ?lexeme (GROUP_CONCAT(DISTINCT ?lemma; separator = \", \") AS ?lemmas) {\n  ?lexeme dct:language wd:Q12107 ; wikibase:lemma ?lemma ; ontolex:sense [] .\n}\nGROUP BY ?lexeme\nORDER BY ?lemmas", "context": "More than 500 lexemes in Breton now have at least one sense", "target": ""}
{"query": "SELECT ?event ?eventLabel (COUNT(?video_game) as ?count) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?video_game wdt:P31 wd:Q7889.\n  ?video_game p:P6949 ?node.\n  ?node pq:P9731 ?event.\n}\nGROUP BY ?event ?eventLabel\nORDER BY DESC(?count)", "context": "Events by number of video games announced", "target": ""}
{"query": "SELECT ?chef ?chefLabel ?restaurant ?restaurantLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?restaurant wdt:P31 wd:Q11707;\n    p:P166 ?awardStatement.\n  ?awardStatement ps:P166 wd:Q20824563.\n  FILTER(NOT EXISTS { ?awardStatement pq:P582 _:b26. })\n  {\n    ?chef p:P108 ?employerStatement.\n    ?employerStatement ps:P108 ?restaurant;\n      pq:P39 wd:Q3499072.\n  }\n  UNION\n  {\n    ?restaurant wdt:P1037 ?chef.\n    ?chef wdt:P106 wd:Q3499072.\n  }\n  UNION {\n    ?restaurant wdt:P127 ?chef.\n    ?chef wdt:P106 wd:Q3499072.\n  }\n  ?chef wdt:P21 wd:Q6581072.\n  ?awardStatement pq:P1114 3.\n}\nLIMIT 100", "context": "Three-Michelin Stars restaurants with a female chef", "target": ""}
{"query": "SELECT ?state ?string ?languages ?labels ?percent ?ratio\nWITH {\n  SELECT DISTINCT ?state WHERE {\n    ?state wdt:P31/wdt:P279* wd:Q3624078;\n           p:P463 ?memberOfStatement.\n    ?memberOfStatement a wikibase:BestRank;\n                         ps:P463 wd:Q1065.\n    MINUS { ?memberOfStatement pq:P582 ?endTime. }\n    MINUS { ?state wdt:P576|wdt:P582 ?end. }\n  }\n} AS %states\nWITH {\n  SELECT ?state ?label ?string WHERE {\n    INCLUDE %states.\n    ?state rdfs:label ?label.\n    BIND(STR(?label) AS ?string)\n  }\n} AS %labelsAndStrings\nWITH {\n  SELECT ?state ?string (COUNT(*) AS ?languages) WHERE {\n    INCLUDE %labelsAndStrings.\n  }\n  GROUP BY ?state ?string\n  HAVING(?languages > 1)\n} AS %stringsAndLanguages\nWITH {\n  SELECT ?state (COUNT(*) AS ?labels) WHERE {\n    INCLUDE %labelsAndStrings.\n  }\n  GROUP BY ?state\n} AS %labels\nWHERE {\n  INCLUDE %stringsAndLanguages.\n  INCLUDE %labels.\n  BIND(?languages / ?labels AS ?ratio)\n  BIND(CONCAT(SUBSTR(STR(?ratio * 100), 1, 5), \"%\") AS ?percent)\n}\nORDER BY DESC(?languages)", "context": "Countries with count of same Wikidata labels in different languages", "target": ""}
{"query": "SELECT ?teamLabel ?player ?playerLabel ?positionLabel ?from ?to\nWHERE\n{\n#  ?player wdt:P54 ?team.\n  ?player p:P54 ?statement.\n  ?statement ps:P54 ?team.\n  OPTIONAL{ ?statement pq:P413 ?position. }\n  OPTIONAL{ ?statement pq:P580 ?startDate. }\n  OPTIONAL{ ?statement pq:P582 ?endDate. }\n  ?team wdt:P31 wd:Q12973014;\n        wdt:P463 wd:Q109743445.\n  BIND(YEAR(?startDate) AS ?from)\n  BIND(YEAR(?endDate) AS ?to)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\".\n                          ?team rdfs:label ?teamLabel.\n                          ?player rdfs:label ?playerLabel.\n                          ?position rdfs:label ?positionLabel.\n                         }\n}\nORDER BY ?teamLabel ?playerLabel\n", "context": "Team and positions played by National Women's Football League players", "target": ""}
{"query": "#defaultView:Map\nSELECT  ?layer ?article ?person ?personLabel  ?personDescription  ?coord ?pic ?picGrave ?DoD\nWHERE {\n\t?person wdt:P119 wd:Q311; #Place of burial\n            p:P119 [ \n              pq:P965 ?graveplot;\n              pq:P625 ?coord];\n              wdt:P570 ?DoD.\n   OPTIONAL{ ?person wdt:P18 ?pic .} \t\t\t\t\t# If we have an illustration\n   OPTIONAL{ ?person wdt:P1442 ?picGrave .} \t\t\t# If we have a picture of the grave\n   OPTIONAL {\n      ?article schema:about ?person .\n      ?article schema:inLanguage \"fr\" .\n      ?article schema:isPartOf <https://fr.wikipedia.org/> .\n    }\n   OPTIONAL { ?person wdt:P21 ?group .} \t\t\t# Occupation in Layer\n\n  #BIND(year(?DoD) AS ?Décès) \n  BIND(\n    IF(?DoD < \"1850-01-01T00:00:00Z\"^^xsd:dateTime, \"1800-1849\",\n    IF(?DoD < \"1900-01-01T00:00:00Z\"^^xsd:dateTime, \"1850-1899\",\n    IF(?DoD < \"1950-01-01T00:00:00Z\"^^xsd:dateTime, \"1900-1949\",\n    IF(?DoD < \"2020-01-01T00:00:00Z\"^^xsd:dateTime, \"1950-2000\",\n    \"2000-2022\"))))\n    AS ?layer).\n  \n#FILTER (YEAR(?DoD) >= 2000)\nSERVICE wikibase:label {    bd:serviceParam wikibase:language \"fr, en\" .}\n\n}", "context": "Location of the graves of personalities in Père Lachaise Cemetery who died between 1800 and 1849 (50 year ranges)", "target": ""}
{"query": "#defaultView:Map\nSELECT ?sculpture ?sculptureLabel ?coordinates ?image ?woman ?womanLabel WITH {\n  SELECT ?sculpture (SAMPLE(?coordinates_) AS ?coordinates) (SAMPLE(?image_) AS ?image) ?woman WHERE {\n    ?sculpture wdt:P31/wdt:P279* wd:Q860861;\n               wdt:P17 wd:Q38;\n               wdt:P180|wdt:P547 ?woman.\n    OPTIONAL { ?sculpture wdt:P625 ?coordinates_. }\n    OPTIONAL { ?sculpture wdt:P18 ?image_. }\n    ?woman wdt:P31 wd:Q5;\n           wdt:P21/wdt:P279? wd:Q6581072.\n  }\n  GROUP BY ?sculpture ?woman\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Location of statues of women in Italy", "target": ""}
{"query": "SELECT\n  ?language1 ?language1Label ?language2 ?language2Label \n  ?number_of_lexemes\nWITH {\nSELECT ?language1 ?language2 (COUNT(*) AS ?number_of_lexemes) {\n  [] ^wdt:P5137 / ^ontolex:sense ?lexeme1, ?lexeme2 .\n  FILTER (?lexeme1 != ?lexeme2)\n  ?lexeme1 dct:language ?language1 .\n  ?lexeme2 dct:language ?language2 .\n  FILTER (?language1 != ?language2)\n  FILTER (STR(?language1) > STR(?language2)) \n}\nGROUP BY ?language1 ?language2\nORDER BY DESC(?number_of_lexemes)\nLIMIT 100\n} AS %result\n{\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?number_of_lexemes)\n", "context": "Count of Lexeme pairs between different languages", "target": ""}
{"query": "#defaultView:Map\nselect distinct ?item ?itemLabel ?itemDescription ?pob ?coord where {\n    ?item wdt:P31 wd:Q5;  \n          wdt:P39 wd:Q1048744;\n          wdt:P19 ?pob.\n  ?pob wdt:P625 ?coord.\n\n   \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?sitelinks)", "context": "Birthplaces of General secretaries of USSR", "target": ""}
{"query": "#defaultView:Graph\n#title:Objects related in some way to Abdülmecid I\nSELECT ?from ?fromLabel ?image1 ?to ?toLabel ?image2 ?edgeLabel \nWITH {\n  SELECT ?start ?startimage ?image1 ?from ?prop ?coll  WHERE {\n  VALUES ?start {wd:Q174772}. # Choose person to start with\n    ?start wdt:P18 ?startimage.\n    ?from ?rel ?start; wdt:P195 ?coll.\n    ?prop wikibase:directClaim ?rel ; a wikibase:Property.\n    OPTIONAL {?from wdt:P18 ?image1}.\n   }  } as %core\nWHERE {\n  {\n SELECT ?from  ?image1 (?start AS ?to) (?startimage AS ?image2) (?prop AS ?edge) WHERE {\n  INCLUDE %core.\n } ORDER BY ?coll # put objects from the same collection close together\n }\nUNION {\n  SELECT ?from  ?image1 (?coll AS ?to) ?image2 (wd:P195 AS ?edge) WHERE {\n   INCLUDE %core.\n   OPTIONAL {?coll wdt:P18 ?image2}.\n  }\n} UNION {\n    SELECT ?from ?image1 (?type AS ?to) (\"\" AS ?image2) (wd:P31 AS ?edge) WHERE {\n   INCLUDE %core.\n      ?from wdt:P31 ?type.\n  }\n}\n      SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n     ?from rdfs:label ?fromLabel. ?edge rdfs:label ?edgeLabel.  ?to rdfs:label ?toLabel}\n}", "context": "Items related to Abdülmecid I, their collections, and their types", "target": ""}
{"query": "#defaultView:Graph\n#title:Objects related in some way to Aurangzeb\nSELECT ?from ?fromLabel ?image1 ?to ?toLabel ?image2 ?edgeLabel \nWITH {\n  SELECT ?start ?startimage ?image1 ?from ?prop ?coll  WHERE {\n  VALUES ?start {wd:Q485547 }. # Choose person to start with\n    ?start wdt:P18 ?startimage.\n    ?from ?rel ?start; wdt:P195 ?coll.\n    ?prop wikibase:directClaim ?rel ; a wikibase:Property.\n    OPTIONAL {?from wdt:P18 ?image1}.\n   }  } as %core\nWHERE {\n  {\n SELECT ?from  ?image1 (?start AS ?to) (?startimage AS ?image2) (?prop AS ?edge) WHERE {\n  INCLUDE %core.\n } ORDER BY ?coll # put objects from the same collection close together\n }\nUNION {\n  SELECT ?from  ?image1 (?coll AS ?to) ?image2 (wd:P195 AS ?edge) WHERE {\n   INCLUDE %core.\n   OPTIONAL {?coll wdt:P18 ?image2}.\n  }\n} #UNION {\n  #  SELECT ?from ?image1 (?type AS ?to) (\"\" AS ?image2) (wd:P31 AS ?edge) WHERE {\n #  INCLUDE %core.\n #     ?from wdt:P31 ?type.\n # }\n#}\n      SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n     ?from rdfs:label ?fromLabel. ?edge rdfs:label ?edgeLabel.  ?to rdfs:label ?toLabel}\n}", "context": "Objects related in some way to Aurangzeb", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?site ?siteLabel ?coordinates ?image WHERE {\n  ?site (wdt:P31/(wdt:P279*)) wd:Q18615527;\n    wdt:P17 wd:Q142;\n    wdt:P625 ?coordinates.\n  OPTIONAL { ?site wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr,en\". }\n}", "context": "Tram bridges in France", "target": ""}
{"query": "SELECT ?person ?personLabel\n  (GROUP_CONCAT(DISTINCT ?b; SEPARATOR=' or ') AS ?born)\n  (GROUP_CONCAT(DISTINCT ?d; SEPARATOR=' or ') AS ?died)\n  (GROUP_CONCAT(DISTINCT ?r; SEPARATOR=' or ') AS ?recognized)\n  (URI(CONCAT('https://righteous.yadvashem.org/index.html?itemId=', ?yv)) AS ?yad_vashem) {\n  ?person wdt:P31 wd:Q5; p:P166 ?award.\n  ?award ps:P166 wd:Q112197; pq:P27 wd:Q36\n  OPTIONAL { ?award pq:P585 ?r }\n  OPTIONAL { ?person wdt:P569 ?b }\n  OPTIONAL { ?person wdt:P570 ?d }\n  OPTIONAL { ?person wdt:P1979 ?yv }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language '[AUTO_LANGUAGE],en' }\n} GROUP BY ?person ?personLabel ?yv", "context": "Polish Righteous Among the Nations", "target": ""}
{"query": "SELECT ?medsos ?medsosLabel ?vp571 ?diluncurkan (SAMPLE(?logo) AS ?logo2) ?ditutup ?situs WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id,en\". }\n  ?medsos wdt:P31 wd:Q3220391.\n  OPTIONAL { ?medsos wdt:P571 ?vp571.\n            BIND(YEAR(?vp571) AS ?diluncurkan) }\n  OPTIONAL { ?medsos wdt:P154 ?logo. }\n  OPTIONAL { ?medsos wdt:P582 ?vp582.\n           BIND(YEAR(?vp582) AS ?ditutup)}\n  OPTIONAL { ?medsos wdt:P856 ?situs. }\n}\nGROUP BY ?medsos ?medsosLabel ?vp571 ?diluncurkan ?ditutup ?situs\n#defaultView:Timeline\n#view:Timeline{\"hide\": \"?vp571\"}", "context": "Launch date, logo of social media services", "target": ""}
{"query": "SELECT ?human ?humanLabel ?awardEditionLabel ?awardLabel ?awardWork ?awardWorkLabel ?director ?directorLabel ?time\nWHERE\n{\n{\nSELECT (SAMPLE(?human) AS ?human) ?award ?awardWork (SAMPLE(?director) AS ?director) (SAMPLE(?awardEdition) AS ?awardEdition) (SAMPLE(?time) AS ?time) WHERE {\n?award wdt:P31 wd:Q19020 . # All items that are instance of(P31) of Academy awards (Q19020)\n{\n?human wdt:P31 wd:Q5 . # Humans\n?human p:P166 ?awardStat . # with an awarded(P166) statement\n?awardStat ps:P166 ?award ; # that has any of the values of ?award\npq:P805 ?awardEdition ; # Get the award edition (which is \"subject of\" XXth Academy Awards)\npq:P1686 ?awardWork . # The work they have been awarded for\n} UNION {\n?awardWork wdt:P31 wd:Q11424 . # Films\n?awardWork p:P166 ?awardStat . # with an awarded(P166) statement\n?awardStat ps:P166 ?award ; # that has any of the values of ?award\npq:P805 ?awardEdition . # Get the award edition (which is \"subject of\" XXth Academy Awards)\n}\nOPTIONAL {\n?awardEdition wdt:P585 ?time . # the \"point of time\" of the Academy Award\n?awardWork wdt:P57 ?director .\n}\n}\nGROUP BY ?awardWork ?award # We only want every movie once for a category (a 'random' person is selected)\n}\nSERVICE wikibase:label { # ... include the labels\nbd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" .\n}\n}\nORDER BY DESC(?time)", "context": "Oscar winners from 1929", "target": ""}
{"query": "#title:Biggest biologists ever\n#defaultView:ImageGrid\nSELECT DISTINCT \n?person ?personLabel ?heigth_in_meters \n(SAMPLE(?pic) as ?image)\nWHERE \n{\n  ?person wdt:P106/wdt:P279* wd:Q864503 .\n  ?person wdt:P31 wd:Q5 . \n  ?person wdt:P2048 ?height  . \n  OPTIONAL{?person wdt:P18 ?pic .} \n  ?person p:P2048 ?statement  .    \n  ?statement       psv:P2048    ?valuenode .\n  ?valuenode    wikibase:quantityAmount    ?value .\n  ?valuenode    wikibase:quantityUnit  ?unit .\n  \n  ?unit p:P2370/psv:P2370 [                # conversion to SI unit\n     wikibase:quantityAmount ?conversion;\n     wikibase:quantityUnit wd:Q11573;      # meter\n  ]\n  BIND((?value * ?conversion) AS ?heigth_in_meters).\n  \n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}\nGROUP BY ?person ?personLabel ?heigth_in_meters \nORDER BY DESC(?heigth_in_meters)", "context": "Images of biologists by height", "target": ""}
{"query": "#title: Biggest coins outside the U.S.\n#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?pic ?diameter_in_cm ?country ?countryLabel ?material ?materialLabel\nWHERE \n{\n  ?item wdt:P279* wd:Q41207 . \n  \n  ?item wdt:P18 ?pic .\n  ?item wdt:P17 ?country .\n  MINUS {?item wdt:P17 wd:Q30 . }\n  OPTIONAL{?item wdt:P186 ?material .}\n\n  ?item p:P2386 ?statement_P2386  .    \n  ?statement_P2386       psv:P2386    ?valuenode_P2386 .\n  ?valuenode_P2386    wikibase:quantityAmount     ?value_P2386 .\n  ?valuenode_P2386    wikibase:quantityUnit  ?unit_P2386 .\n\n\n  ?unit_P2386 p:P2370/psv:P2370 [                # conversion to SI unit\n     wikibase:quantityAmount ?conversion;\n     wikibase:quantityUnit wd:Q11573;      # meter\n  ]\n  BIND((?value_P2386 * ?conversion)* 100 AS ?diameter_in_cm).\n  \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}\nORDER BY DESC(?diameter_in_cm)", "context": "Biggest coins outside the U.S.", "target": ""}
{"query": "SELECT ?letter (COUNT(?letter) AS ?count) WITH {\nSELECT ?l WHERE {\n?l a ontolex:LexicalEntry ; \ndct:language wd:Q1860 # English\n}} AS %lexemes\nWITH { SELECT ?wl { \nBIND ( 5 AS ?wl )\n}} AS %word_length\nWITH { SELECT DISTINCT ?word {\nINCLUDE %word_length\nINCLUDE %lexemes\n?l ontolex:lexicalForm [ontolex:representation ?word ]\nBIND (CONCAT(\"^[a-z]{\", STR(?wl), \"}$\") AS ?r )  \nFILTER(REGEX(?word, ?r)) \n}} AS %word_matches \nWITH { SELECT DISTINCT ?index WHERE {\nINCLUDE %word_length\nVALUES ?start { 1 }\nBIND (?wl AS ?end)\nBIND ( (?end - ?start ) + 1 AS ?x )\n?n p:P31 [\nps:P31 wd:Q21199 ;\npq:P155 ?prev\n] .\n?prev wdt:P1181 ?index .\nFILTER (?index > 0 && ?index < ?x + 1)\n}\n} AS %indices\nWHERE {\nINCLUDE %word_matches\nINCLUDE %indices\nVALUES ?letter { \"a\" \"b\" \"c\" \"d\" \"e\" \"f\" \"g\" \"h\" \"i\" \"j\" \"k\" \"l\" \"m\" \"n\" \"o\" \"p\" \"q\" \"r\" \"s\" \"t\" \"u\" \"v\" \"w\" \"x\" \"y\" \"z\" }\nFILTER (CONTAINS(SUBSTR (?word, ?index, 1 ), ?letter))\n}        \nGROUP BY ?letter\nORDER BY DESC (?count)", "context": "Frequency of letters in five-letter words in Wikidata lexeme forms", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?xLabel ?size ?rgb WITH {\nSELECT DISTINCT ?n WHERE {   # generate a sequence of natural numbers\n  VALUES ?start { 1 } \n  BIND (2000 AS ?end) \n  BIND ( (?end - ?start ) + 1 AS ?x )\n  [] p:P31 [\n     ps:P31 wd:Q21199 ;  # is a natural number\n     pq:P155 ?prev\n            ] .\n  ?prev wdt:P1181 ?n .\n  FILTER (?n > 0 && ?n < ?x + 1)\n  }\n} AS %numbers\nWHERE {\n  INCLUDE %numbers\n  ?x wdt:P31 wd:Q21199 .\n  ?x wdt:P1181 ?n .\n  FILTER (?x NOT IN (wd:Q713113)) # exclude the annoying 0.999\n  OPTIONAL { ?x wdt:P31 wd:Q49008 . BIND (true AS ?prime) }  # highlight prime numbers\n  BIND(1 AS ?size)   # used for the size of the bubble: want them all the same size    \n  BIND ( COALESCE(IF(?prime, \"FD7F7F\", 1/0),\"71C1FD\") AS ?rgb )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?n\n", "context": "Visualization of the Prime/Ulam Spiral using natural numbers and primes stored in Wikidata", "target": ""}
{"query": "#title:monuments that are named after somebody without being connected to them by any other property\n#defaultView:Map\nSELECT ?monument ?monumentLabel ?person ?personLabel (SAMPLE(?coordinates_) AS ?coordinates) (SAMPLE(?image_) AS ?image) WITH {\n  SELECT DISTINCT ?monument ?person WHERE {\n    ?monument wdt:P31/wdt:P279* wd:Q4989906;\n              wdt:P138 ?person.\n    MINUS {\n      ?monument ?wdt ?person.\n      FILTER(?wdt != wdt:P138)\n    }\n    ?person wdt:P31 wd:Q5.\n  }\n  LIMIT 100\n} AS %results WHERE {\n  INCLUDE %results.\n  OPTIONAL { ?monument wdt:P625 ?coordinates_. }\n  OPTIONAL { ?monument wdt:P18 ?image_. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?monument ?monumentLabel ?person ?personLabel\nLIMIT 100", "context": "Monuments that are named after somebody without being connected to them by any other property", "target": ""}
{"query": "#title:Carte des Saint-Valentin\n#defaultView:Map\nSELECT DISTINCT ?q ?qLabel ?coord ?valentinsLabel WHERE {\n  VALUES ?valentins { wd:Q37587 wd:Q192151 wd:Q192151 wd:Q26203089 }\n  ?q wdt:P625 ?coord ; wdt:P138 ?valentins .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Places named after Valentine's Day (Saint Valentine)", "target": ""}
{"query": "#title: Gold medal winners in the Olympic Games by age \nSELECT DISTINCT ?winner ?winnerLabel ?sport ?sportLabel ?eventTime ?age WHERE {\n  ?winner wdt:P31 wd:Q5;\n          wdt:P569 ?dob.\n  MINUS { ?winner p:P569/psv:P569 [\n                wikibase:timePrecision \"7\"^^xsd:integer ; # Century\n                wikibase:timeValue ?dob] } # Meant to remove those born \"19th century\"\n  FILTER (! wikibase:isSomeValue(?dob) ) # Removes those with unknown birth date\n  \n  ?sport wdt:P1346 ?winner ;\n         wdt:P31 wd:Q18536594.\n  \n  # Work-around due to inconsistent modeling of the time of events \n  OPTIONAL { ?sport wdt:P585 ?eventPoint }\n  OPTIONAL { ?sport wdt:P582 ?eventEnd }\n  OPTIONAL { ?sport wdt:P361/wdt:P585 ?olympicPoint }\n  BIND(COALESCE(?eventEnd,?eventPoint,?olympicPoint) AS ?eventTime)\n  \n  # Quick and dirty way to calculate age\n  BIND ((ROUND(((?eventTime - ?dob)/3.6524))/100) AS ?age )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} ORDER BY ?age", "context": "Gold medal winners in the Olympic Games by age", "target": ""}
{"query": "SELECT ?language ?languageLabel ?lexemes ?withExternalId ?percent WITH {\n  SELECT ?language (COUNT(*) AS ?count) { ?lexeme dct:language ?language }\n  GROUP BY ?language\n  ORDER BY DESC(COUNT(*))\n  LIMIT 20\n} AS %languages WITH {\n  SELECT ?language ?lexemes ?withExternalId (ROUND(10000 / ?lexemes * ?withExternalId) / 100 AS ?percent) {\n    SELECT ?language (COUNT(*) AS ?lexemes) (SUM(?hasExternalId) AS ?withExternalId) {\n    INCLUDE %languages\n    ?lexeme dct:language ?language .\n      BIND(IF(EXISTS {\n        ?property wdt:P31 wd:Q19847637 ; wikibase:directClaim ?wdt .\n        ?lexeme ?wdt [] .\n      }, 1, 0) AS ?hasExternalId) .\n    }\n    GROUP BY ?language\n  }\n  GROUP BY ?language ?lexemes ?withExternalId\n} AS %stats WHERE {\n  INCLUDE %stats\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY DESC(?lexemes)", "context": "Top 20 languages in number of lexemes in Wikidata and percentage of lexemes with at least one external id", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?property ?propertyLabel ?count ?report WITH { \n  SELECT ?property (count(distinct ?item) as ?count)  WHERE {\n    ?item wdt:P31 wd:Q23397 . \n    hint:Prior hint:runFirst true .\n    ?item wdt:P17 wd:Q145 . \n    ?item ?predicate ?value .\n    ?property wikibase:directClaim ?predicate .\n  } group by ?property  } as %i\nWHERE\n{\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\nBIND(IRI(CONCAT(\"https://query.wikidata.org/#SELECT%20%3Fitem%20%3FitemLabel%20%3Fvalue%20%3FvalueLabel%20WHERE%20%7B%0A%20%20%20%20%3Fitem%20wdt%3AP31%20wd%3AQ23397%20.%20%0A%20%20%20%20%3Fitem%20wdt%3AP17%20wd%3AQ145%20.%20%0A%20%20%20%20%3Fitem%20wdt%3A\",STRAFTER(STR(?property),\"http://www.wikidata.org/entity/\"),\"%20%3Fvalue.%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22en%22%20.%20%7D%0A%7D%0A\")) as ?report)\n  \n} order by desc(?count)", "context": "Count of PropertyLabels for lakes in the UK", "target": ""}
{"query": "SELECT distinct ?planet ?planetLabel ?satellite ?satelliteLabel ?namedLabel ?namedDescription WHERE {\n  ?planet wdt:P31/wdt:P279* wd:Q13205267.\n  ?satellite wdt:P31/wdt:P279* wd:Q2537.\n  ?satellite wdt:P397 ?planet.\n  ?satellite wdt:P138 ?named.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?planet p:P2244/psv:P2244 ?statement.\n      ?statement wikibase:quantityUnit ?unit.\n      ?statement wikibase:quantityAmount ?distance.\n      ?statement wikibase:quantityNormalized/wikibase:quantityAmount ?distanceNormalized. # convert to normalized unit (meter)\n      BIND (?distanceNormalized / 9460800000000000  AS ?lightyears)\n}\nORDER BY ?lightyears", "context": "Moons of solar system planets and what they are names for", "target": ""}
{"query": "SELECT ?language ?languageLabel ?lexemes ?withExternalId ?percent WITH {\n  SELECT ?language (COUNT(*) AS ?count) { ?lexeme dct:language ?language }\n  GROUP BY ?language\n  ORDER BY DESC(COUNT(*))\n  LIMIT 20\n} AS %languages WITH {\n  SELECT ?language ?lexemes ?withExternalId (ROUND(10000 / ?lexemes * ?withExternalId) / 100 AS ?percent) {\n    SELECT ?language (COUNT(*) AS ?lexemes) (SUM(?hasExternalId) AS ?withExternalId) {\n    INCLUDE %languages\n    ?lexeme dct:language ?language .\n      BIND(IF(EXISTS {\n        ?property wdt:P31 wd:Q19847637 ; wikibase:directClaim ?wdt .\n        ?lexeme ?wdt [] .\n      }, 1, 0) AS ?hasExternalId) .\n    }\n    GROUP BY ?language\n  }\n  GROUP BY ?language ?lexemes ?withExternalId\n} AS %stats WHERE {\n  INCLUDE %stats\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY DESC(?lexemes)", "context": "Top 20 languages in number of Lexemes in Wikidata and percentage of Lexemes with at least one external ID", "target": ""}
{"query": "SELECT ?coup ?coupLabel ?success ?time ?countryLabel WHERE {\n  VALUES ?type {wd:Q45382 wd:Q25906438 wd:Q4460509}\n  ?coup wdt:P31 ?type .\n  bind (?type != wd:Q25906438 as ?success) \n  ?coup wdt:P17 ?country.\n  ?country wdt:P30 wd:Q15.\n  optional {?coup wdt:P585 ?time}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } \n} order by desc(?time)", "context": "Successful coups and attempts in Africa", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?dob ?age\nWHERE {\n  ?item wdt:P106 wd:Q1930187;\n        p:P108 ?NYT_statement.\n  ?NYT_statement ps:P108 wd:Q9684;\n  OPTIONAL {\n    ?item wdt:P569 ?dob.\n    BIND(xsd:integer((NOW() - ?dob) / 365.25) AS ?age)\n  }\n  MINUS { # Former employee\n    ?NYT_statement pq:P582 [].\n  }\n  MINUS { ?item wdt:P570 ?dod. } # Living deprived\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?dob)", "context": "New York Times journalists that are alive", "target": ""}
{"query": "#title: Ukrainian coins and banknotes\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?pic\nWHERE \n{\n  ?item wdt:P279* wd:Q693464 .\n  ?item wdt:P17 wd:Q212 . \n  ?item wdt:P18 ?pic . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Ukrainian coins and banknotes", "target": ""}
{"query": "#title: Types of quartz\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?pic\nWHERE\n{\n?item wdt:P279* wd:Q43010 .\n?item wdt:P18 ?pic\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}", "context": "Types of quartz", "target": ""}
{"query": "#defaultView:BubbleChart\n#title: Use of free software license for describing software\nSELECT ?licenseLabel (count(DISTINCT ?software) as ?count)\nWITH {\n    SELECT ?software ?license WHERE {\n        ?license wdt:P31/wdt:P279* wd:Q3943414.\n        ?software wdt:P31/wdt:P279* wd:Q7397;\n            wdt:P275 ?license.\n    }\n} AS %result\nWHERE {\n  INCLUDE %result\n  ?license rdfs:label ?licenseLabel. FILTER(lang(?licenseLabel)=\"en\")\n} \nGROUP BY ?licenseLabel\nORDER by DESC(?count)", "context": "Use of free software license for describing software", "target": ""}
{"query": "#defaultView:BubbleChart\n#title: Count of latest available Twitter follower count for different programming languages\nSELECT ?proglang ?proglangLabel ?maxdate ?followers\nWITH {\n  SELECT ?proglang (MAX(?date) as ?maxdate) WHERE {\n    ?proglang wdt:P31 wd:Q9143;\n              p:P8687 ?socialmedia.\n    ?socialmedia ps:P8687 ?followers;\n                 pq:P6552 ?twitter;\n                 pq:P585 ?date.\n  }\n  GROUP by ?proglang      \n} AS %result\nWHERE {\n  INCLUDE %result\n  ?proglang wdt:P31 wd:Q9143;\n            p:P8687 ?socialmedia.\n  ?socialmedia ps:P8687 ?followers;\n          pq:P585 ?maxdate.\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} ORDER BY DESC(?followers)", "context": "Count of latest available Twitter follower count for different programming languages", "target": ""}
{"query": "#title: Timeline of programming languages and their paradigms\nSELECT ?proglangLabel ?paradigmLabel (Year(?date) as ?year) {\n  ?proglang wdt:P31/wdt:P279* wd:Q9143;\n            wdt:P3966 ?paradigm;\n            (wdt:P577 | wdt:P571) ?date.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER by ?year", "context": "Timeline of programming languages and their paradigms", "target": ""}
{"query": "#defaultView:BubbleChart\n#title: Number of Wikipedia articles on different programming languages\nSELECT ?proglangLabel (count(DISTINCT ?language) as ?count) {\n  ?proglang wdt:P31/wdt:P279* wd:Q9143;\n           wdt:P275 [wdt:P31/wdt:P279* wd:Q3943414].\n  ?article schema:about ?proglang ;\n              schema:inLanguage ?lang.\n  ?language wdt:P218 ?lang.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP By ?proglangLabel\nHAVING (?count >20)\nORDER by DESC(?count)", "context": "Most popular programming language on Wikipedia with multilingual articles", "target": ""}
{"query": "#defaultView:Map\n#by Jura1, February 2020\nSELECT ?p ?pLabel ?pob ?pobLabel ?coor\nWHERE\n{\n    ?p wdt:P735 wd:Q919943 .\n    ?p wdt:P19 ?pob .\n    ?pob wdt:P625 ?coor .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Places of birth of people named \"Désirée\"", "target": ""}
{"query": "SELECT ?human ?humanLabel ?humanDescription ?cityLabel ?naissance ?décès WHERE {\n  ?human wdt:P119 wd:Q311 . \n  ?human wdt:P19 ?city .\n  ?city wdt:P17 wd:Q212 .\n  ?human wdt:P569 ?naissance .\n  ?human wdt:P570 ?décès .\nSERVICE wikibase:label {bd:serviceParam wikibase:language \"fr,en\" .}\n}\nORDER BY ?countryLabel ?cityLabel", "context": "People in the Père-Lachaise cemetery who were born in a city that is or was in Ukraine", "target": ""}
{"query": "#title: Propriétés entre une église et son saint\n#defaultView:BubbleChart\nSELECT ?propertyItem ?propertyItemLabel (COUNT(?item) AS ?compte ) WHERE {\n  ?item wdt:P31 wd:Q16970 ;\n        ?prop ?saint .\n  ?saint wdt:P411 wd:Q43115 .\n  ?propertyItem wikibase:directClaim ?prop . #nécessaire pour récupérer le label\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?propertyItem ?propertyItemLabel", "context": "Properties linking between a church and its saint", "target": ""}
{"query": "SELECT ?prop ?propLabel ?item_count ?stmt_count ?samp ?sampLabel ?valLabel \n\nWITH {\n   SELECT ?prop (COUNT(DISTINCT ?item) AS ?item_count)  (COUNT(DISTINCT ?stmt) AS ?stmt_count) (SAMPLE(?item) AS ?samp) (SAMPLE(?value) AS ?val) WHERE {           \n      hint:Query hint:optimizer \"None\" .\n      ?item wdt:P1679 ?yp_id .\n      ?item ?p ?stmt .\n      ?prop wikibase:claim ?p .\n      ?prop wikibase:statementProperty ?ps .\n      ?stmt ?ps ?value .\n    } GROUP BY ?prop\n} AS %counts\n\nWHERE {\n  INCLUDE %counts .\n  MINUS {\n     VALUES ?identifier {wd:Q19847637 wd:Q18614948} . # Wikidata property representing a unique identifier, Wikidata property for authority control \n     ?prop wdt:P31/wdt:P279* ?identifier . \n  }\n\n  ?prop rdfs:label ?prop_label . FILTER(lang(?prop_label) = 'en') .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n   \n\n} ORDER BY DESC(?item_count) DESC(?stmt_count)", "context": "Properties describing Art UK artworks", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?item ?itemLabel ?date ?countryLabel ?image\nWHERE\n{\n  {\n    SELECT ?item ?date ?image (SAMPLE(?country2) AS ?country) WHERE {\n      ?item p:P1196/pq:P607/wdt:P361* wd:Q110999040.\n      OPTIONAL{ ?item wdt:P570 ?date. }\n      OPTIONAL{ ?item wdt:P27 ?country2. }\n      OPTIONAL{ ?item wdt:P18 ?image. }\n      MINUS{ ?item wdt:P27 wdt:Q15180. } #exclude USSR citizenship from the duplicates\n    }\n    GROUP BY ?item ?date ?image\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,uk,ru\" . }\n}\nORDER BY DESC(?date)", "context": "Timeline of victims of the 2022 Russian invasion of Ukraine", "target": ""}
{"query": "SELECT ?country ?countryLabel ?population {\n  ?country wdt:P47 wd:Q159.\n  ?country p:P463 ?stat .\n  ?stat ps:P463 wd:Q1065 .\n  ?country wdt:P1082 ?population\n  FILTER NOT EXISTS {?stat pq:P583 [] . }\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n\n}\n                ", "context": "Population of countries that share a border with Russia", "target": ""}
{"query": "PREFIX wd: <http://www.wikidata.org/entity/>\nPREFIX wdt: <http://www.wikidata.org/prop/direct/>\n\n##defaultView:Map\nSELECT ?xLabel ?clip ?loc WHERE {\n\n{?x wdt:P131 wd:Q25 }\nUNION { ?x wdt:P131 ?pob . ?pob wdt:P131* wd:Q25 }\n\n?x wdt:P443 ?clip .\n  ?x wdt:P625 ?loc .\n \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cy\". }\n}", "context": "Audio pronunciation of places in Wales", "target": ""}
{"query": "#all things with a national gallery of scotland ID where we know the artist and the artist was or is a woman\nSELECT  ?item ?itemLabel ?natgalIDLabel ?itemwikipage  ?creatorLabel ?creatorwikipage \n\nWHERE {\n  ?item wdt:P8946  ?natgalID ;\n     wdt:P170 ?creator .\n  ?creator wdt:P21 wd:Q6581072 .\n\n  OPTIONAL { ?itemwikipage schema:about ?item; schema:isPartOf <https://en.wikipedia.org/> }\n  OPTIONAL { ?creatorwikipage schema:about ?creator; schema:isPartOf <https://en.wikipedia.org/>}\n  \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Things with a national gallery of Scotland ID where the artist was or is a woman", "target": ""}
{"query": "#title: Software with gender information of developer, designer and the person named after\n#defaultView:BarChart\nSELECT ?gender ?genderLabel (count(DISTINCT ?developer) as ?count) \nWITH {\n  SELECT DISTINCT ?developer WHERE {\n    ?software  wdt:P31/wdt:P279* wd:Q7397; #software\n              (wdt:P178 | wdt:P287 | wdt:P138) ?developer. #developer, designer and the person named after         \n  }\n} AS %result\nWHERE {\n  INCLUDE %result\n  VALUES ?gender { wd:Q6581097 wd:Q6581072  wd:Q1052281 wd:Q48270 wd:Q1052281 wd:Q52261234 wd:Q44148 wd:Q2449503 wd:Q12964198 wd:Q505371}.\n  ?developer wdt:P21 ?gender.  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} \nGROUP by ?gender ?genderLabel", "context": "Software with gender information of developer, designer and the person named after", "target": ""}
{"query": "#defaultView:ImageGrid{\"hide\":[\"?placeOfBirthCoordinates\", \"?signature\"]}\n#view:Map{\"hide\":[\"?placeOfBirthCoordinates\"]}\nSELECT ?person ?personLabel ?signature ?placeOfBirthCoordinates WITH {\n  # get all the actual data\n  SELECT ?person (SAMPLE(?signature_) AS ?signature) (SAMPLE(?placeOfBirthCoordinates_) AS ?placeOfBirthCoordinates) WHERE {\n    # get some random signature image statements\n    SERVICE bd:sample {\n      ?person wdt:P109 ?signature_.\n      bd:serviceParam bd:sample.limit 110 . # a bit over 100 in case some of them happen to be non-humans\n    }\n    # ensure they’re humans (most are but there are some exceptions)\n    ?person wdt:P31 wd:Q5.\n    # for the map display, optionally add the place of birth\n    OPTIONAL {\n      ?person wdt:P19 ?placeOfBirth.\n      ?placeOfBirth wdt:P625 ?placeOfBirthCoordinates_.\n    }\n  }\n  GROUP BY ?person # avoid multiple results in case of multiple signature / place of death (coordinate) statements\n  LIMIT 100\n} AS %results WHERE {\n  # add labels\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Signature images from Wikidata (change the view to “map” to see the signatures arranged by the person’s place of birth!", "target": ""}
{"query": "#title:Count of UK lake items with a P7548 'UK Lakes Portal ID' property statement\nSELECT ?WBID (count(?item) as ?count)\n{\n  ?item wdt:P31/wdt:P279* wd:Q23397 .\n  ?item wdt:P17 wd:Q145.\n  OPTIONAL { ?item wdt:P7548 ?value .}\n  BIND(IF(!BOUND(?value),\"No WBID\",\"WBID\") as ?WBID)\n  \n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\"  }\n} group by ?WBID\n", "context": "Count of UK lake items with a 'UK Lakes Portal ID' (P7548) property statement", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?itemLabel ?img ?ann_date WHERE {\n  ?item wdt:P361 wd:Q101791498;\n        wdt:P18  ?img;\n        wdt:P6949 ?ann_date\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Timeline for the Apple \"M\" series of Systems on a Chip (SoC)", "target": ""}
{"query": "SELECT ?religion ?religionLabel (COUNT(?item) as ?count)\nWHERE\n{\n     ?item wdt:P735 wd:Q25413386.\n     ?item wdt:P21 wd:Q6581097.\n     ?item wdt:P140 ?religion.\n     SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?religion ?religionLabel", "context": "Religion of men named “Maria” (as one of their given names)", "target": ""}
{"query": "# (Thanks, James Heald, for the original query!)\n#defaultView:Map\nSELECT ?station ?stationLabel ?cds ?line ?layer WHERE { \n  { SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; gas:in wd:Q3097173 ; gas:target wd:Q6132101 ; gas:out ?station ; gas:out1 [] ; gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . } }\n  FILTER(?station != wd:Q3097173)\n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"bn,en\". }\n}", "context": "Shortest rail link between Narvik and Singapore (passing through Finland and Kazakhstan", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT\n  ?coordinate\n  ?institution\n  ?institutionLabel\n  ?item\n  ?itemLabel\n  ?author\n  ?authorLabel\nWITH {\n  SELECT ?item {\n    VALUES ?subject {\n      wd:Q33002955\n      wd:Q593744\n      wd:Q515701\n    }\n    ?item wdt:P921 ?subject.\n  }\n} AS %0\nWITH {\n  SELECT ?item {\n    INCLUDE %0.\n    ?item wdt:P31/wdt:P279* wd:Q47461344.\n  }\n} AS %2\n{\n  INCLUDE %2.\n\n  ?item wdt:P50 ?author.\n\n  ?author wdt:P69 ?institution.\n\n  ?institution wdt:P625 ?coordinate.\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Map of institutions where \"where people who studied there\" have created written works whose main subject is knowledge graph (Q33002955), knowledge base (Q515701) and (Q33002955)", "target": ""}
{"query": "SELECT ?colony ?colonyLabel ?languageLabel ?iso WHERE {\n  ?colony p:P31 ?statement;\n          wdt:P30 wd:Q15.\n  ?statement a wikibase:BestRank;\n             ps:P31 wd:Q133156.\n  { ?colony wdt:P37 ?language. } UNION\n  { ?statement pq:P642/wdt:P37 ?language. }\n  ?language wdt:P220 ?iso.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?iso", "context": "Colonies of Africa with their or their “main state”’s official language and ISO code", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?item1 ?image ?item1Label ?item2 ?image2 ?item2Label ?edgeLabel WHERE {\nVALUES ?item1 {wd:Q28873655 wd:Q169390 wd:Q4184 wd:Q1344 wd:Q189729 wd:Q63490060 wd:Q2858869 wd:Q109026069 wd:Q148584 wd:Q3073495 wd:Q105096136 wd:Q5363058 wd:Q105095679 wd:Q215062 wd:Q91 wd:Q200539 wd:Q98534768 wd:Q7725634}\nVALUES ?item2 {wd:Q28873655 wd:Q169390 wd:Q4184 wd:Q1344 wd:Q189729 wd:Q63490060 wd:Q2858869 wd:Q109026069 wd:Q148584 wd:Q3073495 wd:Q105096136 wd:Q5363058 wd:Q105095679 wd:Q215062 wd:Q91 wd:Q200539 wd:Q98534768 wd:Q7725634}\n?item1 ?prop ?item2.\n?edge ?dummy ?prop ; rdf:type wikibase:Property\nOPTIONAL {?item1 wdt:P18 ?image}\nOPTIONAL {?item2 wdt:P18 ?image2}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Wikidata knowledge graph of Elizabeth Keckley, dressmaker to U.S. First Lady Mary Todd Lincoln", "target": ""}
{"query": "SELECT ?menteri ?menteriLabel ?jabatan ?jabatanLabel ?negaraLabel ?gambar ?menjabat WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"id,en\". }\n  ?menteri wdt:P31 wd:Q5.\n  ?menteri wdt:P21 wd:Q6581072.\n  ?menteri wdt:P39 ?jabatan.\n  ?jabatan wdt:P279 wd:Q2518691.\n  OPTIONAL { ?jabatan wdt:P17 ?negara. }\n  OPTIONAL { ?menteri wdt:P18 ?gambar. }\n  ?menteri p:P39 ?st.\n  ?st ps:P39 ?jabatan.\n  ?st pq:P580 ?mulai.\n  BIND(YEAR(?mulai) as ?mulai2).\n  OPTIONAL {?st pq:P582 ?selesai.\n            BIND(xsd:integer(YEAR(?selesai)) AS ?selesai2).\n                 }\n  BIND(CONCAT(STR(?mulai2),\"-\",If(?selesai2>0,str(?selesai2),\"sekarang\")) as ?menjabat).\n}\n#defaultView:ImageGrid", "context": "Women who served as defense ministers in various countries", "target": ""}
{"query": "SELECT DISTINCT ?item1 ?item1Label ?article1 ?item2 ?item2Label ?article2\nWITH { SELECT DISTINCT ?item WHERE {\nVALUES ?membership {wd:Q16707842 wd:Q1801564 wd:Q18018860} .\n?item wdt:P31 wd:Q5 ; wdt:P39/wdt:P279* ?membership . \n} } AS %mps \nWITH { SELECT DISTINCT ?item ?familyname ?firstname_form WHERE {\nINCLUDE %mps .\n?item wdt:P734 ?familyname .\n?firstname_form wdt:P1533 ?familyname .\n} } AS %mps1 \nWITH { SELECT DISTINCT ?item ?familyname ?firstname_form ?firstname ?familyname_form WHERE {\nINCLUDE %mps1 .\n?item wdt:P735 ?firstname .\n?firstname wdt:P1533 ?familyname_form .\n} } AS %mps2\nWITH { SELECT (?item AS ?item1) (?firstname AS ?firstname1) (?familyname AS ?familyname1) WHERE {\nINCLUDE %mps2 .\n} } AS %mps2_1 \nWITH { SELECT (?item AS ?item2) (?firstname_form AS ?firstname1) (?familyname_form AS ?familyname1) WHERE {\nINCLUDE %mps2 .\n} } AS %mps2_inverted \nWHERE {\nINCLUDE %mps2_1 .\nINCLUDE %mps2_inverted .\nFILTER (?item1 != ?item2) .\noptional { ?article1 schema:about ?item1 ; schema:isPartOf <https://en.wikipedia.org/> }\noptional { ?article2 schema:about ?item2 ; schema:isPartOf <https://en.wikipedia.org/> }\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?item2Label", "context": "UK MPs who had paired names (e.g. Owen Thomas / Thomas Owen)", "target": ""}
{"query": "SELECT DISTINCT ?prop ?propLabel WHERE {\n  ?s (wdt:P31/(wdt:P279*)) wd:Q235557;\n    ?props ?o.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?prop wikibase:directClaim ?props.\n}\nORDER BY (?propLabel)", "context": "List of properties associated with items that are class/subclass of File Format", "target": ""}
{"query": "SELECT\n  ?property\n  ?propertyLabel\n  ?count\nWITH {\n  SELECT DISTINCT ?item {\n    ?item wdt:P31/wdt:P279* wd:Q28564.\n  }\n} AS %0\nWITH {\n  SELECT ?predicate (COUNT(*) AS ?count) {\n    INCLUDE %0.\n    ?item ?predicate [].\n  }\n  GROUP BY ?predicate\n} AS %1\nWITH {\n  SELECT ?predicate ?property {\n    INCLUDE %1.\n    ?property wikibase:claim ?predicate.\n  }\n} AS %2\n{\n  INCLUDE %1.\n  INCLUDE %2.\n\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}.\n}\nORDER BY DESC(?count)", "context": "Table frequency of properties used in instances of public libraries", "target": ""}
{"query": "#title: Long swedish / german nouns in Wikidata\nSELECT DISTINCT ?l ?word ?len WHERE {\n  {\n    {\n      ?l rdf:type ontolex:LexicalEntry;\n        dct:language wd:Q9027;\n        wikibase:lemma ?word.\n    }\n    UNION\n    {\n      ?l rdf:type ontolex:LexicalEntry;\n        dct:language wd:Q188;\n        wikibase:lemma ?word.\n    }\n  }\n  BIND(STRLEN(?word) AS ?len)\n  ?l wikibase:lexicalCategory wd:Q1084.\n}\nORDER BY DESC (?len)\nLIMIT 1000", "context": "Long swedish / german nouns in Wikidata", "target": ""}
{"query": "#title: Most popular chess openings \n#defaultView:BubbleChart\nSELECT\n  ?item ?itemLabel \n  ((?sitelinks * ?sitelinks) as ?links_squared)\nWHERE \n{\n  ?item wdt:P31 wd:Q103632 .\n  ?item wikibase:sitelinks ?sitelinks . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY\n  DESC (?sitelinks)", "context": "Most popular Chess openings (by number of sitelinks)", "target": ""}
{"query": "SELECT ?item ?itemLabel ?propertyLabel (IRI(CONCAT(SUBSTR(?formatter,1,STRLEN(?formatter)-2), ?value)) AS ?URL) WHERE {\n  VALUES ?wm { wd:Q22337472 wd:Q15924535 wd:Q24524653 }\n  ?item wdt:P31 ?wm .\n  ?property wdt:P31 wd:Q105388954;\n            wikibase:directClaim ?wdt;\n            wdt:P1630 ?formatter .\n  ?item ?wdt ?value .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} ORDER BY ?itemLabel", "context": "Wikimedia affiliates on social media", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?designationLabel ?img ?id ?coords (?designationLabel AS ?layer) WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q181348 .\n  ?item wdt:P1216|wdt:P709|wdt:P1459 ?id .\n  ?item wdt:P625 ?coords .\n  ?item wdt:P1435 ?designation .\n  OPTIONAL {?item wdt:P18 ?img} .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?itemLabel", "context": "Listed viaducts in the UK", "target": ""}
{"query": "#title: Pages en lien avec les universités de Clermont selon le nombre d'articles sur les projets Wikimédia\nSELECT ?item ?itemLabel ?itemDescription (COUNT(DISTINCT ?page) AS ?nb_pages) WHERE {\n  VALUES ?univ { wd:Q1319786 wd:Q1381437 wd:Q612648 wd:Q3551576 }\n  ?item ?lien ?univ .\n  ?page schema:about ?item .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription\nHAVING ( ?nb_pages > 1 )\nORDER BY DESC(?nb_pages)", "context": "Pages linked to the University of Clermont according to the number of articles on Wikimedia projects", "target": ""}
{"query": "#defaultView:TreeMap\nselect ?lemma ?languageLabel {\n  ?sense wdt:P5137 wd:Q634 .\n  ?lexeme ontolex:sense ?sense .\n  ?lexeme dct:language ?language .\n  ?lexeme wikibase:lemma ?lemma .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Which languages share a word for the same thing (visualized as a tree map). e.g. planet", "target": ""}
{"query": "#defaultView:Map\n#Cats, with pictures\nSELECT ?item ?itemLabel ?pic ?birthplaceLabel ?location\nWHERE\n{\n?item wdt:P106 wd:Q2252262 .\n?item wdt:P19 ?birthplace .\n?birthplace wdt:P625 ?location\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}", "context": "Birthplace of rappers", "target": ""}
{"query": "#title: Occupation des personnes liées aux universités de Clermont\n#defaultView:BubbleChart\nSELECT ?occupation1Label (COUNT(DISTINCT ?researcher) AS ?nb) WHERE {\n  VALUES ?univ { wd:Q1319786 wd:Q1381437 wd:Q612648 wd:Q3551576 }\n  ?researcher (wdt:P108|wdt:P463|wdt:P1416) ?univ .\n  ?researcher wdt:P106 ?occupation1 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?occupation1Label", "context": "Bubble chart of occupation of people linked to University of Clermont", "target": ""}
{"query": "# List corporate archives, location, and address where available. \nSELECT ?item ?itemLabel ?opened ?countryLabel ?coords ?website ?address \n{\n  ?item wdt:P31 wd:Q10605195 .\n  optional { ?item wdt:P17 ?country . }\n  optional { ?item wdt:P1619 ?opened . }\n  optional { ?item wdt:P625 ?coords . }\n  optional { ?item wdt:P856 ?website . }\n  optional { ?item wdt:P6375 ?address . } \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,fr,ja,it,sv\"  }\n}", "context": "List of corporate archives, location, and address where available", "target": ""}
{"query": "SELECT DISTINCT ?video_game ?video_gameLabel ?first_publication_date (GROUP_CONCAT(DISTINCT ?developerLabel; SEPARATOR = \", \") AS ?developers) (GROUP_CONCAT(DISTINCT ?publisherLabel; SEPARATOR = \", \") AS ?publishers) (GROUP_CONCAT(DISTINCT ?platformLabel; SEPARATOR = \", \") AS ?platforms) WHERE {\n  {\n    SELECT ?video_game (YEAR(MIN(?publication_date)) AS ?first_publication_date) WHERE {\n      ?video_game wdt:P31 wd:Q7889;         # All video games...\n        (wdt:P136/(wdt:P279*)) wd:Q23916;   # ...with genre=adventure game (or a subgenre)...\n        wdt:P495 wd:Q142.                   # ...and country of origin = France\n      OPTIONAL { ?video_game wdt:P577 ?publication_date. }\n    }\n    GROUP BY ?video_game\n  }\n  OPTIONAL { ?video_game wdt:P178 ?developer. }\n  OPTIONAL { ?video_game wdt:P123 ?publisher. }\n  OPTIONAL { ?video_game wdt:P400 ?platform. }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\".\n    ?developer rdfs:label ?developerLabel.\n    ?publisher rdfs:label ?publisherLabel.\n    ?platform rdfs:label ?platformLabel.\n    ?video_game rdfs:label ?video_gameLabel.\n  }\n}\nGROUP BY ?video_game ?video_gameLabel ?first_publication_date\nORDER BY (?first_publication_date)", "context": "French adventure video games", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription ?sitelinken (COUNT(distinct ?sitelink) as ?count) \nWHERE { \n  ?item wdt:P31 wd:Q5 . \n  ?item wdt:P21 wd:Q6581072 . \n  ?item wdt:P27 wd:Q235 . \n  ?sitelink schema:about ?item . \n  ?sitelinken schema:about ?item . \n  ?sitelinken schema:isPartOf <https://en.wikipedia.org/>;\n  FILTER NOT EXISTS { \n    ?wfr schema:about ?item . ?wfr schema:inLanguage \"fr\" \n  }\n  FILTER EXISTS { \n    ?wen schema:about ?item . ?wen schema:inLanguage \"en\" \n  }\n  \n                                                     \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # le label viendra de préférence dans votre langue, et autrement en anglais                                                         \n                                                         }  \n\nGROUP BY ?item ?itemLabel ?itemDescription ?sitelinken ORDER BY DESC(?count)\n", "context": "Women with the citizenship of a country and the most articles in other languages (including English) but without an article in French Wikipedia", "target": ""}
{"query": "SELECT\n  ?language\n  ?languageLabel\n  ?lexeme\n  ?lemmas\n  ?count\nWITH {\n  SELECT ?lexeme ?sense {\n    ?lexeme ontolex:sense ?sense.\n  }\n} AS %0\nWITH {\n  SELECT ?lexeme (COUNT(*) AS ?count) {\n    INCLUDE %0.\n  }\n  GROUP BY ?lexeme\n} AS %1\nWITH {\n  SELECT ?lexeme ?count {\n    INCLUDE %1.\n  }\n  ORDER BY DESC(?count)\n  LIMIT 100\n} AS %2\nWITH {\n  SELECT\n    ?lexeme\n    (GROUP_CONCAT(?lemma; SEPARATOR=', ')  AS ?lemmas)\n  {\n    INCLUDE %2.\n    ?lexeme wikibase:lemma ?lemma.\n  }\n  GROUP BY ?lexeme\n} AS %3\n{\n  INCLUDE %2.\n  INCLUDE %3.\n  ?lexeme dct:language ?language.\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}.\n}\nORDER BY DESC(?count)", "context": "Top 100 lexemes with the most number of senses", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT\n  ?coordinates\n  ?location\n  ?locationLabel\n  ?lemma\n  ?lexeme\n{\n  ?lexeme\n     # INPUT: Insert language here.\n     dct:language wd:Q1321;\n     wikibase:lemma ?lemma;\n     ontolex:sense/wdt:P6271 ?location.\n  \n  ?location\n    # INPUT: Insert country here.\n    wdt:P131* wd:Q419;\n    wdt:P625 ?coordinates.\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Given a language and a country, show a map of the demonyms in that language that are used in any part within that country", "target": ""}
{"query": "# Most common classes of the values of \"depicts\" (P180) on Commons\nSELECT ((?sum / 40) AS ?percent) ?class ?class_label ?instance ?instance_label ?sample\n\nWITH { \n  SELECT (COUNT(DISTINCT(?file)) AS ?count) ?value (SAMPLE(?file) AS ?example) WHERE {\n     service bd:sample {\n       ?file wdt:P180 ?value . \n        bd:serviceParam bd:sample.limit 4000 .\n        bd:serviceParam bd:sample.sampleType \"RANDOM\" .\n     }  \n  } GROUP BY ?value\n  ORDER BY DESC(?count)\n#  LIMIT 2000           \n} AS %values\n \nWITH {\n  SELECT (SUM(?count) AS ?sum) ?class (SAMPLE(?value) AS ?instance) (SAMPLE(?example) AS ?sample) WHERE {\n    INCLUDE %values .\n    service <https://query.wikidata.org/sparql> {\n         ?value wdt:P31 ?class\n    }\n  } GROUP BY ?class \n} AS %classes\n\nWHERE {\n  INCLUDE %classes . \n  service <https://query.wikidata.org/sparql> {\n      OPTIONAL {?instance rdfs:label ?instance_label FILTER (lang(?instance_label) = 'en') } .\n      OPTIONAL {?class rdfs:label ?class_label FILTER (lang(?class_label) = 'en') } .\n  }\n} \nORDER BY DESC(?sum)", "context": "Most common classes for values of \"depicts\" (P180) on Commons", "target": ""}
{"query": "#Drainage basins of Scottish rivers \n#defaultView:Map\nSELECT DISTINCT ?layer ?layerLabel ?dot ?dotLabel ?coord \n\nWITH {\n  SELECT DISTINCT ?somethingelse ?sea WHERE {\n    {?somethingelse wdt:P131/wdt:P131 wd:Q22 .} hint:Prior hint:runFirst true.\n    ?somethingelse wdt:P403|wdt:P201 ?sea . \n  } \n} as %i\n\nWITH { \n  SELECT ?sea ?layer WHERE {\n    INCLUDE %i .\n    FILTER NOT EXISTS {\n      ?sea wdt:P31/wdt:P279* wd:Q355304 . hint:Prior hint:gearing \"forward\". # exclude watercourse\n      FILTER NOT EXISTS {?sea wdt:P31 wd:Q47053. }   # unless estuary,\n      FILTER NOT EXISTS {?sea wdt:P31 wd:Q1210950. } # or channel\n    }\n    FILTER NOT EXISTS {?sea wdt:P403?/wdt:P31/wdt:P279* wd:Q23397 . hint:Prior hint:gearing \"forward\". } # lake\n    ?sea ^wdt:P403|^wdt:P201 ?layer. # ?layer is the river before the sea\n    ?layer wdt:P131/wdt:P131 wd:Q22 . hint:Prior hint:gearing \"forward\".\n  } \n} as %j\n\nWHERE {\n  INCLUDE %j\n  {?layer (^wdt:P403|^wdt:P201|wdt:P200)*/(^wdt:P177|^wdt:P206)? ?dot. }\n  ?dot wdt:P625|((p:P885|p:P403|p:P200|p:P201)/pq:P625) ?coord .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Scottish river drainage basins", "target": ""}
{"query": "select distinct ?item ?itemLabel ?itemDescription ?date where {\n    ?item wdt:P31 wd:Q5;  # Any instance of a human.\n          wdt:P509 wd:Q9687;\n          wdt:P570 ?date.\n   \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,nl\" }\n}\nORDER BY ASC(?date)", "context": "The earliest road accident victims", "target": ""}
{"query": "#title: Country of nationality of people linked to the Ghana's top 3 tradional universities\nSELECT ?lieuLabel ?contLabel (COUNT(DISTINCT ?researcher) AS ?nb) WHERE {\n  VALUES ?univ { wd:Q2303765 wd:Q1654025 wd:Q746079 }\n  ?researcher (wdt:P108|wdt:P463|wdt:P1416) ?univ .\n  ?researcher wdt:P27 ?lieu .\n  ?lieu wdt:P30 ?cont .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?lieuLabel ?contLabel", "context": "Country of nationality of people linked to the Ghana's top 3 traditional universities", "target": ""}
{"query": "SELECT ?propType (COUNT(DISTINCT ?prop) AS ?props) WHERE {\n   ?prop wikibase:propertyType ?propType .\n}\nGROUP BY ?propType \nORDER BY DESC(?props)", "context": "Count of Wikidata property types", "target": ""}
{"query": "#title: Languages and dialects with number of first language speakers (preferred rank)\n#credit: https://www.wikidata.org/wiki/Wikidata:Request_a_query#Languages_by_number_of_native_speakers_at_point_in_time\nSELECT DISTINCT (?item AS ?QNumber) ?P220 ?LanguageLabel ?P1098 ?Qualifier_P585\nWHERE\n{\n  VALUES ?instance_of { wd:Q34770 wd:Q33384 } # Language or dialect\n  ?item wdt:P31 ?instance_of .\n  OPTIONAL { ?item wdt:P220 ?P220 . } # ISO 639 3 code\n  ?item p:P1098 ?no_of_speakers_statement .\n  ?no_of_speakers_statement wikibase:rank wikibase:PreferredRank . # Has preferred rank\n  ?no_of_speakers_statement pq:P518 wd:Q36870 . # Has the qualifier applies to part = first language\n  ?no_of_speakers_statement ps:P1098 ?P1098 . # No of speakers\n  OPTIONAL { ?no_of_speakers_statement pq:P585 ?Qualifier_P585 . } # Optional timestamp\n  SERVICE wikibase:label\n  {\n    bd:serviceParam wikibase:language \"en\" .\n    ?item rdfs:label ?LanguageLabel .\n  }\n}\nORDER BY DESC(?P1098)", "context": "Languages and dialects with number of first language speakers (preferred rank)", "target": ""}
{"query": "#defaultView:Graph\n#title:Graph of influences in the age of Enlightenment\n\nSELECT DISTINCT ?a ?aLabel ?b ?bLabel\n\n{?a wdt:P31 wd:Q5;\n    wdt:P569 ?birth;\n    wdt:P737 ?b .\n ?b wdt:P31 wd:Q5 .\n \n FILTER (?birth > \"1590-01-01\"^^xsd:dateTime && ?birth < \"1750-01-01\"^^xsd:dateTime)   \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n \n }", "context": "Graph of influences in the age of Enlightenment", "target": ""}
{"query": "# Countries which are named after a person\nSELECT DISTINCT ?item ?itemLabel ?namedAfter ?namedAfterLabel WHERE {\n  ?item wdt:P31 wd:Q6256. # list of all countries (Q6256)\n  {\n    ?item wdt:P138 ?namedAfter. # countries that are named after a person (Q5)\n    ?namedAfter wdt:P31 wd:Q5.\n  }\n  UNION\n  {\n    ?item wdt:P138 ?middleNamedAfter. # some countries are named after things that are named after people\n    ?middleNamedAfter wdt:P138 ?namedAfter.\n    ?namedAfter wdt:P31 wd:Q5.\n  }\n  UNION\n  {\n    ?item wdt:P138 ?middleNamedAfter. # some countries are named after things that are named after things that are named after people\n    ?middleNamedAfter wdt:P138 ?middle2NamedAfter.\n    ?middle2NamedAfter wdt:P138 ?namedAfter.\n    ?namedAfter wdt:P31 wd:Q5.\n  }\n  FILTER(NOT EXISTS { ?namedAfter wdt:P411 wd:Q43115. }) # exclude any Saints (people whose canonisation status is \"Saint\")\n  FILTER(NOT EXISTS { ?namedAfter wdt:P411 wd:Q3464126. }) # exclude any Saints (people whose canonisation status is \"Catholic Saint\")\n  FILTER(NOT EXISTS { ?namedAfter wdt:P31 wd:Q20643955. }) # exclude any biblical figures (people who are an instance of Human Biblical figures)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY (?namedAfterLabel)", "context": "Countries which are named after a person", "target": ""}
{"query": "#title: Number of musical works/compositions (Q105543609) by language (P407) in descending order\n\nSELECT (count(?work) as ?works) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?work wdt:P31 wd:Q105543609.\n  OPTIONAL { ?work wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC(?works) ASC(?P407)\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus\n\n", "context": "Number of musical works (compositions) in Wikidata by language, in descending order", "target": ""}
{"query": "# Liste des adminisrations françaises avec l'identifiant sur l'annuaire du service public, le code SIREN, le portail open data (lorsqu'il a un élément dédié sur wikidata) et son URL\nSELECT ?item ?itemLabel ?annuaire_url ?siren \n?portal ?portalLabel ?url WHERE {\n  ?item wdt:P6671 ?annuaire_id;\n    wdt:P8402 ?portal;\n        wdt:P1616 ?siren;\n    rdfs:label ?itemLabel.\n  FILTER((LANG(?itemLabel)) = \"[AUTO_LANGUAGE]\")\n  ?portal rdfs:label ?portalLabel.\n  FILTER((LANG(?portalLabel)) = \"[AUTO_LANGUAGE]\")\n  ?portal wdt:P856 ?url.\n  BIND(IRI(CONCAT('https://lannuaire.service-public.fr/',?annuaire_id)) as ?annuaire_url)\n}", "context": "List of French public administrations with an open data portal, a siren number (P1616) and a servicepublic id (P6671)", "target": ""}
{"query": "#title:Largest cities with female mayor\n#defaultView:ImageGrid\nSELECT DISTINCT ?mayorImage ?mayor ?mayorLabel ?city ?cityLabel ?population\n{\n  ?city (wdt:P31/(wdt:P279*)) wd:Q515;\n    p:P6 ?statement.\n  ?statement ps:P6 ?mayor.\n  ?mayor wdt:P21 wd:Q6581072.\n  FILTER(NOT EXISTS { ?statement pq:P582 ?x. })\n  ?mayor wdt:P18 ?mayorImage.\n  ?city wdt:P1082 ?population.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?population)\nLIMIT 30", "context": "Largest cities with a female mayor", "target": ""}
{"query": "SELECT ?item ?twitter ?subscribers WHERE {\n  ?item p:P2002 ?statement.\n  ?statement ps:P2002 ?twitter;\n             pq:P3744 ?subscribers.\n}\nORDER BY DESC(?subscribers)", "context": "Reach of Twitter accounts on Wikidata", "target": ""}
{"query": "# tool: scholia\n#defaultView:Table\n\nPREFIX target: <http://www.wikidata.org/entity/Q23498>\n\nSELECT ?count ?cited_work ?cited_workLabel\nWITH {\n  SELECT (count(?work) as ?count) ?cited_work WHERE {\n    { ?work wdt:P921/wdt:P31*/wdt:P279* target: . }\n    union { ?work wdt:P921/wdt:P361+ target: . }\n    union { ?work wdt:P921/wdt:P1269+ target: . }\n    ?work wdt:P2860 ?cited_work .\n    ?cited_work wdt:P577 ?publicationdate .\nFILTER (?publicationdate > \"1969-12-31T00:00:00\"^^xsd:dateTime) . #works published on the subject after a certain date\nFILTER (?publicationdate < \"1980-01-01T00:00:00\"^^xsd:dateTime) . #works published on the subject after a certain date\n  }\n\n  GROUP BY ?cited_work\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,nl,no,ru,sv,zh\" . } \n}\nORDER BY DESC(?count)\nLIMIT 10\n", "context": "Which works published in the 1970s have been most cited from works on archaeology?", "target": ""}
{"query": "SELECT ?cc ?ccLabel ?date (ROUND((NOW() - ?date)/365.2425) AS ?age)\n{\n  ?cc wdt:P31 wd:Q13479982.\n  OPTIONAL { ?cc wdt:P571 ?date. }\n  FILTER(BOUND(?date)).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY ?date\nLIMIT 100", "context": "List of oldest cryptocurrencies", "target": ""}
{"query": "SELECT ?watercourse ?watercourseLabel ?confluenceWith ?confluenceWithLabel ?givesRiseTo ?givesRiseToLabel \nWHERE \n{\n  ?watercourse p:P2789 ?stat .\n  ?stat pq:P1552 wd:Q111794957 .\n  ?stat ps:P2789 ?confluenceWith.\n  filter(str(?watercourse) < str(?confluenceWith))\n  ?watercourse wdt:P403 ?givesRiseTo .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Scottish rivers that merge to make a river with a new name", "target": ""}
{"query": "SELECT ?item ?itemLabel ?circo ?circoLabel (COUNT(*) AS ?count)  WHERE {\n  ?item wdt:P31 wd:Q5;\n     p:P3602 ?statement.\n  ?statement ps:P3602  wd:Q106253677.\n  OPTIONAL { ?statement pq:P768 ?circo. }\n  ?sitelink schema:about ?item.\n  FILTER (SUBSTR(str(?sitelink), 11, 15) = \".wikipedia.org/\") .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr,en\". }\n}\nGROUP BY ?item ?itemLabel ?circo ?circoLabel \nORDER BY DESC (?count)\nLIMIT 100", "context": "List of candidates in the French legislative elections", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription (COUNT(*) AS ?count)  WHERE {\n  VALUES ?target {\n    wd:Q385468\n    wd:Q17524931\n    wd:Q18093037\n  }\n  ?item wdt:P31 wd:Q5;\n     p:P735 ?statement.\n  ?statement ps:P735  ?target.\n  OPTIONAL { ?statement pq:P1545 ?rank. }\n  BIND(IF(BOUND(?rank),?rank, 0) AS ?rank2)\n  FILTER(xsd:integer(?rank2) < xsd:integer(2))\n  ?sitelink schema:about ?item.\n  FILTER (SUBSTR(str(?sitelink), 11, 15) = \".wikipedia.org/\") .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr,en\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription\nORDER BY DESC (?count)\nLIMIT 100", "context": "List of people with Elisabeth, Élisabeth or Elizabeth  as first name", "target": ""}
{"query": "SELECT ?wikimedian ?wikimedianLabel ?twitch_url {\n  ?wikimedian wdt:P31 wd:Q5 ; wdt:P106 wd:Q41546637 ; wdt:P5797 ?twitch .\n  BIND(URI(CONCAT(\"https://www.twitch.tv/\", ?twitch)) AS ?twitch_url) .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY ?twitch_url", "context": "Wikimedians with a Twitch channel", "target": ""}
{"query": "#defaultView:Graph\n#title:Roman deities\n\nSELECT DISTINCT ?d ?dLabel ?child ?childLabel ?dImage ?childImage\n\n{?d wdt:P31 wd:Q11688446;\n    wdt:P40|^wdt:P22|^wdt:P25 ?child;\n    wdt:P18 ?dImage .\n \n?child wdt:P18 ?childImage .\n  \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n \n }", "context": "Relationships of Roman deities", "target": ""}
{"query": "SELECT ?dish ?dishLabel (COUNT(DISTINCT ?ingredient) AS ?ingredientCount) (GROUP_CONCAT(?ingredientLabel; separator = \", \") as ?ingredients) {\n  {\n    SELECT ?dish ?ingredient ?ingredientLabel {\n      ?dish wdt:P31 wd:Q746549 ; wdt:P527 ?ingredient .\n      SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n    }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?dish ?dishLabel ORDER BY DESC(?ingredientCount)\n", "context": "Ingrediants of dishes on Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?place ?placeLabel ?coords ?img ?layer WHERE {\n  ?item wdt:P31?/wdt:P279* wd:Q2095 .\n  ?item wdt:P138 ?place .\n  ?place wdt:P17 wd:Q145 .\n  ?place wdt:P131 [] .\n  OPTIONAL {?place wdt:P625 ?coords} .\n  OPTIONAL {?item wdt:P18 ?img} .\n  \n  OPTIONAL {?item wdt:P31?/wdt:P279*  wd:Q10943 . BIND (wd:Q10943 AS ?type)} .\n  OPTIONAL {?item wdt:P31?/wdt:P279*  wd:Q3314483 . BIND (wd:Q3314483 AS ?type)} .\n  OPTIONAL {?item wdt:P31?/wdt:P279*  wd:Q40050 . BIND (wd:Q40050 AS ?type)} .\n  OPTIONAL {BIND (wd:Q2095 AS ?type)} .\n  OPTIONAL {?type rdfs:label ?layer . FILTER(lang(?layer) = 'en')}\n            \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?itemLabel", "context": "Food names after a place in the UK", "target": ""}
{"query": "#Chefs de gouvernement français classés par durée de fonction\nSELECT DISTINCT ?item ?itemLabel ?positionLabel ?start ?fonction ?fonctionLabel WHERE {\n  ?item wdt:P31 wd:Q5;\n    p:P39 ?position_statement.\n  ?position_statement ps:P39 ?position;\n    pq:P580 ?start.\n  FILTER(?start >= \"1800-01-01T00:00:00Z\"^^xsd:dateTime)\n  ?position (wdt:P279*) wd:Q15135541.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL { ?item wdt:P39 ?fonction. }\n}\nORDER BY DESC (?start) (?itemLabel)\nLIMIT 10000", "context": "French heads of government classified by tenure", "target": ""}
{"query": "#title: Places in Antarctica over 3000km away from the South Pole\n#defaultView:Map\nSELECT ?place ?placeLabel ?location WHERE {\n  wd:Q933 wdt:P625 ?southPole.                         # coordinates of south pole\n  ?place wdt:P30 wd:Q51;                               # continent: antarctica\n         wdt:P625 ?location.\n  FILTER(geof:distance(?location, ?southPole) > 3000). # over 3000km away from south pole\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Places in Antarctica over 3000km away from the South Pole", "target": ""}
{"query": "#title: Topics that members of the Swedish Parliament motioned about 2020/21\n#defaultView:Graph\nSELECT ?item1 ?item1Label ?rgb ?item2 ?item2Label ?image2 # ?edgeLabel \n WHERE {\n  ?edge wdt:P179 wd:Q99037794 ;\n        wdt:P921 ?item2 ;\n        wdt:P50 ?item1 .\n  ?item1 wdt:P102/wdt:P465 ?rgb .\n#  OPTIONAL { ?item2 wdt:P18 ?image2. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,sv\". }\n}", "context": "Topics that members of the Swedish Parliament motioned about 2020/21", "target": ""}
{"query": "#title: Albums with more than one language statement where none has preferred rank \nselect distinct ?i ?iLabel {\n  ?i wdt:P31 wd:Q482994 ; p:P407 ?a , ?b .\n  filter(str(?a) < str(?b))\n  minus { ?i p:P407/wikibase:rank wikibase:PreferredRank }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Albums with more than one language statement where none has preferred rank", "target": ""}
{"query": "#title: List of candidates by constituency\n SELECT ?item ?itemLabel ?constituency ?constituencyLabel ?party ?partyLabel ?votes WHERE {\n  ?item wdt:P31 wd:Q5;\n     p:P3602 ?statement.\n  ?statement ps:P3602  wd:Q106253677.\n  OPTIONAL { ?statement pq:P768 ?constituency. }\n  OPTIONAL { ?statement pq:P1268 ?party. }\n  OPTIONAL { ?statement pq:P1111 ?votes. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?constituencyLabel)", "context": "List of candidates for the next French legislative elections", "target": ""}
{"query": "SELECT ?occupation   ?occupationLabel (COUNT(*) AS ?count)  WHERE {\n  VALUES ?target {\n    wd:Q385468\n    wd:Q17524931\n    wd:Q18093037\n  }\n  ?item wdt:P31 wd:Q5;\n    wdt:P106 ?occupation;   \n     p:P735 ?statement.\n  ?statement ps:P735  ?target.\n  OPTIONAL { ?statement pq:P1545 ?rank. }\n  BIND(IF(BOUND(?rank),?rank, 0) AS ?rank2)\n  FILTER(xsd:integer(?rank2) < xsd:integer(2))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY  ?occupation   ?occupationLabel\nORDER BY DESC (?count)\nLIMIT 20", "context": "Occupation of people named Elizabeth, Élisabeth or Elisabeth in Wikidata", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?pride ?time ?image {\n  ?pride (wdt:P31|wdt:P279) wd:Q51404;\n         wdt:P571 ?time.\n  OPTIONAL {?pride wdt:P18 ?image}\n}", "context": "Timeline of the start of pride parades from 1970", "target": ""}
{"query": "#defaultView:BubbleChart\n#title: Top 10 genes associated with most diseases on Wikidata\nSELECT \n  ?item ?itemLabel        \n  (COUNT(DISTINCT ?disease) AS ?n_diseases)\n\nWHERE \n{\n  ?item wdt:P31 wd:Q7187 . \n  \n  ?disease wdt:P2293|^wdt:P2293 ?item.\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}\nGROUP BY\n  ?item ?itemLabel\nORDER BY\n  DESC (?n_diseases)\nLIMIT 20", "context": "Top 100 genes with most genetic associations on Wikidata", "target": ""}
{"query": "SELECT ?biennale ?biennaleLabel WHERE {\n  SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:endpoint \"www.wikidata.org\";\n                    wikibase:api \"Search\";\n                    mwapi:srsearch \"inlabel:biennale@en\";\n                    mwapi:srlimit \"max\".\n    ?biennale wikibase:apiOutputItem mwapi:title.\n  }\n  ?biennale p:P2257/psv:P2257 ?interval.\n  ?interval wikibase:quantityAmount ?intervalAmount;\n            wikibase:quantityUnit ?intervalUnit.\n  FILTER(!(?intervalAmount = 2 && ?intervalUnit IN (wd:Q577, wd:Q1092296)))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Biennales that aren’t biennial", "target": ""}
{"query": "#title: Which are the most popular natural products based on the number of statements on their corresponding QID?\nSELECT DISTINCT ?np ?statements\nwith {\n  select distinct ?statements {\n    [] wikibase:statements ?statements.\n  }\n} as %values\nWHERE {\n  hint:Query hint:optimizer \"None\".\n  VALUES ?instances {\n    # Those are the different groups of \"chemicals\" we can be interested in\n    # What I would like would  be this query not to timeout without commenting the 4 below. \n    # (just letting the last very small group to illustrate)\n    wd:Q11173 # chemical compound\n    wd:Q59199015 # group of stereoisomers\n    wd:Q79529 # chemical substance\n    wd:Q17339814 # group of chemical substances\n    wd:Q47154513 # structural class of chemical compounds\n  }\n\n  include %values.\n  filter (?statements > 100).\n  \n  ?np wikibase:statements ?statements;\n    wdt:P31 ?instances;\n    (p:P703/ps:P703) ?t. # This filters chemicals found in a taxon as a proxy for \"natural product\"\n}\nORDER BY DESC (?statements)", "context": "Which are the most popular natural products based on the number of statements on their corresponding QID?", "target": ""}
{"query": "SELECT ?occupation   ?occupationLabel (COUNT(*) AS ?count)  WHERE {\n  VALUES ?target {\n    wd:Q385468\n    wd:Q17524931\n    wd:Q18093037\n  }\n  ?item wdt:P31 wd:Q5;\n    wdt:P106 ?occupation;   \n     p:P735 ?statement.\n  ?statement ps:P735  ?target.\n  OPTIONAL { ?statement pq:P1545 ?rank. }\n  BIND(IF(BOUND(?rank),?rank, 0) AS ?rank2)\n  FILTER(xsd:integer(?rank2) < xsd:integer(2))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY  ?occupation   ?occupationLabel\nORDER BY DESC (?count)\nLIMIT 20", "context": "Occupation of people named Elizabeth, Elisabeth or Élisabeth", "target": ""}
{"query": "#title:Thomas Telford's alleged association with buildings\nSELECT ?role (count(distinct ?item) as ?count)\n\n\nWITH {\n  SELECT DISTINCT ?item ?property ?property_label WHERE {\n     ?item ?predicate wd:Q380875. hint:Prior hint:runFirst true.\n     ?item wdt:P31/wdt:P279* wd:Q811430 .hint:Prior hint:gearing \"forward\".\n     ?property wikibase:directClaim ?predicate .\n     ?property rdfs:label ?property_label . FILTER(lang(?property_label) = 'en') .\n  }\n} AS %items\n\n\nWITH {\n  SELECT ?item (GROUP_CONCAT(?property_label; separator = ' + ') AS ?role) WHERE {\n    INCLUDE %items\n  } GROUP BY ?item\n} AS %roles\n\nWHERE {\n  INCLUDE %roles .\n} GROUP BY ?role ORDER BY DESC(?count)", "context": "Thomas Telford's different alleged associations with buildings, according to wikidata statements", "target": ""}
{"query": "#TEMPLATE={ \"template\": { \"en\": \"Items with senses in the most number of languages\", \"fr\": \"Éléments avec des sens dans le plus de langues\" } } \n\nselect ?item ?itemLabel ?count ?langExample ?langExampleLabel (sample(?lexeme) as ?lex) ?lemma (sample(?sens) as ?sense) {\n  {\n    select ?item (count(distinct ?lang) as ?count) (sample(?lang) as ?langExample) {\n      ?sens wdt:P5137 ?item .\n      ?lexeme ontolex:sense ?sens ;\n              dct:language ?lang .\n  \n    } group by ?item having (?count > 2) \n  }\n  \n  ?lexeme ontolex:sense ?sens ;\n          dct:language ?langExample ;\n          wikibase:lemma ?lemma .\n  ?sens wdt:P5137 ?item .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?item ?itemLabel ?count ?langExample ?lemma ?langExampleLabel order by desc(?count) ", "context": "items with senses in the most languages on Wikidata", "target": ""}
{"query": "SELECT distinct ?item ?itemLabel ?sitelinks ?coord  ?layer ?layerLabel WITH { SELECT ?bridge ?item \nWHERE \n{\n{  ?bridge wdt:P131/wdt:P131 wd:Q22 . } hint:Prior hint:runFirst true.\n  ?bridge wdt:P2505 ?item.\n  ?bridge wdt:P31/wdt:P279* wd:Q12280. hint:Prior hint:gearing \"forward\".\n  ?item wdt:P16 wd:Q387692 .\n  \n} } as %i\nWHERE\n{\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n  ?bridge wdt:P625 ?coord .\n  ?bridge wdt:P2505 ?layer .\n} order by desc(?sitelinks) ?itemLabel", "context": "A & B roads carried on Scottish bridges", "target": ""}
{"query": "#defaultView:Timeline\n#title: Rafael Nadal, denominaciones y premios en wikidata\nselect distinct ?o ?fecha  ?denominacion  where \n{\n  {   wd:Q10132 p:P1344 ?o .\n wd:Q10132  wdt:P18 ?pic . \n?o ?l ?t .\n   optional { ?t  wdt:P585 ?fecha .}\n                            ?t  rdfs:label ?denominacion . \n                                \n                            }\nunion\n{               wd:Q10132 p:P166 ?o .\n  wd:Q10132  wdt:P109 ?pic . \n optional { ?o  pq:P585 ?fecha .}\n                           ?o ?l ?t .\n                            ?t  rdfs:label ?denominacion . \n                      \n                          \n                         \n           \n}\n                        FILTER (lang(?denominacion) = 'es')  \n\n  \n         \n  \n\n}\n  order by ?fecha", "context": "Timeline of Rafael Nadal awards and nominations", "target": ""}
{"query": "#title: What are the chemical compounds found in marine organisms and the articles reporting them?\nSELECT DISTINCT ?structure ?structureLabel ?structure_smiles ?reference ?referenceLabel ?reference_doi\n  WITH {\n  SELECT ?child WHERE {\n    VALUES ?taxon {\n    wd:Q152 # fishes\n    wd:Q93315 # cyanobacteria\n    wd:Q103169 # red algae\n    wd:Q264543 # chlorohyta\n    wd:Q18960 # sponges\n    wd:Q28524 # corals\n    }\n    ?child (wdt:P171*) ?taxon.\n  }\n  }\nAS %child\nWHERE {\n  INCLUDE %child\n          ?structure wdt:P233 ?structure_smiles; \n          p:P703[       \n            ps:P703 ?child;            \n                    (prov:wasDerivedFrom/pr:P248) ?reference ].\n  ?reference wdt:P356 ?reference_doi.  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Articles studying chemicals from the oceans", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?population ?altitude ?populationTimesAltitude WITH {\n  SELECT ?item ?population ?altitude {\n    ?item p:P31 ?pStatement ;\n          wdt:P1082 ?population ;\n          wdt:P2044 ?altitude .\n    ?pStatement ps:P31/wdt:P279* wd:Q484170 .\n    FILTER NOT EXISTS { ?pStatement pq:P576 ?x }\n  }\n} AS %i\nWHERE\n{\n  INCLUDE %i\n  BIND ( (?population * ?altitude) AS ?populationTimesAltitude)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?populationTimesAltitude)\nLIMIT 100", "context": "Municipalities of France, by their population and their altitude", "target": ""}
{"query": "SELECT ?person ?personLabel ?spouse ?spouseLabel ?grandparent ?grandparentLabel ?linkcount WHERE {\n    ?person wdt:P31 wd:Q5 . # en människa\n    ?person wdt:P26 ?spouse .\n    ?person wdt:P569 ?date .\n    FILTER(datatype(?date)=xsd:dateTime\n           && ?date >= \"1800-01-01T00:00:00\"^^xsd:dateTime)\n    ?person wikibase:sitelinks ?linkcount .\n    ?spouse wikibase:sitelinks ?spouselinkcount .\n    FILTER (?linkcount >= ?spouselinkcount) # för att inte få paren två gånger (utom när lika \"kända\")\n    ?parent wdt:P40 ?person .\n    ?grandparent wdt:P40 ?parent .\n    ?spouseparent wdt:P40 ?spouse .\n    ?grandparent wdt:P40 ?spouseparent .\n    ?person wikibase:sitelinks ?linkcount . # hur sortera på t.ex. summa av linkcount?\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv,en,de,fr\" }\n}\nGROUP BY  ?linkcount ?person ?personLabel ?spouse ?spouseLabel ?grandparent ?grandparentLabel\nORDER BY DESC(?linkcount)\n", "context": "In cousin marriages (born 1800 and later)", "target": ""}
{"query": "SELECT ?actor ?actorLabel ?characterLabel ?count WITH {\n  SELECT ?actor ?character (COUNT(DISTINCT ?film) AS ?count) WHERE {\n    ?film wdt:P31/wdt:P279* wd:Q11424;\n          p:P161 [\n            ps:P161 ?actor;\n            pq:P453 ?character\n          ].\n    FILTER(?actor != ?character)\n    ?character wdt:P31 wd:Q5;\n               wdt:P39 ?position.\n  }\n  GROUP BY ?actor ?character\n  HAVING(?count > 1)\n  ORDER BY DESC(?count)\n  LIMIT 100\n} AS %results WHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?count)\nLIMIT 100", "context": "Actors who played the same real politician the most times", "target": ""}
{"query": "#defaultView:ImageGrid\n#title: Most famous heritage locations of the world (measured by sitelinks)\nSELECT DISTINCT\n  ?item ?itemLabel ?sitelinks (SAMPLE(?pic) as ?sample_pic)\nWHERE \n{\n  ?item wdt:P1435 [] . \n  ?item wikibase:sitelinks ?sitelinks . hint:Prior hint:rangeSafe true.\n  FILTER (?sitelinks > 50) \n  \n  ?item wdt:P18 ?pic .\n  ?item rdfs:label ?itemLabel . \n  FILTER (LANG (?itemLabel) = \"en\")\n}\nGROUP BY ?item ?itemLabel ?sitelinks\nORDER BY DESC(?sitelinks)\n", "context": "Most famous heritage locations (measured by sitelinks)", "target": ""}
{"query": "SELECT DISTINCT ?island ?islandLabel ?countryLabel ?size ?coord ?landwatersLabel ?lwnatureLabel\n\nWHERE \n{\n  ?island wdt:P31 wd:Q23442 ;\n          wdt:P2046 ?size ;\n          wdt:P17 ?country ;\n          wdt:P625 ?coord .\n \n  ?landwaters wdt:P31 ?lwnature .\n  ?lwnature wdt:P279* wd:Q863944 .\n  ?landwaters wdt:P276 ?island .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # le label viendra de préférence dans votre langue, et autrement en anglais\n} ORDER BY ?size", "context": "Islands with at least 1 spring or freshwater body", "target": ""}
{"query": "SELECT ?item ?articlename ?itemLabel ?itemDescription ?sl\nWHERE {\n   VALUES ?dod {\"+1966-08-25\"^^xsd:dateTime}\n    ?dod ^wdt:P569 ?item .\n    ?item wikibase:sitelinks ?sl .\n    ?item ^schema:about ?article .\n    ?article schema:isPartOf <https://en.wikipedia.org/>;\n    schema:name ?articlename .\n  SERVICE wikibase:label\n    {\n      bd:serviceParam wikibase:language \"en\" .\n      ?item rdfs:label ?itemLabel .\n      ?item schema:description ?itemDescription .\n    }\n  BIND(REPLACE(?itemLabel, \"^.*(?<! [Vv][ao]n| [Dd][aeiu]| [Dd][e][lns]| [Ll][ae]) (?!([SJ]r\\\\.?|[XVI]+)$)\", \"\") AS ?sortname)  \n} ORDER BY ASC(UCASE(?sortname)) ASC(UCASE(?itemLabel))", "context": "Notable people who you share a birthday with (find the string \"1966-08-25\" and replace it with your date of birth)", "target": ""}
{"query": "SELECT DISTINCT ?director ?directorLabel (COUNT(*) AS ?count) WHERE {\n  ?item wdt:P161 wd:Q106204;\n    wdt:P57 ?director.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?director ?directorLabel\nORDER BY DESC(?count)", "context": "List of filmmakers with whom Jean-Louis Trintignant has played", "target": ""}
{"query": "SELECT ?cemetery ?cemeteryLabel ?churchLabel ?church ?distance {\n  ?cemetery wdt:P31/wdt:P279* wd:Q39614;\n            wdt:P17 wd:Q142;\n            wdt:P625 ?cem_coords.\n  SERVICE wikibase:around {\n      ?church wdt:P625 ?chr_coords.\n      bd:serviceParam wikibase:center ?cem_coords.\n      bd:serviceParam wikibase:radius \"0.02\".\n      bd:serviceParam wikibase:distance ?distance.\n  }\n  FILTER EXISTS {\n    ?church wdt:P31/wdt:P279* wd:Q16970.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en\". }\n}", "context": "Cemeteries in France near churches", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?from ?fromLabel ?to ?toLabel ?rgb WHERE {\n  {SELECT (?fictional_war AS ?from) (?universe AS ?to) (\"F7FF00\" as ?rgb) WHERE {\n  ?fictional_war wdt:P31 wd:Q17198419. OPTIONAL{?fictional_war wdt:P1080 ?narr_universe}\n    OPTIONAL {?fictional_war wdt:P8345 ?franchise }\n    OPTIONAL {?fictional_war wdt:P1441 ?work }\n    BIND(COALESCE(?narr_universe, ?franchise, ?work, '') AS ?universe)\n  } }\nUNION {SELECT (?participant AS ?from) (?fictional_war AS ?to) (\"7FFF00\" as ?rgb) WHERE {\n   ?fictional_war wdt:P31 wd:Q17198419.\nOPTIONAL { ?fictional_war wdt:P710 ?participant } } }\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Graph of fictional wars and their participants", "target": ""}
{"query": "#defaultView:BubbleChart\n\nPREFIX target: <http://www.wikidata.org/prop/direct/P921>\n\nSELECT ?count ?topic ?taxonname\nWITH {\n  SELECT (COUNT(?publication) AS ?count) ?topic WHERE {\n    ?publication target: ?topic .\n    ?topic wdt:P105 wd:Q7432 .\n  }\n  GROUP BY ?topic\n  ORDER BY DESC(?count)\n  LIMIT 100\n} AS %result\nWHERE {\n  INCLUDE %result\n  ?topic wdt:P225 ?taxonname .\n}\nORDER BY DESC(?count)", "context": "The 100 most common species as subjects of publications known to Wikidata", "target": ""}
{"query": "#title: Lieux de naissance des joueurs de l'ASM Clermont Auvergne\n#defaultView:Map\nSELECT ?item ?itemLabel ?lieu ?date ?coord ?layer ?img WHERE {\n  ?item wdt:P54 wd:Q297901 ;\n        wdt:P19 ?lieu ;\n        wdt:P569 ?date .\n  ?lieu wdt:P625 ?coord .\n  OPTIONAL { ?item wdt:P18 ?img }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  BIND ( CONCAT(\"Années \",STR(FLOOR(YEAR(?date)/10)),\"0\") AS ?layer )\n}", "context": "Map of birthplaces of ASM Clermont Auvergne players", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\nSELECT (count(?album) as ?albums) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC(?albums) ASC(?P407)\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Number of albums in Wikidata by language, in descending order", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT  ?ocu ?ocuLabel  (COUNT(DISTINCT ?item) AS ?count)\n(SAMPLE(?ocu) as ?ocu_sampled)\nwhere {\n  hint:Query hint:optimizer \"None\".\n  ?item wdt:P2966 ?llgc .\n  ?item wdt:P106 ?ocu .\n?ocu wdt:P279+ wd:Q639669 .\n \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n} \nGROUP BY ?ocu ?ocuLabel\nORDER BY DESC(?count) ?ocuLabel\n\n", "context": "Occupation about musicians in Wales", "target": ""}
{"query": "#title: Carte des dépôts de tramways en France\n#defaultView:Map\nSELECT DISTINCT ?site ?siteLabel ?coordinates ?image WHERE {\n  ?site (wdt:P31/(wdt:P279*)) wd:Q27028153;\n    wdt:P17 wd:Q142;\n    wdt:P625 ?coordinates.\n  OPTIONAL { ?site wdt:P18 ?image. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Map of tram depots in France", "target": ""}
{"query": "#defaultView:Map\n#title : List of all mountains in France higher than 2,500 meters\nSELECT ?item ?itemLabel ?itemDescription ?instance ?instanceLabel ?altitude ?coord\nWHERE  {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  wd:Q8502 ^(wdt:P279*) ?instance.\n  ?item wdt:P31 ?instance;\n        wdt:P17 wd:Q142;\n        wdt:P625 ?coord;\n        wdt:P2044 ?altitude. \n  FILTER(?altitude >= 2500) \n}\nORDER BY DESC(?altitude) \n", "context": "Mountains higher than 2,500 meters in France", "target": ""}
{"query": "SELECT ?tour ?tourLabel ?startdate ?tournumber ?item ?itemLabel ?stagenumber ?winner ?winnerLabel ?country ?countryLabel WHERE {\n  ?tour wdt:P31 wd:Q33881;\n    rdfs:label ?tourLabel.\n  FILTER((LANG(?tourLabel)) = \"en\")\n  ?item wdt:P361 ?tour;\n    rdfs:label ?itemLabel.\n  FILTER((LANG(?itemLabel)) = \"en\")\n  ?tour wdt:P580 ?startdate.\n  ?tour wdt:P393 ?tournumber.\n  ?item wdt:P1545 ?stagenumber.\n  ?item p:P1346 ?statement.\n  ?statement ps:P1346 ?winner.\n  ?statement pq:P642 wd:Q20882747 .\n  ?winner rdfs:label ?winnerLabel.\n  FILTER((LANG(?winnerLabel)) = \"en\" )\n  ?winner wdt:P27 ?country.\n  ?country rdfs:label ?countryLabel.\n  FILTER((LANG(?countryLabel)) = \"en\" )\n}", "context": "List of all Tour de France's stage winners by nationality from 1903 to 2022", "target": ""}
{"query": "#title: Nombre d’équipes de rugby français selon la date de création\n#defaultView:BubbleChart\nSELECT ?année (COUNT(?équipe) AS ?nombre) WHERE {\n  ?équipe wdt:P31 wd:Q14645593 ; \n          wdt:P17 wd:Q142 .\n  OPTIONAL { ?équipe wdt:P571 ?date . }\n  BIND ( STR(YEAR(?date)) AS ?year )\n  BIND(IF(BOUND(?year),?year,\"sans date\") AS ?année)\n}\nGROUP BY ?année", "context": "French rugby teams according to the year of creation", "target": ""}
{"query": "# French members of parliament that were on the same legislature and are or have been married \nSELECT DISTINCT ?person1 ?person1Label ?person2 ?person2Label\nWHERE \n{\n  ?person1 wdt:P31 wd:Q5 ;\n           wdt:P26 ?person2 .\n  ?person1 p:P39 ?function1Statement .\n\n  ?function1Statement ps:P39 wd:Q3044918 .\n  ?function1Statement pq:P2937 ?legislature1 .\n\n  ?person2 wdt:P31 wd:Q5 .\n  ?person2 p:P39 ?function2Statement .\n\n  ?function2Statement ps:P39 wd:Q3044918 .\n  ?function1Statement pq:P2937 ?legislature2 .\n  \n  FILTER(?legislature1 = ?legislature2)\n  FILTER(str(?person1) < str(?person2))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr,en\". }\n}", "context": "French members of parliament that were on the same legislature and are or have been married", "target": ""}
{"query": "SELECT ?country ?countryLabel (COUNT(DISTINCT ?item) AS ?count) WHERE {\n  ?item (wdt:P31|wdt:P279) wd:Q51404;\n    wdt:P17 ?country;\n    wdt:P571 ?time.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?country ?countryLabel\nORDER BY (?countryLabel)", "context": "Number of countries on Wikidata where at least one pride parade has been held", "target": ""}
{"query": "#title: Football players whose birthday is today (different everyday)\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?pic ?birth_date WHERE \n{\n  BIND(MONTH(NOW()) AS ?nowMonth)\n  BIND(DAY(NOW()) AS ?nowDay)\n  \n  ?item wdt:P106 wd:Q937857 .\n  ?item wdt:P18 ?pic .\n  \n  ?item wikibase:sitelinks ?sitelinks .\n  hint:Prior hint:rangeSafe true.\n  FILTER(?sitelinks > 20)\n  \n  ?item wdt:P569 ?birth_date . \n  hint:Prior hint:rangeSafe true.\n  \n  FILTER (MONTH(?birth_date) = ?nowMonth && DAY(?birth_date) = ?nowDay)\n  \n  ?item rdfs:label ?itemLabel. \n  FILTER(LANG(?itemLabel) = \"en\")\n  \n }\nORDER BY\n  DESC(?sitelinks)", "context": "Football players whose birthday is today (different every day)", "target": ""}
{"query": "#title: Current list of French departments\nSELECT ?codeInsee ?item ?itemLabel ?itemDescription ?instance ?instanceLabel  WHERE {\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" . }\n{\nwd:Q6465 ^(wdt:P279*) ?instance.\n?item p:P31 ?typeStatement.\n?typeStatement ps:P31 ?instance.\nFILTER(NOT EXISTS { ?typeStatement pq:P582 _:b2. })\n}\nUNION\n{\n?item p:P31 ?typeStatement.\n?typeStatement ps:P31 wd:Q22923920;\npq:P2868 wd:Q6465.\n}\n?item wdt:P2586 ?codeInsee\n}\nORDER BY (?codeInsee)", "context": "Current list of French departments", "target": ""}
{"query": "SELECT ?item ?itemLabel ?date\nWHERE \n{\n  ?item wdt:P39 wd:Q274948 ;\n        wdt:P1196 wd:Q149086 ;\n        wdt:P570 ?date .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # le label viendra de préférence dans votre langue, et autrement en anglais\n}\nORDER BY ?date", "context": "Prime ministers of Japan whose manner of death is homicide", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?shape\", \"?coords\"]}\nSELECT  ?item ?countryLabel ?itemLabel ?population ?coords ?shape ?layer\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q10864048 ;\n        wdt:P1082 ?population ;\n        wdt:P17 ?country .\n  FILTER ( ?population > 10000000 )\n  FILTER NOT EXISTS { ?item wdt:P31 wd:Q19953632 }\n  FILTER NOT EXISTS { ?item wdt:P576 ?x }\n  OPTIONAL { ?item wdt:P625 ?coords }\n  OPTIONAL { ?item wdt:P3896 ?shape }\n  BIND(\n    IF(?population < 20000000, \"10M-20M\",\n    IF(?population < 30000000, \"20M-30M\",\n    IF(?population < 40000000, \"30M-40M\",\n    IF(?population < 50000000, \"40M-50M\",\n    IF(?population < 60000000, \"50M-60M\",\n    IF(?population < 70000000, \"60M-70M\",\n    IF(?population < 80000000, \"70M-80M\",\n    IF(?population < 90000000, \"80M-90M\",\n    IF(?population < 100000000, \"90M-100M\",   \n    \"100M+\")))))))))\n    AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?population", "context": "1st level administrative subdivisions with more than 10 million inhabitants", "target": ""}
{"query": "#title: List of globes and how many times they've been used\nselect *\nwith {\n  select ?globe (count(*) as ?total) {\n      [] wikibase:geoGlobe ?globe.\n    } group by ?globe\n} as %globes\n{\n  include %globes.\n  optional { ?globe rdfs:label ?en filter (lang(?en) = \"en\") }\n} order by desc(?total) ?en", "context": "List of globes and how many times they've been used", "target": ""}
{"query": "# Query generated by [[Template:Item classification]]\n#title: List of instances\nSELECT ?item ?itemLabel ?itemDescription ?instance ?instanceLabel ?date WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  wd:Q215864 ^(wdt:P279*) ?instance.\n  ?item wdt:P31 ?instance.\n  OPTIONAL { ?item wdt:P585 ?date. }\n}\nORDER BY DESC (?date)\nLIMIT 1000", "context": "List of recent heatwaves", "target": ""}
{"query": "# Query generated by [[Template:Item classification]]\n#title: List of instances \nSELECT ?item ?itemLabel ?itemDescription ?instance ?instanceLabel ?date WHERE  {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  wd:Q2904148 ^(wdt:P279*) ?instance.\n  ?item wdt:P31 ?instance. \n  OPTIONAL {?item wdt:P585  ?date } \n}\nORDER BY DESC(?date) \nLIMIT 1000", "context": "Most recent information leaks according to Wikidata", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (count(?item) as ?count) ?heriotzaLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?lehen_ministro wdt:P279 wd:Q14212.\n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P39 ?lehen_ministro.\n  ?item wdt:P509 ?heriotza\n}\nGROUP by ?heriotzaLabel\nORDER by DESC(?count)", "context": "Cause", "target": ""}
{"query": "#title: Brazilian writers born in a city with less than 20.000 inhabitants\n#defaultView:ImageGrid\nSELECT DISTINCT ?item ?itemLabel ?city ?cityLabel ?pic\nWHERE \n{\n  ?item wdt:P27 wd:Q155 . \n  ?item wdt:P19 ?city . \n  ?item wdt:P106 wd:Q36180 . \n  ?item wdt:P18 ?pic . \n  ?item wikibase:sitelinks ?sitelinks .\n  ?city wdt:P1082 ?pop .  \n  FILTER (?pop <20000)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}\nORDER BY DESC(?sitelinks)", "context": "Brazilian writers born in a city with less than 20000 inhabitants", "target": ""}
{"query": "SELECT ?lexicalCategory ?lexicalCategoryLabel ?count ?sample WITH {\n  SELECT ?lexicalCategory (COUNT(DISTINCT ?language) AS ?count) (SAMPLE(?lexeme) AS ?sample) {\n    ?lexeme wikibase:lexicalCategory ?lexicalCategory ; dct:language ?language .\n  }\n  GROUP BY ?lexicalCategory\n} AS %i WHERE {\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY DESC(?count)", "context": "Lexical categories sorted by number of languages using them in Wikidata lexemes", "target": ""}
{"query": "#title: Personnes jouant au rugby à XV par nombre de pages Wiki\n#defaultView:BubbleChart\nSELECT ?item ?itemLabel ?nb WHERE {\n  ?item wdt:P106 wd:Q14089670 ;\n        wikibase:sitelinks ?nb .\n  FILTER ( ?nb > 1 )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "People playing rugby union by number of Wikipages", "target": ""}
{"query": "#title: Map of driverless rapid transit railway lines worldwide\n#defaultView:Map{\"hide\":[\"?coords\",\"?line\",\"?rgb\"]}\nSELECT ?st ?stLabel ?sl ?coords ?line ?layer ?rgb WHERE {\n?search (wdt:P31/wdt:P279*) wd:Q5503;wdt:P527 ?lignes.\n?lignes wdt:P559 ?termini;(wdt:P31/wdt:P279*) wd:Q107343049.\n?st (wdt:P31/wdt:P279*) wd:Q928830;wdt:P16 ?search;wdt:P625 ?coords;(wdt:P81|wdt:P1192) ?sl;wdt:P197 ?pred;p:P197[ps:P197 ?pred;pq:P5051 ?towards;(pq:P81|pq:P1192) ?line_pq].\n?pred wdt:P625 ?coords_pred;(wdt:P81|wdt:P1192) ?sl_pred.\nFILTER(?sl_pred = ?lignes)FILTER(?sl = ?lignes)FILTER(?towards = ?termini)\n?st wdt:P5817 wd:Q55654238.\n?pred wdt:P5817 wd:Q55654238.\n?lignes wdt:P5817 wd:Q55654238.\n?pred p:P625[psv:P625[wikibase:geoLatitude ?lat1;wikibase:geoLongitude ?lon1]; rdf:type wikibase:BestRank].\n?st p:P625[psv:P625[wikibase:geoLatitude ?lat2;wikibase:geoLongitude ?lon2]; rdf:type wikibase:BestRank].\nBIND(STRDT(CONCAT(\"LINESTRING(\",STR(?lon1),\" \",STR(?lat1),\",\",STR(?lon2),\" \",STR(?lat2),\")\"),geo:wktLiteral)AS ?line)\nOPTIONAL{?sl wdt:P465 ?rgb.}\nSERVICE wikibase:label{bd:serviceParam wikibase:language \"en\". ?sl rdfs:label ?layer. ?st rdfs:label ?stLabel} \n}", "context": "Map of driverless rapid transit railway lines worldwide", "target": ""}
{"query": "#An example of finding problematic references\n#References that are 'stated in' a geographic region. By: User:Infrastruktur\nSELECT ?st ?itemLabel\nWITH {\n  SELECT DISTINCT ?st ?item\n  WHERE {\n    ?refnode pr:P248 ?statedin ;\n      ^prov:wasDerivedFrom ?st .\n    ?statedin wdt:P31/wdt:P279* wd:Q82794. # Geographic region\n    ?item ?p ?st.\n  }\n  LIMIT 250\n} AS %i\nWHERE {\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n}\n", "context": "An example of finding problematic references", "target": ""}
{"query": "#title:scholarly papers by University of Leeds researchers in White Rose repository with a free licence\nSELECT DISTINCT ?paper ?paperLabel ?licenceLabel ?fulltext WHERE {\n  ?author wdt:P108 wd:Q503424.   # Employee of University of Leeds\n  ?paper wdt:P31 wd:Q13442814; wdt:P50 ?author.   # scholarly article by this author\n  VALUES ?freelicence {wd:Q6905323 wd:Q6905942}   # CC-BY or CC-BY-SA\n  ?paper wdt:P275 ?licence . ?licence wdt:P629? ?freelicence.  # Article has this licence, or a specific edition such as CC-BY 3.0\n  ?paper wdt:P953 ?fulltext FILTER(CONTAINS(STR(?fulltext), \"whiterose.ac.uk\"))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\"}\n} ORDER BY ?paper", "context": "Papers by University of Leeds researchers that might have figures suitable for Wikimedia Commons (with a CC-BY or CC-BY-SA licence, with full text online)", "target": ""}
{"query": "SELECT ?q ?qLabel ?loc ?locLabel { ?q wdt:P19 ?loc . ?loc wdt:P31/wdt:P279* wd:Q4022 . SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }", "context": "People born on rivers", "target": ""}
{"query": "#defaultView:Map\nSELECT\n  ?coordinates\n  ?institution\n  ?institutionLabel\n  ?award\n  ?awardLabel\n  ?item\n  ?itemLabel\nWITH {\n  SELECT DISTINCT ?item {\n    ?item wdt:P31 wd:Q5;\n          wdt:P103 wd:Q188.\n  }\n} AS %0\nWITH {\n  SELECT DISTINCT ?item ?award {\n    INCLUDE %0.\n    ?item wdt:P166 ?award.\n  }\n} AS %1\nWITH {\n  SELECT DISTINCT ?institution ?award {\n    INCLUDE %1.\n    {?institution wdt:P5460 ?award}\n    UNION\n    {?award wdt:P1027 ?institution}.\n  }\n} AS %2\nWITH {\n  SELECT DISTINCT ?institution ?coordinates {\n    INCLUDE %2.\n\n    OPTIONAL{?institution wdt:P625 ?P625}.\n    OPTIONAL{?institution wdt:P159/wdt:P625 ?P159_P625}.\n    OPTIONAL{?institution wdt:P131+/wdt:P625 ?P131_P625}.\n\n    BIND(COALESCE(IF(BOUND(?P625), ?P625, 1/0),\n                  IF(BOUND(?P159_P625), ?P159_P625, 1/0),\n                  IF(BOUND(?P131_P625), ?P131_P625, 1/0))\n         AS ?coordinates)\n\n    FILTER(BOUND(?coordinates))\n  }\n} AS %3\n{\n  INCLUDE %1.\n  INCLUDE %2.\n  INCLUDE %3.\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"en\"}.\n}", "context": "Humans with \"native language\" \"German\"", "target": ""}
{"query": "SELECT ?grammaticalFeature ?grammaticalFeatureLabel (COUNT(DISTINCT ?lexeme) AS ?count) (SAMPLE(?lexeme) AS ?sample) {\n  ?lexeme dct:language wd:Q150 ; ontolex:lexicalForm/wikibase:grammaticalFeature ?grammaticalFeature .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nGROUP BY ?grammaticalFeature ?grammaticalFeatureLabel\nORDER BY DESC(?count) ?grammaticalFeatureLabel", "context": "Grammatical features used on forms of French lexemes", "target": ""}
{"query": "#defaultView:Map\nSELECT ?person ?personLabel ?linkcount ?birthplace ?birthplaceLabel ?coordinates {\n  VALUES ?linkcount { 100 101 102 103 104 105 106 107 108 109 110 111 112 113 114 115 116 117 118 119 120 121 122 123 124 125 126 127 128 129 130 131 132 133 134 135 136 137 138 139 140 141 142 143 144 145 146 147 148 149 150 151 152 153 154 155 156 157 158 159 160 161 162 163 164 165 166 167 168 169 170 171 172 173 174 175 176 177 178 179 180 181 182 183 184 185 186 187 188 189 190 191 192 193 194 195 196 197 198 199 200 201 202 203 204 205 206 207 208 209 210 211 212 213 214 215 216 217 218 219 220 221 222 223 224 225 226 227 228 229 230 231 232 233 234 235 236 237 238 239 240 241 242 243 244 245 246 247 248 249 250 251 252 253 254 255 256 257 258 259 260 261 262 263 264 265 266 267 268 269 270 271 272 273 274 275 276 277 278 279 280 281 282 283 284 285 286 287 288 289 290 291 292 293 294 295 296 297 298 299 300 301 302 303 304 305 306 307 308 309 310 311 312 313 314 315 316 317 318 319 320 } \n  ?person wdt:P31 wd:Q5 ; wikibase:sitelinks ?linkcount ; wdt:P19 ?birthplace .\n  ?birthplace wdt:P625 ?coordinates .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\nORDER BY DESC(?linkcount)\n", "context": "Most notable people", "target": ""}
{"query": "# Query generated by [[Template:Item classification]]\n#title: List of instances\nSELECT ?item ?itemLabel ?itemDescription ?instance ?instanceLabel ?date WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  wd:Q43059 ^(wdt:P279*) ?instance.\n  ?item wdt:P31 ?instance.\n  \n  OPTIONAL { ?instance wdt:P585 ?date. }\n}\nORDER BY DESC(?date) \nLIMIT 1000", "context": "List of draughts", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?person ?personLabel  ?coord  WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?person WHERE {\n      ?person wdt:P5008 wd:Q111645234.\n      \n\n  }\n  }\n OPTIONAL     {?person       wdt:P108/wdt:P625 ?coord. }\n\n}\n", "context": "Map of NZ graduates based on coordinates of employer", "target": ""}
{"query": "#title:Most recent date not used as a date of birth (P569) or date of death (P570)\nSELECT ?date\n\nWITH {\n  SELECT ?date WHERE {\n    wd:Q47150325 ^wdt:P31 [wdt:P585 ?date] .\n    FILTER(xsd:date(?date) < xsd:date(NOW()))\n  }\n} AS %results1 \n\nWITH {\n  SELECT ?date WHERE {\n    INCLUDE %results1.\n    MINUS{[] ps:P570 ?date .}\n  }\n} AS %results2\n\nWHERE {\n  INCLUDE %results2.\n  MINUS{[] ps:P569 ?date .}\n} ORDER BY DESC(?date) LIMIT 1", "context": "Most recent date not used as a date of birth (P569) or date of death (P570)", "target": ""}
{"query": "#Map of war memorials, showing EN wikipedia article if it exists\n#defaultView:Map\nSELECT DISTINCT ?geo ?memorial ?memorialLabel ?article ?sitelink WHERE {\n  ?memorial wdt:P31/wdt:P279* wd:Q575759;\n  wdt:P625 ?geo.\n  OPTIONAL {?article schema:about ?memorial ;\n  schema:isPartOf <https://en.wikipedia.org/> ; \n  schema:name ?sitelink . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of war memorials, showing EN Wikipedia article if it exists", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?itemDescription ?place ?placeLabel ?sitelinks WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?item wikibase:sitelinks ?sitelinks.\n  ?item wdt:P19 ?place. \n  ?place wdt:P131* wd:Q173695.\n}\nORDER BY DESC (?sitelinks)\nLIMIT 200", "context": "Most frequent occupations of people born in Épinal", "target": ""}
{"query": "#title: Espécies nomeadas em homenagem a lugares no Espírito Santo\nSELECT ?item ?itemLabel ?location ?locationLabel\nWHERE \n{\n  ?item wdt:P138 ?location. \n  ?item wdt:P31 wd:Q16521 .\n  ?item wdt:P105 wd:Q7432 .\n\n  {?location wdt:P131* wd:Q43233 .}\n  UNION\n  {VALUES ?location {wd:Q43233 wd:Q111802884}}\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Species named after places in the state of Espírito Santo", "target": ""}
{"query": "SELECT ?Vladimir_Lenin ?Vladimir_LeninLabel ?koordenatuak WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Vladimir_Lenin wdt:P138 wd:Q1394.\n  OPTIONAL { ?Vladimir_Lenin wdt:P625 ?koordenatuak. }\n}", "context": "Places named after Lenin", "target": ""}
{"query": "#defaultView:LineChart\nSELECT ?statements  ?countz \n\nWITH {\n  SELECT ?statements (count(*) as ?countz) WHERE {\n     ?item wikibase:statements ?statements\n  } group by ?statements \n} AS %counts\n\nWHERE {\n  INCLUDE %counts .\n  FILTER (?statements <= 35) .\n} ORDER BY ?statements\n            ", "context": "Most common numbers of statements per item", "target": ""}
{"query": "#art people who died in Nazi camps\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?placediedLabel ?placedied  WHERE {\n{ ?item wdt:P106 wd:Q1792450.} UNION { ?item wdt:P31 wd:Q1007870. } UNION { ?item wdt:P106 wd:Q173950.} UNION { ?item wdt:P921 wd:Q328376.} UNION { ?item wdt:P106 wd:Q10732476.} UNION { ?item wdt:P106 wd:Q446966.} UNION { ?item wdt:P106 wd:Q22132694.} UNION { ?item wdt:P106 wd:Q674426.}\n\n\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n?item wdt:P20 ?placedied.  \n{ ?placedied wdt:P31 wd:Q328468.} UNION { ?placedied wdt:P31 wd:Q152081. } UNION { ?placedied wdt:P31 wd:Q153813.} UNION { ?placedied wdt:P31 wd:Q153813.} UNION { ?placedied wdt:P31 wd:Q2583015.}\n\n# FILTER (YEAR(?birth) = 1860 &amp;&amp; YEAR(?birth) &lt;= 1990)\n\n}\nLIMIT 300", "context": "Art people who died in Nazi camps", "target": ""}
{"query": "SELECT ?statements (count(*) as ?countz)\nWHERE \n{\n?item wikibase:statements ?statements\n  } group by ?statements order by desc(?countz)", "context": "Count of items in Wikidata by number of statements in the item", "target": ""}
{"query": "PREFIX schema: <http://schema.org/>\n\nSELECT DISTINCT ?item ?taxonname ?itemLabel ?irudia ?sitelinks WHERE {\n  ?item wdt:P31 wd:Q16521.\n  ?item wdt:P105 wd:Q7432.\n  ?item wdt:P225 ?taxonname.\n  ?item wdt:P171+ wd:Q27720.\n  ?item wdt:P171+ ?family.\n  ?family wdt:P105 wd:Q37517.\n  ?item wdt:P18 ?irudia.\n  ?item wikibase:sitelinks ?sitelinks.\n  ?wen schema:about ?item.\n  ?wen schema:isPartOf <https://ca.wikipedia.org/>.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"la,eu\". }\n  FILTER(NOT EXISTS {\n    ?weu schema:about ?item.\n    ?weu schema:inLanguage \"eu\".\n  })\n}\nORDER BY DESC(?sitelinks)\nLIMIT 100", "context": "Fungi with image, ordered by number of languages it has an article", "target": ""}
{"query": "#title: Longest rivers in France\nSELECT DISTINCT ?item ?itemLabel ?length ?unit ?unitLabel ?length_in_km WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q55659167;\n    wdt:P17 wd:Q142;\n    p:P2043 ?length_statement.\n  ?length_statement psv:P2043 ?length_value.\n  ?length_value wikibase:quantityAmount ?length;\n    wikibase:quantityUnit ?unit.\n  ?unit p:P2370 ?unit_statement.\n  ?unit_statement psv:P2370 ?unit_value.\n  ?unit_value wikibase:quantityAmount ?conversion;\n    wikibase:quantityUnit wd:Q11573.\n  BIND(?length * ?conversion / 1000 AS ?length_in_km)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en\". }\n}\nORDER BY DESC (?length_in_km)\nLIMIT 100", "context": "Longest natural watercourses in France", "target": ""}
{"query": "select distinct ?form1 ?form1Label ?form2 ?form2Label {\n  ?item ontolex:lexicalForm ?form1, ?form2;\n        dct:language wd:Q9027;\n        wikibase:lexicalCategory wd:Q24905.\n  filter(str(?form1) < str(?form2)).\n  minus { ?form1 ?thing [] filter (strstarts(str(?thing), str(p:))) }\n  minus { ?form2 ?thing [] filter (strstarts(str(?thing), str(p:))) }\n  ?form1 ontolex:representation ?form1Label.\n  ?form2 ontolex:representation ?form2Label.\n  minus { ?item dct:language wd:Q9027; ontolex:lexicalForm ?form1. ?form1 wikibase:grammaticalFeature ?feat. filter not exists { ?form2 wikibase:grammaticalFeature ?feat } }\n  minus { ?item dct:language wd:Q9027; ontolex:lexicalForm ?form2. ?form2 wikibase:grammaticalFeature ?feat. filter not exists { ?form1 wikibase:grammaticalFeature ?feat } }\n}", "context": "Multiple forms of a Lexeme with the same grammatical features and where no of these forms have any other statement", "target": ""}
{"query": "select distinct ?i ?lemma {\n  ?i dct:language wd:Q9610 ; wikibase:lemma ?lemma .\n  ?i wdt:P5238 ?j . ?j wdt:P5191 ?k . ?k dct:language wd:Q11059 .\n  minus { ?i wdt:P5191 [] }\n  minus { ?i wdt:P5238 ?l .\n          minus { ?l wdt:P5191/dct:language wd:Q11059 }\n        }\n}\norder by ?lemma", "context": "Bengali Lexemes all of whose parts are derived from Sanskrit words, but which themselves aren't derived from Sanskrit words yet", "target": ""}
{"query": "SELECT ?base ?count ?sample_item ?sample_itemLabel ?sample_itemDescription ?sample_url WHERE {\n  {\n    select (IRI(CONCAT(?stem, '.uk/')) AS ?base) (count(?item) as ?count) (SAMPLE(?item) AS ?sample_item) (SAMPLE(?url) AS ?sample_url) {\n      ?item wdt:P973 ?url .\n      FILTER(CONTAINS(str(?url), '.uk/')) .\n      BIND(STRBEFORE(str(?url), '.uk/') AS ?stem) .                \n    } group by ?stem\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?count)\n", "context": "Most frequent .uk domains for P973 \"described at URL\"", "target": ""}
{"query": "SELECT\n  ?property\n  ?propertyLabel\n  ?count\nWITH {\n  SELECT DISTINCT ?property ?wdt {\n    ?property\n      a wikibase:Property;\n      wdt:P31/wdt:P279* wd:Q105388954;\n      wikibase:directClaim ?wdt.\n  }\n} AS %0\nWITH {\n  SELECT DISTINCT ?item {\n    ?item wdt:P106 wd:Q5482740.\n  }\n  LIMIT 1234\n} AS %1\nWITH {\n  SELECT ?property (COUNT(*) AS ?count) {\n    INCLUDE %0.\n    INCLUDE %1.\n    ?item ?wdt [].\n  }\n  GROUP BY ?property\n} AS %2\n{\n  INCLUDE %2.\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}.\n}\nORDER BY DESC(?count)", "context": "Online accounts used by people that were born in People's Republic of China", "target": ""}
{"query": "SELECT ?journal ?journalLabel (COUNT(DISTINCT ?work) AS ?retractions) (GROUP_CONCAT(DISTINCT ?publisherrLabel;separator=\"; \") AS ?publisher) WHERE {\n#SELECT ?journal ?journalLabel (COUNT(DISTINCT ?work) AS ?retractions) ((SAMPLE(?publisherrLabel) AS ?publisher) WHERE {\n  { ?work wdt:P31 wd:Q45182324 . }\n  UNION\n  { ?work wdt:P793 wd:Q7316896 . }\n  UNION\n  { ?work wdt:P5824 [] . }\n  ?work wdt:P1433 ?journal .\n  OPTIONAL { ?journal wdt:P123 ?publisherr . ?publisherr rdfs:label ?publisherrLabel . FILTER (LANG(?publisherrLabel)=\"en\")}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?journal ?journalLabel\n  ORDER BY DESC(?retractions)", "context": "Journals with the most retractions", "target": ""}
{"query": "SELECT DISTINCT ?url ?code ?named_as ?links ?stmts ?item ?itemLabel ?itemDescription \n\nWITH {\n  SELECT DISTINCT ?url ?code ?named_as  ?item WHERE {\n    { \n      ?item p:P4652 ?stmt . \n      OPTIONAL {?stmt ps:P4652 ?esco .\n        wd:P4652 wdt:P1630 ?fmt .\n        BIND(IRI(REPLACE(?esco, '(^.*)', ?fmt)) AS ?esco_url) .  \n        OPTIONAL {?stmt pq:P528 ?code} .\n      }\n      OPTIONAL {?stmt pq:P4900/p:P4652/pq:P528 ?c1 . BIND(CONCAT(?c1, \".x\") AS ?code)} .\n    } UNION {\n      ?item wdt:P31 wd:Q108300140 . \n      ?item p:P8283 ?stmt .\n      ?stmt ps:P8283 ?code .\n      wd:P8283 wdt:P1630 ?fmt .\n      BIND(IRI(REPLACE(?code, '(^.*)', ?fmt)) AS ?isco_url) .\n    } .\n    OPTIONAL {?stmt pq:P1810 ?named_as} .\n    BIND(COALESCE(?isco_url, ?esco_url) AS ?url) .\n  }\n} AS %items \n\nWHERE {\n  INCLUDE %items .\n  ?item wikibase:statements ?stmts . \n  ?item wikibase:sitelinks  ?links . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?code ?named_as ?itemLabel", "context": "Occupations as classified by ISCO/ESCO", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT DISTINCT ?valueLabel (count(?valueLabel) as ?count)\nWHERE \n{  \n  ?person  wdt:P5008 wd:Q111645234 .\n      ?person p:P106 ?prop . OPTIONAL { ?prop ps:P106 ?value }  \n       SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n      }\nGROUP BY ?valueLabel\nORDER BY DESC(?count)\n\n \n  \n", "context": "Occupation of people in the", "target": ""}
{"query": "#title: Average weights and heights of sportsmen\n#defaultView:ScatterChart\nselect ?sport ?avg_weight ?avg_height ?sportLabel with {\n    select distinct * where \n     {\n    ?item wdt:P1447 [] .\n     } \n  } as %sportspeople with {\n  select distinct ?sport (STR(round(avg(?height)*100)/100) as ?avg_height) (STR(round(avg(?weight)*100)/100) as ?avg_weight) where {\n  include %sportspeople\n  ?item wdt:P641 ?sport .\n  {?sport wdt:P31 wd:Q212434 .} union {?sport wdt:P279 wd:Q212434 .}\n          ?item\n            p:P2067 ?weightProp;\n            p:P2048 ?heightProp;\n            wdt:P21 wd:Q6581097 .\n          MINUS { ?weightProp wikibase:rank wikibase:DeprecatedRank. }\n          MINUS { ?heightProp wikibase:rank wikibase:DeprecatedRank. }\n          ?weightProp (psn:P2067/wikibase:quantityAmount) ?weight.\n          ?heightProp (psn:P2048/wikibase:quantityAmount) ?height.\n  } group by ?sport } as %results where {\n\n  include %results  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Average weights and heights of sportsmen", "target": ""}
{"query": "#title: Letters by numbers of alphabet where they are found\nSELECT ?letter ?letterLabel (COUNT(DISTINCT ?alphab) AS ?nb) (GROUP_CONCAT(?alphabLabel ; separator=\", \") as ?alphabets) WHERE {\n  ?letter wdt:P361 ?alphab .\n  ?alphab wdt:P31/wdt:P279* wd:Q9779 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\".\n                          ?letter rdfs:label ?letterLabel .\n                         ?alphab rdfs:label ?alphabLabel . }\n}\nGROUP BY ?letter ?letterLabel\nORDER BY DESC(?nb)", "context": "Letters by numbers of alphabet where they are found", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?loc WHERE {\n  ?item p:P119 ?stmt .\n  ?stmt ps:P119 wd:Q311 .\n  ?stmt pq:P625 ?loc .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,en\"  }\n} ORDER BY ?itemLabel", "context": "Map of burials at Père Lachaise Cemetery in Paris", "target": ""}
{"query": "#defaultView:LineChart\nselect (STR(?year) as ?year1) (min(?age) as ?age1) where {\n  wd:Q38111 p:P451 ?s. \n    ?s ps:P451 ?partner .\n    ?s pq:P580 ?from .\n    ?s pq:P582 ?to .\n  values ?year {1995 1996 1997 1998 1999 2000 2001 2002 2003 2004 2005 \n                2006 2007 2008 2009 2010 2011 2012 2013 2014 \n                2015 2016 2017 2018 2019 2020 2021 2022 }\n  filter( (YEAR(?from) <= ?year) && (YEAR(?to) >= ?year )  ) . \n?partner wdt:P569 ?date_of_birth .\nbind((?year - YEAR(?date_of_birth)) as ?age) .\n} group by ?year", "context": "Age of Leonardo DiCaprio's girlfriends 1997-2002", "target": ""}
{"query": "#title: Scolarité des préfets\n#defaultView:BubbleChart\nSELECT ?formationLabel (COUNT(DISTINCT ?prefet) AS ?nb) WHERE {\n \n   ?prefet p:P39 ?position.\n  ?position ps:P39 ?prefetde.\n  ?prefetde (wdt:P279*) wd:Q1285463.\n  \n ?prefet wdt:P69 ?formation .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?formationLabel", "context": "Education of French prefects", "target": ""}
{"query": "#title: Number of substance records in PubChem Compounds\n#defaultView:LineChart\nSELECT ?time ?casRecords WHERE {\n  wd:P662 p:P4876 [\n    ps:P4876 ?casRecords ;\n    pq:P585 ?time\n  ]\n}", "context": "Number of substance records in PubChem Compounds", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?shape\", \"?layer\"]}\nSELECT DISTINCT ?river ?riverLabel ?length ?shape ?layer WHERE {\n  {\n    SELECT DISTINCT ?river ?riverLabel ?basinNameLabel ?length WHERE {\n      ?river wdt:P17 wd:Q142 ;\n             wdt:P2043 ?l .\n      BIND (ROUND(?l) AS ?length)\n      FILTER( ?length >= 50 )\n      VALUES ?type { wd:Q573344 wd:Q355304 wd:Q4022 wd:Q3073652 }\n      ?river wdt:P31 ?type .\n      ?river wdt:P4614+ ?basin .\n      ?basin wdt:P138 ?basinName .\n      FILTER NOT EXISTS { ?basinName wdt:P403/wdt:P31 wd:Q573344 }\n      FILTER NOT EXISTS { ?basinName wdt:P403/wdt:P31 wd:Q4022 }\n      FILTER NOT EXISTS { ?basinName wdt:P31 wd:Q165 }\n      FILTER NOT EXISTS { ?basinName wdt:P31 wd:Q9430 }\n      FILTER NOT EXISTS { ?basinName wdt:P31 wd:Q39594 }\n      FILTER NOT EXISTS { ?basinName wdt:P31 wd:Q1973404 }\n      FILTER NOT EXISTS { ?basin wdt:P31 wd:Q82794 }\n      SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n    }\n  }\n  OPTIONAL { ?river wdt:P3896 ?shape }\n  BIND(?basinNameLabel AS ?layer) \n}  \nORDER BY REPLACE(REPLACE(LCASE(?basinNameLabel), \"é\", \"e\"), \"è\", \"e\") ?riverLabel", "context": "Map of rivers longer than 50 km passing through French territory, grouped by watershed", "target": ""}
{"query": "#title: Brazilian players in the Fifa World Cup 2022 Sticker Album\n#defaultView:ImageGrid\nSELECT ?player ?playerLabel ?pic\nWHERE \n{\n  wd:Q113646122 wdt:P180 ?player . \n  ?player wdt:P1532 wd:Q155 . \n  OPTIONAL{?player wdt:P18 ?pic}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Brazilian players in the Fifa World Cup 2022 Sticker Album!", "target": ""}
{"query": "SELECT ?partof ?itemLabel ?count ?Cameron ?May ?Johnson ?Truss\nWITH {\nSELECT ?partof ?C WHERE {\n  ?article schema:about wd:Q192;\n  schema:isPartOf ?partof.\n  ?partof wikibase:wikiGroup \"wikipedia\" .\n  BIND (\"Cameron\" as ?C)\n} } as %i \nWITH {\nSELECT ?partof ?M WHERE {\n  ?article schema:about wd:Q264766;\n  schema:isPartOf ?partof.\n  ?partof wikibase:wikiGroup \"wikipedia\" .\n  BIND (\"May\" as ?M)\n} } as %j\nWITH {\nSELECT ?partof ?J WHERE {\n  ?article schema:about wd:Q180589;\n  schema:isPartOf ?partof.\n  ?partof wikibase:wikiGroup \"wikipedia\" .\n  BIND (\"Johnson\" as ?J)\n} } as %k\nWITH {\nSELECT ?partof ?T WHERE {\n  ?article schema:about wd:Q272201;\n  schema:isPartOf ?partof.\n  ?partof wikibase:wikiGroup \"wikipedia\" .\n  BIND (\"Truss\" as ?T)\n} } as %l\nWITH {\n  SELECT ?partof ?itemLabel (count(*) as ?count) (SAMPLE(?C) as ?Cameron) (SAMPLE(?M) as ?May) (SAMPLE (?J) as ?Johnson) (SAMPLE (?T) as ?Truss) WHERE\n{\n  {INCLUDE %i}\n  UNION\n  {INCLUDE %j}\n  UNION\n  {INCLUDE %k}\n  UNION\n  {INCLUDE %l}\n} group by ?partof ?itemLabel } as %m\nWHERE\n{\n  INCLUDE %m\n  ?item wdt:P856 ?partof. \n  ?item wdt:P31 wd:Q10876391 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } \n} \n", "context": "Language Wikipedias having biographies on recent UK Prime Ministers", "target": ""}
{"query": "SELECT DISTINCT ?end ?item ?itemLabel ?position ?positionLabel  ?causeLabel ?item2 ?item2Label ?b\nWHERE \n{\n  VALUES ?end { \"+2022-07-05\"^^xsd:dateTime \"+2022-07-06\"^^xsd:dateTime \"+2022-07-07\"^^xsd:dateTime }\n  ?item p:P39 ?ps. \n  ?ps ps:P39 ?position .\n  ?ps pq:P582 ?end .\n  OPTIONAL { ?ps pq:P1534 ?cause }\n  ?position wdt:P1001 wd:Q145 \n    OPTIONAL { ?position wdt:P17 wd:Q145 }\n  OPTIONAL { ?item2 p:P39 ?ps2. \n  ?ps2 ps:P39 ?position .\n  ?ps2 pq:P580 ?start .\n  filter(?start >= ?end)\n  filter(?start < \"2022-09-06\"^^xsd:dateTime)            \n  filter not exists {?ps2 pq:P582 [] .}\n    }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } \n}\nORDER BY ?item2\n", "context": "Has Wikidata been updated to show replacement UK ministers post the Great Boris Resignation of July 2022?", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?pic ?coord ?GBIFlink WHERE {\n  ?item wdt:P8714 ?GADM .\n  ?item wdt:P625 ?coord .\n  OPTIONAL { ?item wdt:P18 ?pic . }\n  BIND(URI(CONCAT(\"https://www.gbif.org/occurrence/map?gadm_gid=\", STR(?GADM))) AS ?GBIFlink)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of Global Administrative Areas (GADM) with links to Global Biodiversity Information Facility (GBIF) datasets", "target": ""}
{"query": "#defaultView:Map\nselect distinct ?item ?itemLabel ?itemDescription ?pob ?coord where {\n    ?item wdt:P31 wd:Q5;  \n          wdt:P39 wd:Q11116728;\n          wdt:P19 ?pob.\n  ?pob wdt:P625 ?coord.\n\n   \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?sitelinks)", "context": "Places of birth of heads of the city of Riga 1890-2022", "target": ""}
{"query": "SELECT ?item ?itemLabel \nWHERE \n{\n  ?item wdt:P138 wd:Q9682 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Things named after Queen Elizabeth II", "target": ""}
{"query": "#title: Essencial Open Source Software funded by CZI (sized by number of Wiki pages)\n#defaultView:BubbleChart\nSELECT ?item ?itemLabel ?sitelinks WHERE {\n  \n  \n    ?item p:P8324 [ ps:P8324 wd:Q21623039 ;\n                    pq:P6195 wd:Q113788223 ] . \n     \n    ?item wikibase:sitelinks ?sitelinks . \n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  \n  \n  }\nORDER BY DESC(?sitelinks)", "context": "Essential Open-Source Software funded by CZI Science", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?item ?itemLabel ?itemDescription (MIN(?date2) AS ?date) WHERE {\n  ?item wdt:P57 wd:Q53001;\n    wdt:P577 ?date2.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, fr\". }\n}\nGROUP BY ?item ?itemLabel ?itemDescription\nORDER BY (?date)", "context": "Timeline of movies directed by Jean Luc Godard", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?place ?placeLabel ?coord WHERE {\n  ?item wdt:P57 wd:Q53001;\n    wdt:P840 ?place.\n ?place wdt:P625 ?coord\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, fr\". }\n}\nORDER BY (?year)", "context": "Map of narrative locations of movies directed by Jean-Luc Godard", "target": ""}
{"query": "SELECT DISTINCT ?distribution ?distributionLabel (COUNT(*) AS ?count) WHERE {\n  ?item wdt:P57 wd:Q53001;\n    wdt:P161 ?distribution.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?distribution ?distributionLabel\nORDER BY DESC (?count)", "context": "List of actors and actresses who have worked with Jean-Luc Godard by number of movies", "target": ""}
{"query": "#title:Route of the Aberdeenshire Canal, as indicated by its remains\n#defaultView:Map\nSELECT ?item ?itemLabel ?coords WHERE \n{\n  ?item ?pred wd:Q4667031.\n  ?item wdt:P625 ?coords. \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}", "context": "Route of the Aberdeenshire Canal, as indicated by its remains", "target": ""}
{"query": "#title: Personnes nées à Rennes classées selon le nombre de liens qui pointent vers elles\nSELECT ?q (COUNT(?x) AS ?nb) WHERE {\n  ?q wdt:P19 wd:Q647 .\n  OPTIONAL { ?x ?lien ?q }\n}\nGROUP BY ?q\nORDER BY DESC(?nb)", "context": "People born in Rennes classified according to the number of links pointing to them", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coords\"]}\nSELECT DISTINCT ?item ?itemLabel ?place ?placeLabel ?position ?positionLabel ?date ?coords\nWHERE \n{\n  BIND(wd:Q142 AS ?requestedCountry)\n  ?item wdt:P39 ?position ;\n        wdt:P19 ?place ;\n        wdt:P569 ?date .\n  ?position wdt:P279* ?head ;\n            wdt:P17 ?requestedCountry .\n  { ?head wdt:P279 wd:Q48352 } UNION\n  { ?head wdt:P279 wd:Q2285706 }\n  { ?head wdt:P1001 ?requestedCountry } UNION\n  { FILTER NOT EXISTS { ?head wdt:P1001 ?x } }\n  ?head wdt:P17 ?requestedCountry .\n  ?place wdt:P625 ?coords .\n  FILTER NOT EXISTS { ?place wdt:P17 ?requestedCountry }\n  FILTER ( ?date >= \"+1789-01-01\"^^xsd:dateTime )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?date) ", "context": "French heads of state and government who were not born on current French territory (and after 1789)", "target": ""}
{"query": "#defaultView:Map\n#title: Where were players in the album born? \nSELECT DISTINCT ?player ?playerLabel ?sticker_number ?image ?birth_place ?birth_placeLabel ?coord \nWHERE \n{\n  wd:Q113646122 p:P180 [\n                          ps:P180 ?player ;\n                          pq:P1545 ?sticker_number  \n                       ]  .\n  FILTER (!CONTAINS ( ?sticker_number,\"FWC\"))\n  ?player wdt:P31 wd:Q5 . \n  ?player wdt:P19 ?birth_place .\n  ?birth_place wdt:P625 ?coord .\n  ?birth_place wdt:P17 ?birth_country . \n  ?player wdt:P1532 ?sports_country . \n  BIND(now() as ?today)  \n  OPTIONAL{?player wdt:P18 ?image}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } # Helps get the label in your language, if not, then en language\n}\n", "context": "Where were the players in the FIFA 2022 Sticker Album born?", "target": ""}
{"query": "#title: Heaviest teams in the FIFA 2022 sticker album\nSELECT DISTINCT ?country_for_sports ?country_for_sportsLabel  (AVG(?weight) as ?avg_weight) (COUNT(?player) as ?count)\nWHERE \n{\n  wd:Q113646122 p:P180 [\n                          ps:P180 ?player ;\n                          pq:P1545 ?sticker_number  \n                       ]  .\n  FILTER (!CONTAINS ( ?sticker_number,\"FWC\"))\n  ?player wdt:P31 wd:Q5 . \n  ?player wdt:P1532 ?country_for_sports . \n\n  ?player p:P2067 ?weight_triples .\n  ?weight_triples ps:P2067 ?weight .\n  ?weight_triples psv:P2067 ?weight_triple_value .\n\n  ?weight_triple_value wikibase:quantityUnit wd:Q11570 . #only weight in kilos\n  OPTIONAL{?player wdt:P18 ?image}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } # Helps get the label in your language, if not, then en language\n}\nGROUP BY  ?country_for_sports ?country_for_sportsLabel \nHAVING (?count > 2)\nORDER BY DESC(?avg_weight)\n", "context": "World Cup Qatar 2022: The heaviest team is Ghana, averaging 87 kilos, and the lightest is Qatar, averaging 70 kilos", "target": ""}
{"query": "#defaultView:Map\nSELECT ?street ?streetLabel ?layer ?coords WHERE {\n  ?street wdt:P31 wd:Q79007;\n    wdt:P131 wd:Q490;\n    wdt:P138 ?namedafter;\n    wdt:P625 ?coords.\n  ?namedafter wdt:P21 ?gender.\n  BIND(IF(?gender = wd:Q6581072, \"female\", \"male\") AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Streets of Milan, Italy named after males (blue) vs. females (orange)", "target": ""}
{"query": "#defaultView:Map\nSELECT ?theatreLabel ?coords\n  WHERE {\n    {?theatre wdt:P31/wdt:P279* wd:Q7362268 . }\n    UNION\n    {?theatre wdt:P31/wdt:P279* wd:Q19757 . }\n    \n    ?theatre wdt:P625 ?coords .\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"de\". }\n  }\n", "context": "Map of Roman theaters and amphitheaters", "target": ""}
{"query": "#defaultView:BarChart\n\nSELECT (?namesLabel AS ?name) (COUNT(?monarch) AS ?dead_monarchs) (SAMPLE(?cause_of_deathLabel) AS ?causes_of_death) WHERE {\n  VALUES ?monarchies {wd:Q9134365 wd:Q111722535 wd:Q110324075 wd:Q18810062}\n  ?monarch wdt:P39 ?monarchies;\n           wdt:P735 ?names.\n  ?monarch wdt:P1196 ?cause_of_death.\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                           ?names rdfs:label ?namesLabel .\n                           ?monarch rdfs:label ?monarchLabel .\n                           ?cause_of_death rdfs:label ?cause_of_deathLabel .}\n} GROUP BY ?namesLabel ?cause_of_death\n  ORDER BY DESC (?dead_monarchs)", "context": "Chart of English monarch names by cause of death", "target": ""}
{"query": "#defaultView:BarChart\n\nSELECT (?partiesLabel AS ?party) (COUNT(?president) AS ?dead_presidents) (SAMPLE(?cause_of_deathLabel) AS ?causes_of_death) WHERE {\n  ?president wdt:P39 wd:Q11696;\n           wdt:P102 ?parties.\n  ?president wdt:P1196 ?cause_of_death.\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                           ?parties rdfs:label ?partiesLabel .\n                           ?president rdfs:label ?presidentLabel .\n                           ?cause_of_death rdfs:label ?cause_of_deathLabel .}\n} GROUP BY ?partiesLabel ?cause_of_death\n  ORDER BY DESC (?dead_presidents)", "context": "Cause of death of US presidents by party", "target": ""}
{"query": "SELECT ?localisation ?localisationLabel ?population (COUNT(DISTINCT ?line) AS ?lines) {\n  ?line wdt:P31/wdt:P279* wd:Q15079663 ; wdt:P131 ?localisation .\n  ?localisation wdt:P1082 ?population .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?localisation ?localisationLabel ?population\nHAVING (?lines >= 2)\nORDER BY ?population", "context": "Smallest cities with at least two metro lines", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?occupationLabel (COUNT(DISTINCT ?person) as ?personCount) \nWHERE {\n   ?person wdt:P31 wd:Q5;\n         wdt:P19 ?birthPlace;\n         wdt:P106 ?occupation.\n   ?birthPlace wdt:P17 wd:Q664.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?occupationLabel ORDER BY DESC(?personCount)", "context": "Chart of occupation of people born in New Zealander", "target": ""}
{"query": "#title: Timeline of births of capoeira practitioners\n#defaultView:Timeline\nSELECT ?person ?personLabel ?birthdate ?pic where {\n  ?person wdt:P106 wd:Q29840095 . \n  OPTIONAL {?person wdt:P18 ?pic . }\n  ?person wdt:P569 ?birthdate\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],nl,en\". }\n}", "context": "Capoeira practitioners through time!", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT distinct ?bish ?bishLabel ?dioceseLabel  (year(?death) as ?deathYear) ?image\nWHERE {\n  ?bish wdt:P106 wd:Q42603.\n  ?bish wdt:P570 ?death.\n  ?bish wdt:P708 ?diocese.\n  \n  OPTIONAL { ?bish wdt:P18 ?image.}\n  \n  ?diocese wdt:P31 wd:Q18917976 .\n\n  FILTER (?death <= \"1700-01-01T00:00:00Z\"^^xsd:dateTime)\n  FILTER (?death >= \"1638-01-01T00:00:00Z\"^^xsd:dateTime)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?bish ?bishLabel ?dioceseLabel ?death ?image", "context": "Gallery of mid-seventeenth-century bishops", "target": ""}
{"query": "#title:musical works/compositions with titles that are female given names\nSELECT ?song ?female_name ?title (LANG(?title) AS ?title_language) ?note\nWITH\n{\n  SELECT DISTINCT ?song ?title\n  {\n    ?song wdt:P31 / wdt:P279 * wd:Q105543609 .\n    ?song wdt:P1476 ?title .\n  }\n} AS %songs\n{\n  {\n    INCLUDE %songs\n    ?female_name wdt:P1705 ?title .\n    ?female_name wdt:P31 wd:Q11879590 .\n  }\n  UNION\n  {\n    INCLUDE %songs\n    FILTER (LANG(?title) != \"mul\")\n    BIND (STRLANG(STR(?title), \"mul\") AS ?title_mul)\n    ?female_name wdt:P1705 ?title_mul .\n    ?female_name wdt:P31 wd:Q11879590 . hint:Prior hint:runLast true. \n    BIND (\"match one to multiple\" AS ?note)\n  }\n}", "context": "Song titles that match female given names", "target": ""}
{"query": "SELECT ?song ?songLabel ?performer ?performerLabel (YEAR(?date) AS ?year)\nWHERE \n{\n  ?song wdt:P921 wd:Q9682 ;\n        wdt:P31/wdt:P279* wd:Q105543609 ;\n        wdt:P175 ?performer .\n  OPTIONAL { ?song wdt:P577 ?date }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?year  ", "context": "Songs about Elizabeth II", "target": ""}
{"query": "#defaultView:Graph\nSELECT ?author1 ?author1Label ?rgb ?author2 ?author2Label \nWITH {\n  SELECT ?author1 ?author2 ?rgb WHERE {\n    { ?author1 wdt:P5008 wd:Q113628913 .}\n    { ?author2 wdt:P5008 wd:Q113628913 .}\n    {?author1 wdt:P184 ?author2 }\n    UNION\n    {?author2 wdt:P185 ?author1 }\n    ?author1 wdt:P21 ?gender1 . \n    BIND ( IF(?gender1 = wd:Q6581097, \"3182BD\", \"E6550D\") AS ?rgb)\n  }\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,da,de,es,fr,jp,sv,ru,zh\".\n  }\n}\n", "context": "Student-advisor graph", "target": ""}
{"query": "#title: Official Languages of the Latin America and Caribbean region\n#defaultView:BubbleChart\nSELECT DISTINCT ?language ?languageLabel (SUM(?population) as ?sum_pop)\nWHERE \n{\n  ?item wdt:P37 ?language .\n  ?item wdt:P1082 ?population . \n  ?item wdt:P31 wd:Q3624078 . \n  ?item wdt:P361* wd:Q72829598 . \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } # Helps get the label in your language, if not, then en language\n}\nGROUP BY ?language ?languageLabel", "context": "Official languages of Latin America and the Caribbean!", "target": ""}
{"query": "SELECT ?item ?itemLabel ?partiLabel ?valkretsLabel WHERE {\n ?item wdt:P31 wd:Q5; p:P39 ?parl.\n ?parl ps:P39 wd:Q10655178; pq:P580 ?start ; pq:P2937 wd:Q110585041 .\n FILTER NOT EXISTS { ?parl pq:P582 []}\n ?parl pq:P768 ?valkrets .\n ?parl pq:P4100 ?parti .\n\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv\". }\n} \nORDER BY ?item", "context": "People in the 2022–2026 parliamentary term of the Swedish Riksdag", "target": ""}
{"query": "# software licenses in timeline\n#defaultView:Timeline\nSELECT DISTINCT ?item ?itemLabel ?date\nWHERE \n{\n  # item must be a copyright license or any subclass of copyright license\n  ?item  wdt:P31/wdt:P279* wd:Q77205602.\n  \n  # item must have a date (whatever publication date or inceptin date is fine)\n  ?item  wdt:P577|wdt:P571 ?date\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Software license, by year", "target": ""}
{"query": "SELECT ?item ?title ?wd_coord ?wiki_coord ?dist\nWITH\n{\n  SELECT DISTINCT ?item\n  {\n    ?item wdt:P31 / wdt:P279 * wd:Q55488 .\n    ?item wdt:P17 wd:Q145 .\n    FILTER STRENDS(STR(?item), \"0\")\n  }\n} AS %get_items\n{\n  INCLUDE %get_items\n  ?item p:P625 ?s .\n  ?s ps:P625 ?wd_coord .\n  ?s prov:wasDerivedFrom ?ref .\n  ?ref pr:P143 wd:Q328 .\n  ?sitelink schema:about ?item .\n  ?sitelink schema:isPartOf <https://en.wikipedia.org/> .\n  ?sitelink schema:name ?title .\n  SERVICE wikibase:mwapi\n  {\n    bd:serviceParam wikibase:endpoint \"en.wikipedia.org\" .\n    bd:serviceParam wikibase:api \"Generator\" .\n    bd:serviceParam mwapi:generator \"allpages\" .\n    bd:serviceParam mwapi:gapfrom ?title .\n    bd:serviceParam mwapi:gapto ?title .\n    bd:serviceParam mwapi:prop \"coordinates\" .\n    ?lat wikibase:apiOutput \"coordinates/co/@lat\" .\n    ?lon wikibase:apiOutput \"coordinates/co/@lon\" .\n  }\n  BIND (STRDT(CONCAT(\"Point(\", ?lon, \" \", ?lat, \")\"), geo:wktLiteral) AS ?wiki_coord)\n  BIND (geof:distance(?wd_coord, ?wiki_coord) AS ?dist)\n  FILTER (! BOUND(?dist) || ?dist > 0.0)\n} ORDER BY DESC(?dist)", "context": "UK railway stations with coordinates sourced to English Wikipedia that have different coordinates on English Wikipedia", "target": ""}
{"query": "SELECT DISTINCT ?parent ?parentLabel ?prize1Label ?child ?childLabel ?prize2Label WHERE {\n  ?prize1 wdt:P279?/wdt:P31? wd:Q7191 .\n  ?parent wdt:P166? ?prize1 ;\n          wdt:P40 ?child .\n  ?prize2 wdt:P279?/wdt:P31? wd:Q7191 .\n  ?child wdt:P166? ?prize2 .\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"sv,en,[AUTO_LANGUAGE]\" .\n  }\n}", "context": "Children of Nobel laureates have themselves been awarded the prize", "target": ""}
{"query": "#title:Profession et genre des personnes dont des œuvres sont conservées au musée de Bretagne\n#d'après https://twitter.com/01factory/status/1574893386343092226\n#illustrates bubblechart view, count, using ?rgb as color\n\n#defaultView:BubbleChart\nSELECT ?occupation_fr ?rgb (COUNT(DISTINCT ?person) AS ?personCount) WHERE {\n  ?person wdt:P31 wd:Q5;\n    wdt:P6379 wd:Q3329701 ;\n    wdt:P106 ?occupation;\n    wdt:P21 ?gender.\n  bind( if(?gender=wd:Q6581097,\"94D459\",\"4B1270\") as ?rgb ) .\n  OPTIONAL {?occupation rdfs:label ?occupation_fr filter (lang(?occupation_fr) = \"fr\")}. \n}\nGROUP BY ?occupation_fr ?rgb\nORDER BY DESC(?personCount)", "context": "Profession and gender of people whose works are kept in the Musée de Bretagne", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?localityLabel ?cords WHERE {\n  \n      ?item (wdt:P31/(wdt:P279*)) wd:Q16521.\n      ?item (wdt:P183/(wdt:P279*)) wd:Q664.\n      ?item wdt:P5304 ?locality.\n      ?locality wdt:P625 ?cords.\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\nLIMIT 1000\n", "context": "Map the type locality of New Zealand endemic species", "target": ""}
{"query": "#title: Cartes des lieux de naissance de femmes en Finistère  (avec et sans photo)\n#defaultView:Map\nSELECT ?personne ?personneLabel ?locLabel ?coord ?rgb WHERE {\n  ?personne wdt:P31 wd:Q5 ;\n     wdt:P21 wd:Q6581072 ;\n     wdt:P19 ?loc .\n  ?loc wdt:P131* wd:Q3389 ; wdt:P625 ?coord .\n  OPTIONAL { ?personne wdt:P18 ?image }\n  BIND(IF(BOUND(?image),\"60BB00\",\"666666\") AS ?rgb).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of birthplaces of women in Finistère (with and without photo)", "target": ""}
{"query": "#title:People in the British Museum's person-institution thesaurus by gender and occupation\n#illustrates bubblechart view, count, using ?rgb as color\n\n#defaultView:BubbleChart\nSELECT ?occupation_en ?rgb (COUNT(DISTINCT ?person) AS ?personCount) WHERE {\n  ?person wdt:P31 wd:Q5;\n    wdt:P1711 ?bm_id;\n    wdt:P106 ?occupation;\n    wdt:P21 ?gender.\n  bind( if(?gender=wd:Q6581097,\"3399FF\",\"FF99FF\") as ?rgb ) .\n  OPTIONAL {?occupation rdfs:label ?occupation_en filter (lang(?occupation_en) = \"en\")}. \n}\nGROUP BY ?occupation_en ?rgb\nORDER BY DESC(?personCount)", "context": "People in the British Museum's person-institution thesaurus by gender and occupation", "target": ""}
{"query": "SELECT ?item ?itemLabel ?title\nWHERE {\n  ?item wdt:P31 wd:Q11424;\n        wdt:P1476 ?title.\n  FILTER (CONTAINS(?title, ' it ') || CONTAINS(?title, ' It '))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL { ?item wdt:P18 ?_image. }\n}\nORDER BY (?title)", "context": "Films with \"it\" in the title", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coords\"]}\nSELECT ?building ?buildingLabel (COUNT(DISTINCT ?item) AS ?count) (SAMPLE(?image) AS ?image) (SAMPLE(?coords) AS ?coords)\nWHERE \n{\n  ?item wdt:P31 wd:Q3305213 ;\n        wdt:P180 ?building .\n  ?building wdt:P31/wdt:P279* wd:Q41176 .\n  OPTIONAL { ?building wdt:P625 ?coords }\n  OPTIONAL { ?building wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?building ?buildingLabel\nHAVING (?count >= 5)\nORDER BY DESC(?count) ?buildingLabel", "context": "Buildings most depicted on paintings", "target": ""}
{"query": "SELECT ?language ?languageLabel ?lexemes ?withExternalId ?percent WITH {\n  SELECT ?language (COUNT(*) AS ?count) { ?lexeme dct:language ?language }\n  GROUP BY ?language\n  ORDER BY DESC(COUNT(*))\n  LIMIT 20\n} AS %languages WITH {\n  SELECT ?language ?lexemes ?withExternalId (ROUND(10000 / ?lexemes * ?withExternalId) / 100 AS ?percent) {\n    SELECT ?language (COUNT(*) AS ?lexemes) (SUM(?hasExternalId) AS ?withExternalId) {\n    INCLUDE %languages\n    ?lexeme dct:language ?language .\n      BIND(IF(EXISTS {\n        ?property wikibase:propertyType wikibase:ExternalId ; wikibase:directClaim ?wdt .\n        ?lexeme ?wdt [] .\n      }, 1, 0) AS ?hasExternalId) .\n    }\n    GROUP BY ?language\n  }\n  GROUP BY ?language ?lexemes ?withExternalId\n} AS %stats WHERE {\n  INCLUDE %stats\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY DESC(?lexemes)", "context": "Number and percent of lexemes with an external ID for top 20 languages in Wikidata", "target": ""}
{"query": "#defaultView:Map\n\nSELECT DISTINCT ?item ?itemLabel ?coords ?esgobLabel ?layer \nwhere {\n  ?item wdt:P31 wd:Q102496 .\n  ?item wdt:P7352 ?id .\n?item wdt:P625 ?coords . \n OPTIONAL { ?item wdt:P708 ?esgob . }\n  \n\n  VALUES ?gender { wd:Q6581097 wd:Q6581072 }\n\n  \n  BIND (\n  IF(?esgob = wd:Q3028621, \"St Asaph\",\n IF(?esgob = wd:Q2376211, \"St Davids\",\n    IF(?esgob = wd:Q1403394, \"Bangor\",\n       IF(?esgob = wd:Q1230651, \"Llandaf\",\n        \"\"))))  AS ?layer).\n  \n  \n  \n\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n} \n\nORDER BY ?esgobLabel \n", "context": "Map of historic Welsh parishes", "target": ""}
{"query": "#title: Are you cited by Nobel Prize winners?\n\nSELECT (MIN(?dates) AS ?date) ?work ?workLabel (GROUP_CONCAT(DISTINCT ?winnerLabel; SEPARATOR = \", \") AS ?winners) (COUNT(DISTINCT(?winnerLabel)) AS ?count) WHERE {\n  VALUES ?nobel {\n    wd:Q7191\n    wd:Q80061\n    wd:Q44585\n    wd:Q38104\n  }\n  ?work wdt:P50/wdt:P496 \"0000-0001-9472-2589\" ; # REPLACE WITH YOUR ORCID id\n    wdt:P577 ?datetimes.\n  [] wdt:P2860 ?work;\n    wdt:P50 ?winner.\n  ?winner wdt:P166 ?nobel.\n  BIND(xsd:date(?datetimes) AS ?dates)\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n    ?winner rdfs:label ?winnerLabel.\n    ?work rdfs:label ?workLabel.\n  }\n}\nGROUP BY ?work ?workLabel\nORDER BY DESC (?count)\n", "context": "Are you cited by Nobel Prize winners?", "target": ""}
{"query": "select distinct ?personLabel ?partyLabel (min(year(?start)) as ?first_elected) ?thesisLabel ?year ?institutionLabel ?ethos ?available where\n{\n  ?person wdt:P31 wd:Q5 . \n  ?person p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q16707842 . filter not exists { ?ps pq:P582 ?end } \n  optional { ?ps pq:P4100 ?party }\n  ?person p:P39 ?ps2 . ?ps2 ps:P39 ?term2 . ?term2 wdt:P279 wd:Q16707842 . ?ps2 pq:P580 ?start .   \n  ?thesis wdt:P50 ?person . ?thesis wdt:P31 ?type . ?type wdt:P279* wd:Q187685 . \n  optional { ?thesis wdt:P4536 ?ethos } optional { ?thesis wdt:P953 ?available }\n  optional { ?thesis wdt:P577 ?date . bind(year(?date) as ?year) }\n  optional { ?thesis wdt:P4101 ?institution }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} group by ?personLabel ?partyLabel ?thesisLabel ?year ?institutionLabel ?ethos ?available\norder by ?year ", "context": "Current members of UK Parliament and their PhD theses", "target": ""}
{"query": "#defaultView:Map\nSELECT ?voie ?voieLabel (SAMPLE(?coord) AS ?loc) (SAMPLE(?plaque) AS ?panneau) ?rgb {\n  hint:Query hint:optimizer \"None\" .\n  ?voie wdt:P131+ wd:Q647 ; wdt:P625 ?coord ; wdt:P31/wdt:P279* wd:Q83620 .\n  OPTIONAL { ?voie wdt:P1766 ?plaque }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" }\n  BIND(IF(BOUND(?plaque), \"00F\", \"F00\") AS ?rgb) .\n}\nGROUP BY ?voie ?voieLabel ?rgb", "context": "Map of Rennes", "target": ""}
{"query": "#defaultView:BubbleChart\n# Count of all external identifiers for people with ADB identifier\nSELECT ?propertyLabel ?propertyDescription ?count WHERE {\n  {\n    SELECT ?propertyclaim (COUNT(*) AS ?count) where {\n      ?item wdt:P31 wd:Q5;\n        wdt:P1907 ?adb_id.\n      ?item ?propertyclaim [] .\n    } GROUP BY ?propertyclaim\n  }\n  ?property wikibase:propertyType wikibase:ExternalId .\n  ?property wikibase:directClaim ?propertyclaim .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC (?count)\n", "context": "Count of all external identifiers for people with ADB identifier", "target": ""}
{"query": "SELECT DISTINCT ?band ?bandLabel ?workingtime (COUNT(?members) as ?numberOfMembers) WHERE {\n?band wdt:P31 wd:Q215380 .\n?band p:P527 ?members.\nOPTIONAL {?band wdt:P2031 ?inception.\n?band wdt:P2032 ?end.\nbind(year(?end)-year(?inception) as ?workingtime)}\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n group by ?band ?bandLabel ?workingtime\norder by DESC (?numberOfMembers)", "context": "Bands that have had more members than the Sugababes", "target": ""}
{"query": "SELECT ?chancellorLabel ?chancellor ?start_time ?end_time ?days_in_office ?replacesLabel ?replacedbyLabel WHERE {\n  ?chancellor p:P39 ?term.\n\n  ?term ps:P39 wd:Q531471 .\n\nOPTIONAL { ?term pq:P580 ?start_time.  }\nOPTIONAL {  ?term pq:P582 ?end_time. }\nOPTIONAL {  ?term pq:P1365 ?replaces. }\nOPTIONAL {  ?term pq:P1366 ?replacedby. } \n  \nBIND(FLOOR((?end_time - ?start_time)) AS ?days_in_office).\n\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\".\n  }\n}\nORDER BY ?days_in_office\n", "context": "How long has each UK chancellor been in office?", "target": ""}
{"query": "SELECT DISTINCT ?street1Label ?street2Label\nWHERE \n{\n  ?street1 wdt:P361 wd:Q16024163 ;\n           wdt:P47 ?street2 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}\nORDER BY ?street1Label ?street2Label", "context": "Streets of Paris that meet", "target": ""}
{"query": "#defaultView:ImageGrid\n#title: Players in the FIFA World Cup 2022 Sticker Album with most years in the national team\nSELECT DISTINCT ?player ?playerLabel  ?national_team  ?national_teamLabel (YEAR(MIN(?national_team_debut)) as ?national_team_debut_year) (SAMPLE(?image) as ?sample_image)\nWHERE \n{\n  wd:Q113646122 p:P180 [\n                          ps:P180 ?player ;\n                          pq:P1545 ?sticker_number  \n                       ]  .\n  FILTER (!CONTAINS ( ?sticker_number,\"FWC\"))\n  ?player wdt:P31 wd:Q5 . \n  ?player wdt:P1532 ?any . \n  ?player p:P54 [ ps:P54 ?national_team ; \n                  pq:P580 ?national_team_debut\n                ] . \n  ?national_team wdt:P31 wd:Q6979593 . \n  ?national_team rdfs:label ?national_team_label .\n  FILTER(LANG(?national_team_label) = \"en\")\n  FILTER (!CONTAINS ( ?national_team_label,\"under\"))\n  FILTER (!CONTAINS ( ?national_team_label,\"Olympic\"))\n\n  OPTIONAL{?player wdt:P18 ?image .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } # Helps get the label in your language, if not, then en language\n}\nGROUP BY ?player ?playerLabel  ?national_team  ?national_teamLabel\nORDER BY ASC (?national_team_debut_year)", "context": "Players in the FIFA World Cup 2022 Sticker Album with most years in the national team", "target": ""}
{"query": "SELECT (COUNT(DISTINCT ?lexeme) AS ?lexeme_count) ?dictionaries_count (SAMPLE(?lexeme) AS ?sample) {\n  SELECT ?lexeme (COUNT(DISTINCT ?wdt) AS ?dictionaries_count) {\n    ?lexeme dct:language wd:Q150 .\n    OPTIONAL {\n      ?lexeme ?wdt [] .\n      VALUES ?wdt { wdt:P7722 wdt:P7724 wdt:P7732 wdt:P7766 wdt:P10338 wdt:P11118 }\n    }\n  }\n  GROUP BY ?lexeme\n}\nGROUP BY ?dictionaries_count\nORDER BY ?dictionaries_count", "context": "Number of French lexemes per number of dictionaries they are linked to", "target": ""}
{"query": "#title: Born in Split or Šibenik, use Latin\nSELECT DISTINCT ?item ?itemLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?item WHERE {\n      {\n        ?item p:P19 ?bornSplit.\n        ?bornSplit (ps:P19) wd:Q1663.\n      }\n      UNION\n      {\n        ?item p:P19 ?bornŠibenik.\n        ?bornŠibenik (ps:P19/(wdt:P279*)) wd:Q3549.\n      }\n      ?item p:P1412 ?useLatin.\n      ?useLatin (ps:P1412/(wdt:P279*)) wd:Q397.\n    }\n    LIMIT 100\n  }\n}", "context": "People born in Split or Šibenik for whom there is a statement that they wrote or spoke Latin", "target": ""}
{"query": "#defaultView:Map\nPREFIX venue: <http://www.wikidata.org/entity/Q6294930>\nSELECT DISTINCT ?author ?geo\n\nWITH {\nSELECT DISTINCT ?author_ ?author WHERE {\n  {\n    ?target p:P50 ?author_statement ; wdt:P1433 venue: .\n    ?author_statement ps:P50 ?author_ .\n    ?author_ rdfs:label ?author .\n    FILTER (LANG(?author) = 'en')\n    BIND(CONCAT(\"../author/\", SUBSTR(STR(?author_), 32)) AS ?authorUrl)\n    OPTIONAL {\n      ?author_statement pq:P1545 ?order_ .\n      BIND(xsd:integer(?order_) AS ?order)\n    }\n  }\n  UNION\n  {\n    ?target p:P2093 ?authorstring_statement ; wdt:P1433 venue: .\n    ?authorstring_statement ps:P2093 ?author_\n    BIND(CONCAT(?author_, \" ↗\") AS ?author)\n    OPTIONAL {\n      ?authorstring_statement pq:P1545 ?order_ .\n      BIND(xsd:integer(?order_) AS ?order)\n    }\n  }\n  FILTER (?order = 1)\n}\n} AS %AUTHORS WHERE {\n  INCLUDE %AUTHORS\n  ?author_ ( wdt:P108 | wdt:P1416 ) / wdt:P361* ?organization . \n  ?organization wdt:P625 ?geo .  \n}", "context": "Locations of employers/affiliations of first authors of articles in the Journal of Cheminformatics", "target": ""}
{"query": "# Coordinates of the birthplaces of the presidents of the American Medical Association\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord\nWHERE\n{\n  ?item wdt:P31 wd:Q5 .   # human\n  ?item wdt:P39 wd:Q104253535.  # who have held the position President of the American Medical Association\n  ?item wdt:P19 ?place.\n  ?place wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Birthplaces of the presidents of the American Medical Association", "target": ""}
{"query": "#defaultView:ImageGrid\n\nSELECT ?Nationalflagge ?NationalflaggeLabel ?Bild WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de\". }\n  ?Nationalflagge wdt:P31 wd:Q186516.\n  ?Nationalflagge wdt:P18  ?Bild.\n  ?Nationalflagge wdt:P462  ?Farbe1.\n  ?Nationalflagge wdt:P462  ?Farbe2.\n#  ?Farbe2 wdt:P279 wd:Q3257809.\n\n#  FILTER (?Farbe1 in (wd:Q3257809)). #Purpur \n  FILTER (?Farbe1 in (wd:Q23444)). #Weiß\n  \n  FILTER (?Farbe2 in (wd:Q943)). #Gelb\n\n}\nLIMIT 100", "context": "Flags containing the colors white and yellow", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel \nWHERE \n{\n  VALUES (?aiModel) {\n    (wd:Q85810444)\n    (wd:Q2202883)\n    (wd:Q5532625)\n    (wd:Q3621696)\n    }\n  ?item wdt:P31 ?aiModel\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n  }\nORDER BY ?itemLabel", "context": "AI models", "target": ""}
{"query": "#title: People accused for churel related to Sweden\n#defaultView:Map\nselect distinct ?person ?personLabel ?deathLabel ?coord where {\n  VALUES ?country { wd:Q34 } # If you want another country, put the country/countries you want to see in here\n  ?person wdt:p1595|wdt:P7781|wdt:P1399 wd:Q259745 .\n  OPTIONAL { ?person wdt:P27 ?citizen }\n  OPTIONAL { ?person wdt:P509 ?death . }\n  OPTIONAL { ?person wdt:P20 ?deathplace . ?deathplace wdt:P17 ?c1 }\n  OPTIONAL { ?person wdt:P19 ?birth . ?birth wdt:P17 ?c2 }\n  BIND(IF(?c1 = ?country || ?c2 = ?country || ?citizen = ?country,?country,'') AS ?countrycheck)\n  FILTER (?countrycheck = ?country )\n  OPTIONAL { ?person wdt:P20 ?dp . ?dp wdt:P625 ?coord }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,sv.\" }\n} ", "context": "Swedes accused for churel", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?itemDescription ?itemgpn ?ns ?nsLabel ?nsgLabel\nWHERE {\n  ?item wdt:P31/wdt:P279* wd:Q1348589.\n  ?item wdt:P2824 ?itemgpn.\n  ?item wdt:P138 ?ns.\n  ?ns wdt:P21 ?nsg\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Lunar craters and gender of their namesakes", "target": ""}
{"query": "#defaultView:Map\nSELECT ?location ?locationLabel ?coord ?date ?timeLabel ?page\nWHERE {\nwd:Q113121949 p:P276 ?statement.\n  ?statement ps:P276 ?location.\n?location wdt:P625 ?coord.\n  ?statement pq:P585 ?date.\n  OPTIONAL {\n  ?statement pq:P4241 ?time.\n  ?statement pq:P856 ?page.\n\n}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Collaborative map of events taking place for Wikidata's 10th birthday", "target": ""}
{"query": "# Birth year of winning candidate of the last eight U.S. presidential elections\nSELECT ?hLabel ?presidentLabel (YEAR(?birthday) as ?birthYear) WHERE {\n?h wdt:P31 wd:Q47566;  # instance of (P31) U.S. presidential election (Q47566)\n   wdt:P585 ?electionDate;  # point in time (P585)\n   wdt:P991 ?president.  # successful candidate (P991)\n?president wdt:P569 ?birthday  # date of birth (P569)\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?electionDate", "context": "Birth year of winning candidate of the last eight U.S. presidential elections", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\nSELECT (COUNT(?album) AS ?albums) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC (?albums) (?P407)", "context": "Number of music albums by language in descending order", "target": ""}
{"query": "#defaultView:ImageGrid\n#Préfets français avec signature dans Wikidata\nSELECT ?prefet ?prefetLabel ?date_naissance ?date_mort ?signature \n(GROUP_CONCAT(?prefetdeLabel ; SEPARATOR = \", \") as ?prefetdeconc) WHERE {\n   ?prefet p:P39 ?position.\n  ?position ps:P39 ?prefetde.\n  ?prefetde (wdt:P279*) wd:Q1285463.\n    ?prefet wdt:P27 wd:Q142.\n   ?prefet wdt:P109 ?signature.\n \n\n  OPTIONAL { ?prefet wdt:P569 ?date_naissance. }\n  OPTIONAL { ?prefet wdt:P570 ?date_mort. } \n  OPTIONAL { ?prefetde wdt:P39 ?fonction. }\n   OPTIONAL { ?prefet wdt:P109 ?signature. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr,en\".\n                         ?prefet rdfs:label ?prefetLabel .\n                         ?prefetde rdfs:label ?prefetdeLabel .}\n}\nGROUP BY ?prefet ?prefetLabel ?date_naissance ?date_mort ?signature", "context": "French préfets with their signature on Commons", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT ?composerLabel (COUNT(?item) as ?count) WHERE {\n  ?item wdt:P31 wd:Q105543609 .  \n  ?item wdt:P136 wd:Q544440 . \n  ?item wdt:P86 ?composer .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n   }\nGROUP BY ?composerLabel\nORDER BY DESC(?count) ", "context": "Graph of filmi music composers", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coords\"]}\nSELECT DISTINCT ?item ?itemLabel ?highestPoint ?highestPointLabel ?coords\nWHERE \n{\n  { ?item wdt:P31 wd:Q162620 } UNION\n  { ?item wdt:P31 wd:Q10742 } UNION\n  { ?item wdt:P31 wd:Q16532593 }\n  FILTER NOT EXISTS { ?item wdt:P31 wd:Q55379806 }\n  OPTIONAL { \n    ?item wdt:P610 ?highestPoint .\n    OPTIONAL {\n      ?highestPoint wdt:P625 ?coords .\n    }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # le label viendra de préférence dans votre langue, et autrement en anglais\n}        ", "context": "Map of the highest points of Spain provinces", "target": ""}
{"query": "#title:Twitter usernames and associated mastodon adresses\nSELECT ?item ?itemLabel ?Mastodon_address ?Twitter_username WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P4033 ?Mastodon_address.\n  ?item wdt:P2002 ?Twitter_username.\n}", "context": "Twitter usernames and associated mastodon adresses", "target": ""}
{"query": "SELECT ?Representative_List_of_the_Intangible_Cultural_Heritage_of_Humanity ?Representative_List_of_the_Intangible_Cultural_Heritage_of_HumanityLabel ?instance_of ?instance_ofLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?Representative_List_of_the_Intangible_Cultural_Heritage_of_Humanity wdt:P3259 wd:Q110319947.\n  OPTIONAL { ?Representative_List_of_the_Intangible_Cultural_Heritage_of_Humanity wdt:P31 ?instance_of. }\n}\nLIMIT 1000", "context": "Intangible cultural heritage", "target": ""}
{"query": "SELECT ?item ?itemLabel ?mastodon ?wikimedia WHERE {\n  ?item wdt:P4033 ?mastodon;\n        wdt:P4174 ?wikimedia.\n\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Wikimedians with Mastodon accounts", "target": ""}
{"query": "SELECT  ?politicianLabel ?mastodon ?countryLabel\nWHERE \n{\n  ?politician wdt:P4033 ?mastodon .\n  ?politician wdt:P106 wd:Q82955 . # occupation politician\n  ?politician wdt:P27 ?country .\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?countryLabel", "context": "Politicians with Mastodon accounts", "target": ""}
{"query": "# Characters in We Are Here\n#defaultView:Graph\nSELECT ?item1 ?image1 ?item1Label ?item2 ?image2 ?item2Label ?size ?rgb ?edgeLabel \nWHERE \n{\n VALUES ?book { wd:Q112075048 }\n { # Get characters in the We Are Here book\n  # VALUES ?rgb { \"FFBD33\" }\n  # VALUES ?size { 2 }\n  VALUES ?item1 { wd:Q112075048 }\n  ?item1 wdt:P674 ?item2 .\n  OPTIONAL { ?item1 wdt:P18 ?image1. }\n  OPTIONAL { ?item2 wdt:P18 ?image2. }\n } \n UNION\n { # Occupations\n  # VALUES ?rgb { \"fff033\" }\n  # VALUES ?size { 1 }\n  ?book wdt:P674 ?item1 .\n  ?item1 wdt:P106 ?item2 .\n  ?item2 wikibase:sitelinks ?size \n  OPTIONAL { ?item1 wdt:P18 ?image1. }\n  BIND(COALESCE(\n    IF(?size >= 100, \"fff033\", 1/0),\n    IF(?size >= 65, \"ddf033\", 1/0),\n    IF(?size >= 25, \"ccf033\", 1/0),\n    IF(?distNum >= 1, \"ffffff\", \"ffffff\")) AS ?rgb).\n } \n UNION\n { # Get relationships\n  ?book wdt:P674 ?item1 .\n  ?book wdt:P674 ?item2 .\n  ?item1 wdt:P26 ?item2 .\n  BIND (\"spouse\" AS ?edgeLabel )\n  OPTIONAL { ?item1 wdt:P18 ?image1. }\n  OPTIONAL { ?item2 wdt:P18 ?image2. }\n } \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} LIMIT 1000", "context": "Graph of the characters of the book \"We Are Here\"", "target": ""}
{"query": "#title: Carte des lieux de naissances des femmes présentes dans « Femmes de lettres en Bretagne »\n#defaultView:Map\nSELECT ?élément ?élémentLabel ?coord WHERE {\n  ?élément wdt:P1343 wd:Q109662197 .\n  ?élément wdt:P19/wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of birth places of women mentioned in the book \"Femmes de lettres en Bretagne\"", "target": ""}
{"query": "#1955 Bandung Conference participants\nSELECT DISTINCT ?p ?pLabel ?pDescription\n                (GROUP_CONCAT(DISTINCT ?cLabel ; separator=\"; \") AS ?country) \n                (GROUP_CONCAT(DISTINCT ?bpLabel ; separator=\"; \") AS ?birthplace) \n                (GROUP_CONCAT(DISTINCT YEAR(?bd) ; separator=\"; \") AS ?born)\n                (GROUP_CONCAT(DISTINCT YEAR(?dd) ; separator=\"; \") AS ?died)\n                ?gLabel ?num_statements    \nWHERE\n{ ?p wdt:P31 wd:Q5 ;\n  wdt:P1344 wd:Q208191 ;\n  wikibase:statements ?num_statements .\n  OPTIONAL { ?p wdt:P27 ?c . ?c rdfs:label  ?cLabel .\n           FILTER ( LANG ( ?cLabel ) = \"en\" ) } .\n  OPTIONAL { ?p wdt:P19 ?bp . ?bp rdfs:label  ?bpLabel .\n           FILTER ( LANG ( ?bpLabel ) = \"en\" ) } .\n  OPTIONAL { ?p wdt:P569 ?bd } .\n  OPTIONAL { ?p wdt:P570 ?dd } .\n  OPTIONAL { ?p wdt:P21 ?g } .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nGROUP BY ?p ?pLabel ?pDescription ?country ?birthplace ?born ?died ?gLabel ?num_statements\nORDER BY DESC(?num_statements)", "context": "Participants in the 1955 Bandung Conference", "target": ""}
{"query": "#title:Biggest mastodon instances accordion to wikidata\n#defaultView:BubbleChart\n\nselect ?domain (count(?domain) as ?count) {\n  { \n    select (replace(?value,\"[^@]+@([^@]+)$\",\"$1\") AS ?domain) {\n      ?item wdt:P4033 ?value \n    }\n  }\n}\ngroup by ?domain order by desc(?count)", "context": "Biggest Mastodon instances according to Wikidata", "target": ""}
{"query": "# Researchers on Mastodon\nSELECT ?item ?itemLabel (GROUP_CONCAT(?fieldLabel; SEPARATOR = \"; \") AS ?fields) ?orcid ?twitter ?mastodon WHERE {\n  SELECT ?item ?itemLabel ?fieldLabel ?orcid ?twitter ?mastodon\nWHERE \n{\n  ?item wdt:P496 ?orcid.\n  OPTIONAL { ?item wdt:P2002 ?twitter.}\n  OPTIONAL { ?item wdt:P106 ?field .}\n  ?item wdt:P4033 ?mastodon .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nLIMIT 20000\n}\nGROUP BY ?item ?itemLabel ?orcid ?twitter ?mastodon", "context": "Mastodon accounts of researchers", "target": ""}
{"query": "SELECT ?server ?count \nWHERE {\n  {\n    SELECT \n    ?server (COUNT(?server) AS ?count)\n    WHERE \n    {\n      ?item wdt:P31 wd:Q5 ;\n            wdt:P4033 ?mastodon .\n      BIND(strafter(?mastodon,\"@\") AS ?server)\n      SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n    }\n    GROUP BY ?server\n   }\n  FILTER(?count > 2)\n}\nORDER BY DESC(?count)", "context": "Most annotated servers according to Wikidata", "target": ""}
{"query": "#defaultView:Map\n#title: Map of birth places of people with known mastodon address.\nSELECT * WHERE {\n\n  ?item wdt:P4033 ?mastodon; wdt:P19 ?pob. ?pob wdt:P625 ?coords. \n  \n}", "context": "Map of birthplaces of people with known mastodon address", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coords\"]}\nSELECT DISTINCT ?island ?islandLabel ?summit ?summitLabel ?coords ?elevation\nWHERE \n{\n  ?island wdt:P31 wd:Q23442 ;\n        wdt:P610 ?summit .\n  { ?island wdt:P30 wd:Q46 } UNION\n  { ?island wdt:P30 wd:Q30 } UNION\n  { ?island wdt:P17/wdt:P30 wd:Q46 } UNION\n  { ?island wdt:P17/wdt:P30 wd:Q30 }\n  FILTER NOT EXISTS { ?island wdt:P17 wd:Q756617 }\n  ?summit p:P625 [\n           psv:P625 [\n             wikibase:geoLatitude ?lat ;\n             wikibase:geoLongitude ?lon ;\n           ] ;\n           ps:P625 ?coords ;\n         ] ;\n         p:P2044 [ a wikibase:BestRank ; psv:P2044 [ wikibase:quantityAmount ?elevation ; wikibase:quantityUnit ?unit ] ] .\n  FILTER( (?elevation >= 1000 && ?unit = wd:Q11573)\n       || (?elevation >= 3281 && ?unit = wd:Q3710) ).\n  FILTER (?lat > 33 && ?lon < 60)\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?elevation)", "context": "European islands culminating at more than 1,000 m of altitude", "target": ""}
{"query": "#defaultView:Map\nSELECT ?lac ?lacLabel ?coordonnées_géographiques WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?lac wdt:P31/wdt:P279* wd:Q3391202;\n    wdt:P17 wd:Q142.\n  OPTIONAL { ?lac wdt:P625 ?coordonnées_géographiques. }\n}\nLIMIT 10000", "context": "Map of water bodies in France", "target": ""}
{"query": "#title:Other awards won by Booker winners (people)\n#defaultView:BubbleChart\nSELECT (COUNT(?item) AS ?count) ?otheraward ?otherawardLabel WHERE {\n  VALUES ?award{wd:Q160082 wd:Q2052291}\n  ?item wdt:P166 ?award.\n#UNION{ ?item wdt:P1411 ?award. BIND(\"nominated\" AS ?rel) }\n  ?item wdt:P31 wd:Q5.\n  ?item p:P166 ?s.?s ps:P166 ?otheraward FILTER(?otheraward NOT IN(wd:Q160082, wd:Q2052291))\n  OPTIONAL{?s pq:P585 ?time}\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n} GROUP BY ?otheraward ?otherawardLabel\nORDER BY DESC(?count)", "context": "Other awards won by winners of the Booker Prize", "target": ""}
{"query": "# FLOSS videogames in timeline\n# author: Valerio Bozzolan, contributors\n# source code: https://phabricator.wikimedia.org/P39517\n# license: CC0\n#defaultView:Timeline\n\n# query with label\nSELECT ?item ?itemLabel ?image ?date\nWHERE \n{\n  {\n    # query without labels\n    SELECT ?item (SAMPLE(?image) as ?image) (MIN(?whatever_date) as ?date)\n    WHERE\n    {\n      # item must be a videogame or any subclass of copyright license\n      wd:Q7889 ^wdt:P279*/^wdt:P31 ?item.\n\n      # item must has a license\n      ?item wdt:P275 ?license.\n\n      # the license must be a libre license\n      ?libre_license ^wdt:P279*/^wdt:P31  ?license.\n      VALUES ?libre_license {\n        wd:Q97044024 # open source\n        wd:Q3943414  # free software\n        wd:Q97662661 # free license\n        wd:Q196294   # open license\n      }\n      OPTIONAL {\n        ?item wdt:P18 ?image.\n      }\n      # item must have a date (whatever publication date or inception date is fine)\n      ?item  wdt:P577|wdt:P571 ?whatever_date.\n    }\n    GROUP BY ?item\n  }\n  # apply labels after the group by\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Timeline of Libre and Open Source videogames", "target": ""}
{"query": "select ?item ?itemLabel ?string ?content ?timestamp ?user {\n  ?item wdt:P31 wd:Q146. # find cats\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  \n  BIND(STRAFTER(wikibase:decodeUri(str(?item)), \"http://www.wikidata.org/entity/\") AS ?string)\n  # need to make the ID something the API can understand\n  \n  SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:endpoint \"www.wikidata.org\";\n       wikibase:api \"Generator\";\n       wikibase:limit \"once\";\n       mwapi:generator \"allpages\";\n       mwapi:gapfrom ?string;\n       mwapi:gapnamespace 0 ; # main namespace\n       mwapi:gaplimit 1; # only one \n       mwapi:prop \"revisions\";\n       mwapi:rvdir \"newer\"; # FIRST revision\n       mwapi:rvprop \"timestamp|content|user\".\n    ?content wikibase:apiOutput 'revisions/rev/text()' . # text of first\n    ?timestamp wikibase:apiOutput 'revisions/rev/@timestamp' . # timestamp\n    ?user wikibase:apiOutput 'revisions/rev/@user' . # user who created it\n  }\n}", "context": "Finding the first revision and original creator of items with mwapi", "target": ""}
{"query": "#title:Mastodon accounts and corresponding Twitter accounts\nSELECT ?twit ?item ?itemLabel ?itemDescription ?id WHERE {\n  ?item wdt:P4033 ?id.\n  OPTIONAL {?item wdt:P2002 ?twit}\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?itemLabel", "context": "Mastodon accounts (with Twitter equivalents)", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coords\"]}\nSELECT ?item ?itemLabel ?country ?countryLabel ?place ?placeLabel (YEAR(?date) AS ?year) ?height ?image ?coords {\n  ?item wdt:P31/wdt:P279* wd:Q170980 ;\n        p:P2048/psn:P2048/wikibase:quantityAmount ?h .\n  BIND (ROUND(?h * 10) / 10 AS ?height)\n  FILTER ( ?height >= 40 )\n  OPTIONAL { ?item wdt:P17 ?country }\n  OPTIONAL { ?item wdt:P131 ?place }\n  OPTIONAL { ?item wdt:P571 ?date }\n  OPTIONAL { ?item wdt:P18 ?image }\n  OPTIONAL { ?item wdt:P625 ?coords }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY DESC(?height)", "context": "Map of obelisks over 40 m high", "target": ""}
{"query": "SELECT ?item ?itemLabel ?mastodon {\n  ?item wdt:P31*/wdt:P279* wd:Q5926733 ; wdt:P4033 ?mastodon .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY ?itemLabel", "context": "Wikimedia affiliates on Mastodon", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?year (COUNT(?herrialdea) AS ?count) (SAMPLE(?herrialdeaLabel) AS ?label) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,es,fr,en\". }\n  ?Bertsosaio wdt:P31 wd:Q8247090.\n  ?Bertsosaio wdt:P710 ?bertsolari.\n  ?bertsolari wdt:P19 ?jaiolekua.\n  ?jaiolekua (wdt:P131*) ?herrialdea.\n  {?herrialdea wdt:P31 wd:Q162620.}\n  UNION\n  {?herrialdea wdt:P31 wd:Q34876.}\n  ?Bertsosaio wdt:P582 ?data.\n  BIND(STR(YEAR(?data)) AS ?year)\n  ?herrialdea rdfs:label ?herrialdeaLabel FILTER((LANG(?herrialdeaLabel)) = \"eu\") .\n}\nGROUP BY ?herrialdeaLabel ?year", "context": "Composers by country of birth and edition", "target": ""}
{"query": "SELECT DISTINCT ?human ?humanLabel ?mastodon ?url (GROUP_CONCAT(?domain_label;separator=\" | \") AS ?domains)  WHERE {\n  ?human wdt:P31 wd:Q5 .\n  ?human wdt:P4033 ?mastodon .\n  ?human wdt:P106 wd:Q1650915 .\n  ?human wdt:P101 ?domain .\n  ?domain rdfs:label ?domain_label .\n  FILTER (LANG(?domain_label) = \"en\")\n  BIND(REPLACE(?mastodon, \"@.*\", \"\") AS ?userhandle)\n  BIND(REPLACE(?mastodon, \".*@\", \"\") AS ?host)\n  BIND(URI(CONCAT(\"https://\", ?host, \"/@\", ?userhandle)) AS ?url)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?human ?humanLabel ?mastodon ?url\nORDER BY ?domains", "context": "Researchers with a Mastodon account and a known field of work", "target": ""}
{"query": "SELECT\n  ?language\n  ?languageLabel\n  ?count\n  ?langLemmas\nWITH {\n  SELECT DISTINCT ?language {\n    [] dct:language ?language.\n  }\n} AS %0\nWITH {\n  SELECT DISTINCT ?language ?langLemma {\n    INCLUDE %0.\n    []\n      dct:language ?language;\n      wikibase:lemma ?lemma.\n    BIND(LANG(?lemma) AS ?langLemma).\n  }\n} AS %1\nWITH {\n  SELECT ?language (COUNT(*) AS ?count) {\n    INCLUDE %1.\n  }\n  GROUP BY ?language\n} AS %2\nWITH {\n  SELECT\n    ?language\n    (GROUP_CONCAT(CONCAT(?langLemma); SEPARATOR = \", \") AS ?langLemmas)\n  {\n    INCLUDE %1.\n  }\n  GROUP BY ?language\n} AS %3\n{\n  INCLUDE %2.\n  INCLUDE %3.\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}.\n}\nORDER BY DESC(?count) ASC(LCASE(?languageLabel))\n\n", "context": "Count per each language of unique language codes used in lemmas for Lexemes in that language", "target": ""}
{"query": "SELECT ?eponym ?eponymLabel ?eponymDescription WHERE {\n  wd:Q1443306 wdt:P31/wdt:P279* wd:Q3114762;\n              wdt:P138 ?eponym.\n  wd:Q85767253 wdt:P31/wdt:P279* wd:Q259864;\n               wdt:P138 ?eponym.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Person(s) after whom both a warship and a microarchitecture are named", "target": ""}
{"query": "#defaultView:ImageGrid{\"hide\":[\"?NoVBIBFix\",\"?NoVBIB\"]}\n#title:Institutionen die an allen #vBIB-Tagungen bislang Beiträge einbrachten.\nSELECT ?employerLabel ?logoImage ?NoVBIBFix (COUNT(?vbib) AS ?NoVBIB) WITH { \n  \nSELECT DISTINCT ?employer ?vbib  ?logoImage WHERE {\n  ?vbib wdt:P179 wd:Q108784175.\n  ?presentation wdt:P5072 ?vbib;\n    wdt:P585 ?pit.\n  BIND(YEAR(?pit) AS ?year)\n  ?presentation wdt:P50 ?author.\n  ?author p:P108 ?employerStmt.\n  ?employerStmt ps:P108 ?employer.\n  OPTIONAL { ?employerStmt pq:P580 ?empStart. \n           }\n  FILTER (?pit > ?empStart)\n  OPTIONAL { ?employerStmt pq:P582 ?empEnd.\n          }\n  FILTER ((bound(?empEnd) && ?pit < ?empEnd) || (!bound(?empEnd) && ?pit > ?empStart) )\n  OPTIONAL {?employer wdt:P154 ?logoImage.}\n  \n}\n}AS %results \n\nWHERE {\n  INCLUDE %results.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  {\n    SELECT (COUNT(?vbib) AS ?NoVBIBFix) WHERE {\n      ?vbib wdt:P179 wd:Q108784175.\n    }\n  }  \n}\nGROUP BY ?employerLabel ?NoVBIBFix ?logoImage\nHAVING (COUNT(?vbib)=?NoVBIBFix)", "context": "Institutions that have contributed to all #vBIB conferences so far", "target": ""}
{"query": "#title: Iceland mountains with no CEB wiki sitelink\nSELECT DISTINCT ?item ?itemLabel WHERE {\n  ?item wdt:P17 wd:Q189. hint:Prior hint:runFirst true.\n  ?item p:P31/ps:P31/wdt:P279* wd:Q8502.\n  FILTER NOT EXISTS {?article schema:about ?item ;\n    schema:isPartOf <https://ceb.wikipedia.org/> .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}", "context": "Mountains in Iceland with no Cebuano Wikipedia sitelink", "target": ""}
{"query": "SELECT ?conceptLabel (count(*) as ?individuals)\nWHERE\n{\n  ?individual wdt:P31 ?concept ;\n          wdt:P4033 ?mastodon .\n  ?concept rdfs:label ?conceptLabel .\n  FILTER(lang(?conceptLabel) = \"en\")\n}\nGROUP BY ?concept ?conceptLabel\nORDER BY desc(?individuals)\n", "context": "Concepts with more individuals having Mastodon IDs", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?name ?followers ?subscribers WHERE \n{\n  { \n  ?item p:P4033 ?stat . \n  ?stat ps:P4033 ?name .\n  OPTIONAL {?stat pq:P8687 ?followers . }\n  OPTIONAL {?stat pq:P3744 ?subscribers . }\n  } \n  UNION\n  {\n  ?item p:P8687 ?stat . \n  ?stat ps:P8687 ?followers . \n  ?stat pq:P4033 ?name .\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}  ORDER BY (!bound(?followers)) ?followers (!bound(?subscribers)) ?subscribers", "context": "Mastodon follower/subscriber figures", "target": ""}
{"query": "#title:Lexeme senses which are described in Jaehoon Yeon and Lucien Brown's Comprehensive Grammar of Korean\nselect ?sense ?lemma ?catLabel ?section ?page {\n  { ?sense ^ontolex:sense ?elem . ?elem wikibase:lemma ?lemma ; wikibase:lexicalCategory ?cat . }\n  union\n  { ?sense rdfs:label ?lemma . filter(lang(?lemma)=\"en\") }\n  ?sense p:P1343 [ ps:P1343 wd:Q115683766 ; pq:P958 ?section ; pq:P304 ?page ] .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by xsd:integer(?page)", "context": "Lexeme senses which are described in Jaehoon Yeon and Lucien Brown's Comprehensive Grammar of Korean", "target": ""}
{"query": "SELECT DISTINCT ?org ?orgLabel ?orgDescription ?mastodonName ?pic \nWHERE {\n  ?org wdt:P4033 ?mastodonName .\n  ?org wdt:P31 ?type .\n  ?type wdt:P279* wd:Q163740 .\n  OPTIONAL { ?org wdt:P18 ?pic . }\n   SERVICE wikibase:label {\n     bd:serviceParam wikibase:language \"en\"\n   }\n}\n", "context": "List of nonprofit organizations / NGOs on the fediverse that Wikidata knows about", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\nSELECT (count(?album) as ?albums) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC(?albums) ASC(?P407)\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Top album languages found on Wikidata right now", "target": ""}
{"query": "SELECT ?state ?stateLabel ?alpha3 WHERE {\n  ?state p:P463 ?memberOfStatement.\n  ?memberOfStatement a wikibase:BestRank;\n                     ps:P463 wd:Q458.\n  MINUS { ?memberOfStatement pq:P582 ?endTime. }\n  ?state wdt:P298 ?alpha3.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Alpha-3 codes for countries in Europe", "target": ""}
{"query": "prefix wdt: <http://www.wikidata.org/prop/direct/>\nprefix entity: <http://www.wikidata.org/entity/>\nSELECT ?item ?itemLabel ?nomLabel ?lieuLabel WHERE {\n  ?item wdt:P31/wdt:P279* entity:Q3305213 .  # peintures\n  ?item wdt:P170 ?nom .             \n  ?nom wdt:P21 entity:Q6581072 .             # oeuvre créé par une femme\n  ?item wdt:P195 entity:Q3044768 .           # département des peintures du musée du Louvre\n  OPTIONAL{?item wdt:P276 ?lieu}.            # emplacement des oeuvres\n  MINUS{?item wdt:P195 entity:Q19013512}.    # retrait des oeuvres spoliées\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"fr\" .   }\n}\nORDER BY ?lieuLabel ", "context": "Location of paintings in the Paintings Department of the Louvre made by women", "target": ""}
{"query": "#title:Most frequent birth places for Italian SBN authors\n#defaultView:Map\nSELECT DISTINCT ?l ?lLabel ?coord ?number ?layer\nWITH\n{\n  SELECT DISTINCT ?l ?lLabel ?coord (COUNT(DISTINCT ?item) AS ?number)\n  WHERE \n  {\n  ?item wdt:P396 [] ; wdt:P19 ?l ; wdt:P27 wd:Q38 . ?l wdt:P625 ?coord .\n  }\n  GROUP BY ?l ?lLabel ?coord ?number\n} AS %what\nWHERE\n{\n  INCLUDE %what\n  # BIND( IF(?number < 6, \"1-5\", IF(?number < 21, \"6-20\", IF(?number < 51, \"21-50\",\"51+\"))) AS ?layer)\n  BIND( IF(?number < 31, \"1-30\", IF(?number < 51, \"31-50\",\"51+\")) AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it\". }\n}\nGROUP BY ?l ?lLabel ?coord ?number ?layer\nORDER BY DESC(?number)", "context": "Most frequent birth places for Italian SBN authors", "target": ""}
{"query": "SELECT \n  ?parent ?parentLabel ?parent_born ?parent_genderLabel ?parent_positionLabel ?child ?childLabel ?child_born ?child_genderLabel ?child_positionLabel\nWHERE\n{?parent wdt:P31 wd:Q5;\n    wdt:P106 wd:Q11774891; # occupation = ice hockey player\n    wdt:P27 wd:Q33; # country of citizenship = Finland\n    wdt:P569 ?parent_born;\n    wdt:P40 ?child . # has child\n    OPTIONAL {?parent wdt:P21 ?parent_gender.}\n    OPTIONAL {?parent wdt:P413 ?parent_position .}\n ?child wdt:P31 wd:Q5 ;\n    wdt:P106 wd:Q11774891; # child's occupation = ice hockey player\n    wdt:P569 ?child_born;\n    OPTIONAL {?child wdt:P21 ?child_gender.}\n    OPTIONAL {?child wdt:P413 ?child_position.}\n \n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n \n }", "context": "Finnish hockey players whose child is also a hockey player", "target": ""}
{"query": "#title:Mappa dei luoghi di nascita degli autori italiani morti nel 1952\n#defaultView:Map{ \"markercluster\": true}\nSELECT DISTINCT ?autore ?autoreLabel ?autoreDescription ?datanascita ?datamorte ?luogonascita ?luogonascitaLabel ?coord\nWHERE {\n  ?autore wdt:P27 ?p ; wdt:P570 ?datamorte ; ?id [] . VALUES ?p { wd:Q172579 wd:Q38 } . VALUES ?id { wdt:P214 wdt:P396 } .\n  FILTER (\"1951-12-31\"^^xsd:dateTime < ?datamorte && ?datamorte < \"1953-01-01\"^^xsd:dateTime)\n  OPTIONAL { ?autore wdt:P569 ?datanascita } .\n  OPTIONAL { ?autore wdt:P19 ?luogonascita . ?luogonascita wdt:P625 ?coord } .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\". }\n}", "context": "Map of the birthplaces of Italian authors who died in 1952", "target": ""}
{"query": "#Opposites\nSELECT ?item ?antonym ?itemLabel ?antonymLabel\nWHERE \n{\n  ?item wdt:P461 ?antonym.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Pairs of opposites", "target": ""}
{"query": "#title: Ratio des tableaux au musée de Bretagne\n#defaultView:BubbleChart\nSELECT ?ratio (COUNT(?q) AS ?nombre) WHERE {\n  ?q wdt:P31 wd:Q3305213 ;   #tableau\n     wdt:P195 wd:Q3329701 ;  #au musée de Bretagne\n     wdt:P2048 ?hauteur ;    #avec une hauteur\n     wdt:P2049 ?largeur .    #avec une largeur\n  BIND ( STR(?hauteur/?largeur) AS ?ratio ) #ratio de la hauteur et de la largeur\n}\nGROUP BY ?ratio", "context": "Ratio of paintings at the Museum of Brittany", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?cheese ?cheeseLabel ?town ?townLabel ?image {\n  ?cheese wdt:P31*/wdt:P279* wd:Q10943 ; wdt:P138 ?town .\n  ?town wdt:P31*/wdt:P279* wd:Q486972 .\n  OPTIONAL { ?cheese wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\" }\n}\nORDER BY ?townLabel", "context": "Cheeses named after towns", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item (SAMPLE(?coords) AS ?coord) ?itemLabel ?out WITH {\n  SELECT DISTINCT ?out ?depth WHERE {\n    SERVICE <https://query.wikidata.org/bigdata/namespace/categories/sparql> {\n      SERVICE mediawiki:categoryTree {\n        bd:serviceParam mediawiki:start <https://commons.wikimedia.org/wiki/Category:Villages_in_Saale-Holzland-Kreis> .\n        bd:serviceParam mediawiki:direction 'Reverse' .\n        bd:serviceParam mediawiki:depth 1 .  # direct subcategories only\n      }\n    }\n  }\n} AS %subquery WHERE {\n  INCLUDE %subquery .\n  FILTER(?depth = 1) .  # remove the host category from results\n  OPTIONAL {\n    ?out schema:about/wdt:P301? ?item .\n    FILTER NOT EXISTS { ?item wdt:P31 wd:Q4167836 }  # remove category items from results set\n    OPTIONAL {\n      ?item wdt:P625 ?coords .\n    }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language 'de' }\n} GROUP BY ?item ?itemLabel ?out # to aggregate sample coordinates in case there are multiple P625 values in items", "context": "Map of sub-categories of a Commons category", "target": ""}
{"query": "SELECT ?project ?total_redirects ?intentional_redirects ?pct_intentional  ?other_redirects ?pct_other\nWHERE {\n  {\n    SELECT ?project (COUNT(DISTINCT ?intentional_sitelink) AS ?intentional_redirects) (COUNT(DISTINCT ?other_sitelink) AS ?other_redirects) WHERE {\n      {\n        ?intentional_sitelink schema:isPartOf ?project; wikibase:badge wd:Q70894304 .\n      } UNION {\n        ?other_sitelink schema:isPartOf ?project; wikibase:badge wd:Q70893996 .\n      }\n    } GROUP BY ?project \n  }\n  BIND (?intentional_redirects + ?other_redirects AS ?total_redirects) .\n  BIND ((ROUND(1000.0 * ?intentional_redirects / ?total_redirects) / 10.0) AS ?pct_intentional) .\n  BIND ((ROUND(1000.0 * ?other_redirects / ?total_redirects) / 10.0) AS ?pct_other) .\n} ORDER BY DESC(?total_redirects)", "context": "Number of sitelinks to redirects by wiki", "target": ""}
{"query": "SELECT DISTINCT ?airport ?airportLabel ?airportCountry ?airportCountryLabel ?person ?personLabel (sample(?birth) as ?birth) (SAMPLE(?death) as ?death) WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?airport (wdt:P31/(wdt:P279*)) wd:Q1248784;\n    wdt:P17 ?airportCountry;\n    p:P138 ?statement.\n  ?statement ps:P138 ?person.\n  ?person wdt:P31 wd:Q5;\n    wdt:P106 wd:Q49757.\n  OPTIONAL { ?person wdt:P569 ?birth }\n  OPTIONAL { ?person wdt:P570 ?death }\n  FILTER(NOT EXISTS { ?statement pq:P582 ?endTime. })\n} group by ?airport ?airportLabel ?airportCountry ?airportCountryLabel ?person ?personLabel ?personImage", "context": "Airports named after poets", "target": ""}
{"query": "#defaultView:Map\n#title: Carte des lieux ayant donnés leur nom à un lieu à Rennes\nSELECT ?loc ?locLabel ?item ?itemLabel ?coord ?layer WHERE {\n  ?loc wdt:P131 wd:Q647 ;\n     wdt:P138 ?item .\n  ?item wdt:P31 ?type ; wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                         ?type rdfs:label ?layer }\n}", "context": "Map of places that places in Rennes are named after", "target": ""}
{"query": "#defaultView:Map\n#title: Carte des lieux à Rennes\nSELECT ?loc ?locLabel ?item ?itemLabel ?coordRennes ?dist ?rgb WHERE {\n  ?loc wdt:P131 wd:Q647 ;\n       wdt:P138 ?item ;\n       wdt:P625 ?coordRennes .\n  ?item wdt:P31 ?type ; wdt:P625 ?coord .\n  BIND(ROUND(geof:distance(?coordRennes, ?coord)*10)/10 AS ?dist ).\n  BIND( IF( ?dist < 400 , \"FBB806\", IF ( ?dist < 1000 , \"DE4710\" , IF ( ?dist < 2000 , \"DE4710\" , \"940E0E\" ) ) ) AS ?rgb).  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }  \n}", "context": "Map of places named after a place in Rennes, color coded by distance of the place from Rennes", "target": ""}
{"query": "#title: Modern descendants of the Achaemenes (Achaemenid dynasty of rulers of Persia, 7th century BCE)\nSELECT ?item ?itemLabel ?itemDescription  ?depth {\n  SERVICE gas:service {\n    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" ;\n                gas:in wd:Q312032 ;\n                gas:traversalDirection \"Forward\" ;\n                gas:out ?item ;\n                gas:out1 ?depth ;\n                gas:linkType wdt:P40 .\n  }\n  FILTER NOT EXISTS {?item wdt:P4638 []}\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?depth)\nLIMIT 100", "context": "Modern descendants of the Achaemenes (Achaemenid dynasty of rulers of Persia, 7th century BCE)", "target": ""}
{"query": "#map of disasters\n#defaultView:Map{\"layer\":\"?typeLabel\"}\nSELECT ?item ?itemLabel ?geo ?type ?typeLabel (SAMPLE(?_image) AS ?image) WHERE {\n  ?type wdt:P279* wd:Q3839081 .\n  ?item wdt:P31 ?type ;\n        wdt:P625 ?geo .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL { ?item wdt:P18 ?_image }\n}\nGROUP BY ?item ?itemLabel ?geo ?type ?typeLabel", "context": "Map of disasters by type", "target": ""}
{"query": "#Semua muzium di Malaysia dengan koordinatnya\n#Semua muzium (termasuk subkelas muzium) di Malaysia dengan koordinat\n#defaultView:Map\nSELECT DISTINCT ?butir ?nama ?koord ?lat ?lon\nWHERE\n{\n hint:Query hint:optimizer \"None\" .\n ?butir wdt:P17* wd:Q833 .\n ?butir wdt:P31/wdt:P279* wd:Q33506 .\n ?butir wdt:P625 ?koord .\n ?butir p:P625 ?coordinate .\n ?coordinate psv:P625 ?coordinate_node .\n ?coordinate_node wikibase:geoLatitude ?lat .\n ?coordinate_node wikibase:geoLongitude ?lon .\n SERVICE wikibase:label {\n bd:serviceParam wikibase:language \"ms\" .\n ?butir rdfs:label ?nama\n }\n}\nORDER BY ASC (?nama)", "context": "Maps of museums in Malaysia", "target": ""}
{"query": "#defaultView:Map\n#title: Karte ar vietvārdiem, kas devuši nosaukumu vietām Rīgā\nSELECT ?loc ?locLabel ?item ?itemLabel ?coord ?layer WHERE {\n  ?loc wdt:P131 wd:Q1773;\n    wdt:P138 ?item.\n  ?item wdt:P31 ?type;\n    wdt:P625 ?coord.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"lv,en\".\n    ?type rdfs:label ?layer.\n  }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"lv,en\".\n  }  \n}", "context": "Map with place names that gave names to places in Riga", "target": ""}
{"query": "SELECT distinct ?country ?countryLabel ?area\nWHERE {\n  {SELECT distinct ?country WHERE {\n    ?country wdt:P31/wdt:P279* wd:Q3624078.\n    } } \n  MINUS \n  {SELECT distinct ?country #(COUNT(?river) AS ?cnt) \n                   WHERE {\n    ?river wdt:P31/wdt:P279* wd:Q4022.\n    ?river p:P17 [ps:P17 ?country].\n    } GROUP BY ?country\n  } \n  MINUS {?country wdt:P31 wd:Q3024240}\n  MINUS {?country wdt:P31 wd:Q11514315}\n  MINUS {?country wdt:P31 wd:Q7840353}\n  MINUS {?country wdt:P31 wd:Q11514315}\n  MINUS {?country wdt:P31 wd:Q1790360}\n OPTIONAL {?country wdt:P2046 ?area}\n # MINUS %includes\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?area", "context": "Area of sovereign states without a river", "target": ""}
{"query": "#title: Propriétés Wikidata dont le format de l'URL correspond au Ministère de la Culture\nSELECT ?p ?pLabel ?url WHERE {\n  ?p wdt:P1630 ?url .\n  FILTER ( REGEX ( ?url , \"http://data.culture.fr/thesaurus/page/ark:/67717/\" ) )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?p", "context": "Wikidata properties whose URL format matches some format", "target": ""}
{"query": "SELECT ?item ?itemLabel\nWHERE \n{\n  ?item wdt:P31 wd:Q5 ;\n        wdt:P19/wdt:P17* wd:Q237 .\n  FILTER NOT EXISTS { ?item wdt:P19 wd:Q145694 }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "People born in the Vatican", "target": ""}
{"query": "# Source https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/qotw/2022#March \"Shortest rail link between Narvik and Singapore\"\n#defaultView:Map\nSELECT ?station ?stationLabel ?cds ?line ?layer WHERE { \n  { SERVICE gas:service {\n       gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" ; \n                   gas:in wd:Q3374603 ; # Umeå central station\n                  # gas:target wd:Q18337797 ; # Didymoteicho railway station. From here on, adjacent stations rapidly vanish\n                   gas:out ?station ; \n                   gas:out1 [] ; \n                   gas:out2 ?pred .\n       gas:program gas:linkType wdt:P197 . } # adjacent station\n  }\n  FILTER(?station != wd:Q3374603) \n  ?pred p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; \n        p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; \n           p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . \n  BIND(STRDT(?str, geo:wktLiteral) AS ?line) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\n", "context": "Shortest rail link between Narvik and Singapore", "target": ""}
{"query": "SELECT ?item ?itemLabel WHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q978.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Memes", "target": ""}
{"query": "#title:Number of literary works (Q7725634) by language (P407) in descending order\nSELECT (count(?thing) as ?things) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?thing wdt:P31 wd:Q7725634.\n  OPTIONAL { ?thing wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC(?things) ASC(?P407)\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Number of literary works (Q7725634) by language (P407) in descending order", "target": ""}
{"query": "SELECT DISTINCT ?human ?humanLabel (GROUP_CONCAT(DISTINCT ?propertyLabel; separator=\", \") as ?relationship) WHERE {\n  ?human ?predicate ?statement . \n  ?statement ?predicate2 wd:Q503424. hint:Prior hint:runFirst true.\n  ?property wikibase:claim ?predicate.\n  FILTER NOT EXISTS { ?human wdt:P496 []. }\n  ?human wdt:P31 wd:Q5.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n                         ?human rdfs:label ?humanLabel .\n                         ?property rdfs:label ?propertyLabel .}\n} group by ?human ?humanLabel", "context": "Humans that have a 'relationship' with University of Leeds", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?itemCountry ?itemCountryLabel ?work ?workLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  ?item wdt:P31/wdt:P279* wd:Q55488;\n    wdt:P17 ?itemCountry;\n    p:P138 ?statement.\n  ?statement ps:P138/wdt:P138* ?work.\n  ?work wdt:P31/wdt:P279* wd:Q7725634.\n}", "context": "Railway stations named after literary works", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\nSELECT (count(?album) as ?albums) ?P407Label ?P407 WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n}\nGROUP BY ?P407 ?P407Label\nORDER BY DESC(?albums) ASC(?P407)\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 6, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:Tree\nSELECT\n  ?P277\n  ?P277Label\n  ?P31\n  ?P31Label\n  ?software\n  ?softwareLabel\nWITH {\n  SELECT DISTINCT ?license {\n    ?license wdt:P629*/wdt:P31/wdt:P279* wd:Q3943414.\n  }\n} AS %0\nWITH {\n  SELECT DISTINCT ?software {\n    INCLUDE %0.\n    # \"GNU Emacs\" (Q1252773) has copyright license \"GPLv3+\" (Q27016754)\n    # \"GPLv3+\" (Q27016754) is en edition or translation of \"GNU GPL\" (Q7603)\n    # \"GNU GPL\" (Q7603) is an instance of \"free software license\" (Q3943414)\n    {?software wdt:P275 ?license}\n    UNION\n    # \"Vim\" is an instance of \"free and open-source software\" (Q506883)\n    # \"free and open-source software\" (Q506883) is a subclass of \"free software\" (Q341)\n    {?software wdt:P31/wdt:P279* wd:Q341}.\n  }\n} AS %1\nWITH {\n  SELECT DISTINCT ?software ?P31 ?P277 {\n    INCLUDE %1.\n    ?software\n      wdt:P31 ?P31;\n      wdt:P277 ?P277.\n  }\n} AS %2\n{\n  INCLUDE %2.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}\n}\nORDER BY\n  ASC(LCASE(?P277Label))\n  ASC(LCASE(?P31Label))\n", "context": "Free software grouped by programming language and instance of", "target": ""}
{"query": "#defaultView:Timeline\n#title:Timeline of WorldPride events\nSELECT ?WorldPride ?label ?time ?image WHERE {\n  ?WorldPride wdt:P31 wd:Q4127414;\n    rdfs:label ?label.\n  { ?WorldPride wdt:P585 ?time. }\n  UNION\n  { ?WorldPride wdt:P580 ?time. }\n  OPTIONAL { ?WorldPride wdt:P18 ?image. }\n  FILTER((LANG(?label)) = \"en\")\n}\nLIMIT 20", "context": "Historical timeline of WorldPride events: (2000 in Rome to 2023 in Sydney)", "target": ""}
{"query": "SELECT ?item ?itemLabel ?character ?characterLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P31 wd:Q35666 .\n  ?item wdt:P138 ?character .\n  ?character wdt:P1441 wd:Q174596 .\n}", "context": "Which glaciers have been named after Moby Dick characters?", "target": ""}
{"query": "#title: Free software grouped by licenses\n#defaultView:BubbleChart\nSELECT ?licLabel (COUNT(?lic) AS ?count) WHERE {\n ?item wdt:P31/wdt:P279* wd:Q7397 ;\n       wdt:P275 ?lic .\n  FILTER EXISTS { ?lic wdt:P31/wdt:P279* wd:Q196294 } .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n} GROUP BY ?licLabel\nORDER BY DESC(?count)", "context": "Free software grouped by licenses", "target": ""}
{"query": "#title: German nouns with unparseable combines statements\nselect *\nwith {\n  select distinct ?l {\n    ?l dct:language wd:Q188;\n       wikibase:lexicalCategory wd:Q1084;\n       wikibase:lemma ?lemma;\n       wdt:P5238 ?comb1;\n       wdt:P5238 ?comb2 filter (?comb1 != ?comb2).\n\n    ?comb1 wikibase:lemma ?combl1.\n    optional {\n      ?comb1 ontolex:lexicalForm [\n        wikibase:grammaticalFeature wd:Q107614077;\n        ontolex:representation ?combf1\n      ]\n    }\n\n    ?comb2 wikibase:lemma ?combl2.\n\n    bind(concat(replace(coalesce(?combf1, ?combl1), \"-$\", \"\"), lcase(replace(?combl2, \"^-\", \"\"))) as ?genlemma).\n    filter(?lemma = ?genlemma).\n  }\n} as %ok\nwhere {\n  ?l dct:language wd:Q188;\n     wikibase:lexicalCategory wd:Q1084;\n     wikibase:lemma ?lemma;\n     wdt:P5238 ?comb.\n  ?comb wikibase:lemma ?combl.\n  minus { include %ok }\n} order by ?lemma ?comb", "context": "German nouns with unparseable combines statements", "target": ""}
{"query": "#title: Grammatical features used on English nouns and proper nouns\nselect ?feat ?en (count(*) as ?total)\nwith {\n  select * {\n    values ?lexcat { wd:Q1084 wd:Q147276 }\n    ?l dct:language wd:Q1860;\n       wikibase:lexicalCategory ?lexcat;\n       ontolex:lexicalForm ?f.\n    ?f wikibase:grammaticalFeature ?feat.\n  }\n} as %feats\n{\n  include %feats.\n  ?feat rdfs:label ?en filter (lang(?en) = \"en\").\n} group by ?feat ?en order by desc(?total) ?en", "context": "Grammatical features used on English nouns and proper nouns", "target": ""}
{"query": "# List of countries sorted by life expectancy\nSELECT ?country ?countryLabel ?lifeExpectancy\nWHERE\n{\n    ?country wdt:P31 wd:Q6256.    # instance of country\n    ?country p:P2250/ps:P2250 ?lifeExpectancy. # life expectancy\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?lifeExpectancy)", "context": "List of countries sorted by life expectancy", "target": ""}
{"query": "#title: Railway bridges in Germany and Austria\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel ?coord ?image\nWHERE\n{\n  ?item wdt:P31 wd:Q39486269 .\n  ?item wdt:P17 ?o .\n\n  VALUES ?o{ wd:Q183 wd:Q40 }\n\n  ?item wdt:P625 ?coord .\n  ?item wdt:P18 ?image .\n\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "Railway bridges in Germany and Austria", "target": ""}
{"query": "SELECT DISTINCT ?train_wreck ?train_wreckLabel ?number_of_deaths ?countryLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?train_wreck wdt:P31 wd:Q1078765;\n    wdt:P1120 ?number_of_deaths.\n  OPTIONAL { ?train_wreck wdt:P17 ?country. }\n}\nORDER BY DESC (?number_of_deaths)\nLIMIT 124", "context": "Number of deaths in train wrecks per country", "target": ""}
{"query": "select distinct ?item1 ?article1 ?item2 ?article2 where\n{\n  { ?item1 wdt:P26 ?item2 } union { ?item2 wdt:P26 ?item1 }\n  filter(str(?item1) > str(?item2)) .\n  ?item1 wdt:P31 wd:Q5 . ?item2 wdt:P31 wd:Q5 .\n  ?article1 schema:about ?item1 . ?article1 schema:isPartOf <https://en.wikipedia.org/> . \n  ?article2 schema:about ?item2 . ?article2 schema:isPartOf <https://en.wikipedia.org/> . \n}", "context": "Pairs of people who both have an English #Wikipedia  article and are married to one another", "target": ""}
{"query": "#title: Lieux de naissances ou de décès de femmes peintres\n#defaultView:Map\nSELECT ?q ?qLabel ?layer ?coord WHERE {\n  VALUES ?Bretagne { wd:Q3389 wd:Q3349 wd:Q12642 wd:Q12549 wd:Q3068 }\n  ?q wdt:P21 wd:Q6581072 ;\n     wdt:P106 wd:Q1028181 ;\n     (wdt:P19|wdt:P20) ?layer .\n  ?layer wdt:P625 ?coord ; wdt:P131* ?Bretagne .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Places of birth or death of female painters in Brittany", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (COUNT(?gizaki) AS ?count) ?jardueraLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?gizaki wdt:P31 wd:Q5;\n    wdt:P21 wd:Q6581072.\n  ?wikilink schema:about ?gizaki;\n    schema:isPartOf <https://eu.wikipedia.org/>.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu\". }\n  ?gizaki wdt:P106 ?jarduera.\n}\nGROUP BY ?jardueraLabel", "context": "What profession do women in Basque Wikipedia biographies have?", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT ?item ?itemLabel ?rgb ?borders ?bordersLabel ?continentLabel\n{\n  ?item wdt:P31 wd:Q6256 .\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  \n  ?item wdt:P47 ?borders .\n  ?borders wdt:P31 wd:Q6256 . #also needs to be a country\n\n  OPTIONAL {?item wdt:P30 ?continent .\n    values (?continent ?rgb) { \n      (wd:Q46 \"7700FF\") #Europe\n      (wd:Q48 \"FF00FF\") #Asia\n      (wd:Q15 \"FFFF00\") #Africa\n      (wd:Q49 \"FF0000\") #North America\n      (wd:Q18 \"00FF00\") #South America\n      (wd:Q51 \"0000FF\") #Antarctica\n      #(wd:Q5401 \"FFFF77\") #Eurasia\n      (wd:Q538 \"00FFFF\") #Insular Oceania\n      (wd:Q3960 \"00FFFF\") #Australian Continent\n      (wd:Q55643 \"00FFFF\") #Oceania\n      (wd:Q27611 \"FF7777\") #Central America\n      (wd:Q828 \"770000\") #The Americas\n      (wd:Q664609 \"7777FF\") #Carribean\n      (wd:Q19464773 \"FF7700\") #Northern America and Mexico\n    }\n  }  \n  BIND( IF(!BOUND(?continent), \"AAAAAA\" ,  ?rgb )  AS ?rgb).  \n\n}\n", "context": "World map according to shared borders", "target": ""}
{"query": "#defaultView:Map\nSELECT ?lemma ?layer ?coord WHERE {\n  ?l dct:language ?lang ;\n     wikibase:lemma ?lemma ;\n     ontolex:sense/wdt:P5137 ?loc .\n  ?loc wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                         ?lang rdfs:label ?layer . }\n}", "context": "Lexeme with a Sense related to a geolocated Item", "target": ""}
{"query": "SELECT ?seasonNumber ?season ?seasonLabel ?showLabel {\n  ?season wdt:P31/wdt:P279* wd:Q3464665;\n          p:P179 [\n            a wikibase:BestRank;\n            ps:P179 ?show;\n            pq:P1545 ?seasonNumber\n          ].\n  FILTER(xsd:integer(?seasonNumber) > 10)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?seasonNumber)\nLIMIT 100", "context": "Highest TV seasons numbers", "target": ""}
{"query": "SELECT ?thingLabel ?Parltermlink ?subredditlink WHERE {\n  ?thing wdt:P4527 ?ParlID .\n  ?thing wdt:P3984 ?subreddit .\n  BIND(CONCAT(\"http://lda.data.parliament.uk/terms/\",STR(?ParlID)) as ?Parltermlink ).\n  BIND(CONCAT(\"https://reddit.com/\",STR(?subreddit)) as ?subredditlink ).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n  }", "context": "Wikidata items which have both a UK Parliament Thesaurus ID and a subreddit", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT (COUNT(DISTINCT ?author) AS ?count) ?provinceLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"eu,en\". }\n  ?liburu wdt:P31 wd:Q7725634;\n    wdt:P407 wd:Q8752;\n    wdt:P50 ?author.\n  ?author wdt:P19 ?birthplace.\n  ?birthplace (wdt:P131*) ?province.\n  { ?province wdt:P31 wd:Q162620. }\n  UNION\n  { ?province wdt:P31 wd:Q34876. }\n}\nGROUP BY ?provinceLabel", "context": "Number of books written in Basque grouped by the birth place of their authors", "target": ""}
{"query": "#Escritoras/es que nacieron \"el día de hoy\"\n#defaultView:Map{\"hide\": [\"?coord\"]}\n#Query by Silvia Gutiérrez (@espejolento)\n\nSELECT ?personItem ?person ?personDescription  ?date ?Country_of_citizenship  ?Gender ?image ?rank ?coord (CONCAT(?person,' (',?personDescription,')') as ?title)\nWHERE {\n{\nSELECT ?personItem ?date ?countryItem ?genderItem ?causeOfDeathItem ?ethnicGroupItem ?educatedAtItem ?coord\n(SAMPLE(?image) as ?image)\n(COUNT(?article) as ?rank) \nWHERE {\t\n?personItem wdt:P569 ?date .\n?personItem wdt:P18 ?image .\n?personItem wdt:P21 ?genderItem .\n?personItem wdt:P106 wd:Q36180 .\n?personItem wdt:P19 ?birthplace .   \n?birthplace wdt:P17 ?countryItem .  \n?countryItem p:P361/ps:P361 wd:Q12585 .\n?countryItem wdt:P625 ?coord .\nOPTIONAL { ?article schema:about ?personItem }\nFILTER (datatype(?date) = xsd:dateTime && month(?date) = month(now()) && day(?date) = day(now()))\t\n} GROUP BY ?personItem ?date ?countryItem ?genderItem ?causeOfDeathItem ?ethnicGroupItem ?educatedAtItem ?coord\n}\nSERVICE wikibase:label {\nbd:serviceParam wikibase:language 'es' .\n?personItem schema:description ?personDescription .\n?personItem rdfs:label ?person .\n?countryItem rdfs:label ?Country_of_citizenship .\n?genderItem rdfs:label ?Gender .\n}\n}\nORDER BY DESC(?rank)", "context": "Authors from Latin America born today", "target": ""}
{"query": "select ?companyLabel ?website where {\n?company wdt:P31 wd:Q4830453 ;\n         wdt:P856 ?website .\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\nFILTER regex (STR(?website), \"biz$\") .\n              \n              \n \n}\n", "context": "Businesses who's official websites use the \".biz\" top-level domain", "target": ""}
{"query": "SELECT ?comics_characterLabel ?educationLabel ?degreeLabel ?degreeStatementLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?comics_character wdt:P31 wd:Q1114461.\n    VALUES ?universes {\n    wd:Q1152150\n    wd:Q931597\n  }\n  ?comics_character wdt:P1080 ?universes.\n  OPTIONAL {\n    ?comics_character p:P69 ?educationstatement;\n      wdt:P69 ?education.\n    ?education (wdt:P31/(wdt:P279*)) wd:Q38723.\n  }\n  OPTIONAL {?comics_character p:P69 ?educationstatement.\n                              ?educationstatement pq:P512 ?degree. }\n  OPTIONAL { ?comics_character wdt:P512 ?degreeStatement. }\n}\n", "context": "Superheroes with a degree in the humanities", "target": ""}
{"query": "#title: Lieux de découvertes des objets conservés au musée de Bretagne\n#defaultView:Map\nSELECT DISTINCT ?loc ?locLabel ?coord ?layer WHERE {\n  ?item wdt:P195 wd:Q3329701 ;\n        wdt:P189 ?loc ;\n        wdt:P2348 ?periode .\n  ?loc wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\".\n                         ?loc rdfs:label ?locLabel .\n                         ?periode rdfs:label ?layer . }\n}", "context": "Places of discovery of objects held in the Musée de Bretagne (colored according to the period of the object)", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Top album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:Map{\"hide\":[\"?atm\",\"?geometry\"], \"layer\": \"?bankLabel\"}\n\nPREFIX lgdo: <http://linkedgeodata.org/ontology/>\nPREFIX geom: <http://geovocab.org/geometry#>\nPREFIX bif: <bif:>\n\nSELECT ?atm ?geometry ?bank ?bankLabel WHERE {\n  hint:Query hint:optimizer \"None\".\n  \n  SERVICE <http://linkedgeodata.org/sparql> {\n    { ?atm a lgdo:Bank; lgdo:atm true. }\n    UNION { ?atm a lgdo:Atm. }\n    \n    ?atm geom:geometry [geo:asWKT ?geometry];\n         lgdo:operator ?operator.\n    FILTER(bif:st_intersects(?geometry, bif:st_point(11.5746898, 48.1479876), 5)) # 5 km around Munich\n  }\n  \n  BIND(STRLANG(?operator, \"de\") as ?bankLabel) \n  ?bank rdfs:label ?bankLabel.\n  \n  # bank is part of the Bankcard service network, either via an explicit statement or implicitly due to its legal form (unless explicitly excluded)\n  { ?bank wdt:P527 wd:Q806724. }\n  UNION { ?bank wdt:P1454 wd:Q5349747. }\n  MINUS { wd:Q806724 wdt:P3113 ?bank. }\n}", "context": "ATMs around Munich belonging to the Bankcard-Servicenetz interbank network (federated query)", "target": ""}
{"query": "#Population growth in Ghana from 1960 onward\n#defaultView:LineChart\nSELECT ?year ?population WHERE {\n  wd:Q117 p:P1082 ?p.\n  ?p pq:P585 ?year;\n    ps:P1082 ?population.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY (?year)", "context": "Population growth in Ghana from 1960 onward", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?shape\", \"?coords\"]}\nSELECT  ?item ?countryLabel ?itemLabel ?population ?coords ?shape ?layer\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q10864048 ;\n        wdt:P1082 ?population ;\n        wdt:P17 ?country .\n  FILTER ( ?population > 10000000 )\n  FILTER NOT EXISTS { ?item wdt:P31 wd:Q19953632 }\n  FILTER NOT EXISTS { ?item wdt:P576 ?x }\n  OPTIONAL { ?item wdt:P625 ?coords }\n  OPTIONAL { ?item wdt:P3896 ?shape }\n  BIND(\n    IF(?population < 20000000, \"10M-20M\",\n    IF(?population < 30000000, \"20M-30M\",\n    IF(?population < 40000000, \"30M-40M\",\n    IF(?population < 50000000, \"40M-50M\",\n    IF(?population < 60000000, \"50M-60M\",\n    IF(?population < 70000000, \"60M-70M\",\n    IF(?population < 80000000, \"70M-80M\",\n    IF(?population < 90000000, \"80M-90M\",\n    IF(?population < 100000000, \"90M-100M\",   \n    \"100M+\")))))))))\n    AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?population", "context": "Administrative subdivisions with more than 10 million inhabitants", "target": ""}
{"query": "# Mental disorders @ Wikidata\nSELECT ?entity ?entityLabel ?entityDesc\nWHERE {\n  ?entity wdt:P31/wdt:P279* wd:Q12135 .\n  ?entity schema:description ?entityDesc .\n  FILTER(LANG(?entityDesc) = \"en\")\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en\" . }\n} ", "context": "List of mental disorders", "target": ""}
{"query": "#title:Paintings once owned by Red Flag Names\n\n#defaultView:ImageGrid\nSELECT DISTINCT ?pic ?painting ?paintingLabel ?location ?locationLabel\nWHERE\n{\n  ?painting wdt:P31 wd:Q3305213; #painting\n            wdt:P127 [ wdt:P1840 wd:Q30335959 ].  #the painting is owned by someone who is a red flag name\n  OPTIONAL  {?painting wdt:P276 ?location.}\n  ?painting wdt:P18 ?pic.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}", "context": "Paintings once owned by Red Flag Names", "target": ""}
{"query": "#defaultView:Map\nSELECT\n  ?item\n  ?itemLabel\n  ?location\n  ?locationLabel\n  ?coords\nWITH {\n  SELECT DISTINCT ?item ?location {\n    ?item\n      wdt:P31 wd:Q5;\n      wdt:P2037 [];\n      wdt:P19 ?location.\n  }\n} AS %0\nWITH {\n  SELECT DISTINCT ?location {\n    INCLUDE %0.\n  }\n} AS %2\nWITH {\n  SELECT DISTINCT ?location ?coords {\n    INCLUDE %2.\n    ?location (wdt:P131|wdt:P276)*/wdt:P625 ?coords.\n  }\n} AS %3\n{\n  INCLUDE %0.\n  INCLUDE %3.\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}.\n}", "context": "Place of birth of humans with Github username", "target": ""}
{"query": "#Map of institutions where Canadian citizens got their PhD\n#defaultView:Map\nSELECT DISTINCT ?institution ?institutionLabel ?academics ?degree ?geoloc ?image ?academicsLabel WHERE {\n  ?academics wdt:P31 wd:Q5;\n    p:P69 ?statement.\n  OPTIONAL { ?academics wdt:P18 ?image. }\n  ?statement ps:P69 ?institution.\n  ?institution wdt:P625 ?geoloc.\n  ?statement pq:P512 ?degree.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?academics wdt:P39 wd:Q117838816.\n}", "context": "Map of where the presidents of BCALA were educated", "target": ""}
{"query": "#title:Number of Wikipedia sitelinks for Art Looting Investigation Unit Red Flag Names\nSELECT ?lang (COUNT(DISTINCT ?article) AS ?count) WHERE {\n  hint:Query hint:optimizer \"None\".\n  ?item wdt:P1840 wd:Q30335959 . #Red Flag names\n  ?article schema:about ?item . # find articles about \n  ?article schema:isPartOf / wikibase:wikiGroup \"wikipedia\" . # only Wikipedias articles\n  hint:Prior hint:gearing \"forward\" .\n  # This hint says to search the property chain above from left to right (\"forward\"),\n  # i.e. it is checked if each previously found value for ?article is linked to a Wikipedia.\n  # The default (\"reverse\") would be to search it from right to left, i.e. find all Wikipedia\n  # articles first, and then select the intersection with the previously found values of ?article.\n  ?article schema:inLanguage ?lang .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?lang\nORDER BY DESC (?count)\n", "context": "Number of Wikipedia sitelinks for Art Looting Investigation", "target": ""}
{"query": "#title: Female women of the Dutch resistance, the point on the line is the place of death, the other end place of birth.\n#defaultView:Map{\"hide\":[\"?line\",\"?bcoord\",\"?dcoord\",\"?lat1\",\"?lon1\",\"?lat2\",\"?lon2\"]}\nSELECT DISTINCT ?women ?womenLabel ?pobLabel ?podLabel ?dcoord ?line ?img\nWHERE {\n  ?women wdt:P31 wd:Q5 ; wdt:P19 ?pob ; wdt:P20 ?pod ; wdt:P21 wd:Q6581072 ; wdt:P27 wd:Q29999 ; wdt:P106 wd:Q1397808 . \n  ?pob wdt:P625 ?bcoord .\n  ?pod wdt:P625 ?dcoord .\n  OPTIONAL { ?women wdt:P18 ?img }\n  BIND(IF(xsd:double(?depth/10) = xsd:integer(?depth/10),?bcoord,\"\") as ?dcoord)\n  ?pob p:P625/psv:P625/wikibase:geoLatitude ?lat1 ; p:P625/psv:P625/wikibase:geoLongitude ?lon1 .\n  ?pod p:P625/psv:P625/wikibase:geoLatitude ?lat2 ; p:P625/psv:P625/wikibase:geoLongitude ?lon2 .\n  BIND(CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')') AS ?str) . BIND(STRDT(?str, geo:wktLiteral) AS ?line)  \n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"nl,en\" }\n}", "context": "Mapping birthplace and place of death of female Dutch Resistance Fighters on a line", "target": ""}
{"query": "#defaultView:Map\n#Pays qui ont des liens de sites vers Wikipédia en français avec une parenthèse dans le titre\nSELECT ?country ?countryLabel ?geo ?article ?title WHERE {\n  ?country wdt:P31 wd:Q3624078 .\n  ?country wdt:P3896 ?geo .\n  ?article schema:about ?country .\n  ?article schema:isPartOf <https://fr.wikipedia.org/> .\n  ?article schema:name ?title .\n  \n  FILTER REGEX(STR(?title), \"\\\\(\") .\n  \n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n  }\n} ORDER BY ?countryLabel", "context": "Countries that have site links to French Wikipedia with a parenthesis in the title", "target": ""}
{"query": "#title: Nom de langues contenant les mots \"vieux\", \"vieil\" ou \"ancien\"\nSELECT DISTINCT ?lang ?nom WHERE {\n  ?lang wdt:P31/wdt:P279* wd:Q17376908 ; rdfs:label ?nom .\n  FILTER ( LANG(?nom) = \"fr\" )\n  FILTER ( regex( ?nom , \"(vieux|vieil|ancien)\" ) )\n}\nORDER BY ?nom", "context": "Name of languages ​​containing the words \"old\" or \"ancient\"", "target": ""}
{"query": "#Birthplaces of humans named Antoine\n# Coordinates of the birth places of people named Antoine\n#defaultView:Map\nSELECT ?item ?itemLabel ?coord\nWHERE\n{\n  ?item wdt:P31 wd:Q5 .   # human\n  ?item wdt:P735 wd:Q15235674.\n  ?item wdt:P19 ?place.\n  ?place wdt:P625 ?coord.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Birthplaces of humans called \"Antoine\"", "target": ""}
{"query": "#Art historians dealers collectors museum directors restorers member of the Nazi party\n\n#defaultView:Graph\n\n#title:Owned by Nazis or Art Looting Red Flag Names\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?art ?artLabel ?pic\nWHERE \n{\n  {?item wdt:P106 wd:Q1792450} UNION {?item wdt:P106 wd:Q173950} UNION {?item wdt:P106 wd:Q10732476} UNION {?item wdt:P106 wd:Q674426} UNION {?item wdt:P106 wd:Q22132694} UNION {?item wdt:P106 wd:Q2145981} \n   { ?item wdt:P102 wd:Q7320.} UNION  { ?item wdt:P1840 wd:Q30335959.} # member of Nazi Party or Red Flag Name\n  ?art wdt:P127 ?item.\n  OPTIONAL { ?art wdt:P18 ?pic .}\n\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Graph of art (and other things) owned by Art Looting Red Flag Names or members of the Nazi Party at any time", "target": ""}
{"query": "SELECT (count(?england_) as ?england) (count(?scotland_) as ?scotland) (count(?wales_) as ?wales) WITH {\n  SELECT DISTINCT ?item ?scotland_ ?england_ ?wales_ WHERE \n  {\n    # item is a civil parish\n    { ?item wdt:P31/wdt:P279* wd:Q4976993 . hint:Prior hint:gearing \"reverse\".} hint:Prior hint:runFirst true.\n    # item is also a human settlement\n    ?item wdt:P31/wdt:P279* wd:Q486972. hint:Prior hint:gearing \"forward\".\n  \n    { ?item wdt:P131/wdt:P131* wd:Q22 . hint:Prior hint:gearing \"forward\". BIND(\"S\" as ?scotland_) }\n    UNION\n    { ?item wdt:P131/wdt:P131* wd:Q21 . hint:Prior hint:gearing \"forward\". BIND(\"E\" as ?england_) }\n    UNION\n    { ?item wdt:P131/wdt:P131* wd:Q25 . hint:Prior hint:gearing \"forward\". BIND(\"W\" as ?wales_) }\n} } as %i\nWHERE\n{\n  INCLUDE %i\n}", "context": "Count comparison of England, Scotland and Wales items claiming to be a human settlement as well as a parish", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?entry ?entryLabel ?artist ?artistLabel (MIN(?date) AS ?date_) (SAMPLE(?image) AS ?image_) WHERE {\n  {\n    ?entry wdt:P1344 ?contest.\n    ?contest wdt:P31 wd:Q110288240.\n  } UNION {\n    ?entry wdt:P31 wd:Q63481999.\n  }\n  ?entry wdt:P585|wdt:P577 ?date;\n         wdt:P175 ?artist.\n  OPTIONAL { ?artist wdt:P18 ?image. }\n  {\n    ?artist (wdt:P527|^wdt:P361)?/wdt:P361?/(wdt:P31|wdt:P1552)/wdt:P279* wd:Q14756018.\n  } UNION {\n    ?artist (wdt:P527|^wdt:P361|^wdt:P463)?/p:P3373/pq:P1039/wdt:P279* wd:Q159979.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?entry ?entryLabel ?artist ?artistLabel", "context": "Twins at Eurovision", "target": ""}
{"query": "SELECT DISTINCT ?stationLabel\nWHERE \n{\n  ?item wdt:P16 wd:Q389554 ;\n        wdt:P559 ?station .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?stationLabel", "context": "Terminus stations of the Transilien railroad network", "target": ""}
{"query": "SELECT ?childrenOf ?childrenOfLabel ?giveNameLabel ?familyNameLabel ?nationality ?nationalityLabel ?mother ?motherLabel\n ?image\nWHERE \n{\n  ?childrenOf wdt:P22 wd:Q1339.\n  ?childrenOf wdt:P735 ?giveName.\n  ?childrenOf wdt:P734 ?familyName.\n  ?childrenOf wdt:P27 ?nationality .\n  ?childrenOf wdt:P18 ?image.\n  ?childrenOf wdt:P25 ?mother.\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],es\". } \n}", "context": "List of children of Johann Sebastian Bach", "target": ""}
{"query": "PREFIX dct: <http://purl.org/dc/terms/>\nPREFIX dbc: <http://dbpedia.org/resource/Category:>\nPREFIX wdt: <http://www.wikidata.org/prop/direct/>\nPREFIX owl: <http://www.w3.org/2002/07/owl#>\n\nSELECT DISTINCT ?item ?wditem ?wditemLabel ?date WHERE {\n  SERVICE <http://dbpedia.org/sparql> {\n    ?item dct:subject dbc:Mountaineering_deaths_on_Mount_Everest ; \n          owl:sameAs ?wditem .\n    FILTER regex(?wditem, \"wikidata.org\").\n  }\n\n  SERVICE <https://query.wikidata.org/sparql> {\n    ?wditem wdt:P570 ?date .\n    OPTIONAL { ?wditem wdt:P18 ?image . }\n    ?wditem rdfs:label ?wditemLabel .\n    FILTER (LANG(?wditemLabel) = \"en\").\n  }\n}\nORDER BY ?date", "context": "Which mountaineers died on Mount Everest ordered by their death date?", "target": ""}
{"query": "#title: Personnes nées en Bretagne et prénommée Jeanne, Jean ou Yann\n#defaultView:ImageGrid\nSELECT ?personne ?personneLabel ?img WHERE {\n  VALUES ?prénom { wd:Q2438057 wd:Q7521081 wd:Q13564380 }\n  ?personne wdt:P735 ?prénom ;\n            wdt:P19/wdt:P131* wd:Q12130 ;\n            wdt:P18 ?img\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Gallery of people born in Brittany and named Jeanne, Jean or Yann", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?item WHERE {\n      ?item p:P106 ?statement0.\n      ?statement0 (ps:P106/(wdt:P279*)) wd:Q2083925.\n      ?item p:P570 ?statement_1.\n      ?statement_1 psv:P570 ?statementValue_1.\n      ?statementValue_1 wikibase:timeValue ?P570_1.\n      MINUS {\n        ?item p:P6944 ?statement2.\n        ?statement2 (ps:P6944) _:anyValueP6944.\n      }\n    }\n    LIMIT 100\n  }\n}", "context": "List of botanical collectors who have a death date but don't have a Bionomia ID", "target": ""}
{"query": "SELECT DISTINCT ?mp ?mpLabel ?end ?seat1Label ?cause1Label ?end2 ?seat2Label ?cause2Label\n {\n   values ?cause1 { wd:Q796919 wd:Q52112831 wd:Q46993416 wd:Q36717319 wd:Q46992747 }\n   values ?cause2 { wd:Q796919 wd:Q52112831 wd:Q46993416 wd:Q36717319 wd:Q46992747 }\n ?membership wdt:P279 wd:Q16707842 . ?membership2 wdt:P279 wd:Q16707842 .\n ?mp p:P39 ?positionStatement . ?positionStatement ps:P39 ?membership . \n ?positionStatement pq:P1534 ?cause1 . ?positionStatement pq:P768 ?seat1 .\n ?mp p:P39 ?positionStatement2 . ?positionStatement2 ps:P39 ?membership2 . \n ?positionStatement2 pq:P1534 ?cause2 . ?positionStatement pq:P768 ?seat2 .\n ?positionStatement pq:P582 ?end . ?positionStatement2 pq:P582 ?end2 .\n   filter(?end < ?end2 )\n filter(year(?end2) > 1940)\n SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }\n}\nORDER BY desc(?end2)", "context": "British MPs who resigned from the Commons mid-parliament on two separate occasions", "target": ""}
{"query": "#title: Dutch members of parliament with a Mastodon account\nSELECT ?mp ?mpLabel ?mastodon ?party ?partyLabel WHERE {\n  ?mp wdt:P4033 ?mastodon .\n  ?mp p:P39 ?positionStatement .\n  ?positionStatement ps:P39 wd:Q18887908 ;\n                     pq:P4100 ?party .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Dutch members of parliament with a Mastodon account", "target": ""}
{"query": "SELECT DISTINCT ?writer ?writerLabel ?lastWords ?langcode ?languageLabel ?dateOfDeath\nWHERE {\n  ?writer wdt:P106/wdt:P279? wd:Q36180\n          ; wdt:P570 ?dateOfDeath .\n  FILTER NOT EXISTS { ?writer wdt:P570 ?dateOfDeath2 . FILTER(?dateOfDeath2 > ?dateOfDeath) }\n  ?writer wdt:P3909 ?lastWords .\n  BIND (lang(?lastWords) as ?langcode)\n  OPTIONAL { ?language wdt:P218 ?langcode }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" . }\n} ORDER BY ?dateOfDeath", "context": "Last words of authors (adapted from a query generated by ChatGPT)", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?year ?gender ?genderLabel ?father ?mother ?fatherLabel ?motherLabel WHERE {\n  ?item wdt:P31 wd:Q5;\n    wdt:P21 ?gender;\n    (wdt:P106/(wdt:P279*)) wd:Q33999;\n    wdt:P27 wd:Q30;\n    wdt:P569 ?birthdate;\n    rdfs:label ?itemLabel.\n  FILTER((LANG(?itemLabel)) = \"en\")\n  ?sitelink schema:about ?item;\n    schema:isPartOf <https://en.wikipedia.org/>.\n  ?gender rdfs:label ?genderLabel.\n  FILTER((LANG(?genderLabel)) = \"en\")\n  FILTER((YEAR(?birthdate)) >= 1970 )\n  BIND(YEAR(?birthdate) AS ?year)\n  OPTIONAL {\n    ?item wdt:P25 ?mother.\n    ?motherlink schema:about ?mother;\n      schema:isPartOf <https://en.wikipedia.org/>.\n    ?mother rdfs:label ?motherLabel.\n    FILTER((LANG(?motherLabel)) = \"en\")\n  }\n  OPTIONAL {\n    ?item wdt:P22 ?father.\n    ?fatherlink schema:about ?father;\n      schema:isPartOf <https://en.wikipedia.org/>.\n    ?father rdfs:label ?fatherLabel.\n    FILTER((LANG(?fatherLabel)) = \"en\")\n  }\n}", "context": "List of American actors and actresses having a wikipedia page with parents having a wikipedia page", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 25, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#title:Replication Crisis by Subject area\n#defaultView:BubbleChart\nSELECT ?subject ?subjectLabel (COUNT(DISTINCT ?article) AS ?count) WHERE {\n?article wdt:P921 wd:Q25303778; wdt:P1433 ?journal .\n?journal wdt:P921 ?subject\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?subject ?subjectLabel", "context": "Subject areas of journals that have published papers about the replication crisis", "target": ""}
{"query": "# count of games published in ROM format per platform per year, 1995-1997\n#defaultView:BarChart\nSELECT DISTINCT\n?platformLabel\n(COUNT(*) AS ?game_count) (STR(?pubYear_) AS ?pubYear) # turn back pubYear_ YEAR into STR for the bar chart x-axis to look good\nWHERE { \n  ?game wdt:P31 wd:Q7889. # instance of video game\n  ?game wdt:P437 wd:Q633454. # distribution format is ROM cartridge\n  ?game wdt:P577 ?pubDate.\n  ?game wdt:P400 ?platform.\n\n  BIND (YEAR(?pubDate) AS ?pubYear_) # cast pubDate as YEAR for filtering\n  \n  FILTER (?pubYear_ >= 1995 && ?pubYear_ <= 1997)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?pubYear_ ?platformLabel\nORDER BY ASC(?platformLabel)", "context": "Count of games published in ROM format per platform per year, 1995-1997", "target": ""}
{"query": "SELECT ?country ?countryLabel ?population\n(GROUP_CONCAT(DISTINCT ?countryBLabel ; separator = \", \") AS ?similarPopulation)\nWHERE {\n  ?country wdt:P31 wd:Q6256 .\n  ?country wdt:P1082 ?population .\n  ?countryB wdt:P31 wd:Q6256 .\n  ?countryB wdt:P1082 ?populationB .\n  FILTER (?country != ?countryB && ?populationB >= ?population * 0.95 && ?populationB <= ?population * 1.05)\n  ?country rdfs:label ?countryLabel . FILTER (LANG(?countryLabel) = \"en\") .\n  ?countryB rdfs:label ?countryBLabel . FILTER (LANG(?countryBLabel) = \"en\") .\n}\nGROUP BY ?country ?countryLabel ?population\nORDER BY ?countryLabel", "context": "Countries with similar populations", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 26, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel (sample(?_image) as ?photo) WHERE {\n  ?item wdt:P91 wd:Q6636.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"fr\".\n    ?item rdfs:label ?label.\n  }\n  OPTIONAL { ?item wdt:P18 ?_image. }\n  ?item wdt:P21 wd:Q6581097.\n  minus { ?item wdt:P570 ?date_de_mort. }\n} group by ?item ?itemLabel", "context": "Portraits of queer men people", "target": ""}
{"query": "#defaultView:Map\n#title: World Universities on Mastodon\nSELECT DISTINCT (SAMPLE(?location_) AS ?location) ?uni ?uniLabel ?mastodon WHERE {\n  ?uni wdt:P31/wdt:P279* wd:Q3918 ;\n    wdt:P4033 ?mastodon .\n  { ?uni wdt:P625 ?location_ }\n  UNION\n  { ?uni wdt:P159 / wdt:P625 ?location_ }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?uni ?uniLabel ?mastodon\n", "context": "World Universities on Mastodon", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 28, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#title: Bubble graph of the first letter of the word for \"one\"\n#defaultView:BubbleChart\nSELECT ?initial (COUNT(DISTINCT ?l) AS ?count) WHERE {\n  ?l dct:language ?lang ;\n     wikibase:lemma ?lemma ;\n     ontolex:sense ?sense .\n  BIND ( UCASE(SUBSTR(STR(?lemma),1,1)) AS ?initial )\n  ?sense wdt:P5137 wd:Q199 .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?initial", "context": "Bubble graph of the first letter of the word for \"one\" for languages where Wikidata has a Lexeme", "target": ""}
{"query": "SELECT DISTINCT ?reasonLabel ?nw ?old_id  (IRI(CONCAT(STR(?item), '#P1367')) AS ?stmt) ?itemLabel (year(?dob) AS ?born) (year(?dod) AS ?died) ?search ?itemDescription   WHERE {\n  ?item p:P1367 ?stmt1 .\n  ?stmt1 wikibase:rank wikibase:DeprecatedRank .\n  ?stmt1 ps:P1367 ?old_id .\n  ?stmt1 pq:P2241 ?reason .\n  OPTIONAL {?stmt1 pq:P3740 ?nw} .\n\n \n  MINUS { \n    ?item p:P1367 ?stmt2 .\n    ?stmt2 a wikibase:BestRank .\n#    {?stmt2 ps:P1367 ?new_id } UNION {?stmt2 a wdno:P1367} .\n  }\n  \n  OPTIONAL { ?item wdt:P569 ?dob } .\n  OPTIONAL { ?item wdt:P570 ?dod } .\n  \n  ?item rdfs:label ?item_label FILTER (lang(?item_label) = 'en') .\n  \n  BIND(IRI(CONCAT('https://artuk.org/discover/artists/view_as/grid/search/keyword:', LCASE(REPLACE(?item_label, ' ', '-')))) AS ?search) .\n    \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY (xsd:integer(STRAFTER(str(?item), 'Q'))) ?died ?born", "context": "Searches for trying to replace broken Art UK external IDs", "target": ""}
{"query": "SELECT DISTINCT ?linkedItems ?title WHERE {\n  hint:Query hint:optimizer \"None\".\n  <https://en.wikipedia.org/wiki/List_of_current_United_States_governors> schema:about ?item;\n                                            schema:name ?name.\n  SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:api \"Generator\";\n                    wikibase:endpoint \"en.wikipedia.org\"; \n                    mwapi:generator \"links\";          \n                    mwapi:titles ?name;\n                    mwapi:gplnamespace \"0\";\n                    mwapi:gpllimit \"max\". \n    ?title_ wikibase:apiOutput mwapi:title.\n  }\n  FILTER(BOUND(?title_))\n  BIND(STRLANG(?title_, \"en\") AS ?title)\n  ?linkedArticles schema:name ?title;\n                  schema:about ?linkedItems.\n  ?linkedItems wdt:P31 wd:Q5 .\n}", "context": "People linked in the English Wikipedia page \"List of current United States governors\"", "target": ""}
{"query": "SELECT DISTINCT ?entity ?entityLabel WHERE {  ?entity wdt:P5008 wd:Q111645234; wdt:P31 wd:Q5 .  MINUS {    {?entity a wdno:P21 .} UNION    {?entity wdt:P21 ?prop .}  }  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }}", "context": "List of people in the NZThesisProject with no assigned gender", "target": ""}
{"query": "#title: Institutions culturelles sur Mastodon (avec date de début et langue)\nSELECT ?q ?qLabel ?nom_Mastodon ?date_début ?langueLabel WHERE {\n  ?q wdt:P31/wdt:P279* wd:Q1030034 ;\n     p:P4033 ?node_Mastodon .\n  ?node_Mastodon ps:P4033 ?nom_Mastodon .\n  OPTIONAL { ?node_Mastodon pq:P580 ?date_début }\n  OPTIONAL { ?node_Mastodon pq:P407 ?langue }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Cultural institutions having a Mastodon handle", "target": ""}
{"query": "#title: Lieux de Rennes avec un article sur la Wikipédia en français\n#defaultView:Map\nSELECT DISTINCT ?q ?qLabel ?coord ?page ?layer WHERE {\n  ?q wdt:P131+ wd:Q647 ;\n     wikibase:sitelinks ?layer .\n  wd:Q647 wdt:P625 ?coordRennes .\n    SERVICE wikibase:around {\n      ?q wdt:P625 ?coord .\n      bd:serviceParam wikibase:center ?coordRennes .\n      bd:serviceParam wikibase:radius \"10\" .\n    }\n  ?page schema:about ?q ;\n        schema:isPartOf <https://fr.wikipedia.org/> .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Maps of places in Rennes with an article on French Wikipedia, color-coded by number of pages on the Wikimedia Projects", "target": ""}
{"query": "#title: Dates d'entrée et de découverte d'objets du musée de Bretagne\nSELECT * WHERE {\n  ?q wdt:P575 ?date_découverte ;\n     wdt:P217 ?num ;\n     wdt:P195 wd:Q3329701 .\n  FILTER ( REGEX(?num, \"^[89]\\\\d\\\\d\\\\.\" ) )\n  BIND ( year(?date_découverte) AS ?année_découverte )\n  BIND ( xsd:integer(substr(?num,0,4))+1000 AS ?année_entrée )\n  BIND(IF(?année_entrée < ?année_découverte , \"à vérifier\", \"ok\" ) AS ?vérification )\n}", "context": "Entry dates and discovery dates of the objects of the museum of Brittany", "target": ""}
{"query": "#title: Lieux de Rennes avec un article sur la Wikipédia en français\n#defaultView:Map\nSELECT DISTINCT ?q ?qLabel ?coord ?page ?layer WHERE {\n  ?q wdt:P131+ wd:Q647 ;\n     wikibase:sitelinks ?layer .\n  wd:Q647 wdt:P625 ?coordRennes .\n    SERVICE wikibase:around {\n      ?q wdt:P625 ?coord .\n      bd:serviceParam wikibase:center ?coordRennes .\n      bd:serviceParam wikibase:radius \"10\" .\n    }\n  ?page schema:about ?q ;\n        schema:isPartOf <https://fr.wikipedia.org/> .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of places located in Rennes with an article on the French Wikipedia", "target": ""}
{"query": "SELECT ?item ?itemLabel ?weapon ?weaponLabel ?colorLabel WHERE {\n  ?item wdt:P361 wd:Q1381762.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  \nOPTIONAL { ?item wdt:P520 ?weapon }\n  OPTIONAL {?item p:P3828 [ps:P3828 wd:Q2262288; pq:P462 ?color;]}\n}", "context": "Teenage Mutant Ninja Turtles weapon types, and primary colors", "target": ""}
{"query": "#defaultView:BarChart\nSELECT ?grouping (COUNT(DISTINCT ?entity) as ?count) WHERE { ?entity wdt:P31/wdt:P279* wd:Q482994. ?entity wdt:P577 ?grouping_ . bind(str(year(?grouping_)) as ?grouping) filter(bound(?grouping)).} GROUP BY ?grouping HAVING (?count >= 0) ORDER BY DESC(?count) LIMIT 1000", "context": "Number of albums in Wikidata by year of release", "target": ""}
{"query": "#defaultView:Table\nSELECT ?item ?itemLabel  ?significant_eventLabel ?typeLabel ?time \nWHERE {\n  ?item (wdt:P131/wdt:P279*) wd:Q1731.                # items in Dresden\n  ?item wdt:P793 [  pq:* ?significant_event  ].       # significant event\n  OPTIONAL {?significant_event wdt:P31 ?type}\n  ?item p:P793 ?eventstatment .   \n  ?eventstatment pq:P585 ?time.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}\norder by ?time ?significant_eventLabel ?itemLabel", "context": "Objects in Dresden with significant event", "target": ""}
{"query": "SELECT ?county ?countryLabel (COUNT(*) AS ?count) WHERE {\n  ?sitelink schema:about ?item;\n    schema:isPartOf <https://fr.wikivoyage.org/>;\n    schema:name ?string.\n  ?item wdt:P17 ?country.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}\nGROUP BY  ?county ?countryLabel \nORDER BY DESC(?count)", "context": "Count of items with an article on fr.wikivoyage.org by country", "target": ""}
{"query": "SELECT ?instance ?instanceLabel  (COUNT(*) AS ?count) WHERE {\n  ?sitelink schema:about ?item;\n    schema:isPartOf <https://fr.wikiquote.org/>;\n    schema:name ?string.\n  ?item wdt:P31 ?instance .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}\nGROUP BY  ?instance ?instanceLabel \nORDER BY DESC(?count)", "context": "Count of items with an article on fr.wikiquote.org by instance of", "target": ""}
{"query": "# Number of entities in Wikidata with X amount of statements. By: User:Infrastruktur\n# Even if this query seemingly has to do calculations on all the 100M+ items in the graph.\n# what happens here is it uses two of Blazegraph's optimizations to finish in less than a second.\n# Due to technical limitations this has to count any type of entity, not just items, but also lexemes and properties.\n#defaultView:LineChart\nselect ?val ?count\nwith {\n  select ?val (count(*) AS ?count)\n  where { [] wikibase:statements ?val . }\n  group by ?val  \n} as %i\nwhere {\n  include %i\n  filter (?val <= 60)\n}\norder by ?val\n", "context": "Count of items that have X amount of statements. A simple showcase of Blazegraph optimizations.", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 33, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coord WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?item ?coord ?pob WHERE {\n      ?item p:P27 ?statement3.\n      ?statement3 (ps:P27/(wdt:P279*)) wd:Q183.\n      ?item p:P106 ?statement0.\n      ?statement0 (ps:P106/(wdt:P279*)) wd:Q36180.\n      ?item p:P21 ?statement1.\n      ?statement1 (ps:P21/(wdt:P279*)) wd:Q6581072.\n      ?item wdt:P19 ?pob.\n      ?pob wdt:P625 ?coord.\n    }\n  }\n}", "context": "Map of birthplaces of German women writers", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 34, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:Map\nSELECT ?astronautLabel ?timeinspace ?coordinates WHERE {\n   ?astronaut wdt:P31 wd:Q5 ;\n              wdt:P19 ?birthPlace ;\n         wdt:P2873 ?timeinspace .\n  ?birthPlace wdt:P625 ?coordinates .\n  FILTER (?timeinspace >0) \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Where were people who  went to space born?", "target": ""}
{"query": "#defaultView:Graph\n\n# 220 Old Greek personalities with entries in Prosopographia attica by Johannes Ernst Kirchner (Q1697841), 1901.\n# For the full source document see https://archive.org/details/prosopographiaa00kircgoog/page/0/mode/2up\n\nSELECT DISTINCT ?item ?itemLabel ?Image ?greekname ?pa  WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de\". }\n  {\n    SELECT DISTINCT ?item ?itemlabel ?Image ?greekname ?pa  WHERE {\n      ?item p:P31 ?statement0.\n      ?statement0 (ps:P31/(wdt:P279*)) wd:Q5. #a human\n      ?item wdt:P2421 ?pa. #has an entry in Prosopographia attica (Q2113277) \n      OPTIONAL {\n        ?item wdt:P1559 ?greekname.\n       }\n      OPTIONAL {\n      ?item wdt:P18 ?Image.\n      }\n      bind (if(bound(?Image), \n           ?Image, \n           <http://commons.wikimedia.org/wiki/Special:FilePath/File:Discurso_funebre_pericles.PNG>) as ?Image) #dummy image\n    }    \n  }\n}", "context": "Old Greek personalities with entries in Prosopographia attica", "target": ""}
{"query": "SELECT distinct ?item ?itemLabel ?time ?damage ?unitLabel WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q2727213.\n  ?item wdt:P585 ?time.\n  ?item p:P2630/psv:P2630 ?damagetriple.\n  ?damagetriple wikibase:quantityAmount ?damage.\n  ?damagetriple wikibase:quantityUnit ?unit.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?damage)", "context": "Largest heist by damage", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT ?item ?itemLabel (SAMPLE(?map) AS ?maps) WHERE {\nVALUES ?STUFFS { wd:Q3115110 wd:Q182274 }\n  ?item wdt:P31  ?STUFFS.\n  SERVICE wikibase:label { \n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,fr\" . \n  }         \nOPTIONAL {  ?item wdt:P1621|wdt:P18 ?map}\n} group by ?item ?itemLabel       ", "context": "Territories claimed by irredentism", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coord WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?item ?itemLabel ?coord WHERE {\n      ?item wdt:P31/wdt:P279* wd:Q2175765;\n            wdt:P625 ?coord.\n    }\n  }\n}", "context": "Map of tram stop all over the world", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?coords WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de\". }\n  {\n    SELECT DISTINCT ?item ?itemLabel ?coords WHERE {\n      {\n        ?item wdt:P31/wdt:P279* wd:Q27031009.\n      }\n      UNION\n      {\n        ?item wdt:P31/wdt:P279* wd:Q166118.\n      }\n      ?item wdt:P17/wdt:P279* wd:Q183;\n       wdt:P625 ?coords.\n    }\n  }\n}", "context": "Map of public archives in Germany", "target": ""}
{"query": "#title:location of items with property P11961 statements\n#defaultView:Map{\"hide\":[\"?coordinates\"]}\nSELECT DISTINCT ?site ?siteLabel ?coordinates ?image ?value\nWHERE \n{\n  ?site wdt:P11961 ?value; wdt:P625 ?coordinates.\n  OPTIONAL { ?site wdt:P18 ?image }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Google Scholar Organization IDs to Wikidata", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?geo WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, de\". }\n  {\n    SELECT DISTINCT ?item ?itemLabel ?geo WHERE {\n      ?item wdt:P31/wdt:P279* wd:Q159719;\n            wdt:P625 ?geo\n    }\n  }\n}", "context": "Power stations of this world", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?item2 ?item2Label ?geo ?usage ?usageLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,fr\". }\n  {\n    SELECT DISTINCT ?item ?itemLabel ?geo ?usage ?usageLabel WHERE {\n      OPTIONAL {\n      ?item wdt:P31/wdt:P279* wd:Q134447;\n        wdt:P625 ?geo .\n       } \n    }\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,fr\". }\n  {\n    SELECT DISTINCT ?item2 ?itemLabel2 ?geo ?usage ?usageLabel WHERE {\n      OPTIONAL {\n      ?item2 wdt:P31/wdt:P279* wd:Q134447;\n        wdt:P625 ?geo;\n             wdt:P5817 ?usage .\n       } \n    }\n  }\n}\n", "context": "Usage state of the nuclear power plant", "target": ""}
{"query": "SELECT ?profesi ?profesiLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?profesi wdt:P31 wd:Q28640.\n  ?profesi wdt:P279 wd:Q8246794.\n}\nLIMIT 100", "context": "Subclass of blogger", "target": ""}
{"query": "# taxa with/without an article where author name is Dennis P. Gordon\nSELECT ?item  ?taxon_name ?article ?afd_id WHERE {\n  ?item p:P225 ?stat . \n  ?stat ps:P225 ?taxon_name.\n  ?stat pq:P405 wd:Q19858629.\n  OPTIONAL {?item wdt:P6039 ?afd_id.}\n  OPTIONAL {\n     ?article schema:about ?item. # and have an article about them\n     ?article schema:isPartOf <https://en.wikipedia.org/> . # on *English* Wikipedia}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". } \n    }\n}\nORDER BY ?taxon_name ?item", "context": "List of taxon name as first having been described by Dennis P. Gordon", "target": ""}
{"query": "#defaultView:ImageGrid\nSELECT DISTINCT ?item  ?itemLabel  ?imageLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,fr\". }\n  {\n    SELECT DISTINCT ?item ?image  WHERE {\n      ?item p:P31 ?statement0.\n      ?statement0 (ps:P31/(wdt:P279*)) wd:Q18219090.\n      ?item p:P571 ?statement_1.\n      ?statement_1 psv:P571 ?statementValue_1.\n      ?statementValue_1 wikibase:timePrecision ?precision_1.\n      hint:Prior hint:rangeSafe \"true\"^^xsd:boolean.\n      FILTER(?precision_1 >= 9 )\n      ?statementValue_1 wikibase:timeValue ?P571_1.\n      hint:Prior hint:rangeSafe \"true\"^^xsd:boolean.\n      FILTER(?P571_1 < \"+1600-00-00T00:00:00Z\"^^xsd:dateTime)\n      ?item wdt:P18 ?image.\n    }\n    LIMIT 1000\n  }\n}", "context": "Gallery of wood cuts made before 1600, including file description and the link to the Wikimedia Commons file", "target": ""}
{"query": "#title:Most frequent birth places for PTBNP authors\n#defaultView:Map\nSELECT DISTINCT ?l ?lLabel ?coord ?number ?layer\nWITH\n{\n  SELECT DISTINCT ?l ?lLabel ?coord (COUNT(DISTINCT ?item) AS ?number)\n  WHERE \n  {\n  ?item wdt:P1005 [] ; wdt:P19 ?l . ?l wdt:P625 ?coord .\n  }\n  GROUP BY ?l ?lLabel ?coord ?number\n} AS %what\nWHERE\n{\n  INCLUDE %what\n  BIND( IF(?number < 6, \"1-5\", IF(?number < 21, \"6-20\", IF(?number < 51, \"21-50\",\"51+\"))) AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it\". }\n}\nGROUP BY ?l ?lLabel ?coord ?number ?layer\nORDER BY DESC(?number)", "context": "Most frequent birth places for PTBNP authors", "target": ""}
{"query": "#defaultView:Map\nSELECT ?musée_universitaire ?musée_universitaireLabel ?localisation_administrativeLabel ?coordonnées_géographiques WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?musée_universitaire wdt:P31 wd:Q866133;\n    wdt:P17 wd:Q142.\n  \n  OPTIONAL { ?musée_universitaire wdt:P131 ?localisation_administrative. }\n  OPTIONAL { ?musée_universitaire wdt:P625 ?coordonnées_géographiques. }\n}", "context": "University's museum in France", "target": ""}
{"query": "#Chats\nSELECT ?entity ?entityLabel ?entity_male_label ?entity_female_label\nWHERE \n{\n  ?entity wdt:P2521 ?entity_female_label. \n  \n  OPTIONAL { \n  ?entity wdt:P3321 ?entity_male_label.\n  FILTER(LANG(?entity_male_label) = \"fr\").\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". } \n  FILTER(LANG(?entity_female_label) = \"fr\").\n}", "context": "52,153 French words which have both masculine and feminine forms", "target": ""}
{"query": "\nSELECT DISTINCT ?item ?itemLabel ?accusedurl ?chargeLabel ?residenceLabel ?coords ?image (?Appears as ?layer) \nWHERE \n{\n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P4478 ?accused .\n  wd:P4478 wdt:P1630 ?formatterurl .\n   BIND(IRI(REPLACE(?accused, '^(.+)$', ?formatterurl)) AS ?accusedurl).\n\n  ?item wdt:P1595 wd:Q30172593   . \n  ?item wdt:P551 ?residence .\n  ?residence wdt:P625 ?coords.\n   {?item p:P1595 ?ChargeStatement . \n              ?ChargeStatement ps:P1595 ?charge ; \n                 pq:P4675 ?appears .\n            OPTIONAL { ?appears wdt:P18 ?image }  #end time qualifier value .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".  \n                          ?appears rdfs:label ?Appears .\n                          ?charge rdfs:label ?chargeLabel .\n                          ?residence rdfs:label ?residenceLabel .\n                          ?item rdfs:label ?itemLabel .\n                          ?accused rdfs:label ?accusedLabel .}", "context": "Accused witches in Scotland accused of shapeshifting", "target": ""}
{"query": "SELECT ?jugador ?jugadorLabel ?num_partidos ?fecha_nacimiento WHERE {\n  ?jugador wdt:P641 wd:Q2736;               # Jugadores de fútbol\n          p:P54 ?membershipStatement.      # Statement de membresía a algún equipo\n\n  ?membershipStatement ps:P54 wd:Q79800.   # Miembros de la selección argentina de fútbol\n\n  OPTIONAL {\n    ?membershipStatement pq:P1350 ?num_partidos.  # Número de partidos jugados para la selección\n  }\n\n  OPTIONAL {\n    ?jugador wdt:P569 ?fecha_nacimiento.    # Fecha de nacimiento\n  }\n\n  # Filtro para jugadores con menos de 3 partidos, excluir valores NULL y jugadores nacidos después de 1969\n  FILTER(?num_partidos < 4 && BOUND(?num_partidos))\n\n  SERVICE wikibase:label { \n    bd:serviceParam wikibase:language \"es\". \n    ?jugador rdfs:label ?jugadorLabel.\n  }\n}\nLIMIT 100\n\n", "context": "Argentinian national team football players that participating in 3 matches or bellow", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?itemDescription ?status WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  VALUES ?occupation { wd:Q10732476 wd:Q173950 wd:Q2146005 wd:Q187668 wd:Q328376 wd:Q664017}\n  ?item p:P106 ?statement.\n  ?statement ps:P106 ?occupation.\n  OPTIONAL {\n    ?item wdt:P1840 wd:Q30335959.\n    BIND(\"ALIU Red Flag\" AS ?ALIUredFLAG)\n  }\n  BIND(\n    IF(BOUND(?ALIUredFLAG),\n       IF(EXISTS{?item wdt:P102 wd:Q7320}, CONCAT(\"ALIU Red Flag\", \" ,NSDAP\"), \"ALIU Red Flag\"),\n       IF(EXISTS{?item p:P2868 ?statement2. ?statement2 ps:P2868 wd:Q5883980}, \n          IF(EXISTS{?item ?p wd:Q104820943}, CONCAT(\"Holocaust victim\", \", Jewish collectors and art dealers victim of Nazi persecution\"), \"Holocaust victim\"),\n          IF(EXISTS{?item wdt:P102 wd:Q7320}, \"Member of NSDAP\",\n             IF(EXISTS{?item ?p wd:Q104820943}, \"Jewish collectors and art dealers victim of Nazi persecution\",\n                \"Unknown\")\n          )\n       )\n    ) AS ?status\n  )\n}\n", "context": "List of people who are in Art Looting Investigation Unit red flag list", "target": ""}
{"query": "#Main Subject bubble chart of Articles in Journals with AJOL ID, published in Ghana, about diseases\n#defaultView:BubbleChart\n#Author: Silvia Gutiérrez (@espejolento)\nSELECT ?mainsubject ?mainsubjectLabel (COUNT(?mainsubject) AS ?count)\nWHERE {\n  ?article wdt:P31 wd:Q13442814. #scholarly articles\n  ?article wdt:P1433 ?item. #published in journal\n  ?item wdt:P6183 ?id . #with AJOL ID\n  ?item wdt:P495 wd:Q117. #country of journal = Ghana\n  ?article wdt:P921 ?mainsubject.\n  ?mainsubject wdt:P279 wd:Q12136. #Main Subject is a subclass of a disease\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n}\nGROUP BY ?mainsubject ?mainsubjectLabel\nORDER BY DESC (?count)\n", "context": "Main Subject bubble chart of Articles in Journals with AJOL ID, published in Ghana, about diseases", "target": ""}
{"query": "SELECT ?phare ?phareLabel ?coordonnées_géographiques WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr\". }\n  ?phare wdt:P31 wd:Q39715;\n    wdt:P17 wd:Q142.\n  OPTIONAL { ?phare wdt:P625 ?coordonnées_géographiques. }\n}\nLIMIT 500", "context": "List of lighthouses in France", "target": ""}
{"query": "# Histogram of most common journals that have articles about ISS experiment results?\n#defaultView:BarChart\nSELECT ?q ?qLabel (COUNT(?q) as ?count) WHERE {\n  ?item wdt:P31 wd:Q13442814 . \n  ?item wdt:P4510 ?thing .\n  ?thing wdt:P31 wd:Q108572660 .\n  ?item wdt:P1433 ?q .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} \nGROUP BY ?q ?qLabel\nORDER BY DESC (?count)\nLIMIT 50\n", "context": "Which academic journals publish the most International Space Station Research?", "target": ""}
{"query": "# tamil lexemes missing item for this sense statements\nselect ?item ?lemma ?sense ?sensedef_en ?sensedef_ta\nwhere {\n  ?item dct:language wd:Q5885 .\n  ?item wikibase:lemma ?lemma . \n  ?item ontolex:sense ?sense .\n  ?sense skos:definition ?sensedef_en . filter(lang(?sensedef_en)=\"en\")\n  ?sense skos:definition ?sensedef_ta . filter(lang(?sensedef_ta)=\"ta\")\n  filter not exists { ?sense wdt:P5137 [] . }\n}\n", "context": "Tamil Lexemes missing Item for this sense statements", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?lastname ?lastnameLabel with {\n  SELECT (COUNT(*) AS ?count) ?lastname WHERE {\n    ?person wdt:P27 wd:Q20 . hint:Prior hint:runFirst true.\n    ?person wdt:P734 ?lastname . \n  } GROUP BY ?lastname HAVING (?count>99) } as %i\n\nWITH { SELECT ?lastname ?lastnameLabel WHERE\n{\n  INCLUDE %i\n  ?lastname rdfs:label ?lastnameLabel. filter(lang(?lastnameLabel)=\"nn\") \n} } as %j\n\nWHERE\n{\n  INCLUDE %j\n          \n  hint:Query hint:optimizer \"None\".\n  SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:api \"Search\";\n                    wikibase:endpoint \"www.wikidata.org\";\n                    mwapi:srsearch ?lastnameLabel.\n    ?item wikibase:apiOutputItem mwapi:title .\n  }\n  \n  ?item wdt:P31 wd:Q5.\n  FILTER NOT EXISTS { ?item wdt:P734 []. }\n  FILTER NOT EXISTS { ?item wdt:P27 []. }\n  ?item rdfs:label ?itemLabel .  filter(lang(?itemLabel)=\"en\") filter(strends(str(?itemLabel),?lastnameLabel))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],nn,en\". }\n} ORDER BY ?lastnameLabel ?itemLabel", "context": "People with no country of citizenship having Norwegian names", "target": ""}
{"query": "#defaultView:Map\nSELECT ?cross ?crossLabel ?jaiolekua ?jaiolekuaLabel ?koord WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  { ?cross wdt:P166 wd:Q165558. }\n  UNION\n  { ?cross wdt:P166 wd:Q3003469. }\n  UNION\n  { ?cross wdt:P166 wd:Q11371757. }\n  UNION\n  { ?cross wdt:P166 wd:Q11353044. }\n  ?cross wdt:P19 ?jaiolekua.\n  ?jaiolekua wdt:P625 ?koord.\n  ?cross wdt:P607 wd:Q362.\n}", "context": "Birthplace of recipients of the Nazi Germany Iron Cross", "target": ""}
{"query": "#defaultView:Map{ \"markercluster\": {\"maxClusterRadius\": 70}}\n#title: Swedish PM and have a coordinate on a grave\nselect distinct  ?article ?person ?personLabel  ?personDescription  ?graveplot  ?pic ?coord ?picGrave  \n?namedAs ?ref ?page {\n{\n    ?svCemetery wdt:P31 wd:Q39614.\n  VALUES ?member {\n    wd:Q33071890\n    wd:Q81531912\n    wd:Q82697153\n    wd:Q10655178\n  }\n  ?person wdt:P39 ?member.\n\n   ?person wdt:P119 ?svCemetery; #Place of burial\n            p:P119 [ \n              pq:P625 ?coord;   \n              pqv:P625 [\n                wikibase:geoLatitude ?lat;\n                wikibase:geoLongitude ?lon\n              ]\n            ].     \n\n   OPTIONAL {?person wdt:P569 ?birthdate} \n   OPTIONAL{ ?person wdt:P18 ?pic .} \t\t\t\t\t\n   OPTIONAL{ ?person wdt:P1442 ?picGrave .} \n   OPTIONAL {\n      ?article schema:about ?person .\n      ?article schema:inLanguage \"sv\" .\n      ?article schema:isPartOf <https://sv.wikipedia.org/> .\n    }\n}   \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"sv\",\"en\"}\n }\nOrder by ?personLabel", "context": "Burial place of Swedish Prime Ministers", "target": ""}
{"query": "#defaultView:ImageGrid\nselect ?s ?altura ?nombre ?image where \n{ ?s ?p ?catedral .\n VALUES ?catedral {  wd:Q56242215 wd:Q56242235 } \n                                  \n               ?s rdfs:label ?nombre .\n                optional {?s wdt:P2048 ?altura .}\n            optional {  ?s wdt:P18 ?image .}\n           FILTER ( lang(?nombre) = \"es\" )}\norder by desc (?altura)\n", "context": "List of cathedrals around the world with its height", "target": ""}
{"query": "select distinct ?item ?itemLabel ?pocet_obyvatel where {\n\n  ?item wdt:P31 wd:Q5153359 .\n  ?item wdt:P1082 ?pocet_obyvatel filter(?pocet_obyvatel>25000). \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs,en\". }\n  minus {?item wdt:P206 ?vodni_tok .} \n  \n} order by desc(?pocet_obyvatel) limit 5 ##", "context": "Population of Check villages with rivers flowing through them", "target": ""}
{"query": "SELECT distinct ?item ?itemLabel WHERE {\n  ?item wdt:P31 wd:Q161705 .\n  ?item wdt:P137 ?operator1.\n  ?item wdt:P137 ?operator2.\n  filter (str(?operator1) != str(?operator2) )\n  filter not exists {?item p:P1448/pq:P137 ?operator1 . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }  \n} order by ?itemLabel", "context": "Ships with multiple operators that do not have multiple official names", "target": ""}
{"query": "#title:Map of the narrative locations of romantic comedies\n#defaultView:Map{\"hide\":\"?coords\"}\nSELECT DISTINCT ?item ?itemLabel ?placeLabel ?coords (YEAR(?date) AS ?year) ?url WHERE {\n  ?item wdt:P31/wdt:P279? wd:Q11424.\n  ?item wdt:P136 wd:Q860626.\n  ?item wdt:P840 ?place.\n  ?place wdt:P625 ?coords.\n  OPTIONAL {?item wdt:P577 ?date}\n  OPTIONAL {?item wdt:P856 ?url}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of narrative locations of romantic comedies", "target": ""}
{"query": "#defaultView:ImageGrid\nselect ?s ?altura ?nombre ?image where \n{ ?s ?p ?catedral .\n VALUES ?catedral {  wd:Q56242215 wd:Q56242235 } \n                                  \n               ?s rdfs:label ?nombre .\n                optional {?s wdt:P2048 ?altura .}\n            optional {  ?s wdt:P18 ?image .}\n           FILTER ( lang(?nombre) = \"es\" )}\norder by desc (?altura)\n", "context": "Which cathedrals are the tallest in the world?", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?person ?personLabel ?familynameLabel WHERE {\n  \n  ?item wdt:P5008 wd:Q117322976;     # item on focus list of the LSESuffrageInterviewsProject\n    wdt:P31 wd:Q178651 .             # item is an interview\n   ?item p:P710 ?statement .         # item has a participant statement\n   ?statement ps:P710 ?person .      # participant is ?person\n   ?statement pq:P3831 wd:Q55534929. # qualifier object has role is interviewee\n   ?item wdt:P921 ?person  .         # main subject is same as participant\n   OPTIONAL { ?person wdt:P734 ?familyname }\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY (?familynameLabel)", "context": "Interviewees who were part of the Suffragette Movement", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?person ?personLabel ?familynameLabel ?subjectLabel WHERE {\n  \n  ?item wdt:P5008 wd:Q117322976;     # item on focus list of the LSESuffrageInterviewsProject\n    wdt:P31 wd:Q178651 .             # item is an interview\n   ?item p:P710 ?statement .         # item has a participant statement\n   ?statement ps:P710 ?person .      # participant is ?person\n   ?statement pq:P3831 wd:Q55534929. # qualifier object has role is interviewee\n   filter not exists {?item wdt:P921 ?person .}        # main subject is not same as participant\n   ?item wdt:P921 ?subject .         # main subject is ...\n   ?subject wdt:P31 wd:Q5 .          # ...a person\n   OPTIONAL { ?person wdt:P734 ?familyname }\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY (?familynameLabel)", "context": "Interviewees who had a relative or friend in the Suffragette Movement", "target": ""}
{"query": "#A map of every recorded battle the past 4,500 years (Wikipedia mentions)\n#defaultView:Map\nSELECT DISTINCT * WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q178561;\n        wdt:P625 ?geo .\n}", "context": "Map of every recorded battle the past 4,500 years", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?image ?coords ?locationLabel\nWHERE \n{\n  ?item wdt:P361 wd:Q123249004 .\n  OPTIONAL { ?item wdt:P131 ?location .}\n  OPTIONAL { ?item wdt:P625 ?coords . }\n  OPTIONAL {\n  ?item wdt:P18 ?image .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } # Helps get the label in your language, if not, then en language\n}", "context": "Map of locations of witch memorials and sites of interest in Scotland", "target": ""}
{"query": "SELECT ?item ?itemLabel ?sitelink ?article\nWHERE \n{\n  hint:Query hint:optimizer \"None\".\n  SERVICE wikibase:mwapi {\n     bd:serviceParam wikibase:endpoint \"en.wikipedia.org\";\n                     wikibase:api \"Generator\";\n                     mwapi:generator \"categorymembers\";\n                     mwapi:gcmtitle \"Category:Water pollution\" ;\n                     mwapi:gcmprop \"ids|title|type\";\n                     mwapi:gcmlimit \"max\".\n     # out\n     ?name wikibase:apiOutput mwapi:title.        # en-wikipedia article / category name\n     ?item wikibase:apiOutputItem mwapi:item.            # wikidata QId for the person's item\n    }\n\n  ?article schema:about ?item ;\n  schema:isPartOf <https://en.wikipedia.org/> ; \n  schema:name ?sitelink .\n  \n  filter not exists {?article2 schema:about ?item ;\n  schema:isPartOf <https://es.wikipedia.org/> .}\n  \n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Articles on English Wikipedia tagged 'Water pollution' with no equivalent article in Spanish", "target": ""}
{"query": "SELECT ?point ?pointLabel ?height ?countyLabel WITH  {\n  SELECT ?county (MAX(?height_) AS ?height) WHERE {\n    ?point p:P2044 ?stat . \n    ?stat psn:P2044/wikibase:quantityAmount ?height_.\n    ?stat a wikibase:BestRank.\n    ?point wdt:P131 ?county.\n    ?county wdt:P17 wd:Q33.\n  } GROUP BY ?county } as %i\nWHERE\n{\n  INCLUDE %i\n  ?point p:P2044 ?stat2 . \n  ?stat2 psn:P2044/wikibase:quantityAmount ?height.\n  ?stat2 a wikibase:BestRank.\n  ?point wdt:P131 ?county.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fi,en\". }\n}", "context": "Highest point (in meters) per counties in Finland", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 46, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Week 47, 2023: Top album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?witch ?ItemLabel ?Residence ?coordinate (?Gender as ?layer) ?rgb\nWHERE {\n  ?item wdt:P4478 ?witch;\n    wdt:P551 ?residence.\n  ?residence wdt:P625 ?coordinate.\n  OPTIONAL \n  {\n    ?item wdt:P21 ?gender .\n  }\n\n  BIND(IF(!BOUND(?gender),\"00FF00\",IF(CONTAINS(STR(?gender),\"Q6581097\"),\"0000FF\",\"FF0000\")) as ?rgb)\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?item rdfs:label ?ItemLabel.\n    ?residence rdfs:label ?Residence .\n    ?gender rdfs:label ?Gender.\n  }\n}", "context": "Gender representation of Scottish witches", "target": ""}
{"query": "# Popular names among Ghanaians\n#defaultView:BubbleChart\nSELECT ?cid ?firstname (COUNT(*) AS ?count) WHERE {\n  ?pid wdt:P27 wd:Q117;\n    wdt:P735 ?cid.\n  OPTIONAL {\n    ?cid rdfs:label ?firstname.\n    FILTER((LANG(?firstname)) = \"en\")\n  }\n}\nGROUP BY ?cid ?firstname\nORDER BY DESC (?count) (?firstname)\nLIMIT 50", "context": "Most popular names in Ghana among Ghanaians on Wikidata", "target": ""}
{"query": "SELECT ?person ?personLabel ?dob ?dod ?place ?placeLabel ?country ?countryLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?person wdt:P19 ?place .\n  ?place wdt:P138 ?person .\n  optional { ?person wdt:P569 ?dob }\n  optional { ?person wdt:P570 ?dod }\n  optional { ?place wdt:P17 ?country }\n}", "context": "People born somewhere named after them", "target": ""}
{"query": "SELECT ?person ?personLabel ?dob ?dod ?item ?itemLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P31 wd:Q26817508 .\n  ?item wdt:P138 ?person .\n  ?person wdt:P106 wd:Q28389 .\n  optional { ?person wdt:P569 ?dob }\n  optional { ?person wdt:P570 ?dod }\n}", "context": "Rose cultivars named after screen writers", "target": ""}
{"query": "#defaultView:Map\n\nselect ?item ?Label ?img \n(group_concat(distinct ?natLabel;separator=\", \") as ?nature) \n(sample(?coord) as ?coordonées )\n(?nature as ?layer)\nwith {\n\n    select distinct ?nom {\n      ?reference wdt:P138/wdt:P279* wd:Q16970 .\n      ?reference rdfs:label ?nom .\n      minus { ?reference wdt:P31  wd:Q4167410 } #exclusion des pages d’homonymies\n    }\n}  as %noms\n{\n  include %noms .\n#      ?reference wdt:P138/wdt:P31?/wdt:P279* wd:Q16970 .\n#      ?reference rdfs:label ?nom .\n  \n  ?item rdfs:label ?nom .\n\n  \n  optional {\n    ?item wdt:P31 ?nat \n  }    \n  optional {\n    ?item wdt:P625 ?coord \n  } \n  optional {\n    ?item wdt:image ?img .\n  }\n  SERVICE wikibase:label { \n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n    ?item rdfs:label ?Label .\n    ?nat rdfs:label ?natLabel\n  }\n} group by ?item ?Label ?img", "context": "Geolocalized object that have the same name than something named after some some kind of church building", "target": ""}
{"query": "#title:Timeline of games per platform\n#defaultView:AreaChart\nSELECT (SAMPLE(?year) AS ?year) (COUNT(?_platform) AS ?count) (SAMPLE(?_platformLabel) AS ?platformLabel)  WHERE {\n  ?video_game wdt:P31 wd:Q7889.\n  ?video_game wdt:P400 ?_platform.\n  ?video_game wdt:P577 ?date.\n  BIND ( STR(year(?date)) as ?year )\n  FILTER(BOUND(?year))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                          ?_platform rdfs:label ?_platformLabel.}\n}\nGROUP BY ?year ?_platform\nHAVING (?count > 100)", "context": "Video games per Platform", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?gender ?movie ?gerard WHERE {\n  ?item (wdt:P106/(wdt:P279*)) wd:Q3455803;\n    wdt:P27 wd:Q142;\n    wdt:P21 ?gender;\n    wdt:P569 ?birthdate;\n    rdfs:label ?itemLabel.\n  FILTER((YEAR(?birthdate)) > 1930 )\n  FILTER((LANG(?itemLabel)) = \"[AUTO_LANGUAGE]\")\n  ?movie wdt:P57 ?item;\n    wdt:P161 ?distribution.\n  BIND(wd:Q106508 IN(?distribution) AS ?gerard)\n}", "context": "List of movies directed by French filmmakers born after 1930 with or without Depardieu", "target": ""}
{"query": "#defaultView:Map\nselect ?item ?itemLabel ?coord where {\n\n  ?item wdt:P31/wdt:P279* wd:Q11587139 .\n  ?item wdt:P17 wd:Q213 .\n  ?item wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"cs,en\". }\n}", "context": "Czech buildings carved into the rock", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?stadLabel ?itemLabel  ?spoor ?layer ?geo ?article WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q15640053 .\n  ?item wdt:P17 wd:Q183 .\n  ?item wdt:P131 ?stad.\n  ?stad wdt:P625 ?geo.\n  ?item wdt:P1064 ?spoorbreedte.\n  ?spoorbreedte wdt:P2049 ?spoor.\n  FILTER NOT EXISTS { ?item wdt:P3999 [] } .\n  FILTER NOT EXISTS { ?item wdt:P5817 wd:Q11639308 }\n  BIND(\n    IF(?spoor < 1435, \"smalspoor\",\n    IF(?spoor = 1435, \"normaalspoor\",\n    \"breedspoor\"))\n    AS ?layer).\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"de\" .\n  }\n  OPTIONAL {\n      ?article schema:about ?item .\n      ?article schema:inLanguage \"de\" .\n      FILTER (SUBSTR(str(?article), 1, 25) = \"https://de.wikipedia.org/\")\n  }\n}", "context": "Distribution of the different tram gauges in Germany", "target": ""}
{"query": "#title:Stampedes repertoried on Wikidata\n#Built on August 2022 by Capucine-Marin Dubroca-Voisin (work in progress)\n#defaultView:Map{\"hide\": \"?coords\"}\n#view:Table{\"hide\": \"?coords\"}\n#view:Timeline{\"hide\": \"?coords\"}\nSELECT DISTINCT ?layer ?coords ?coordsp (CONCAT(STR(?numberOfInjuries), \" injured\") AS ?injured) (CONCAT(STR(?numberOfDeaths), \" dead\") AS ?dead) ?placePicture ?placeLabel ?countryLabel ?date ?item ?itemLabel WHERE {\n  VALUES ?crushtype {wd:Q109905701 wd:Q2165983 wd:Q106673346}\n  ?item wdt:P31 ?crushtype.\n  OPTIONAL {\n    ?item wdt:P585 ?date;\n      wdt:P1120 ?numberOfDeaths;\n      wdt:P1339 ?numberOfInjuries;\n      wdt:P17 ?country.\n  }\n  OPTIONAL {\n    ?item wdt:P625 ?coords.\n  }\n  OPTIONAL {\n    ?item wdt:P276 ?place.\n    ?place wdt:P625 ?coordsp;\n     wdt:P18 ?placePicture.\n  }\n  BIND(IF(?numberOfDeaths > 1000 , \"More than 1000 deaths\", IF(?numberOfDeaths > 100 , \"More than 100 deaths\", IF(?numberOfDeaths > 10 , \"More than 10 deaths\", \"Less than 10 deaths\"))) AS ?layer)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY DESC (?numberOfDeaths)", "context": "Scale of stampedes reported on Wikidata", "target": ""}
{"query": "#Personas artistas visuales extranjeras que entraron en Dominio Público en México el 1 de enero de 2024.\nSELECT DISTINCT ?item ?itemLabel ?sitelinks ?when (YEAR(?when) as ?date)\nWHERE {\n  ?item wdt:P106/wdt:P279* wd:Q3391743 . #Personas artistas visuales\n  ?item wdt:P570 ?when . \n  ?item wikibase:sitelinks ?sitelinks.\nFILTER(YEAR(?when) = 1923)\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n}\nORDER BY desc(?sitelinks)", "context": "Visual artists whose works will enter the Public Domain in Mexico on January 1, 2024", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "Top music album languages found on Wikidata right now", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?composer ?composerLabel ?dateOD ?placeODLabel (SAMPLE(?img) AS ?image) (COUNT(?work) as ?CW)\nWHERE {\n  ?work wdt:P86 ?composer .                 # wdt:P86 composer\n  ?composer wdt:P570 ?dateOD ;              # wdt:P570 date of death (Monteverdi died in 1643)\n            wdt:P20 ?placeOD                # wdt:P20 place of death\n            FILTER(YEAR(?dateOD) > 1000 \n                   && YEAR(?dateOD) < 1644) . \n  ?placeOD wdt:P17 ?country .\n  # positive list is ugly, I don't know better yet.\n  VALUES ?countries {\n      wd:Q28 wd:Q29 wd:Q31 wd:Q34 wd:Q36 wd:Q38 wd:Q39 wd:Q40 wd:Q41 wd:Q45 wd:Q55 wd:Q142 wd:Q145 wd:Q183 wd:Q212 wd:Q213 wd:Q219 wd:Q38872\n      #  Mexico https://de.wikipedia.org/wiki/Gaspar_Fernandes, currently only one of his works is registered, so having CW>1 will loose him\n      wd:Q96 } .\n  FILTER(?country IN (?countries)) .\n  OPTIONAL { ?composer wdt:P18 ?img . }     # wdt:P18 image\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?composer ?composerLabel ?dateOD ?placeODLabel\nHAVING (?CW >= 2)\nLIMIT 180", "context": "Western composers until the end of the Renaissance", "target": ""}
{"query": "#title: Average age of people supporting Depardieu \nSELECT (AVG(?age) AS ?agemean) WHERE {\n  wd:Q124005357 wdt:P577 ?publication;\n    wdt:P1891 ?signataire.\n  ?signataire wdt:P569 ?naissance.\n  BIND((YEAR(?publication)) - (YEAR(?naissance)) AS ?age)\n}", "context": "Average age of supporters of Gérard Depardieu", "target": ""}
{"query": "#title: Map of running/out of service railway stations in France ; Carte des gares ferroviaires en France métropolitaine selon leur état d'usage\n#defaultView:Map\nSELECT ?gare ?gareLabel ?layer ?layerLabel ?rgb ?coords WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n   VALUES ?gares {wd:Q55488 wd:Q55678 wd:Q1793804 wd:Q4663385}\n  ?gare wdt:P31 ?gares;\n    wdt:P17 wd:Q142;wdt:P625 ?coords.\n  ?gare p:P625 [ psv:P625 [ wikibase:geoLatitude ?latitude; wikibase:geoLongitude ?longitude ] ].\n filter( ?latitude>0).\n  OPTIONAL {\n    ?gare wdt:P5817 ?layer.\n    ?layer wdt:P465 ?rgb.\n  }\n}", "context": "", "target": ""}
{"query": "#defaultView:Map\nSELECT distinct ?item ?itemLabel ?img ?coord\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q12280.\n  ?item wdt:P18 ?img.\n  ?item wdt:P17 wd:Q145.\n  ?item wdt:P625 ?coord. \n  {?item wdt:P131/wdt:P131* wd:Q21 . hint:Prior hint:gearing \"forward\".} UNION\n  {?item wdt:P131/wdt:P131* wd:Q22 . hint:Prior hint:gearing \"forward\".} UNION\n  {?item wdt:P131/wdt:P131* wd:Q25 . hint:Prior hint:gearing \"forward\".} UNION\n  {?item wdt:P131/wdt:P131* wd:Q26 . hint:Prior hint:gearing \"forward\".}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n  \n}", "context": "Bridges in the UK with photos", "target": ""}
{"query": "#title:programming languages influenced by Niklaus Wirth (directly or indirectly)\nSELECT ?programmingLanguage ?programmingLanguageLabel ?programmingLanguageDescription WITH {\n  SELECT DISTINCT * WHERE {\n    ?programmingLanguage wdt:P31/wdt:P279* wd:Q9143;\n                         (wdt:P737|wdt:P144|wdt:P287)+ wd:Q92604.\n  }\n} AS %results WHERE {\n  INCLUDE %results.\n  ?programmingLanguage wikibase:sitelinks ?sitelinks.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?sitelinks) # rough approximation of popularity", "context": "Programming languages influenced by Niklaus Wirth (directly or indirectly)", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?sitelinks\nWHERE \n{\n  { {?item wdt:P276/wdt:P276* wd:Q84 .}\n  UNION\n  {?item wdt:P131/wdt:P131* wd:Q23306 . } }  hint:Prior hint:runFirst true.\n\n  ?item wdt:P31/wdt:P279* wd:Q1190554. hint:Prior hint:gearing \"forward\".\n  \n  ?item wikibase:sitelinks ?sitelinks .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n} order by desc(?sitelinks)", "context": "Historical events by a city sorted by importance", "target": ""}
{"query": "#Bubble chart showing number of out of school children per country in 2013\n#defaultView:BubbleChart\nSELECT DISTINCT ?country ?countryLabel ?number_of_out_of_school_chidren ?date ?methodLabel\nWHERE {\n  ?country wdt:P31 wd:Q6256 .\n  ?country p:P2573 ?out_of_school_chidren .\n  ?out_of_school_chidren ps:P2573 ?number_of_out_of_school_chidren .\n  ?out_of_school_chidren pq:P585 ?date .\n  ?out_of_school_chidren pq:P459 ?method .\n  \n  FILTER (year(?date) = 2013)\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  \n} \n\nORDER BY DESC (?number_of_out_of_school_chidren)", "context": "Bubble chart showing countries with the highest number of children out of school in 2013", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?author ?authorLabel ?image  ?date  WHERE {\n  ?author wdt:P279+|wdt:P31/wdt:P279* wd:Q8192 .\n  ?author wdt:P571 ?date .\n  OPTIONAL {?author wdt:P18 ?image . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en, it, de, es, fr\" }\n}", "context": "Timeline of writing systems and alphabets", "target": ""}
{"query": "#defaultView:Map\nSELECT distinct ?item ?itemLabel ?pobLabel (ROUND(?dist) as ?distance) ?coord ?coord2 ?line\nWHERE \n{\n  ?item wdt:P39 ?position.\n  wd:Q112014 wdt:P2670 ?position.\n  ?item p:P39 ?stat . \n  ?stat ps:P39 ?position . \n  filter not exists {?stat pq:P582 [] .}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } \n  ?item wdt:P39 wd:Q77685926 .\n  ?item wdt:P19 ?pob .\n  ?pob wdt:P625 ?coord . \n  wd:Q62408 wdt:P625 ?coord2 .\n  BIND(geof:distance(?coord, ?coord2) as ?dist) \n  \n  ?pob p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].\n  wd:Q62408 p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].\n  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .\n  BIND(STRDT(?str, geo:wktLiteral) AS ?line)  \n} order by ?distance ", "context": "Distance between the birthplace of UK ministers and London", "target": ""}
{"query": "#title: Smallest mountains in France\nSELECT DISTINCT ?item ?itemLabel ?elevation ?unit ?unitLabel ?conversion ?elevation_in_m WHERE {\n  ?item (wdt:P31/(wdt:P279*)) wd:Q8502;\n    wdt:P17 wd:Q142;\n    p:P2044 ?elevation_statement.\n  ?elevation_statement psv:P2044 ?elevation_value.\n  ?elevation_value wikibase:quantityAmount ?elevation;\n    wikibase:quantityUnit ?unit.\n  ?unit p:P2370 ?unit_statement.\n  ?unit_statement psv:P2370 ?unit_value.\n  ?unit_value wikibase:quantityAmount ?conversion;\n    wikibase:quantityUnit wd:Q11573.\n  BIND(?elevation * ?conversion AS ?elevation_in_m)\n  FILTER(?elevation_in_m >= 0 )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en\". }\n}\nORDER BY (?elevation_in_m)\nLIMIT 100", "context": "The 100 smallest mountains in France", "target": ""}
{"query": "SELECT ?item ?itemLabel ?start ?end ?age WHERE {\n  ?item p:P39 ?statement.\n  ?statement ps:P39 wd:Q1587677;\n    pq:P580 ?start.\n  ?item wdt:P569 ?birthdate. \n  OPTIONAL { ?statement pq:P582 ?end. }\n  BIND(ROUND((?start - ?birthdate )/365.25) AS ?age)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ?age", "context": "French Prime ministers ordered by age when they start in office", "target": ""}
{"query": "SELECT ?project ?projectLabel (count (?item) as ?items) WITH {\n  SELECT ?item (count(?f) as ?count) WHERE \n  {\n  ?item wdt:P5008 ?f . hint:Prior hint:runFirst true.\n  ?item wdt:P5008 wd:Q111645234 .  \n  ?item wdt:P31 wd:Q5 .\n  } group by ?item ?itemLabel having (?count>1) } as %i\nWHERE\n{\n  INCLUDE %i\n  ?item wdt:P5008 ?project .\n  filter (?project not in (wd:Q111645234 ))\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". \n                           ?item rdfs:label ?itemLabel .\n                           ?item schema:description ?itemDescription .\n                           ?project rdfs:label ?projectLabel . } \n} group by ?project ?projectLabel order by desc(?items)", "context": "Projects with which New Zealand Thesis people overlap", "target": ""}
{"query": "SELECT ?wikipediaLabel ?filmcount WHERE {\n  ?wikipedia wdt:P31 wd:Q10876391 .\n  ?wikipedia wdt:P424 ?sprachcode .\n  ?wikipedia rdfs:label ?wikipediaLabel \n  FILTER(lang(?wikipediaLabel) = \"en\") \n  { SELECT ?sprachcode (COUNT(DISTINCT ?film) as ?filmcount) WHERE {\n    \t?film wdt:P31/wdt:P279* wd:Q11424 .\n    \t?article schema:about ?film . \n    \t?article schema:inLanguage ?sprachcode\n    } GROUP BY ?sprachcode \n  }                                       \n} ORDER BY DESC (?filmcount) LIMIT 10", "context": "Most articles about films in each Wikipedia", "target": ""}
{"query": "#defaultView:Map\n#title: Carte des lieux baignés par\nSELECT ?item ?itemLabel ?coord\nWHERE {\n?item wdt:P206 wd:Q1471;\n      wdt:P625 ?coord;\n      rdfs:label ?itemLabel . \n  FILTER(LANG(?itemLabel) = \"fr\") \n\n} ", "context": "Map of items next to the Seine", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT DISTINCT ?address ?followers WHERE {\n ?item p:P8687 ?s .\n  ?s pq:P4033 ?address;\n     pq:P585 ?poit ;\n     ps:P8687 ?followers .\n FILTER NOT EXISTS { # get only most recent date\n   ?item p:P8687/pq:P585 ?date_ .\n    FILTER (?date_ > ?poit)\n}\n  } ORDER BY DESC(?followers) LIMIT 300\n", "context": "300 most followed Mastodon accounts", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT \n  ?loc ?item ?itemLabel ?hosp ?hospLabel ?hbeds ?himg\n  WHERE {  \n     SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  {?item wdt:P31 wd:Q83425186.}\n  ?item wdt:P355 ?hosp. \n  ?hosp wdt:P6801 ?hbeds.\n  OPTIONAL {?hosp wdt:P625 ?loc}.\n  optional {?hosp wdt:P18 ?himg.}.\n} ", "context": "Hospitals across Scotland", "target": ""}
{"query": "#People affiliated with the University of Leeds\n#title: People affiliated with the University of Leeds\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?sitelinks ?article \nWHERE {\n    ?item wdt:P69 wd:Q503424; # Employer is University of Leeds  \n\n          wikibase:sitelinks ?sitelinks.\n  ?article schema:about ?item ;\n  schema:isPartOf <https://en.wikipedia.org/> .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY DESC(?sitelinks)", "context": "People educated at the University of Leeds with a Wikipedia article", "target": ""}
{"query": "SELECT ?property ?propertyLabel ?count ?report WITH { \n  SELECT ?property (count(distinct ?item) as ?count)  WHERE {\n    {?item wdt:P31/wdt:P279* wd:Q23397 .}     # The class & subclasses \n    hint:Prior hint:runFirst true .\n    ?item wdt:P17 wd:Q145 .                   # in UK\n    ?item ?predicate ?value .\n    ?property wikibase:directClaim ?predicate .\n  } group by ?property  } as %i\nWHERE\n{\n  INCLUDE %i\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\nBIND(IRI(CONCAT(\"https://query.wikidata.org/#SELECT%20%3Fitem%20%3FitemLabel%20%3Fvalue%20%3FvalueLabel%20WHERE%20%7B%0A%20%20%20%20%3Fitem%20wdt%3AP31%20wd%3AQ23397%20.%20%0A%20%20%20%20%3Fitem%20wdt%3AP17%20wd%3AQ145%20.%20%0A%20%20%20%20%3Fitem%20wdt%3A\",STRAFTER(STR(?property),\"http://www.wikidata.org/entity/\"),\"%20%3Fvalue.%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22en%22%20.%20%7D%0A%7D%0A\")) as ?report)\n  \n} order by desc(?count)", "context": "Properties for UK lakes", "target": ""}
{"query": "#title: 10 most famous places in New Dehli (measured by sitelinks)\n#defaultView:Map\nSELECT ?place ?placeLabel ?sitelinks ?coord ?article (SAMPLE(?image) as ?sample_image) \nWHERE\n{\n  ?place wdt:P131 wd:Q1353 . \n  ?place wikibase:sitelinks ?sitelinks . \n  ?place wdt:P18 ?image . \n  ?place wdt:P625 ?coord . \n  ?article schema:about ?place .\n  ?article schema:inLanguage \"en\" .\n  ?article schema:isPartOf <https://en.wikipedia.org/> .\n\n  # The bit below is needed to exclude the Aravalli Range \n  wd:Q1353 wdt:P625 ?loc .\n  SERVICE wikibase:around {\n      ?place wdt:P625 ?location .\n      bd:serviceParam wikibase:center ?loc .\n      bd:serviceParam wikibase:radius \"50\" .\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?place ?placeLabel ?sitelinks ?coord ?article \nORDER BY DESC(?sitelinks)\nLIMIT 10\n", "context": "10 most famous places in New Dehli (measured by Sitelinks)", "target": ""}
{"query": "#defaultView:Map\n#title: Карта объектов на берегу Камы и рек, впадающих в неё\nSELECT ?item ?itemLabel ?coord\nWHERE {\n{?item wdt:P206 wd:Q79082; wdt:P625 ?coord.} \n  UNION \n{?item wdt:P403 wd:Q79082.\n?item p:P625 [ps:P625 ?coord; pq:P518 wd:Q1233637]}\n\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }  \n\n} ", "context": "Map of objects on the banks of the Kama and the rivers flowing into it", "target": ""}
{"query": "SELECT  ?employerLabel ?item ?itemLabel ?layer ?startyear ?length ?link {\n  ?item p:P106 ?wir .\n  ?wir ps:P106 wd:Q3809586 .\n  OPTIONAL { ?wir pq:P582 ?end . ?wir pqv:P582 [ wikibase:timePrecision ?tp_end]  }\n  OPTIONAL { ?wir pq:P580 ?start . ?wir pqv:P580 [ wikibase:timePrecision ?tp_start] }\n  BIND(IF(?tp_end=?tp_start,\n          CONCAT(STR(12*(YEAR(?end)-YEAR(?start))+MONTH(?end)-MONTH(?start)), ' months'),\n          'duration: not calculable') AS ?length)\n  BIND(IF(!BOUND(?end),   'no end date',   CONCAT('end: ',   STR(YEAR(?end))))   AS ?layer)\n  BIND(IF(!BOUND(?start), 'no start date', CONCAT('start: ', STR(YEAR(?start)))) AS ?startyear)\n  OPTIONAL { ?wir pq:P108 ?employer }\n  OPTIONAL { ?wir pq:P2699 ?link }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC(?end)", "context": "Duration of Wikimedia residencies (the average duration is 11 months)", "target": ""}
{"query": "PREFIX wd: <http://www.wikidata.org/entity/>\nPREFIX wdt: <http://www.wikidata.org/prop/direct/>\nPREFIX wikibase: <http://wikiba.se/ontology#>\nPREFIX p: <http://www.wikidata.org/prop/>\nPREFIX v: <http://www.wikidata.org/prop/statement/>\nPREFIX q: <http://www.wikidata.org/prop/qualifier/>\nPREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n\nSELECT ?numberLabel ?value WHERE {\n   ?number  wdt:P31 wd:Q12916 . \n   OPTIONAL { \n     ?number  wdt:P1181 ?value .\n   }\n\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n   }\n} order by desc(?value)", "context": "Real numbers with their approximate value", "target": ""}
{"query": "SELECT ?obiekt ?obiektLabel ?geo ?miastoLabel ?osobaLabel\n\nWHERE {\n  ?obiekt (wdt:P547|wdt:P138) ?osoba.\n  ?osoba (wdt:P102|wdt:P361) wd:Q210431.\n  ?obiekt wdt:P625 ?geo.\n  OPTIONAL {?obiekt wdt:P131 ?miasto.}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],pl\". }\n}", "context": "exploratory query to find new items", "target": ""}
{"query": "#defaultView:Map\nSELECT ?koord ?jaio ?emakumezko ?emakumezkoLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?emakumezko wdt:P21 wd:Q6581072;\n    (wdt:P19/(wdt:P131*)/^wdt:P527) wd:Q47588;\n    ^schema:about ?article.\n  ?article schema:isPartOf <https://eu.wikipedia.org/>.\n  ?emakumezko wdt:P19 ?jaio.\n  ?jaio wdt:P625 ?koord.\n}", "context": "Exploring Wikidata Items for people born in the Basque country", "target": ""}
{"query": "# Lugares de  nacimiento de creadores en dominio publico de Colombia\n#defaultView:Map\nSELECT  ?persona ?personaLabel ?pob ?pobLabel ?coords\nWHERE {\n  ?persona wdt:P27 wd:Q739;\n           wdt:P7763 wd:Q71887839.\n  ?persona wdt:P19 ?pob.          \n  ?pob wdt:P625 ?coords.          \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n}", "context": "Birthplace of Colombians in the Public Domain", "target": ""}
{"query": "#view:graph\n#defaultView:Graph\n#title: Relación institución, personas y obras\nSELECT ?Persona ?PersonaLabel ?obra_destacada ?obra_destacadaLabel ?tiene_obras_en_la_colección ?tiene_obras_en_la_colecciónLabel WHERE {\n  ?Persona wdt:P27 wd:Q739;\n    wdt:P7763 wd:Q71887839.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  OPTIONAL { ?Persona wdt:P800 ?obra_destacada. }\n  OPTIONAL { ?Persona wdt:P6379 ?tiene_obras_en_la_colección. }\n}\nLIMIT 200", "context": "Relationship between Institutions, Works and Persons in the Colombian Public Domain", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT ?author ?authorLabel ?author_image ?oralhistoryat ?oralhistoryatLabel ?oralhistoryat_image WHERE {\n  ?author wdt:P5008 wd:Q117322976 . \n  ?author wdt:P31 wd:Q5.\n  \n  OPTIONAL { ?author wdt:P18 ?author_image }\n\n ?author wdt:P9600 ?oralhistoryat. \n  \n   OPTIONAL { ?oralhistoryat wdt:P18 ?oralhistoryat_image }\n   \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n\n}", "context": "Graph of all individuals connected to the Suffrage Interviews and institutions holding their oral history", "target": ""}
{"query": "SELECT ?item ?itemLabel ?authorLabel ?uniLabel (YEAR(?date) as ?year) WHERE\n{\n      ?item wdt:P407 wd:Q36451 .\n      ?item wdt:P31 wd:Q1907875 .\n      ?item wdt:P577 ?date . \n      OPTIONAL {?item wdt:P4101 ?uni . }\n      OPTIONAL {?item wdt:P50 ?author . }\n      OPTIONAL {?item wdt:P2093 ?authorLabel . }\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"en\" .}\n} order by ?year", "context": "List of masters theses written in Māori", "target": ""}
{"query": "# Map of active standard gauge tram systems\nSELECT  ?tramSystem ?tramSystemLabel ?coordinates\nWHERE {\n  ?tramSystem wdt:P31 wd:Q15640053 .        # Instances of tram systems\n  ?tramSystem wdt:P1064 wd:Q1999572 .      # Standard gauge tram systems\n  ?tramSystem wdt:P625 ?coordinates .     # Geographic coordinates of tram system\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Tram systems with 1435mm wide rails", "target": ""}
{"query": "SELECT ?film ?filmLabel ?year1 ?fl ?remake ?remakeLabel ?year2 ?rl WHERE {\n  ?film wdt:P31 wd:Q11424 . # Films\n  ?film wdt:P495 wd:Q15180.\n  OPTIONAL {?film wdt:P577 ?year1.}\n  ?film wikibase:sitelinks ?fl.\n  ?remake wdt:P144 ?film.\n  OPTIONAL {?remake wdt:P577 ?year2.}\n  ?remake wikibase:sitelinks ?rl.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }", "context": "Remakes of Soviet films with their popularity", "target": ""}
{"query": "SELECT DISTINCT ?object ?objectLabel ?image ?coords WITH {  \n  SELECT ?object ?objectLabel ?coords {\n    SERVICE <https://query.wikidata.org/sparql> {\n      ?object wdt:P4244 ?monID ;\n              wdt:P131* wd:Q10489;\n              wdt:P625 ?coords;\n              rdfs:label ?objectLabel .\n              FILTER (LANG(?objectLabel) = \"de\")\n      }\n  }} AS %monuments WHERE {\n  INCLUDE %monuments .\n  ?image wdt:P180 ?object .\n  }\n    ", "context": "Images for all monuments with a Bavarian monument authority ID in Passau administrative district", "target": ""}
{"query": "#defaultView:ImageGrid\n#title:Coats of Arms\n\nselect ?coa ?image {\n  \n  ?coa wdt:P31 wd:Q14659 ;\n       wdt:P18 ?image\n}", "context": "Most frequent objects depicted on coats of arms", "target": ""}
{"query": "#title:Tolkien plurals\nSELECT ?lexeme ?singular ?plural ?tolkienPlural WHERE {\n  ?lexeme dct:language wd:Q1860;\n          wikibase:lexicalCategory wd:Q1084;\n          ontolex:lexicalForm ?singularForm, ?pluralForm.\n  ?singularForm wikibase:grammaticalFeature wd:Q110786;\n                ontolex:representation ?singular.\n  ?pluralForm wikibase:grammaticalFeature wd:Q146786;\n              ontolex:representation ?plural.\n  FILTER(STRENDS(?singular, \"f\"))\n  FILTER(STRENDS(?plural, \"fs\"))\n  BIND(STRLANG(CONCAT(IF(STRENDS(?plural, \"ffs\"), SUBSTR(?plural, 1, STRLEN(?plural) - 3), SUBSTR(?plural, 1, STRLEN(?plural) - 2)), \"ves\"), LANG(?plural)) AS ?tolkienPlural)\n  MINUS {\n    ?lexeme ontolex:lexicalForm ?tolkienPluralForm.\n    ?tolkienPluralForm wikibase:grammaticalFeature wd:Q146786;\n                       ontolex:representation ?tolkienPlural.\n  }\n}\nORDER BY MD5(?singular)", "context": "Tolkien-style plurals", "target": ""}
{"query": "#defaultView:Map\nSELECT ?lemma ?layer ?coord WHERE {\n  ?l dct:language ?lang ;\n     wikibase:lemma ?lemma ;\n     ontolex:sense/wdt:P5137 ?loc .\n  ?loc wdt:P625 ?coord .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n                         ?lang rdfs:label ?layer . }\n}", "context": "Lexemes with a Sense related to a geolocated Item", "target": ""}
{"query": "#defaultView:Map{\"hide\": [\"?coord\"]}\nSELECT DISTINCT ?subj ?subjLabel ?place ?placeLabel ?layer ?coord \nWITH {\n  SELECT DISTINCT ?subj WHERE {\n  ?subj ((wdt:P19|wdt:P551|wdt:P20|wdt:P1321|wdt:P937)/(wdt:P131*)) wd:Q11943.\n  }\n} as %person # re-use results\nWHERE {\n  {\n    INCLUDE %person\n    ?subj  wdt:P19 ?place.\n    ?place wdt:P625 ?coord.\n    BIND(\"Geburtsort\" AS ?layer)\n  } UNION {\n    INCLUDE %person\n    ?subj wdt:P20 ?place.\n    ?place wdt:P625 ?coord.\n    BIND(\"Sterbeort\" AS ?layer)\n  } UNION {\n    INCLUDE %person\n    ?subj wdt:P551 ?place.\n    ?place wdt:P625 ?coord.\n    BIND(\"Wohnsitz\" AS ?layer)\n  } UNION {\n    INCLUDE %person\n    ?subj wdt:P937 ?place.\n    ?place wdt:P625 ?coord.\n    BIND(\"Wirkungsort\" AS ?layer)\n  } UNION {\n    INCLUDE %person\n    ?subj wdt:P1321 ?place.\n    ?place wdt:P625 ?coord.\n    BIND(\"Bürgerort\" AS ?layer)\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of all places related to persons with layers by property of the place", "target": ""}
{"query": "#title: Coordinates and gender of German river names\n#defaultView:Map\nselect * {\n  values (?gender ?rgb) {\n    (wd:Q499327 \"3333cc\")\n    (wd:Q1775415 \"cc3333\")\n    (wd:Q1775461 \"33cc33\")\n  }\n\n  ?l dct:language wd:Q188;\n     wikibase:lexicalCategory/wdt:P279* wd:Q1084;\n     wikibase:lemma ?lemma;\n     wdt:P5185 ?gender.\n\n  ?gender rdfs:label ?layer filter (lang(?layer) = \"en\").\n\n  ?l ontolex:sense/wdt:P5137 ?item.\n\n  ?item wdt:P31/wdt:P279* wd:Q355304.\n  optional { ?item wdt:P625 ?coords }\n  optional { ?item wdt:P3896 ?geoshape }\n}", "context": "Map of river names in german coloured by gender", "target": ""}
{"query": "#defaultView:Map\nSELECT ?subway ?subwayLabel ?coords  (SAMPLE(?maps) AS ?map)  WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n  VALUES ?subws {\n    wd:Q5503\n    wd:Q1268865\n    wd:Q113455515 \n  }  \n  ?subway wdt:P31 ?subws ;\n    wdt:P5817 wd:Q55654238; wdt:P625 ?coords.  \n  OPTIONAL { ?subway wdt:P15|wdt:P1621 ?maps. }\n}group by ?subway ?subwayLabel ?coords ", "context": "World map of subway maps", "target": ""}
{"query": "#defaultView:Graph\nSELECT DISTINCT ?artist ?artistLabel ?whichOne ?whichOneLabel ?rgb\nWHERE\n{\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  VALUES ?scotArt {wd:Q657377 wd:Q1255562 wd:Q2070445 wd:Q1529484 wd:Q2171024 wd:Q2171114 wd:Q7731585 wd:Q7552623 wd:Q7936532 wd:Q16956257 wd:Q20713695 wd:Q123509806 wd:Q125504469 wd:Q16639232 wd:Q18527865 }\n  {\n    SELECT DISTINCT ?artist (COUNT(?whichOne) AS ?socCnt) WHERE\n    {\n      VALUES ?scotArt {wd:Q657377 wd:Q1255562 wd:Q2070445 wd:Q1529484 wd:Q2171024 wd:Q2171114 wd:Q7731585 wd:Q7552623 wd:Q7936532 wd:Q16956257 wd:Q20713695 wd:Q123509806 wd:Q125504469 wd:Q16639232 wd:Q18527865 }\n      ?artist wdt:P463 ?scotArt.\n      ?artist wdt:P463 ?whichOne.\n      FILTER (?whichOne = ?scotArt).   \n    } GROUP BY ?artist \n  }\n  FILTER (?socCnt > 1).\n  ?artist wdt:P463 ?scotArt.\n  ?artist wdt:P463 ?whichOne.\n  FILTER (?whichOne = ?scotArt).\n  BIND( \"99ffeb\"  AS ?rgb).\n} ", "context": "View of the associative network of artists to societies", "target": ""}
{"query": "select ?item { \n   {\n     select distinct ?item {\n       ?item p:P10565  ?itemP10565stmt1, ?itemP10565stmt2\n                        filter(?itemP10565stmt1 != ?itemP10565stmt2) .\n       ?itemP10565stmt1 wikibase:rank ?itemP10565stmt1_rank \n              filter (?itemP10565stmt1_rank != wikibase:DeprecatedRank) .\n              ?itemP10565stmt2 wikibase:rank ?itemP10565stmt2_rank \n              filter (?itemP10565stmt2_rank != wikibase:DeprecatedRank) .\n     } \n   } }", "context": "Two or more statements on a certain property", "target": ""}
{"query": "#defaultView:Map\n#Fimmakers\nSELECT DISTINCT ?filmmaker ?filmmakerLabel ?lugar ?coord ?birthdate ?year ?TabakaleraURL (\"this is a link \" as ?TabakaleraURLLabel)\n \nWHERE\n{\n  ?filmmaker wdt:P31 wd:Q5 ;\n             wdt:P106 wd:Q2526255 ; #zinemagilea\n             wdt:P19 ?lugar ;\n             wdt:P1344 wd:Q9081343.\n  ?lugar wdt:P625 ?coord.\n \n  \n  OPTIONAL{ ?filmmaker wdt:P569 ?birthdate . } # P569 : Date de naissance\n  BIND(year(?birthdate) as ?year)\n  FILTER(?year > 1800)\n  \n  ?filmmaker  wdt:P10069  ?tabakalera.\n  wd:P10069 wdt:P1630 ?formatterurl .\n  BIND(IRI(REPLACE(?tabakalera, '^(.+)$', ?formatterurl)) AS ?TabakaleraURL).\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n \n}", "context": "Explore data about Tabakalera participants", "target": ""}
{"query": "SELECT DISTINCT ?mother ?motherLabel ?mbirth ?child ?childLabel ?cbirth ?agemom\nWHERE {\n  {\n    SELECT DISTINCT ?mother ?mbirth ?child ?cbirth ((xsd:float((?cbirth - ?mbirth)/365)) AS ?agemom)\n    WHERE {\n      ?child wdt:P31 wd:Q5.\n      ?child wdt:P25 ?mother.\n      ?mother wdt:P569 ?mbirth.\n      ?child wdt:P569 ?cbirth.\n      FILTER (?cbirth > ?mbirth)\n    }\n  }\n # SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY ASC(?agemom)\nLIMIT 1000\n", "context": "Mothers on Wikidata", "target": ""}
{"query": "SELECT ?univ ?univLabel ?debut ?fin ?president ?presidentLabel ?naissance ?deces ?age ?sexeLabel ?roleLabel ?motifLabel \nWHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n  \n  ?univ wdt:P31 wd:Q3551775 . # université de France\n  ?univ p:P488 ?node .        # récupère tous les présidents et pas seulement le dernier\n  ?node ps:P488 ?president . \n  OPTIONAL {?node pq:P1534 ?motif.}\n  OPTIONAL {?node pq:P39 ?role.}\n  \n  OPTIONAL{?node pq:P580 ?année .} # date de début du mandat\n  OPTIONAL{?node pq:P582 ?année2 .} # date de fin du mandat\n  OPTIONAL{?president wdt:P569 ?année3 .} # année de naissance\n  OPTIONAL{?president wdt:P570 ?année4 .} # année de décès\n  OPTIONAL{?president wdt:P21 ?sexe.} # sexe du président\n  \n  BIND(year(?année) as ?debut) # conserve que l'année\n  BIND(year(?année2) as ?fin)\n  BIND(year(?année3) as ?naissance)\n  BIND(year(?année4) as ?deces)\n  BIND(?debut - ?naissance AS ?age) # âge en début de mandat\n}\n\nORDER BY ?univLabel ?debut", "context": "Presidents of French Universities", "target": ""}
{"query": "#title : List of list of ambassadors per sending and hosting countries\nSELECT DISTINCT ?liste ?listeLabel ?hôte_host ?hôte_hostLabel ?opérateur ?opérateurLabel ?contraire ?contraireLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,it,en,de,it,da,no,ru,pl,zh,es\". }\n  ?liste (wdt:P360/(wdt:P279*)) wd:Q121998;\n    wdt:P137 ?opérateur; \n    (wdt:P1001|wdt:P17) ?hôte_host.\n  ?hôte_host wdt:P31 wd:Q3624078.\n  ?opérateur wdt:P31 wd:Q3624078.\n  OPTIONAL { ?liste wdt:P461 ?contraire. }\n}\nORDER BY (?opérateurLabel) (?hôte_hostLabel) (?listeLabel)", "context": "", "target": ""}
{"query": "#title:List of current embassies per ruling and hosting countries, and their counterparts\nSELECT DISTINCT ?amba ?ambaLabel ?opérat ?opératLabel\n?hôte_host ?hôte_hostLabel \n ?contr ?contrLabel WHERE { \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,fr\". }\n  ?amba (wdt:P31/(wdt:P279*)) wd:Q3917681;wdt:P5817 wd:Q55654238;\n    wdt:P137 ?opérat; \n    wdt:P17 ?hôte_host .  \n ?hôte_host wdt:P31 wd:Q3624078.  ?opérat wdt:P31 wd:Q3624078.\n  OPTIONAL { ?amba wdt:P461 ?contr . }\n}   \nORDER BY (?opérateurLabel) (?hôte_hostLabel) (?listeLabel)", "context": "", "target": ""}
{"query": "SELECT ?jenis_tari ?gambar ?nama ?negara_asal ?negara_asalLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?jenis_tari wdt:P31 wd:Q107357104.\n  OPTIONAL { ?jenis_tari wdt:P18 ?gambar. }\n  OPTIONAL { ?jenis_tari wdt:P2561 ?nama. }\n  OPTIONAL { ?jenis_tari wdt:P495 ?negara_asal. }\n}#defaultView:ImageGrid", "context": "Images of types of dance in the world", "target": ""}
{"query": "SELECT ?member ?memberLabel ?memberDescription ?sitelinks ?birthLabel ?workLabel ?residenceLabel\n\nWITH { SELECT DISTINCT * WHERE {\n  ?member wdt:P463 wd:Q110938829;\n  (wdt:P19|wdt:P551|wdt:P937)/wdt:P131* wd:Q16.\n} } AS %i\n\nWHERE {\n  INCLUDE %i.\n  OPTIONAL { ?member wdt:P19 ?birth }\n  OPTIONAL { ?member wdt:P551 ?residence }\n  OPTIONAL { ?member wdt:P937 ?work }\n  ?member wikibase:sitelinks ?sitelinks.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nORDER BY DESC (?sitelinks) ?memberLabel", "context": "Indigenous Performers Arts Alliance (IPAA) Members living, working or from Canada", "target": ""}
{"query": "#defaultView:Map\nSELECT ?label ?coord ?subj\nWHERE\n{\n   ?subj wdt:P170 wd:Q5593 .\n  OPTIONAL {?subj wdt:P276 ?loc .\n    ?loc wdt:P625 ?coord } .\n   ?subj rdfs:label ?label FILTER (lang(?label) = \"en\")\n}", "context": "A map with locations of Pablo Picasso works", "target": ""}
{"query": "#defaultView:Graph\nPREFIX gas: <http://www.bigdata.com/rdf/gas#>\n\nSELECT ?item ?itemLabel ?pic ?linkTo\nWHERE {\n  SERVICE gas:service {\n    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" ;\n                gas:in wd:Q296652 ;\n                gas:traversalDirection \"Forward\" ;\n                gas:out ?item ;\n                gas:out1 ?depth ;\n\n                gas:maxIterations 4 ;\n                gas:linkType wdt:P40 .\n  }\n  OPTIONAL { ?item wdt:P40 ?linkTo }\n  OPTIONAL { ?item wdt:P18 ?pic }\n  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],eu\" }\n}", "context": "Descendants of King Antso III", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?parent ?parentLabel ?coords\nWHERE\n{\n  ?item wdt:P39/wdt:P279* wd:Q111840950 ;\n        ?p ?parent .\n  VALUES ?p { wdt:P22 wdt:P25 } \n  ?parent wdt:P19/wdt:P625 ?coords .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Birthplace of French Heads of State", "target": ""}
{"query": "SELECT ?releaseDate ?filmTitle ?youTubeURL  WHERE {\n  ?castMember rdfs:label \"Cary Grant\"@en . \n  ?director rdfs:label \"Howard Hawks\"@en . \n  ?film wdt:P161 ?castMember ;\n        wdt:P577 ?releaseDate ; \n        rdfs:label ?filmTitle ;\n        wdt:P57 ?director ; \n        wdt:P1651 ?youtubeID .\n  FILTER(lang(?filmTitle) = \"en\")\n  BIND(URI(CONCAT('https://www.youtube.com/watch?v=',?youtubeID)) AS ?youTubeURL)\n}\nORDER BY ?releaseDate\n", "context": "Films starring Cary Grant, directed by Howard Hughes, available on YouTube", "target": ""}
{"query": "# Mountains over 7000 m\nSELECT DISTINCT ?item ?itemLabel ?alt ?coord\n WHERE {\n   ?item wdt:P31 wd:Q8502.\n   ?item p:P2044  ?salt.\n               ?salt psn:P2044  ?saltn.\n                ?saltn wikibase:quantityAmount ?alt. \n   ?item wdt:P625 ?coord.\n   FILTER(?alt > 7000)\n   SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],ca,en,es,fr\".}   \n}\nORDER BY DESC(?alt)\n#defaultView:Map", "context": "Mountains with a height over 7000m", "target": ""}
{"query": "SELECT ?item ?itemLabel ?idnt ?numItems\nWITH {\nSELECT ?idnt (COUNT(DISTINCT(?item)) AS ?numItems) WHERE {\n  ?item   wdt:P5696 ?idnt.\n} \nGROUP BY ?idnt\n} AS %1\nWHERE {\n  INCLUDE %1.\n  ?item   wdt:P5696 ?idnt.\n  FILTER(?numItems > 1)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Find Items that share an Identifier", "target": ""}
{"query": "SELECT DISTINCT ?coworker ?coworkerLabel ?work ?workLabel\n  WITH {\n    SELECT ?work WHERE {\n  {?work wdt:P161 wd:Q220308.}\n  UNION\n  {?work wdt:P725 wd:Q220308.}\n    } } AS %1\n   WHERE {\n     INCLUDE %1\n     {?work wdt:P161 ?coworker.}\n     UNION\n     {?work wdt:P725 ?coworker.}\n   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],ca,en\" } .\n}", "context": "Who has worked with this Celebrity?", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?universidad ?universidadLabel ?fecha WHERE {\n  ?universidad wdt:P17 wd:Q117;\n    wdt:P31 wd:Q3918;\n    wdt:P571 ?fecha.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \" en\". }\n}", "context": "Timeline of inception of Ghanaian universities", "target": ""}
{"query": "#title: Tools for Digital Humanities\n#defaultView:Graph\nPREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\nPREFIX schema: <http://schema.org/>\n\nSELECT DISTINCT\n  ?method ?methodLabel ?tadirahID\n  (?methodColor AS ?rgb)\n  ?tool ?toolLabel ?toolDesc\n  ?image # order is relevant for the graph display.\nWITH {\n  SELECT DISTINCT\n    ?tool ?method ?methodColor\n  WHERE {\n    # select all items that have a TaDiRAH ID and are therefore assumed to be methods\n    ?method wdt:P9309 ?tadirahID.\n    BIND(\"7eb8ff\" AS ?methodColor) # set rgb colour for method nodes\n    # select all items which are linked to these methods through `has use`\n    ?tool wdt:P366 ?method;\n      # limit tools to software in the broadest sense\n      wdt:P31/wdt:P279* wd:Q7397.\n    }\n  LIMIT 3000\n} AS %tools\nWHERE {\n  INCLUDE %tools\n  # retrieve the logos and images for those tools that have them\n  OPTIONAL { ?tool (wdt:P154 | wdt:P18) ?image .}\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?tool rdfs:label ?toolLabel;\n      schema:description ?toolDesc.\n    ?method rdfs:label ?methodLabel.\n  }\n}\nORDER BY ?toolLabel\nLIMIT 5000", "context": "Tool Registry for Digital Humanities", "target": ""}
{"query": "SELECT (replace(group_concat(distinct ?composer;separator=\"; \"), \"http://www.wikidata.org/entity/\", \"\") as ?composers)  #Strip the path in order to get only the Q-number.\n       (group_concat(distinct ?composerLabel;separator=\"; \") as ?composerLabels)\n       ?opera \n       ?operaLabel \n       ?languageLabel \n       ?worldpremiere\nWHERE \n{\n  ?opera wdt:P31/wdt:P279* wd:Q1344.\n  ?opera wdt:P1191 ?worldpremiere .\n  OPTIONAL { ?opera wdt:P86 ?composer }\n  OPTIONAL { ?opera wdt:P86/rdfs:label ?composerLabel . FILTER (lang(?composerLabel) = \"en\") }\n  OPTIONAL { ?opera wdt:P407 ?language }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de,fr,it,es,ru\" }\n}\nGROUP BY ?opera\n         ?operaLabel\n         ?languageLabel \n         ?worldpremiere\n\nORDER BY ASC(?worldpremiere)", "context": "Operas ordered by First Performance", "target": ""}
{"query": "SELECT DISTINCT \n  ?item\n  (SAMPLE(COALESCE(?en_label, ?item_label)) as ?label)\nWHERE \n{\n  ?item wdt:P31/wdt:P279* wd:Q43099500;\n        wdt:P86 ?o.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  OPTIONAL {?item rdfs:label ?item_label}\n}\nGROUP BY ?item", "context": "Performing Arts Productions with a Composer property", "target": ""}
{"query": "#defaultView:LineChart\nselect ?decade (count(DISTINCT ?éclipse) as ?count) where{\nSELECT distinct ?éclipse \n((xsd:integer(YEAR(?date )/10)) * 10 AS ?decade)\nWHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?éclipse (wdt:P31/(wdt:P279*)) wd:Q3887; wdt:P585 ?date;wdt:P3592 ?saros .\n} }\ngroup by ?decade", "context": "Number of solar eclipses per decades", "target": ""}
{"query": "#defaultView:Timeline\nSELECT ?remake ?remakeLabel (MIN(?remakeDate_) AS ?remakeDate) ?original ?originalLabel (MIN(?originalDate_) AS ?originalDate) WHERE {\n  ?remake wdt:P31/wdt:P279* wd:Q111241092;\n          wdt:P144 ?original;\n          wdt:P577 ?remakeDate_.\n  ?original wdt:P31/wdt:P279* wd:Q11424;\n            wdt:P577 ?originalDate_.\n  FILTER NOT EXISTS {\n    ?otherRemake wdt:P31/wdt:P279* wd:Q111241092;\n                 wdt:P144 ?original;\n                 wdt:P577 ?otherRemakeDate_.\n    FILTER(?otherRemakeDate_ < ?remakeDate_)\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?remake ?remakeLabel ?original ?originalLabel\nORDER BY DESC(?remakeDate - ?originalDate)", "context": "Longest gap between original film and remake (without any intermediate remakes)", "target": ""}
{"query": "#title: Number of people who are both politicians and musicians by political spectrum\n#defaultView:BubbleChart\nSELECT ?spectrumLabel (COUNT(?spectrum) AS ?count) WHERE {\n  ?item wdt:P106/wdt:P279* wd:Q639669;\n        wdt:P106 wd:Q82955 ;\n        wdt:P102/wdt:P1387 ?spectrum .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?spectrumLabel\nORDER BY DESC(?count)", "context": "Number of people who are both politicians and musicians by political spectrum", "target": ""}
{"query": "SELECT\n  ?language\n  ?languageLabel\n  (COUNT(DISTINCT ?lexeme) AS ?count)\n\nWHERE {\n  # Languages of Indonesia.\n  wd:Q252 wdt:P2936 ?language.\n  \n  # Exclude English, Arabic, and Mandarin.\n  FILTER(?language != wd:Q1860 && ?language != wd:Q13955 && ?language != wd:Q727694)\n  \n  # Lexemes of those languages.\n  ?lexeme dct:language ?language .\n\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\" .\n  }\n}\n\nGROUP by ?language ?languageLabel\n\nORDER BY DESC(?count)\n", "context": "Lexeme coverage of Indonesian languages", "target": ""}
{"query": "select ?person ?personLabel ?born ?start ?end (round(floor((?start - ?born)/365.25)) as ?age_start) (round(floor((?end - ?born)/365.25)) as ?age_end) where\n{\n  ?person wdt:P31 wd:Q5 . ?person p:P39 ?term . ?term ps:P39 wd:Q14211 . ?term pq:P580 ?start . optional { ?term pq:P582 ?end } .\n  ?person wdt:P569 ?born . \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} order by desc(?start)", "context": "UK MPs and their office start/end years", "target": ""}
{"query": "# # # generate cumulative experience for all members on a given day\n\nSELECT DISTINCT ?electionday (sum(?experience_in_days) as ?total_experience) (count(distinct ?item) as ?mps)\n((sum(?experience_in_days)/(count(distinct ?item))/365.25) as ?average_years)\nwhere \n{  \n  ?election wdt:P31 wd:Q15283424 . ?election wdt:P585 ?electionday . filter(year(?electionday) > 1918) .\n  ?item p:P39 ?positionStatement . ?positionStatement ps:P39 ?term . ?term wdt:P279 wd:Q16707842 .\n  ?positionStatement pq:P580 ?start . ?positionStatement pq:P2715 ?election . # find every MP returned in that election\n  optional { select ?item (sum(round(?duration)) as ?experience_in_days) ?electionday\n    WHERE \n     { \n       ?election wdt:P31 wd:Q15283424 . ?election wdt:P585 ?electionday . filter(year(?electionday) > 1918) . \n       ?item p:P39 ?ps . ?ps ps:P39 ?term . ?term wdt:P279 wd:Q16707842 . \n       ?ps pq:P580 ?start . ?ps pq:P582 ?end . # only looking at ended terms\n       filter ( ?end < ?electionday ) . \n      BIND((?end - ?start) AS ?duration) . \n      # period for all previous terms\n   } group by ?item ?electionday\n }  \n  \n} group by ?electionday order by ?electionday", "context": "Cumulative experience of UK MPs on election day (since 1922)", "target": ""}
{"query": "#OpenGLAM Survey\n#defaultView:Map\nSELECT ?item ?itemLabel ?coordinate_location ?image WHERE {\n  ?item wdt:P1343 wd:Q73357989.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  OPTIONAL {\n    { ?item wdt:P625 ?coordinate_location. }\n    UNION\n    {\n      ?item p:P159 ?headquarters.\n      ?headquarters pq:P625 ?coordinate_location.\n    }\n  }\n  OPTIONAL { ?item wdt:P18 ?image. }\n}", "context": "Location of GLAMs with open access policies", "target": ""}
{"query": "#title:Holdings of Arabic periodicals\n#defaultView:Map{\"hide\":[\"?coords\"], \"markercluster\":\"true\"}\nSELECT DISTINCT\n  (URI(str(?periodical)) as ?item) # link to the wikidata item\n  (CONCAT(?periodicalLabel, \": \", ?periodicalDesc) as ?itemLabel) # construct a label for the item\n  ?coords\n  ?collectionLabel\nWITH {\n  SELECT DISTINCT\n    ?periodical ?dateOnset\n  WHERE {\n   ?periodical wdt:P31/wdt:P279* wd:Q1002697; # get all periodicals\n       wdt:P407/wdt:P279* wd:Q13955.          # limit publication language to Arabic\n    # limit to titles published before 1930\n    OPTIONAL {?periodical (wdt:P571 | wdt:P580) ?dateOnset}.\n    FILTER( YEAR(?dateOnset) < 1930).\n    }\n  LIMIT 4000\n  } as %periodicals\nWITH {\n  SELECT DISTINCT\n   ?periodical ?dateOnset ?collection\n  WHERE {\n    INCLUDE %periodicals\n    ?periodical wdt:P195 ?collection .\n  }\n  LIMIT 10000\n} as %holdings\nWHERE {\n  INCLUDE %holdings\n  OPTIONAL{ ?collection wdt:P625 ?coords .}\n  SERVICE wikibase:label {\n      bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], ar\".\n      ?periodical rdfs:label ?periodicalLabel;\n          schema:description ?periodicalDesc.\n      ?collection rdfs:label ?collectionLabel.\n  }\n}\nORDER BY ?dateOnset\nLIMIT 10000", "context": "Arabic periodicals known to Wikidata", "target": ""}
{"query": "SELECT DISTINCT ?personLabel ?twitterName ?pic WHERE {\n  ?person wdt:P2002 ?twitterName;\n    wdt:P106 ?occupation.\n  OPTIONAL { ?person wdt:P18 ?pic. }\n  ?occupation (wdt:P279*) wd:Q82955.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}", "context": "X account of politicians", "target": ""}
{"query": "SELECT DISTINCT ?actorLabel ?characterLabel WHERE {\n  { [] p:P161 [\n      ps:P161 ?actor;\n      pq:P453 ?character\n    ].\n  } UNION {\n    ?character wdt:P175 ?actor.\n  }\n  ?character wdt:P31/wdt:P279* wd:Q95074.\n  ?actor wdt:P570 ?died.\n  FILTER(?died >= NOW()-\"P50D\"^^xsd:duration).\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nORDER BY ?actor", "context": "Characters whose actors died recently", "target": ""}
{"query": "SELECT ?team ?teamLabel (year(?point_in_time) as ?year) ?players {\n  ?team  wdt:P31/wdt:P279 wd:Q115647787.\n  OPTIONAL {\n    SELECT ?team (count(?player) as ?players) WHERE {\n      ?team  wdt:P31/wdt:P279 wd:Q115647787.\n      OPTIONAL {\n         ?team wdt:P710 ?player.\n       }\n    } GROUP BY ?team\n  }\n  \n  OPTIONAL { ?team wdt:P1344/wdt:P585 ?uefa_pot. }\n  OPTIONAL { ?team wdt:P585 ?team_pot. }\n  OPTIONAL {\n    ?article_fr  schema:about ?team;\n              schema:inLanguage \"fr\".\n    }\n  OPTIONAL {\n    ?article_de  schema:about ?team;\n              schema:inLanguage \"de\".\n    }\n  BIND(IF(bound(?uefa_pot), ?uefa_pot, ?team_pot) AS ?point_in_time)\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY asc(?year)", "context": "UEFA teams and the number of players on the team", "target": ""}
{"query": "SELECT ?bird ?birdLabel ?audioFile\nWHERE {\n  ?bird wdt:P31 wd:Q16521 ;  # Instance of taxon\n        wdt:P171* wd:Q5113 ;  # Subclass of bird\n        wdt:P51 ?audioFile .  # Has audio file\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nLIMIT 10000\n", "context": "Species of birds and their sounds", "target": ""}
{"query": "SELECT ?tapak ?tapakLabel ?lokasi_koordinat WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],ms\". }\n  ?tapak wdt:P1435 wd:Q113756521.\n  OPTIONAL { ?tapak wdt:P625 ?lokasi_koordinat. }\n}", "context": "Archaeological sites and remains in Malaysia with National Heritage status", "target": ""}
{"query": "# Wikidata Map of Filming Locations\n# by Jura1, rev. 2016-10-28, excludes countries\n#defaultView:Map\nSELECT \n\t?film ?title ?filmLabel ?filmDescription \n\t?filminglocation ?filminglocationLabel ?coor\nWHERE\n{\n\t?film wdt:P31 wd:Q11424 .\n  \t?film wdt:P915 ?filminglocation .\n  \t?filminglocation wdt:P625 ?coor .\n  \tOPTIONAL { ?film wdt:P1476 ?title } .\n  \tFILTER NOT EXISTS { ?filminglocation wdt:P17 ?filminglocation }\n\tSERVICE wikibase:label { bd:serviceParam wikibase:language \"en,en\" }\n}", "context": "Map of Filming Locations, excludes countries", "target": ""}
{"query": "#title:The most notable sculptures\n#defaultView:ImageGrid\nSELECT ?item ?itemLabel (SAMPLE(?img) AS ?image) WITH {SELECT DISTINCT ?item WHERE {\n?item wdt:P31/wdt:P279? wd:Q860861.\n?item wikibase:sitelinks ?sites. } ORDER BY DESC(?sites) LIMIT 100 } AS %toplist WHERE {\nINCLUDE %toplist.\n?item wdt:P18 ?img.\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?item ?itemLabel", "context": "Image gallery of the most notable sculptures", "target": ""}
{"query": "#title: Authors with Mastodon that cite one or more of the articles of the author with the given ORCID\nSELECT DISTINCT (COUNT(?citingArticle) AS ?count)\n  ?citingAuthor ?citingAuthorLabel ?mastodon\nWHERE {\n  VALUES ?orcid { \"0000-0001-7542-0286\" }\n  ?you ^wdt:P50 ?article ; wdt:P496 ?orcid . \n  ?citingArticle wdt:P2860 ?article ; wdt:P50 ?citingAuthor .\n  ?citingAuthor wdt:P4033 ?mastodon .\n  FILTER ( ?citingAuthor != ?you )\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} GROUP BY ?citingAuthor ?citingAuthorLabel ?mastodon\n  ORDER BY DESC(?count)", "context": "Authors that cite your article(s)", "target": ""}
{"query": "select ?langLabel (count(?devise) as ?nb) where {\nSELECT ?devise ?deviseLabel ?ressLabel  ?langLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,mul\". }\n  ?devise wdt:P31 wd:Q18888037 . \n  OPTIONAL { ?devise wdt:P1001 ?ress  . } \n  OPTIONAL { ?devise wdt:P407 ?lang .   }\n}                       }\ngroup by ?langLabel", "context": "Of the official mottos of U.S. states, there are more in Latin than in English", "target": ""}
{"query": "SELECT ?item ?itemLabel ?linkcount WHERE {\n  # instance of LGBT  \n  ?item wdt:P31/wdt:P279* wd:Q115870510 ;\n        wikibase:sitelinks ?linkcount .\n  FILTER(?linkcount > 2)\n  MINUS {\n    ?article schema:about ?item ;\n             schema:isPartOf <https://en.wikipedia.org/>\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }\n}\nORDER BY DESC(?linkcount)", "context": "Instance/sub-classes of  that have 3 or more non-English Wikipedia site-links", "target": ""}
{"query": "#title:Number of running subway stations per country\n#defaultView:BarChart\nSELECT ?paysLabel(count(distinct ?station_de_métro) as ?nb_stations)  WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n  ?station_de_métro (wdt:P31/(wdt:P279*)) wd:Q928830;\n    wdt:P5817 wd:Q55654238; wdt:P17 ?pays. \n}group by ?paysLabel", "context": "Of the 15424 existing subway stations on Earth, 5726 of them are in", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coords\nWHERE\n{\n  ?item wdt:P31 wd:Q27345018 ;\n        wdt:P625 ?coords .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of tripoints known to Wikidata", "target": ""}
{"query": "#title:Carte des essais nucléaires français\n#defaultView:Map\nSELECT ?essai ?essaiLabel ?date ?power_kT ?layer\n ( coalesce(?coordonnées_géographiques,?coord2)  as ?coords)\n WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n  ?essai (wdt:P31/(wdt:P279*)) wd:Q210112;\n    wdt:P137 wd:Q142;\n    wdt:P585 ?date.\n  OPTIONAL { ?essai wdt:P625 ?coordonnées_géographiques. }\n  OPTIONAL { ?essai wdt:P276 ?lieu. \n           ?lieu wdt:P625 ?coord2.}    OPTIONAL {\n    ?essai p:P2145 ?yield_statement.\n    ?yield_statement rdf:type wikibase:BestRank;\n      (psn:P2145/wikibase:quantityAmount) ?joule.\n    BIND((?joule / 4184000000 ) / \"1000.0\"^^xsd:decimal AS ?power_kT)\n      BIND( \n    IF(?power_kT < 40, \"<40kt\",\n    IF(?power_kT < 100, \"41-100kt\",\n    IF(?power_kT < 500, \"100kt-500kt\",\n    \">=500kt\")))\n    AS ?layer).\n}\n}  ", "context": "", "target": ""}
{"query": "#title:Number of albums (Q482994) by language (P407) in descending order\n\nSELECT ?albums ?P407Label ?P407\nwith { select (count(?album) as ?albums) ?P407 {\n  ?album wdt:P31 wd:Q482994.\n  OPTIONAL { ?album wdt:P407 ?P407. }\n} GROUP BY ?P407 } as %albums where { include %albums.   SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }\nORDER BY DESC(?albums) ASC(?P407)\n\n#Previous version of this query found at http://w.wiki/5Pbw\n\n#Do you know how to improve this query; make it smarter, better, more elegant?\n#If you do, please don't hesitate to drop us a line at User_talk:Moebeus or join\n#the conversation over on Telegram: https://t.me/exmusica\n", "context": "What Wikidata album languages grew the most this week?", "target": ""}
{"query": "#Emergency numbers by population using them\n#Lists emergency numbers and the total amount of people which can use them\n#defaultView:BubbleChart\nSELECT ?emergencyNumber ?emergencyNumberLabel ?tel ?population ?countries  WHERE {\n ?emergencyNumber wdt:P31 wd:Q694554 .\n  OPTIONAL{SELECT ?emergencyNumber (COUNT(?state) as ?countries) (SUM(?pop) as ?population) WHERE {\n  \t?state wdt:P2852 ?emergencyNumber .\n  \tOPTIONAL{?state wdt:P2046 ?pop} .\n  \t?state wdt:P31/wdt:P279* wd:Q6256\n  } GROUP BY ?emergencyNumber } .\n  OPTIONAL{?emergencyNumber wdt:P1329 ?tel }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" }\n}\nORDER BY DESC(?population)", "context": "Emergency number by country size", "target": ""}
{"query": "#defaultView:BubbleChart\nSELECT distinct ?country ?countryLabel  (count(?site) as ?sites)\n\nWHERE { \n  \n  ?site wdt:P1435 wd:Q9259 . \n  ?site wdt:P17 ?country .\n  SERVICE wikibase:label {\n\t\tbd:serviceParam wikibase:language \"en\" .\n\t}\n}\n\nGROUP BY ?country ?countryLabel\nORDER BY DESC(?sites)\n", "context": "Countries with most UNESCO World Heritage Sites", "target": ""}
{"query": "select ?work ?workLabel ?year ?typeLabel ?namesake ?namesakeLabel ?birthyear ?deathyear\nwhere {\n  ?work wdt:P31 ?type . ?type wdt:P279* wd:Q7366 .\n  # work is a type of song or poem\n  ?work wdt:P138 ?namesake . ?namesake wdt:P31 wd:Q5 . \n  # named after a person\n  optional { ?namesake wdt:P569 ?born . bind(year(?born) as ?birthyear) }\n  optional { ?namesake wdt:P570 ?died . bind(year(?died) as ?deathyear) }\n  optional { ?work wdt:P577|wdt:P571 ?date . bind(year(?date) as ?year) }\n  # dates\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  }\n  ", "context": "People with songs named after them", "target": ""}
{"query": "# A list of all black and white movies shot more recent than the 2000's in Greece, in which there is an actor who also been on Saturday Night Live,\n# with a female assistant director who studied in the USA, written by an Oscar nominated screen writer and composed by a blind lutheran.\n# Query inspired by:\n# “Until Wikidata can give me a list of all movies shot in the 60s in Spain, in which a black female horse is stolen by a left-handed actor playing a Portuguese orphan, \n# directed by a colorblind German who liked sailing, and written by a dog-owning women from Helsinki, we have more work to do.”\n# https://www.wikidata.org/wiki/Wikidata:Project_chat/Archive/2013/12#c-Tobias1984-2013-12-07T18:39:00.000Z-37.35.115.38-2013-12-06T23:33:00.000Z\n\nSELECT DISTINCT ?item ?itemLabel WHERE {\n?item wdt:P31/wdt:P279* wd:Q11424;\nwdt:P10135 ?date;\nwdt:P915/wdt:P17 wd:Q41;\nwdt:P161 ?actor;\nwdt:P57 ?director; p:P57 ?p;\nwdt:P58/wdt:P1411/wdt:P31 wd:Q19020;\nwdt:P86 ?composer;\nwdt:P462 wd:Q838368.\n?SNL wdt:P161 ?actor; wdt:P31/wdt:P361 wd:Q13979 .\n?p pq:P3831 wd:Q1757008.\n?director wdt:P21 wd:Q6581072; wdt:P69/wdt:P17 wd:Q30.\n?composer wdt:P1050 wd:Q10874; wdt:P140 wd:Q75809.\nFILTER( ?date >= \"2000-01-01T00:00:00\"^^xsd:dateTime )\nSERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}", "context": "A list of black-and-white movies made in Greece after 2000 featuring an actor from Saturday Night Live, with a female assistant director who studied in the USA, written by an Oscar-nominated screenwriter, and composed by a blind Lutheran", "target": ""}
{"query": "#title: Movies where a guy named Jack plays a guy named Jack\nSELECT ?movie ?movieLabel ?actor ?actorLabel ?character ?characterLabel ?role WITH {\n  SELECT ?movietype WHERE {\n    ?movietype wdt:P279* wd:Q11424\n  }\n} AS %m\nWHERE {\n  INCLUDE %m\n  { ?movie wdt:P31 ?movietype;\n         p:P161 ?s .\n  ?s ps:P161 ?actor .\n  ?s pq:P453 ?character .\n  ?actor wdt:P735 wd:Q1159009 .\n  ?character wdt:P735 wd:Q1159009 . }\nUNION\n  { ?movie wdt:P31 ?movietype;\n         p:P161 ?s .\n  ?s ps:P161 ?actor .\n  ?s pq:P453 ?character .\n  ?actor p:P735 ?node .\n  ?node pq:P1449 ?nick .\n  FILTER(STRSTARTS(?nick,\"Jack\")) . \n  ?character wdt:P735 wd:Q1159009 . }\nUNION\n  { ?movie wdt:P31 ?movietype;\n        p:P161 ?s .\n  ?s ps:P161 ?actor .\n  ?s pq:P4633 ?role.\n  ?actor wdt:P735 wd:Q1159009 .\n  FILTER(STRSTARTS(?role,\"Jack\"))\n  }\nUNION\n  { ?movie wdt:P31 ?movietype;\n        p:P161 ?s .\n  ?s ps:P161 ?actor .\n  ?s pq:P4633 ?role.\n  ?actor p:P735 ?node .\n  ?node pq:P1449 ?nick .\n  FILTER(STRSTARTS(?nick,\"Jack\")) . \n  FILTER(STRSTARTS(?role,\"Jack\"))\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}", "context": "Movies where a guy named Jack plays a guy named Jack", "target": ""}
{"query": "#Fiscal map\n\nSELECT ?item ?itemLabel ?coor ?countryLabel ?XuserLabel \n{\n  ?item wdt:P463 wd:Q28133988 .\n  OPTIONAL { ?item wdt:P625 ?coor }\n  OPTIONAL { ?item wdt:P17 ?country }\n  OPTIONAL { ?item wdt:P131 ?location }\n\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\n#defaultView:Map", "context": "Network of European Union Independent Fiscal Institutions", "target": ""}
{"query": "#defaultView:Table;\n#\n# opera singers who are sopranos with article in English Wikipedia\n#\nSELECT DISTINCT ?item ?itemLabel ?itemDescription ?article_en ?article_de  \nWHERE {\n  ?item wdt:P31 wd:Q5.                # is human\n  ?item wdt:P106 wd:Q2865819.         # and occupatition is opera singer\n  ?item wdt:P412 wd:Q30903.           # and voice type is soprano\n\n  ?article_en schema:about ?item.\n  ?article_en schema:isPartOf <https://en.wikipedia.org/>.     # and has article in English\n  \n  OPTIONAL {\n    ?article_de schema:about ?item.\n    ?article_de schema:isPartOf <https://de.wikipedia.org/>.   # and maybe article in German\n  }\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\norder by ?itemLabel", "context": "Opera singers who are sopranos with an article on English Wikipedia", "target": ""}
{"query": "\nSELECT DISTINCT ?item ?itemLabel ?annéePrix ?age ?sexeLabel \nWHERE {\n  ?item p:P166 ?awardStat . # … with an awarded(P166) statement\n  ?awardStat ps:P166 wd:Q478826 . # … that has the value Prix Apollinaire (Q478826)\n  ?awardStat pq:P585 ?datePrix . # when did they receive the Prix Apollinaire \n  ?item wdt:P21 ?sexe. \n  ?item wdt:P569 ?dateNaissance.\n  \n  BIND(year(?datePrix) as ?annéePrix)\n  BIND(year(?dateNaissance) as ?annéeNaissance)\n  BIND(?annéePrix - ?annéeNaissance AS ?age) \nSERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\" . }\n}\n\nORDER BY ?annéePrix", "context": "Winners of the Guillaume Apollinaire Prize (1941-2023)", "target": ""}
{"query": "#Mapa de bibliotecas Argentinas\n# Mapa de Bibliotecas Argentinas\n#defaultView:Map\nSELECT ?library ?libraryLabel ?coords WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,es,en\". }\n  ?library (wdt:P31/wdt:P279*) wd:Q7075.\n  ?library wdt:P17 wd:Q414.\n  ?library wdt:P625 ?coords.\n}", "context": "Libraries in Argentina (on Wikidata)", "target": ""}
{"query": "#defaultView:LineChart{\"options\": {\"yAxis\": {\"scale\": \"log\"}}}\nSELECT ?country ?year ?population ?countryLabel\nWITH\n{\n    SELECT ?country ?year (avg(?populationValue) as ?population)\n    WHERE\n    {\n      ?country wdt:P17 wd:Q801. # Must be a cat\n      ?country wdt:P31 wd:Q515.\n       ?country p:P1082 ?populationStatement.\n        ?populationStatement ps:P1082 ?populationValue.\n        ?populationStatement pq:P585 ?date.\n      filter(year(?date) > 1980)\n      #bind(floor(?populationValue/1000) AS ?populationValue)\n    bind(str(YEAR(?date)) AS ?year)\n      \n      \n  \n    }\n    group by ?country ?year\n} AS %results\nWHERE\n{\n    INCLUDE %results.\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n# Visualization parameters\n#VISUALIZATION:{\"type\": \"LineChart\", \"options\": {\"yAxis\": {\"scale\": \"log\"}}}", "context": "Amount of population in cities in Israel over the last 45 years (where this information is entered)", "target": ""}
{"query": "#---------------------------------------------------------------------------------------\n# Jüngsten Personen (geboren oder gestorben in Dresden)\n#---------------------------------------------------------------------------------------\nSELECT distinct ?item ?itemLabel ?itemDescription ?age ?pobLabel ?podLabel {\n#  ?item wdt:P1196 wd:Q8454 . \n# ?item wdt:P1399 [] .\n  \n  {?item wdt:P19 wd:Q1731}\n  union\n  {?item wdt:P20 wd:Q1731}\n        \n  ?item p:P569 [ a wikibase:BestRank ;\n                 psv:P569 [ wikibase:timeValue ?birth;\n                            wikibase:timePrecision ?precision_birth ] ] .\n  ?item p:P570 [ a wikibase:BestRank ;\n                 psv:P570 [ wikibase:timeValue ?death;\n                            wikibase:timePrecision ?precision_death ] ] . \n  FILTER (?precision_birth > 8 && ?precision_death > 8) \n  BIND ((?death - ?birth)/365.25 AS ?age) \n  #FILTER (?age <= 18.5)\n  \n  optional {?item wdt:P19 ?pob. }\n  optional {?item wdt:P20 ?pod. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en\"}\n} ORDER BY ?age\nlimit 50", "context": "Youngest people (born or died in Dresden)", "target": ""}
{"query": "# Copyright duration by country\n#defaultView:Map{\"layer\": \"?setOfCountries\"}\nSELECT ?country ?countryLabel ?setOfCountries ?setOfCountriesLabel ?shape\nWHERE {  \n  ?country wdt:P3896 ?shape . # Geographical shape \n  ?setOfCountries wdt:P279 wd:Q108698760 . # Look for sets of countries (grouped by copyright duration) that are a subclass of countries with a post mortem auctoris copyright.        \n  ?setOfCountries wdt:P1001 ?country . # Find each country inside each set of countries.\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\n\nORDER BY ASC(?countryLabel)", "context": "Copyright duration by Country", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?coordinates ?appliesPart\nWHERE\n{\n  #?sea wdt:P361* wd:Q166.\n  ?sea wdt:P361* wd:Q1497.\n  ?item wdt:P403* ?sea. \n  ?item p:P625 ?coordinateStatement.\n  ?coordinateStatement ps:P625 ?coordinates. \n  OPTIONAL { \n    ?coordinateStatement pq:P518 ?appliesPart. \n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"he,en\". }\n}\n", "context": "The Mississippi River and its tributaries", "target": ""}
{"query": "SELECT (count(?item) as ?nb)  ?taxon WHERE {\n  ?item wdt:P31 wd:Q26401003;wdt:P10241 ?taxon_. \n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"fr,en,de,it,es\" .          \n    ?item rdfs:label ?label;schema:description ?description.\n          ?taxon_ rdfs:label ?taxon .   \n     }  \n}  group by ?taxon     order by desc(?nb)", "context": "Individual animals counted per species", "target": ""}
{"query": "#Chronologie des décès des mathématiciens, avec leurs théorèmes\n#defaultView:Timeline\nSELECT\n  ?genderLabel\n  ?theorem\n  ?theoremLabel\n  ?nameLabel\n  ?death\n  ?formula\nWHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en,de,pl\". }\n  ?theorem wdt:P31 wd:Q65943;\n           wdt:P138 ?name.\n  ?name    wdt:P570 ?death;\n           wdt:P21 ?gender .\n  # OPTIONAL{\n  ?theorem wdt:P2534 ?formula.\n  # }\n}\nORDER BY DESC(?death)\nLIMIT 100", "context": "Chronology of deaths of mathematicians, with their theorems", "target": ""}
{"query": "select ?lexeme ?lemma ?modified where {\n  ?lexeme a ontolex:LexicalEntry ;\n    dct:language wd:Q9035 ;\n    wikibase:lemma ?lemma .\n  ?lexeme schema:dateModified ?modified . filter (?modified >= \"2024-11-01\"^^xsd:dateTime)\n} order by desc(?modified)\n", "context": "Recently edited lexemes since 'DATE'", "target": ""}
{"query": "select distinct ?name  { ?film wdt:P915/(wdt:P276| wdt:P131 )* wd:Q90 . ?article schema:isPartOf <https://en.wikipedia.org/> ; schema:name ?name; schema:about ?film. }", "context": "List films shot by filming location", "target": ""}
{"query": "SELECT DISTINCT ?item ?labelen ?labelpa WHERE {\n ?item wdt:P31 wd:Q5. \n ?item wdt:P6886 wd:Q397. \n {?item wdt:P106 wd:Q36180} UNION {?item wdt:P106 [wdt:P279* wd:Q36180].} UNION {?item wdt:P106 wd:Q482980.} \n OPTIONAL { ?item rdfs:label ?labelen. FILTER(LANG(?labelen)=\"en\") }\n OPTIONAL { ?item rdfs:label ?labella. FILTER(LANG(?labella)=\"la\") }\n}", "context": "List Authors by work language (Latin)", "target": ""}
{"query": "SELECT DISTINCT ?thing ?ordinal ?type ?string ?lang  WHERE {\n  FILTER (?lang in ('en', 'ja'))\n  {\n    ?thing wdt:P31 wd:Q47018901.\n    ?thing wdt:P361 wd:Q12138.\n    BIND ('month' as ?type)\n  } UNION {\n    ?thing wdt:P31 wd:Q116600668.\n    BIND ('day' as ?type)\n  }\n\n  FILTER NOT EXISTS { ?form wikibase:grammaticalFeature wd:Q146786 }\n\n  ?thing p:P31 ?instanceOf.\n  ?instanceOf pq:P1545 ?ordinal.\n\n  ?sense wdt:P5137 ?thing.\n  ?lexeme ontolex:sense ?sense.\n  ?lexeme ontolex:lexicalForm ?form.\n  ?lexeme dct:language ?language.\n  ?language wdt:P218 ?lang.\n\n  ?form ontolex:representation ?string.\n}", "context": "Return Lexemes of Month and Day in the filtered languages", "target": ""}
{"query": "#Hip hop Map\n#defaultView:Map\nSELECT ?hiphopArtist ?hiphopname ?placeBirth ?placeBirthLabel ?location\nWHERE\n{\n  ?hiphopArtist wdt:P136 wd:Q11401. #genre is hiphop\n  #?hiphopArtist wdt:P106 wd:Q2252262. #occupation is rapper\n  ?hiphopArtist wdt:P31 wd:Q5. # Must be a human\n  ?hiphopArtist wdt:P742 ?hiphopname.\n  ?hiphopArtist wdt:P19 ?placeBirth.\n  #FILTER(?placeBirth = wd:Q18424) \n  ?placeBirth wdt:P625 ?location\n  \n                \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". } # Helps get the label in your language, if not, then default for all languages, then en language\n}\nORDER BY ?hiphopArtistLabel", "context": "Hip Hop artists by place of birth", "target": ""}
{"query": "#defaultView:Timeline\n#title:Fallecidos por desastres en España\nselect ?item ?itemlabel (CONCAT( STR(?muertes),\"⚰️\") AS ?muertesConEtiqueta) ?periodo ?image where\n{ ?item ?p ?desastre .\n?item rdfs:label ?itemlabel .\n ?item wdt:P1120\t?muertes .\n ?item wdt:P17 ?pais .\n ?item wdt:P585 ?periodo .\n  OPTIONAL { ?item wdt:P18 ?image }\n  FILTER (LANG(?itemlabel)=\"es\") \n FILTER(YEAR(?periodo) >= 1900)\nVALUES ?pais { wd:Q29 }\nVALUES ?desastre { wd:Q8068 wd:Q169950 wd:Q7944 wd:Q7935 wd:Q167903 wd:Q7925 wd:Q7692360}}\n", "context": "Timeline of deaths from disasters in Spain", "target": ""}
{"query": "# Using cross-product / cross-join to produce list of categories.\n# Checking if category sitelinks exists on one wikipedia and if so if it exists on other wikipedias.\n# Can be useful for testing coverage on mediawiki categories that form a range.\n# By: User:Infrastruktur\nselect (?cat as ?title_no) ?item ?article_no ?article_sv ?article_da\nwhere {\n  hint:Query hint:optimizer \"None\" .\n  { select ?cat where {\n    values ?century { \"1500\" \"1600\" \"1700\" \"1800\" \"1900\" \"2000\" }\n    values ?country { \"Norge\" \"Sverige\" \"Danmark\" \"England\" \"Tyskland\" \"Frankrike\" }\n    bind(strlang(concat(\"Kategori:\", ?century, \"-tallet i \", ?country), \"nb\") as ?cat)\n  } }\n  optional {\n    ?article_no schema:name ?cat ;\n      schema:about ?item ;\n      schema:isPartOf <https://no.wikipedia.org/> .\n    optional {\n      ?article_sv schema:about ?item ;\n        schema:isPartOf <https://sv.wikipedia.org/> .\n    }\n    optional {\n      ?article_da schema:about ?item ;\n        schema:isPartOf <https://da.wikipedia.org/> .\n    }\n  }\n}\n", "context": "Using cross-product / cross-join to produce list of categories", "target": ""}
{"query": "#defaultView:Map{\"layer\":\"?sexLabel\"}\nSELECT DISTINCT ?person ?personLabel ?pod ?coordinates ?sexLabel WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en.\" }\n  ?person wdt:P1595|wdt:P7781|wdt:P1399 wd:Q259745 ;\n          wdt:P20 ?pod ;\n          wdt:P21 ?sex .\n  ?pod wdt:P625 ?coordinates ;\n       wdt:P131* wd:Q39 .\n}", "context": "Map of individuals charged, convicted and/or exonerated of Witchcraft with place of death in Switzerland", "target": ""}
{"query": "SELECT ?chateau ?chateauLabel ?location ?locationLabel WHERE {\n  ?chateau wdt:P31 wd:Q23413.  # Château\n  ?chateau wdt:P17 wd:Q142.   # En France\n  OPTIONAL { \n    ?chateau wdt:P625 ?location.  # Coordonnées géographiques\n  }\n  OPTIONAL {\n    ?chateau wdt:P131 ?adminLocation.  # Entité administrative\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }\n}", "context": "Names and Locations of French Castles (Château)", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?itemLabel ?itemDescription ?npt ?npf ?countryLabel ?coordinate\nWHERE {\n  ?item wdt:P1103 ?npt.                       # number of platform tracks\n\n  ?item p:P1103 ?statnode .\n  ?statnode prov:wasDerivedFrom ?refnode . \n  ?refnode pr:P143 wd:Q8449.\n  \n  OPTIONAL {?item wdt:P1103 ?npf}    # number of platform faces\n  OPTIONAL {?item wdt:P17 ?country.}\n  OPTIONAL {?item wdt:P625 ?coordinate.}\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\"}\n}", "context": "Train Station information (with a Spanish Wikipedia article)", "target": ""}
{"query": "PREFIX schema: <http://schema.org/>\n#defaultView:Table\nSELECT ?item ?label_en ?desc_en ?label_de ?desc_de ?label_hu ?desc_hu\nWHERE {\n  \n  ?item wdt:P31/wdt:P361* wd:Q199655 .   # part of library science (244 items)\n  #?item wdt:P31/wdt:P361* wd:Q1235196.   # part of documentation science (only 1 item)\n  #?item wdt:P31/wdt:P361* wd:Q16387.      # part of information science (978 items)\n\n  OPTIONAL {?item rdfs:label        ?label_en. filter(lang(?label_en)=\"en\"). }  \n  OPTIONAL {?item schema:description ?desc_en. filter(lang( ?desc_en)=\"en\"). }  \n  \n  OPTIONAL {?item rdfs:label        ?label_de. filter(lang(?label_de)=\"de\"). }  \n  OPTIONAL {?item schema:description ?desc_de. filter(lang( ?desc_de)=\"de\"). }  \n  \n  OPTIONAL {?item rdfs:label        ?label_hu. filter(lang(?label_hu)=\"hu\"). }  \n  OPTIONAL {?item schema:description ?desc_hu. filter(lang( ?desc_hu)=\"hu\"). }  \n\n  FILTER(!BOUND(?label_hu))  # no label_hu \n  #FILTER(!BOUND( ?desc_hu))  # no desc_hu\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de,en,hu\". }\n}\n#limit 50", "context": "Items missing Hungarian labels or description that are part of Library and Information Science (Q13420675)", "target": ""}
{"query": "select ?item ?article ?title ?label ?description (bound(?description) as ?has) {\n  ?article schema:about ?item ;\n           schema:isPartOf <https://mi.wikipedia.org/> ;\n           schema:name ?title .\n  \n  #minus {\n    SERVICE wikibase:label { \n      bd:serviceParam wikibase:language \"mi\".\n      ?item rdfs:label ?label .\n      ?item schema:description ?description\n    }\n  filter (!(bound(?label) && bound(?description))) .\n}", "context": "Items from Maori Wikipedia missing Maori labels or descriptions", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?administration ?admLabel WHERE {\n  {\n    SELECT DISTINCT * WHERE {\n      ?item p:P31 ?statement0.\n      ?statement0 (ps:P31/(wdt:P279*)) wd:Q11315.\n      ?item wdt:P17 wd:Q20;\n            wdt:P131/wdt:P279* ?administration.\n    }\n    LIMIT 1000\n  }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE], en, sv, nb\" .\n    ?item rdfs:label ?itemLabel . ?item schema:description ?description .\n    ?administration rdfs:label ?admLabel .\n  }\n}", "context": "Instances of \"Shopping Center\" located in administrative territorial entity subclass of Norway", "target": ""}
{"query": "#defaultView:Timeline\nSELECT DISTINCT ?universidad ?universidadLabel ?fecha WHERE {\n  ?universidad wdt:P17 wd:Q117;\n    wdt:P31 wd:Q3918;\n    wdt:P571 ?fecha.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"es, en\". }\n}", "context": "Timeline of inception of Ghanaian universities", "target": ""}
{"query": "SELECT DISTINCT ?pays ?paysLabel ?pers ?persLabel\nWHERE\n{\n  ?pers wdt:P31 wd:Q5;\n        wdt:P21 wd:Q6581072;\n        wdt:P39 [\n          wdt:P279 wd:Q2518691;\n          wdt:P1001|wdt:P17 ?pays\n        ].\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en,fr\". }\n}\nORDER BY ?paysLabel", "context": "Countries that have had a woman serving as Minister of Defense", "target": ""}
{"query": "#defaultView:LineChart\nSELECT (STR(?year) AS ?year1) (MIN(?age) AS ?age1) WHERE {\n  wd:Q38111 p:P451 ?s. \n    ?s ps:P451 ?partner .\n    ?s pq:P580 ?from .\n    OPTIONAL { ?s pq:P582 ?to }\n  VALUES ?year {\n    1995 1996 1997 1998 1999 2000 2001 2002 2003 2004 2005 \n    2006 2007 2008 2009 2010 2011 2012 2013 2014 \n    2015 2016 2017 2018 2019 2020 2021 2022 2023 2024\n  }\n  FILTER (\n    (YEAR(?from) <= ?year) && \n    (!BOUND(?to) || YEAR(?to) > ?year)\n  )\n  ?partner wdt:P569 ?date_of_birth .\n  BIND((?year - YEAR(?date_of_birth)) AS ?age) .\n} GROUP BY ?year", "context": "Leonardo DiCaprio's partners", "target": ""}
{"query": "#defaultView:BubbleChart\n# Countries that have most items with Mastodon or PeerTube (ActivityPub) social networks\n# [[User:Valerio Bozzolan]] CC Zero\n# version 2024-12-02\nSELECT ?countryLabel ?count\nWHERE {\n {\n  SELECT ?country (COUNT(DISTINCT ?activitypub) AS ?count) \n  WHERE {\n   ?item wdt:P4033|wdt:P12622 ?activitypub.\n   ?item wdt:P17 ?country.\n  }\n  GROUP BY ?country\n }\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}\nORDER BY DESC(?count)\n\n", "context": "Countries that have most items with Mastodon or PeerTube (ActivityPub) social networks", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel ?DOD ?OlympediaID WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }\n  {\n    SELECT DISTINCT ?item ?DOD ?OlympediaID WHERE {\n      ?item wdt:P8286 ?OlympediaID.\n      ?item wdt:P570 ?DOD. \n      FILTER (YEAR(?DOD)=2024)\n    }\n  }\n}", "context": "Olympians who died during the year 2024", "target": ""}
{"query": "#defaultView:Tree\nSELECT ?item ?itemLabel ?facetcount\n?facet ?facetLabel (COUNT(DISTINCT ?facet2) AS ?facet2count)\nWITH {\n  SELECT ?item (COUNT(DISTINCT ?facet) AS ?facetcount) WHERE {\n  ?item wdt:P31 wd:Q1697305; wdt:P1343 wd:Q114946518.\n  MINUS{?item wdt:P1269/owl:sameAs*/wdt:P31 wd:Q1697305}\n  OPTIONAL{?item ^wdt:P1269+ ?facet}\n  }\n  GROUP BY ?item\n} AS %top_level_motifs\n\nWHERE {\n    INCLUDE %top_level_motifs\n    ?facet wdt:P1269 ?item.\n    OPTIONAL{?facet ^wdt:P1269+ ?facet2}\n    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?item ?itemLabel ?facetcount ?facet ?facetLabel\nORDER BY DESC(?facetcount) DESC(?facet2count) ", "context": "trees of motifs described in Thompson's motif index (first two levels)", "target": ""}
{"query": "#    wdt:P21 wd:Q6581072 ; # Woman (optional filter)\nSELECT DISTINCT ?q ?qLabel ?died WHERE {\n  ?q wdt:P31 wd:Q5;\n    wdt:P106 wd:Q1028181;\n    wdt:P570 ?died.\n  FILTER((YEAR(?died)) < 1925 )\n  MINUS { ?q wdt:P373 _:b1. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}\nLIMIT 100", "context": "Painters that have died before 1925 but do not have a Wikimedia Commons category on their Wikidata Item", "target": ""}
{"query": "SELECT DISTINCT ?item ?itemLabel  ?itemDescription WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,fr\". }\n  ?item wdt:P31 wd:Q5;\n        wdt:P106 wd:Q33231;\n        wdt:P27 wd:Q142;   \n        wdt:P569 ?birth.\n  FILTER(?birth < \"1870-01-01\"^^xsd:dateTime)\n  FILTER(NOT EXISTS {\n    ?wfr schema:about ?item;\n      schema:isPartOf <https://fr.wikipedia.org/>.\n  })\n}", "context": "French Photographers born before 1870, who do not have a French Wikipedia article", "target": ""}
{"query": "SELECT DISTINCT ?countryLabel WHERE {\n  ?item wdt:P31/wdt:P279* wd:Q3491904 ;\n        wdt:P17 ?country .\n  ?country wdt:P2046 ?area .\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\" . }\n}\nORDER BY ?area\nLIMIT 10 ", "context": "The 10 smallest countries with some kind of urban rail transit system", "target": ""}
{"query": "# last meals of people\nSELECT ?person ?personLabel (GROUP_CONCAT(?mealLabel; separator = \", \") AS ?meal) WHERE {\n  ?person wdt:P3902 ?meal.\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?person rdfs:label ?personLabel.\n    ?meal rdfs:label ?mealLabel.\n  }\n}\nGROUP BY ?person ?personLabel", "context": "Last meals of people", "target": ""}
{"query": "\n\nSELECT ?writer ?description {\n  ?writer wdt:P106 wd:Q36180;\n    wdt:P27 wd:Q183;\n    schema:description ?description.\n  FILTER((LANG(?description)) = \"de\")\n  FILTER (REGEX(?description, \"^[A-Z ][A-Z ]\"))\n}\nLIMIT 200", "context": "Search for Items where description begins with capitalised letters, filter by language, country of citizenship and occupation", "target": ""}
{"query": "SELECT ?item ?itemLabel ?smell ?smellLabel {\n  ?item wdt:P5872 ?smell\n SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} LIMIT 1000", "context": "Wikidata Items using the 'smells of' property", "target": ""}
{"query": "SELECT ?eponym (COUNT(?item) AS ?count) (SAMPLE(?item) AS ?sample)\nWHERE { ?item wdt:P138 ?eponym. }\nGROUP BY ?eponym\nORDER BY DESC(?count)", "context": "Most common eponyms", "target": ""}
{"query": "#title: Show boiling point of alkanes\n#defaultView: Table\nSELECT distinct ?item ?itemLabel ?itemDescription ?boiling_point\nWHERE {\n  ?item wdt:P279* wd:Q41581. # item is subclass of alkanes\n  ?item wdt:P2102 ?boiling_point.  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,de\". }\n}\norder by desc(?boiling_point) ?itemLabel", "context": "Boiling point of alkanes", "target": ""}
{"query": "#title:Pairs of things of different types that have the same smell\nSELECT DISTINCT ?item ?itemLabel ?item2 ?item2Label ?smell ?smellLabel {\n  ?item wdt:P5872 ?smell; wdt:P31 ?type .\n  ?item2 wdt:P5872 ?smell; wdt:P31 ?type2\n  FILTER (STR(?item) > STR(?item2))\n  FILTER (?type != ?type2)       \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}", "context": "Pairs of things, of different types, that smell of the same thing", "target": ""}
{"query": "#title: literary work (1700-1830) with more then 25 sitelinks\nSELECT distinct ?item ?itemLabel ?itemDescription ?sitelinks ?date_written\nWHERE\n{\n  ?item wikibase:sitelinks ?sitelinks . hint:Prior hint:rangeSafe true .\n  FILTER (?sitelinks > 25 )                # Minimum sitelinks (language)\n  ?item (wdt:P31/wdt:P279*) wd:Q7725634.   # literary work\n  ?item wdt:P577 ?date_written.     # publication date\n  #?item wdt:P571 ?date_written.    # inception\n  FILTER (?date_written >= \"1700-01-01T00:00:00Z\"^^xsd:dateTime)  # written after\n  FILTER (?date_written <= \"1830-12-31T00:00:00Z\"^^xsd:dateTime)  # written before\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\" . }\n}\norder by ?date_written ?itemLabel", "context": "Literary work (1700-1830) with more than 25 sitelinks", "target": ""}
{"query": "# Statistics for lexemes in language A, derived from lexeme (P5191) in language B\nselect ?lang_a ?lang_aLabel ?sample_a ?lang_b ?lang_bLabel ?count_a ?sum_a ?pct\nwith {\n  select ?lang_a ?lang_b (sample(?lex_a) as ?sample_a) (count(?lex_a) as ?count_a)\n  where {\n    # set ?lang_a equal to the languages we want to do statistics over\n    { select distinct ?lang_a where {\n      ?lang_a wdt:P279* wd:Q33577 . # ?lang_a is subclass of indo-aryan\n    } }\n    #values ?lang_a { wd:Q1860 } # english. for testing\n\n    ?lex_a dct:language ?lang_a .\n    optional {\n      ?lex_a wdt:P5191 ?lex_b .\n      ?lex_b dct:language ?lang_b .\n    }\n  }\n  group by ?lang_a ?lang_b\n} as %raw\nwith {\n  select ?lang_a (sum(?count_a) as ?sum_a) where {\n    include %raw\n  }\n  group by ?lang_a\n} as %total\nwhere {\n  {\n    include %raw\n    include %total\n    bind(round(?count_a * 10000 / ?sum_a) / 100 as ?pct)\n  }\n  service wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\" . }\n}\norder by ?lang_a desc(?count_a)", "context": "What are the statistics for lexemes in language A that are derived from lexemes in language B?", "target": ""}
{"query": "#title: literary work (1700-1830) with more then 25 sitelinks\nSELECT distinct ?item ?itemLabel ?itemDescription ?sitelinks ?date_written\nWHERE\n{\n  ?item wikibase:sitelinks ?sitelinks . hint:Prior hint:rangeSafe true .\n  FILTER (?sitelinks > 25 )                # Minimum sitelinks (language)\n  ?item (wdt:P31/wdt:P279*) wd:Q7725634.   # literary work\n  ?item wdt:P577 ?date_written.     # publication date\n  #?item wdt:P571 ?date_written.    # inception\n  FILTER (?date_written >= \"1700-01-01T00:00:00Z\"^^xsd:dateTime)  # written after\n  FILTER (?date_written <= \"1830-12-31T00:00:00Z\"^^xsd:dateTime)  # written before\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\" . }\n}\norder by ?date_written ?itemLabel", "context": "Old books (1700 - 1830) with many (+25) Sitelinks", "target": ""}
{"query": "select distinct ?item ?itemLabel\nwhere {\n  ?st pq:P13187 ?role .\n      # rdf:type wikibase:BestRank ;\n      #?ps ?val.\n  #?stprop wikibase:statementProperty ?ps .\n  values ?c { wd:Q63141557 wd:Q105543609 }\n  ?item wdt:P31 ?c ;\n        ?p ?st .\n  service wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\" . }\n}\n", "context": "List of translated songs or musical works, with 'role named in credits' as a qualifier", "target": ""}
{"query": "#defaultView:Map\nSELECT ?item ?witch ?ItemLabel ?Residence ?coordinate (?Gender as ?layer) ?rgb\nWHERE {\n  ?item wdt:P4478 ?witch;\n    wdt:P551 ?residence.\n  ?residence wdt:P625 ?coordinate.\n  OPTIONAL \n  {\n    ?item wdt:P21 ?gender .\n  }\n\n  BIND(IF(!BOUND(?gender),\"00FF00\",IF(CONTAINS(STR(?gender),\"Q6581097\"),\"0000FF\",\"FF0000\")) as ?rgb)\n\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".\n    ?item rdfs:label ?ItemLabel.\n    ?residence rdfs:label ?Residence .\n    ?gender rdfs:label ?Gender.\n  }\n}", "context": "Specifiying colours for a gender representation of Scottish witches", "target": ""}
{"query": "SELECT ?band ?bandLabel ?genre ?genreLabel ?location_of_formation ?location_of_formationLabel ?coordinates\n  (MAX(?release_date) AS ?latest_release)\n{\n  ?band wdt:P31 wd:Q215380. # ?band is a band\n  VALUES ?genre { wd:Q617240 wd:Q720959 wd:Q186170 wd:Q11366 wd:Q49451 }\n  ?band wdt:P136 ?genre. # ?band has one of genres in ?genre\n  OPTIONAL { ?band wdt:P576 ?dissolved }\n  FILTER (! BOUND(?dissolved)) # Band has no dissolve date\n  OPTIONAL {\n    ?band wdt:P740 ?location_of_formation.\n    OPTIONAL { ?location_of_formation wdt:P625 ?coordinates. }\n  }\n  OPTIONAL {\n    ?album wdt:P175 ?band.\n    ?album wdt:P577 ?release_date.\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}\nGROUP BY ?band ?bandLabel ?genre ?genreLabel ?location_of_formation ?location_of_formationLabel ?coordinates", "context": "Currently active rock metal bands, their hometowns and latest release", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT ?item ?itemLabel ?pic ?coord ?GBIFlink WHERE {\n  ?item wdt:P8714 ?GADM .\n  ?item wdt:P625 ?coord .\n  OPTIONAL { ?item wdt:P18 ?pic . }\n  BIND(URI(CONCAT(\"https://www.gbif.org/occurrence/search?publishing_org=1f00d75c-f6fc-4224-a595-975e82d7689c&gadm_gid=\", STR(?GADM))) AS ?GBIFlink)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n}", "context": "Map of Global Administrative Areas with links to Xeno-canto datasets in GBIF", "target": ""}
{"query": "#title:Carte des villes englouties\n#defaultView:Map\nSELECT ?ville_engloutie ?ville_engloutieLabel?ville_engloutieDescription ?image ?coordonnées_géographiques WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\". } \n  ?ville_engloutie wdt:P31/wdt:P279* wd:Q111051152.  \n  OPTIONAL { ?ville_engloutie wdt:P18 ?image. }    \n  OPTIONAL { ?ville_engloutie wdt:P625 ?coordonnées_géographiques. }\n}         ", "context": "Map of drowned places and their images", "target": ""}
{"query": "SELECT ?siklon_tropis ?siklon_tropisLabel ?kecepatan_angin_maksimum ?jumlah_korban_tewas ?gambar WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n  ?siklon_tropis wdt:P31 wd:Q8092.\n  ?siklon_tropis wdt:P17 wd:Q252.\n  OPTIONAL { ?siklon_tropis wdt:P2895 ?kecepatan_angin_maksimum. }\n  OPTIONAL { ?siklon_tropis wdt:P1120 ?jumlah_korban_tewas. }\n  OPTIONAL { ?siklon_tropis wdt:P18 ?gambar. }\n}\nLIMIT 100", "context": "What tropical cyclones have hit Indonesia?", "target": ""}
{"query": "SELECT ?item ?variant (LANG(?variant) AS ?lang) WHERE {\n  ?item wdt:P2521|wdt:P3321 ?variant .\n  MINUS { ?item rdfs:label ?variant } .\n  MINUS { ?item skos:altLabel ?variant } .\n}", "context": "Show missing alias when they exist as another gendered form", "target": ""}
{"query": "SELECT ?item ?itemLabel ?itemDescription ?instanceOfLabel ?instanceOptionsLabel ?location ?locationLabel WHERE {\n  ?item wdt:P31 wd:Q486972 .\n  MINUS { ?item wdt:P17 ?some56 . }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de\". }\n}\nLIMIT 1000", "context": "Items with Human Settlement, but lack a Country", "target": ""}
{"query": "#title:Image gallery of works by William Hogarth\n#defaultView:ImageGrid\nSELECT ?artwork ?artworkLabel ?collectionLabel ?image WHERE {\n  ?artwork wdt:P170 wd:Q171344; wdt:P195 ?collection;\n    wdt:P18 ?image. MINUS {?collection wdt:P361 []}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n} ORDER BY ?collectionLabel", "context": "Image gallery of works by William Hogarth", "target": ""}
{"query": "#title: Objects in Sri Lanka, ( Bridges, Dams, Rivers, Structures, Forests, Waterfalls, Power Stations, Mountains, Water bodies, Train stations, National parks, and Buildings )\n#defaultView:Map,Table\nSELECT distinct ?item ?itemLabel ?itemDescription ?regionLabel ?image ?coord ?rgb\nWHERE {\n  ?item wdt:P17 wd:Q854.  # in Sri Lanka\n  VALUES ?type { \n    wd:Q12280  wd:Q12323 wd:Q4022 wd:Q811979 wd:Q4421 wd:Q34038 wd:Q159719 wd:Q8502 wd:Q15324 wd:Q55488 wd:Q46169 wd:Q41176\n  }\n  ?item wdt:P31/wdt:P279* ?type.\n  OPTIONAL { ?item wdt:P625 ?coord }  \n  OPTIONAL { ?item wdt:P18 ?image } \n  OPTIONAL { ?item wdt:P131 ?region }\n  BIND(IF(BOUND(?image),\"00A62C\",\"C90202\") AS ?rgb)\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}", "context": "Objects in Sri Lanka", "target": ""}
{"query": " #defaultView:Map\nSELECT ?person ?personLabel ?article ?birthplaceLabel ?coord WHERE {\n  # Person is a Polish national\n  ?person wdt:P27 wd:Q36.\n  \n  # Person has a Polish Wikipedia article\n  ?article schema:about ?person;\n           schema:isPartOf <https://pl.wikipedia.org/>.\n\n  # Person has a birthplace with coordinates\n  ?person wdt:P19 ?birthplace.\n  ?birthplace wdt:P625 ?coord.\n\n  # Optional: Limit to people born after 1900 (change if needed)\n  ?person wdt:P569 ?birthdate.\n  FILTER(YEAR(?birthdate) >= 1900)\n  \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"pl\". }\n}\nLIMIT 5000", "context": "Map of Birthplace of Polish Nationals, born after 1900 who have an article on Polish Wikipedia", "target": ""}
{"query": "SELECT DISTINCT ?item WHERE {\n  ?item wikibase:statements 0 .\n  MINUS { ?item dct:language [] } . # exclude lexemes\n  MINUS { [] schema:about ?item; schema:isPartOf [] } # exclude items with no Wikipedia, etc. article\n}", "context": "Items with no Statement or Sitelinks", "target": ""}
{"query": "#title: Showcase lexemes and their language/lexical category\nselect *\nwith {\n  select * {\n\n    SERVICE wikibase:mwapi {\n      bd:serviceParam wikibase:api \"Generator\";\n                      wikibase:endpoint \"www.wikidata.org\";\n                      mwapi:gcmtitle ?title;\n                      mwapi:generator \"links\";\n                      mwapi:gpllimit \"max\";\n                      mwapi:gplnamespace \"146\";\n                      mwapi:titles \"Wikidata:Showcase lexemes\".\n      ?title wikibase:apiOutput mwapi:title.\n    }\n  }\n} as %data\n{\n  include %data.\n  bind(uri(replace(?title, \"Lexeme:\", str(wd:))) as ?l).\n  ?l dct:language ?lang;\n     wikibase:lexicalCategory ?lexcat;\n     wikibase:lemma ?lemma.\n} order by str(?lemma)", "context": "Showcase lexemes and their language/lexical category", "target": ""}
{"query": "# Obscure units of measurement and where to find them.\n# This version removes unconnected claims, but won't be able to get samples for all the units.\n# ?count is number of value nodes which might be unconnected or reused.\n# ?truecount is number of claims referencing this value node.\n# By: User:Infrastruktur\nselect ?unit ?unitLabel ?count ?truecount ?sample ?uses\nwith {\n  select ?unit (count(?vn) as ?count) where {\n    ?vn wikibase:quantityUnit ?unit.\n  } group by ?unit\n} as %units\nwith {\n  select ?unit ?count (count(distinct ?st) as ?truecount) (sample(?st) as ?sample) where {\n    include %units\n    filter(?count < 5)\n    _:vn wikibase:quantityUnit ?unit .\n    ?st ?p _:vn .\n    [] wikibase:statementValue|wikibase:statementValueNormalized ?p .\n  } group by ?unit ?count\n} as %samples\nwhere {\n  include %samples\n  bind(uri(concat(\"https://query.wikidata.org/#select%20%3Fst%20where%20%7B%0A%20%20_%3Avn%20wikibase%3AquantityUnit%20wd%3A\", substr(str(?unit), 32), \"%20.%0A%20%20%3Fst%20%3Fp%20_%3Avn%20.%0A%20%20%5B%5D%20wikibase%3AstatementValue%7Cwikibase%3AstatementValueNormalized%20%3Fp%20.%0A%7D\")) as ?uses)\n  service wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\" . }\n}\norder by ?count\n", "context": "Obscure units of measurement and where to find them", "target": ""}
{"query": "#Female scientists with most number of sitelinks (but not English Wikipedia)\n#Female scientists with most number of sitelinks (but not English Wikipedia)\n#PREFIX schema: <http://schema.org/>\n\nSELECT ?item ?itemLabel ?linkcount WHERE {\n    ?item wdt:P31 wd:Q5 .\n    ?item wdt:P21 wd:Q6581072 .\n    ?item wdt:P106 wd:Q901 .\n    ?item wikibase:sitelinks ?linkcount .\n  FILTER (?linkcount >= 1) . # only include items with 1 or more sitelinks\n  FILTER NOT EXISTS {\n    ?article schema:about ?item .\n    ?article schema:inLanguage \"en\" .\n    ?article schema:isPartOf <https://en.wikipedia.org/>\n  }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en,de,es,ar,fr\" }\n}\nGROUP BY ?item ?itemLabel ?linkcount\nORDER BY DESC(?linkcount)", "context": "Female scientists with most number of sitelinks", "target": ""}
{"query": "#defaultView:Map\nSELECT ?estación_de_bomberos ?estación_de_bomberosLabel ?coordenadas WHERE {\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n  ?estación_de_bomberos wdt:P31 wd:Q1195942;\n    wdt:P17 wd:Q29.\n  OPTIONAL { ?estación_de_bomberos wdt:P625 ?coordenadas. }\n}", "context": "Location of fire stations in Spain", "target": ""}
{"query": "#title:oldest known individual per taxon (pre-20th century)\nSELECT ?individual ?individualLabel ?dateOfBirth (SAMPLE(?image_) AS ?image) ?taxon ?taxonLabel WITH {\n  SELECT ?taxon (MIN(?dateOfBirth_) AS ?dateOfBirth) WHERE {\n    ?individual wdt:P10241 ?taxon;\n                wdt:P569 ?dateOfBirth_. hint:Prior hint:rangeSafe true.\n    FILTER(!wikibase:isSomeValue(?dateOfBirth_))\n    FILTER(?dateOfBirth_ < \"1901-00-00\"^^xsd:dateTime) # arbitrary cutoff, past this point results don’t seem all that interesting anymore\n  }\n  GROUP BY ?taxon\n} AS %taxons WITH {\n  SELECT ?taxon ?dateOfBirth (SAMPLE(?individual_) AS ?individual) WHERE {\n    INCLUDE %taxons.\n    ?individual_ wdt:P10241 ?taxon;\n                 wdt:P569 ?dateOfBirth.\n  }\n  GROUP BY ?taxon ?dateOfBirth\n} AS %results WHERE {\n  INCLUDE %results.\n  OPTIONAL { ?individual wdt:P18 ?image_. }\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}\nGROUP BY ?individual ?individualLabel ?dateOfBirth ?taxon ?taxonLabel\nORDER BY ?dateOfBirth", "context": "Oldest known individual per taxon", "target": ""}
{"query": "SELECT DISTINCT ?district ?districtLabel (MAX(?population) AS ?latestPopulation) \n       ?coordinateLocation ?osmRelationId ?officialWebsite WHERE {\n  ?district wdt:P31 wd:Q3539870;  # Administrative divisions of Uganda (including districts)\n            p:P1082 ?populationStatement. # Population statement\n  ?populationStatement ps:P1082 ?population. # Population value\n  OPTIONAL { ?populationStatement pq:P585 ?date } # Population date if available\n  OPTIONAL { ?district wdt:P625 ?coordinateLocation } # Coordinates\n  OPTIONAL { ?district wdt:P402 ?osmRelationId } # OSM Relation ID\n  OPTIONAL { ?district wdt:P856 ?officialWebsite } # Official website\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }\n}\nGROUP BY ?district ?districtLabel ?coordinateLocation ?osmRelationId ?officialWebsite\nORDER BY DESC(?latestPopulation)\n", "context": "2024 Population Census in Uganda", "target": ""}
{"query": "SELECT ?item ?itemLabel ?_articleEN ?_articlePT ?badge_label_PT ?_articleES ?badge_label_ES WHERE {\n  SERVICE wikibase:mwapi {\n    bd:serviceParam wikibase:endpoint \"en.wikipedia.org\" .\n    bd:serviceParam wikibase:api \"Generator\" .\n    bd:serviceParam mwapi:gcmtitle \"Category:Featured articles\" .\n    bd:serviceParam mwapi:generator \"categorymembers\" .\n    bd:serviceParam mwapi:gcmtype \"page\" .\n    bd:serviceParam mwapi:gcmlimit \"max\" .\n    ?item wikibase:apiOutputItem mwapi:item .\n    ?itemLabel wikibase:apiOutput mwapi:title .\n  }\n  OPTIONAL {\n    ?_articleEN schema:about ?item;\n                schema:isPartOf <https://en.wikipedia.org/>. }\n  OPTIONAL {\n    ?_articlePT schema:about ?item;\n                schema:isPartOf <https://pt.wikipedia.org/>;\n                wikibase:badge ?badgePT. }\n  OPTIONAL {\n    ?_articleES schema:about ?item;\n                schema:isPartOf <https://es.wikipedia.org/>;\n                wikibase:badge ?badgeES. }\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"mul,en\" .\n    ?badgePT rdfs:label ?badge_label_PT . ?badgeES rdfs:label ?badge_label_ES .\n  }\n}", "context": "Find Good or Featured Articles in Spanish and Portuguese Wikipedia", "target": ""}
{"query": "SELECT ?item ?itemLabelEn ?itemDescEn ?itemLabelFr ?class\nWHERE {\n  {\n    ?class wdt:P279* wd:Q756.  # ?class must be a subclass of plants\n    ?item wdt:P31 ?class.      # and ?item must be an instance of ?class\n  }\n  UNION                        # or...\n  {\n    ?item wdt:P279* wd:Q756.   # ?item itself must be a subclass of plants\n  }\n\n  # get label and description in French\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"fr\" .\n    ?item rdfs:label ?itemLabelFr .\n    ?item schema:description ?itemDescFr .\n  }\n  FILTER(!bound(?itemDescFr))  # filter out entries with a French description set\n\n  # get label and description in English (optional, remove this section and \"?itemLabelEn ?itemDescEn\" in the selectors if you don't need it)\n  SERVICE wikibase:label {\n    bd:serviceParam wikibase:language \"en\" .\n    ?item rdfs:label ?itemLabelEn .\n    ?item schema:description ?itemDescEn .\n  }\n}\nLIMIT 10                       # and return at most 10 results", "context": "Plants missing a French description", "target": ""}
{"query": "SELECT ?parentOrganization ?parentOrganizationLabel (GROUP_CONCAT(?commercialOrganization_Label; SEPARATOR = \", \") AS ?commercialOrganizations) (COUNT(*) AS ?numberOfChildren) WHERE {\n  ?commercialOrganization (wdt:P31/(wdt:P279*)) wd:Q21980538;\n    wdt:P749 ?parentOrganization.\n  ?commercialOrganization rdfs:label ?commercialOrganization_Label. FILTER(LANG(?commercialOrganization_Label) = \"en\")\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}\nGROUP BY ?parentOrganization ?parentOrganizationLabel\nORDER BY DESC(?numberOfChildren)", "context": "Sorting Organisations by the no. of subsidiaries it owns", "target": ""}
{"query": "SELECT ?sample ?name ?count\nWHERE {\n  { SELECT ?fname (sample(?item_) as ?sample) (count(?item_) as ?count) where {\n    ?item_ wdt:P31 wd:Q5;\n      wdt:P27 wd:Q183;\n      wdt:P734 ?fname.\n  } GROUP BY ?fname }\n  OPTIONAL { ?fname wdt:P1705 ?nameDE. FILTER(LANG(?nameDE)='de') }\n  OPTIONAL { ?fname wdt:P1705 ?nameMUL. FILTER(LANG(?nameMUL)='mul') }\n  BIND(COALESCE(?nameDE, ?nameMUL) AS ?name)\n}\norder by desc(?count)\n", "context": "Popular German Family names", "target": ""}
{"query": "SELECT ?category ?sitelinks WHERE {\n  ?category wdt:P31 wd:Q4167836;\n            wikibase:sitelinks ?sitelinks. hint:Prior hint:rangeSafe true.\n  FILTER(?sitelinks >= 350)\n}\nORDER BY DESC(?sitelinks)\nLIMIT 10", "context": "Top 10 items instance of Wikimedia category ordered by number of Sitelinks", "target": ""}
{"query": "SELECT DISTINCT ?personLabel ?twitterName ?pic WHERE {\n  ?person wdt:P2002 ?twitterName ;\n  wdt:P106 ?occupation .\n  OPTIONAL { ?person wdt:P18 ?pic . }\n  ?occupation wdt:P279* wd:Q864503 . # all subclasses of biologists\n   SERVICE wikibase:label {\n     bd:serviceParam wikibase:language \"en\" .\n   }\n}\n", "context": "Twitter accounts of biologists", "target": ""}
{"query": "SELECT ?item ?en_label ?de_label WHERE {\n  ?item wdt:P31 wd:Q107487333 .\n  OPTIONAL { ?item rdfs:label ?en_label . FILTER (LANG(?en_label) = \"en\")}\n  OPTIONAL { ?item rdfs:label ?de_label . FILTER (LANG(?de_label) = \"de\")}\n}", "context": "All Genres of Classical Musical Compositions and their Labels in English and German", "target": ""}
{"query": "PREFIX target: <http://www.wikidata.org/entity/Q18507561>\n\n# List of related works by co-citation analysis\nSELECT ?count ?work ?workLabel\nWITH {\n  SELECT ?work (COUNT(?work) AS ?count)\n  WHERE {\n    target: (^wdt:P2860 | wdt:P2860) / (^wdt:P2860 | wdt:P2860)? ?work .\n    FILTER (target: != ?work)\n  }\n  GROUP BY ?work\n  LIMIT 500\n} AS %result\nWHERE {\n  INCLUDE %result\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\" . } \n} \nORDER BY DESC(?count)", "context": "Related works from co-citation analysis", "target": ""}
{"query": "SELECT ?seriesNumber ?album ?albumLabel ?albumDescription  ?date ?scenariste ?dessinateur?coloriste  (GROUP_CONCAT(DISTINCT ?personnagesLabel ; SEPARATOR = \", \") AS ?personnages) WHERE {\n  ?album p:P179 ?seriesStmt; wdt:P58 ?scenarist;  wdt:P10837 ?dessinateu;wdt:P577 ?date; wdt:P6338 ?colorist  .\n  ?seriesStmt ps:P179 wd:Q1030833 ;   \n    pq:P1545 ?seriesNumber.     \n  OPTIONAL { ?album wdt:P674 ?personnages.  }     \n  SERVICE wikibase:label {    \n    bd:serviceParam wikibase:language \"fr\".   \n    ?album rdfs:label ?albumLabel;schema:description ?albumDescription.   \n           ?colorist rdfs:label ?coloriste.\n           ?scenarist rdfs:label ?scenariste.  \n    ?dessinateu rdfs:label ?dessinateur . \n    ?personnages rdfs:label ?personnagesLabel.\n  }\n} \nGROUP BY ?album ?albumLabel ?seriesNumber ?albumDescription ?date ?scenariste ?coloriste ?dessinateur\nORDER BY (xsd:integer(?seriesNumber) )", "context": "All you want to know about", "target": ""}
{"query": "#defaultView:Map\nSELECT DISTINCT (?whattype as ?layer) ?itemLabel ?item ?PubID ?img ?web ?loc  WHERE { \n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }\n  ?item wdt:P13516 ?pubID.\n  OPTIONAL {?item wdt:P31 ?thisType. ?thisType rdfs:label ?whattype filter (lang(?whattype) = \"en\"). }\n  VALUES ?scotmarks {wd:Q22 wd:Q15060255 }.\n  {?item wdt:P131 ?scotmarks.} \n    UNION {?item wdt:P131 ?orgloc1. ?orgloc1 wdt:P31/wdt:P131 ?scotmarks.} \n    UNION {?item wdt:P159 ?orgpl. {{?orgpl wdt:P31/wdt:P131 ?scotmarks.} .} \n    UNION {?orgpl wdt:P131 ?orgl2. {{?orgl2 wdt:P31/wdt:P131 ?scotmarks} }}}.\n  optional {?item wdt:P18 ?img.}.\n  ?item wdt:P625 ?loc.\n  OPTIONAL {?item wdt:P856 ?web}.\n  BIND(URI(CONCAT(\"https://camra.org.uk/pubs/\", ?pubID)) AS ?PubID).\n}\n", "context": "Map of pubs in Scotland", "target": ""}
{"query": "#Gats\nSELECT ?org ?orgLabel  ?coordenades   ?estatLabel WHERE {\n  ?org wdt:P463 wd:Q741827.\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n  OPTIONAL { ?org wdt:P625 ?coordenades. }\n  OPTIONAL { ?org wdt:P17 ?estat. }\n}\n\n#defaultView:Map", "context": "Data about all 60 members of the European Association for Quality Assurance in Higher Education", "target": ""}
{"query": "#title:Exemplars of the Magna Carta\nSELECT ?item ?itemLabel (YEAR(?date) AS ?year) ?w ?h ?url WHERE {\n  ?item wdt:P1574 wd:Q12519\n  OPTIONAL {?item wdt:P571 ?date}\n  OPTIONAL {?item wdt:P973 ?url}\n  OPTIONAL {?item wdt:P2049 ?w}\n  OPTIONAL {?item wdt:P2048 ?h}\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,mul\". }\n} ORDER BY ?date ?itemLabel", "context": "Exemplars of the Magna Carta", "target": ""}
{"query": "SELECT ?l ?lemma (xsd:integer(STRAFTER(STR(?l), \"L\")) AS ?numericId) WHERE {\n  ?l a ontolex:LexicalEntry ;\n     dct:language wd:Q13324 ;\n     wikibase:lemma ?lemma .\n}\nORDER BY ?numericId\n", "context": "All lexemes in Minangkabau (sorted chronologically by their entry time)", "target": ""}
{"query": "#title: film directors who are alive \n#defaultView:Table;ImageGrid\nSELECT ?item ?itemLabel ?itemDescription ?date_of_birth \nWHERE {\n  #hint:Query hint:optimizer \"None\" . \n  ?item wdt:P31 wd:Q5.\n  ?item wdt:P106 wd:Q2526255.\n  OPTIONAL { ?item wdt:P569 ?date_of_birth. } \n  MINUS {?item wdt:P570 ?date_of_death. } \n  MINUS {?item wdt:P20 ?place_of_death. } \n  #FILTER (?date_of_birth >= \"1870-01-01T00:00:00Z\"^^xsd:dateTime)   # born after \n  #FILTER (?date_of_birth <= \"1900-07-01T00:00:00Z\"^^xsd:dateTime)  # born before\n  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". }\n}\n#order by ?date_of_birth\nLIMIT 100", "context": "Film Directors who are still alive", "target": ""}
{"query": "FILTER ( ?item not in ( wd:Q4115189,wd:Q13406268,wd:Q15397819 ) )", "context": " FILTER  \n</translate>\n\n  For a sometimes faster alternative to FILTER, you might also look at MINUS, see [[Wikidata:SPARQL query service/queries#Excluding subsets|example]].\n\nFILTER(condition). is a clause you can insert into your SPARQL query to filter the results. Inside the parentheses, you can put any expression of boolean type, and only those results where the expression returns true are used.\n\nFor example, to get a list of all humans born in 2015, we first get all humans with their date of birth –\n\n– and then filter that to only return the results where the year of the date of birth is 2015. There are two ways to do that: extract the year of the date with the YEAR function, and test that it’s 2015 –\n\n– or check that the date is between Jan. 1st (inclusive), 2015 and Jan. 1st, 2016 (exclusive):\n\nI’d say that the first one is more straightforward, but it turns out the second one is much faster, so let’s use that:\n\nAnother possible use of FILTER is related to labels. The label service is very useful if you just want to display the label of a variable. But if you want to do stuff with the label – for example: check if it starts with “Mr. ” – you’ll find that it doesn’t work:\n\nThis query finds all instances of  and tests if their label starts with \"Mr. \" (STRSTARTS is short for “string starts [with]”; there’s also STRENDS and CONTAINS). The reason why this doesn’t work is that the label service adds its variables very late during query evaluation; at the point where we try to filter on ?humanLabel, the label service hasn’t created that variable yet.\n\nFortunately, the label service isn’t the only way to get an item’s label. Labels are also stored as regular triples, using the predicate rdfs:label. Of course, this means all labels, not just English ones; if we only want English labels, we’ll have to filter on the language of the label:\n\nThe LANG function returns the language of a monolingual string, and here we only select those labels that are in English. The full query is:\n\nWe get the label with the ?human rdfs:label ?label triple, restrict it to English labels, and then check if it starts with “Mr. ”.\n\nOne can also use FILTER with a regular expression. In the following example\n\nIf the format constraint for an ID is [A-Za-z][-.0-9A-Za-z]{1,}:\n\nIt is possible to filter out specific elements like this", "target": ""}
{"query": "FILTER ( NOT EXISTS { ?item  wdt:P21 [] } )", "context": " FILTER  \n</translate>\n\n  For a sometimes faster alternative to FILTER, you might also look at MINUS, see [[Wikidata:SPARQL query service/queries#Excluding subsets|example]].\n\nFILTER(condition). is a clause you can insert into your SPARQL query to filter the results. Inside the parentheses, you can put any expression of boolean type, and only those results where the expression returns true are used.\n\nFor example, to get a list of all humans born in 2015, we first get all humans with their date of birth –\n\n– and then filter that to only return the results where the year of the date of birth is 2015. There are two ways to do that: extract the year of the date with the YEAR function, and test that it’s 2015 –\n\n– or check that the date is between Jan. 1st (inclusive), 2015 and Jan. 1st, 2016 (exclusive):\n\nI’d say that the first one is more straightforward, but it turns out the second one is much faster, so let’s use that:\n\nAnother possible use of FILTER is related to labels. The label service is very useful if you just want to display the label of a variable. But if you want to do stuff with the label – for example: check if it starts with “Mr. ” – you’ll find that it doesn’t work:\n\nThis query finds all instances of  and tests if their label starts with \"Mr. \" (STRSTARTS is short for “string starts [with]”; there’s also STRENDS and CONTAINS). The reason why this doesn’t work is that the label service adds its variables very late during query evaluation; at the point where we try to filter on ?humanLabel, the label service hasn’t created that variable yet.\n\nFortunately, the label service isn’t the only way to get an item’s label. Labels are also stored as regular triples, using the predicate rdfs:label. Of course, this means all labels, not just English ones; if we only want English labels, we’ll have to filter on the language of the label:\n\nThe LANG function returns the language of a monolingual string, and here we only select those labels that are in English. The full query is:\n\nWe get the label with the ?human rdfs:label ?label triple, restrict it to English labels, and then check if it starts with “Mr. ”.\n\nOne can also use FILTER with a regular expression. In the following example\n\nIf the format constraint for an ID is [A-Za-z][-.0-9A-Za-z]{1,}:\n\nIt is possible to filter out specific elements like this\nFILTER ( ?item not in ( wd:Q4115189,wd:Q13406268,wd:Q15397819 ) )\n\nIt is possible to filter and have elements that aren't filled:", "target": ""}
{"query": "SERVICE wikibase:label { \n    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],mul,en\". \n    <translate nowrap>?citizenship</translate> rdfs:label <translate nowrap>?citizenshipLabel</translate> .\n    <translate nowrap>?person</translate>      rdfs:label <translate nowrap>?personLabel</translate> .\n  }", "context": " wikibase:Label and aggregations  \n\nA query such as the following, which searches all academic persons with more than two countries of citizenships in Wikidata, does not show the names of those countries in the ?citizenships column:\n</translate>\n\nTo show the ?citizenships, explicitly name the ?personLabel and ?citizenshipLabel in the wikibase:label service call like this:", "target": ""}
